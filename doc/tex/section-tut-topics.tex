
% --------------------------------------------------------------------------------
\subsection{Preface}
\cutdef*{subsubsection}
\label{section:tut:topic:preface}

Tutorials in this chapter cover various advanced topics relating to programming 
in Mythryl, rather than to the Mythryl language itself.  Not all of these topics 
will be of interest to all Mythryl programmers.

\cutend*

% --------------------------------------------------------------------------------
\subsection{Mythryl Compiler Switches}
\cutdef*{subsubsection}
\label{section:tut:topic:compiler-switches}

The Mythryl compiler's operation may be modulated via a large and 
ever-changing collection of control settings.

These Mythryl compiler control settings may be listed 
from the Linux commandline by doing

\begin{verbatim}
    linux$ my -S
\end{verbatim}

which will generate a long listing containing stuff like

\begin{verbatim}
         makelib::verbose = TRUE
         makelib::debug = TRUE
         makelib::keep_going_after_compile_errors = FALSE
\end{verbatim}

The corresponding settings may be changed via commandline switches like

\begin{verbatim}
    linux$ my -Cmakelib::verbose=FALSE
\end{verbatim}

These switches may also be listed, printed and set interactively:

\begin{verbatim}
    linux$ my
    eval:  show_controls();
         ...
         makelib::verbose = TRUE
         makelib::debug = TRUE
         makelib::keep_going_after_compile_errors = FALSE
         ...
    eval:  show_control "makelib::verbose";
    TRUE
    eval:  set_control "makelib::verbose" "FALSE";
    eval:  show_control "makelib::verbose";
    FALSE
\end{verbatim}

For a more concrete example of using these switches, try doing:

\begin{verbatim}
     linux$ cd src/app/tut/test
     linux$ touch test.pkg
     linux$ my
     eval:  set_control "typechecker::type_package_language_debugging" "TRUE";
     eval:  make "test.lib";
\end{verbatim}

This will spew debug printouts of various internal datastructures 
used by the code in 
\ahrefloc{src/lib/compiler/front/typer/main/type-package-language-g.pkg}{src/lib/compiler/front/typer/main/type-package-language-g.pkg} 
which can be a great help in understanding the code.

Many other compiler modules define similar debugging switches.

\cutend*

% --------------------------------------------------------------------------------
\subsection{Concurrent Programming}
\cutdef*{subsubsection}
\label{section:tut:topic:concurrent-programming}

Mythryl is a fork of the \ahref{\notesonprogrammingsmlnj}{SML/NJ} codebase. 
SML was standardized in 1990 and is defined as a single-threaded language, 
but SML/NJ supports {\tt callcc} and SML/NJ's stackless implementation makes 
{\tt callcc} about 100X faster than in typical stack-based languages;  This 
makes SML/NJ an excellent foundation upon which to build a concurrent programming 
language.

Mythryl (and more broadly ML-family languages) are wonderful candidates for 
concurrent and parallel programming because the problems with concurrent and 
parallel programming all revolve around heap side effects, and ML code typically 
uses only about one percent as many side effects as equivalent code in mainstream 
imperative languages like C/C++/Java/etc.  One hundred times fewer side effects 
translates directly to one hundred times fewer race condition bugs, clobbered-shared-variable 
bugs and so forth.  The typesafety provided by ML-family languages is also very 
welcome in the context of concurrent and parallel programming, because they mean 
fewer runtime bugs, and runtime debugging is inherently more difficult in concurrent 
and parallel programs than in old-style single-threaded programs.

Starting in about 1990 \ahref{\johnhreppy}{John H Reppy} developed a 
concurrent programming library for SML called \ahref{\cml}{CML} ("Concurrent ML"), 
documented in his book of that title.

This library has been integrated into the Mythryl codebase and work is under way 
to make concurrent programming the norm in Mythryl.  At present, however, concurrent 
programming in Mythryl is experimental and uses a separate set of libraries.  (The 
Mythryl codebase pervasively assumes single-threaded operation;  making it all 
threadsafe and concurrent-programming oriented will take a lot of detail work.)

The Mythryl port of CML is called "threadkit", and is not well documented because 
the code is still evolving steadily.  For an informal overview of what is working 
so far, take a peek at 

\begin{verbatim}
    src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit-unit-test.pkg
\end{verbatim}

in the Mythryl sourcecode distribution.

As a quick sketch of the current threadkit facilities:

\begin{itemize}
\item 
Threadkit gives the illusion of concurrent execution of threads by 
doing pre-emptive thread scheduling.  (As opposed to cooperative 
multithreading, where each thread must explicitly yield to other 
threads, as in the Bad Old Days on MacOs.) 

This pre-emptive thread switching is driven by 
a 50Hz (by default --- configurable) SIGALRM from 
the host OS.  This is not something one wants to have 
running by default when not being used, so before using 
threadkit facilities you must first explicitly start it up via 
\begin{verbatim}
            thread_scheduler_control::start_up_thread_scheduling ();
\end{verbatim}

The above unit-test code has lots of examples of doing this. 

\item 
Once the above has been done, new threads can be started 
up via just 

\begin{verbatim}
        make_thread   (fn () = whatever());
\end{verbatim}

I usually write that as 

\begin{verbatim}
        make_thread  {.
            #
            whatever ();
        };
\end{verbatim}

taking advantage of Mythryl 'thunk' syntax to improve 
readability a bit.  The 'whatever()' stuff will usually 
in practice be

\begin{verbatim}
        for (;;) {

            do_one_mailop [
                ...
            ];
        }
\end{verbatim}

which is to say, an infinite loop reading and handling 
input from other threads.  (More on "select" in a bit.) 

\item The inter-thread communication facilities provided are:


\begin{itemize}
\item 
Mailslots.  These provide synchronous message-passing 
between threads.  "Synchronous" means that nothing 
happens until both the sending and receiving thread 
are ready for the interaction;  whichever gets to the 
operation first blocks until the other is ready to 
participate. 

The basic protocol is: 

\begin{verbatim}
    include threadkit;

    slot:  Mailslot (Foo)
        =
        make_mailslot ();      # Create a mailslot for
                               # passing values of type Foo.

    give (slot, foo);          # Send a type-Foo value via the slot.


    foo = take slot;           # Receive a type-Foo value via the slot.
\end{verbatim}


Here the 'give' and 'take' operations will of course 
have to be performed in separate threads!  If the above 
code is executed as written in a single thread, the 'give' 
will block forever for lack of a synchronous 'take'. 


\item 
Oneshot-mailslots.  These are just like vanilla mailslots 
except that they can be used only once.  They are typically 
created, passed to a server thread in a 'give' message, and 
then used to read the return value from the server thread. 
(This protocol avoids the race conditions that would arise 
if multiple client threads used a single fixed reply mailslot.) 

The basic protocol is: 

\begin{verbatim}
    include threadkit;

    slot:  Oneshot_Mailslot (Foo)
        =
        make_oneshot_mailslot ();      # Create a oneshot for
                                       # passing values of type Foo.

    set (slot, foo);                   # Send a type-Foo value via the oneshot.


    foo = get slot;                    # Receive a type-Foo value via the oneshot.
\end{verbatim}

\item 
Maildrops.  These provide asynchronous message passing 
between threads:  One thread can deposit a value in a 
maildrop and one or more other threads can later read 
that value.

In general, one should never share REF cells or mutable 
vectors between concurrent threads;  maildrops are in 
essence concurrency-safe replacements for REF cells. 

The basic protocol is: 

\begin{verbatim}
    include threadkit;

    drop:  Maildrop (Foo)
        =
        make_empty_maildrop ();        # Create an empty maildrop holding
                                       # values of type Foo.

    fill (drop, foo);                  # Deposit a a type-Foo value in the maildrop.


    foo = empty drop;                  # Get contents of maildrop, leaving it empty.
\end{verbatim}

Attempts to read from an empty maildrop will 
block until it is filled.

Attempts to fill an already full maildrop 
will generate an error exception.

Void-valued maildrops are often used as 
PV-style locks to provide mutual exclusion 
in monitor-style code.

Additional maildrop operations include:

\begin{verbatim}
    include threadkit;

    drop:  Maildrop (Foo)
        =
        make_full_maildrop foo;              # Create an already-full maildrop holding
                                        # values of type Foo.

    foo = peek drop;                    # Read contents of maildrop without altering maildrop.
    foo = swap (drop, foo');            # Get contents of maildrop, replacing with foo'.
\end{verbatim}

\item 
'do\_one\_mailop' operator.  This provides the 
capability to do whichever of a set of 
mail operations which will not block. 
This is conceptually similar to the Posix 
'select()' function, but at a much higher 
level of abstraction.

A typical use looks like:

\begin{verbatim}
    do_one_mailop [

        take' mailslot1
            ==>
            (fn foo = handle_slot1_read foo),

        take' mailslot2
            ==>
            (fn foo = handle_slot2_read foo)
    ];
\end{verbatim}

Here we are expecting to get input now and 
then on either mailslot1 or mailslot2, but 
don't know which.  This construct lets us 
block until either one is ready, rather than 
having to guess correctly which will be ready 
next, at the risk of deadlock if we guess wrong. 

Note the use of {\tt take'} rather than {\tt take}. 
The difference is that {\tt take} performs the 
mail operation immediately, whereas {\tt take'} 
generates a deferred operation suitable for 
use by {\tt select}.

In general all mail operations which can 
block have primed versions suitable for 
use in {\tt select}, and {\tt select} can handle 
both blocking reads and writes, plus timeouts 
besides.  A fancier {\tt select} statement than 
you are ever likely to write demonstrates this: 

\begin{verbatim}
    do_one_mailop [

        take' mailslot1
            ==>
            (fn foo =  handle_slot1_read  foo),

        give' (mailslot1  foo)
            ==>
            (fn () = handle_slot1_write ()),

        pull' mailqueue1                            # Mailqueues are covered below.
            ==>
            (fn foo =  handle_mailqueue1_read  foo),

        timeout_in' (time::from_milliseconds 100)   # Timeouts are pretty self-explanatory.
            ==>                                     # They are -so- much more convenient
            (fn () =  handle_100_ms_timeout  ())    # than the vanilla-C equivalent! :-)
    ];
\end{verbatim}


One particularly nice aspect of Reppy's concurrent 
programming model, distinguishing it from many other 
such models, is that everything is first class. 
Mailslots, maildrops and mailqueues are all first-class 
values which may be freely constructed at runtime and 
passed around, stored in datastructures etc. 

In particular, the {\tt select} argument list is in fact 
a vanilla Mythryl list, which may be freely re/constructed 
dynamically at runtime, although in most cases it will 
be fixed at compiletime as in the above examples. 

This first-classness provides tremendous reserve flexibility 
for interactive programming, in distinct contrast to 
concurrent programming paradigms in which (for example) 
{\tt select} style statements are completely fixed at compiletime.

(Reppy's model also provides for user definition of compound 
mailops which are likewise first-class;  I'm not going to 
cover that in this brief tutorial.) 


Mailslots, oneshot mailslots, 
maildrops and 'do\_one\_mailop' statements 
suffice for maybe ninety percent of typical 
 concurrent programming;  the 
remaining mail mechanisms are used 
considerably less frequently: 

\item 
Mail queues.  These provide asynchronous 
message passing via an unbounded buffer. 

Reading from an empty mailqueue blocks 
the thread until there is something to 
read. 

Writing to a mailqueue never blocks, 
but the mailqueue contents can grow without 
bound, potentially filling all of memory, 
so they need to be used with considerable 
caution. 

The main virtue of mailqueues is that they 
avoid the risk of deadlock due to a cycle 
of threads all blocking waiting for each 
other. 

The deadlock-avoidance protocol that the 
eXene development crew has arrived at is 
to use mailqueues on all values containing 
user input.  This breaks most potential 
deadlock cycles, and there is very little 
risk of user-generated values filling all 
of memory before threads get enough CPU 
bandwidth to handle them. 

The basic mailqueue protocol is: 

\begin{verbatim}
    include threadkit;

    queue:  Mailqueue (Foo)
        =
        make_mailqueue ();             # Create an empty mailqueue holding
                                       # values of type Foo.

    push (queue, foo);                 # Deposit a a type-Foo value in the mailqueue.


    foo = pull queue;                  # Get one type-Foo value from mailqueue.
\end{verbatim}


\item 
Mail multicasters.  These provide asynchronous 
one-to-many mail broadcast communication between 
threads, with each reader having its own mailqueue. 

This presents considerable risk that if any individual 
reader dies or blocks, its mailqueue may grow without 
bound, eventually filling memory, so mailcasters must 
be used with caution, but there are times when one-to-many 
communication is exactly the functionality needed. 

The basic mailcaster protocol is: 

\begin{verbatim}
    include threadkit;

    mailcaster:  Mailcaster (Foo)
        =
        make_mailcaster ();            # Create an empty mailcaster for
                                       # values of type Foo.

    readqueue1 = make_readqueue mailcaster;
    readqueue2 = make_readqueue mailcaster;
    [...]                              # One readqueue per reading thread.

    send (mailcaster, foo);            # Done in sender thread.


    foo = receive readqueue1;          # Done in first reader thread.
    foo = receive readqueue2;          # Done in second reader thread.
\end{verbatim}

\end{itemize}
\end{itemize}


By far the largest body of code written in CML is \ahref{\exene}{eXene}, 
the X widgetset and and client library that John H Reppy wrote to exercise CML.
The Mythryl port of eXene is called xkit and the code may be found in {\tt src/lib/x-kit/} 
in the Mythryl source distribution.

This code is an ambitious experiment in highly concurrent programming.  Each widget uses 
at least one private thread to animate it, and often more.  In retrospect some of the 
ideas tried out in this package worked very well and some worked not very well at all. 

One of the biggest design problems turned out to be using primarily synchronous communication 
between widgets (i.e. mailslots) while also having widgets send to both parent and child 
widgets;  this proved a fertile breeding ground for deadlock bugs.  (If I were redesigning 
it today I would use mailqueues as the primary interthread communication mechanism.) 

Since xkit needs a fairly complete rewrite to bring it up to production quality 
(and since Gtk is more apropos for most purposes) I have not yet written a tutorial 
set for it, but sample apps may be found in 

\begin{verbatim}
    src/lib/x-kit/tut/
\end{verbatim}

and using the widget kit is not terribly difficult working from these examples plus 
an occasional peek at the widget library sourcecode and/or the original eXene documents. 



\cutend*

% --------------------------------------------------------------------------------
\subsection{Parallel Programming}
\cutdef*{subsubsection}
\label{section:tut:topic:parallel-programming}

The difference between concurrent programming and parallel programming is: 
\begin{itemize}

\item 
In concurrent programming multiple application-program threads appear to run 
"at the same time" but actually run on a single core via pre-emptive timeslicing. 

\item 
In parallel programming application-program threads run simultaneously on 
multiple cores;  on a six-core machine a speedup of six is possible. 
\end{itemize}

Low-level Mythryl support for parallel programming based 
based on the industry-standard posix-threads API is defined in 
\ahrefloc{src/lib/std/src/hostthread.api}{src/lib/std/src/hostthread.api} and 
documented in {\tt src/A.HOSTTHREAD-SUPPORT.OVERVIEW} but not yet sufficiently 
debugged for use as of 2012-01-22. 

\cutend*

% --------------------------------------------------------------------------------
\subsection{Indefinite Precision Integers}
\cutdef*{subsubsection}
\label{section:tut:topic:integer}

The \ahrefloc{pkg:multiword\_int}{multiword\_int} package implements arbitrary-precision integer 
arithmetic.

(TBD)

\cutend*

% --------------------------------------------------------------------------------
\subsection{Regular Expressions}
\cutdef*{subsubsection}
\label{section:tut:topic:regex}

The \ahrefloc{pkg:regex}{regex} package provides the standard Mythryl 
regular expression support;  it is modelled on Perl regular expressions.

(TBD)

\cutend*

