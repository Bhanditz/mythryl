
% --------------------------------------------------------------------------------
\subsection{Overview}
\cutdef*{subsubsection}
\label{section:ref:packages-and-apis:overview}

Mythryl uses packages and APIs where C++ uses classes.  In general 
an API corresponds roughly to a Pure Abstract Base Class (which 
defines an interface), while a package corresponds to a vanilla 
class (which implements such an interface).  Like a C++ class, 
a package may have elements of various kinds, which may be 
accessed outside the class using {\tt package::element} notation.

The analogy should not be pushed too far;  packages are not 
classes.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Package Syntax}
\cutdef*{subsubsection}
\label{section:ref:packages-syntax}

The simplest syntax for defining a package looks like:

\begin{verbatim}
    package binary_tree {

        Binary_Tree
            = LEAF
            | NODE { key:   Float,

                     left_kid:  Binary_Tree,
                     right_kid: Binary_Tree
                   }
            ;

        fun print_tree LEAF
                =>
                ();

            print_tree (NODE { key, left_kid, right_kid })
                =>
                {   print "(";
                    print_tree left_kid;
                    printf "%2.1f" key;
                    print_tree right_kid;
                    print ")";
                };
        end;
    };
\end{verbatim}

Here the reserved word {\tt package} introduces the package name {\tt binary\_tree}, 
while the curly braces delimit the scope of the package, which in this case 
exports one type, {\tt Binary\_Tree} and one function, {\tt print\_tree}. 

Other packages may then make such references as

\begin{verbatim}
    binary_tree::Binary_Tree
    binary_tree::LEAF
    binary_tree::NODE
    binary_tree::print_tree
\end{verbatim}

in the course of making use of the functionality so implemented.

Since {\tt binary\_tree} is a fairly long name, another package might 
well define a shorter synonym for local use by doing

\begin{verbatim}
    package tree = binary_tree;
\end{verbatim}

after which it could instead refer to

\begin{verbatim}
    tree::Binary_Tree
    tree::LEAF
    tree::NODE
    tree::print_tree
\end{verbatim}

Alternatively, if it is a small package working heavily with binary trees, 
it might simply import everything from package {\tt binary\_tree} wholesale 
into its own namespace by doing

\begin{verbatim}
    include binary_tree;
\end{verbatim}

after which it could simply refer to

\begin{verbatim}
    Binary_Tree
    LEAF
    NODE
    print_tree
\end{verbatim}

just as though they had been locally defined.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Api Syntax}
\cutdef*{subsubsection}
\label{section:ref:api-syntax}

The simplest syntax for defining an API looks like:

\begin{verbatim}
    api Binary_Tree {

        Binary_Tree
            = LEAF
            | NODE { key:   Float,

                     left_kid:  Binary_Tree,
                     right_kid: Binary_Tree
                   }
            ;

        print_tree: Binary_Tree -> Void;
    };
\end{verbatim}

Here the definition of the {\tt Binary\_Tree} type is exactly  
as in the previous package declaration, but only the type of 
the function is declared.

This is exactly the information 
needed by other packages in order to use the facilities of 
package {\tt binary\_tree}:  They need to know the data structure 
in order to construct it, and they need to know the type of the 
{\tt print\_tree} function in order to invoke it correctly, but 
they need know nothing about the implementation of the {\tt print\_tree} function.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Package Sealing}
\cutdef*{subsubsection}
\label{section:ref:package-sealing}

The main use of an API is to {\it seal} a package, 
restricting the set of externally visible package 
elements to just those listed in the API.  This 
allows us to impose implementation hiding by 
protecting package-internal types, values and 
functions from external view.

Here is an example:

\begin{verbatim}
    api Counter {

        Counter;

        make_counter:      Void -> Counter;
        increment_counter: Counter -> Void;
        decrement_counter: Counter -> Void;
        counter_value:     Counter -> Int; 
    };

    package counter: Counter {

        Counter = COUNTER { count: Ref(Int), calls: Ref(Int) };

        fun make_counter () =  COUNTER { count => REF 0, calls => REF 0 };

        fun increment_counter (COUNTER { count, calls })
            =
            {   count := *count + 1;
                calls := *calls + 1;
            };

        fun decrement_counter (COUNTER { count, calls })
            =
            {   count := *count - 1;
                calls := *calls + 1;
            };

        fun counter_value (COUNTER { count, calls })
            =
            *count;
    };

\end{verbatim}

Here we are keeping both a counter value and also a count 
of calls made, perhaps for debugging purposes, but our 
API declares type {\tt Count} to be abstract, hiding its 
internal structure from external view, so if we later decide 
to remove the {\tt calls} field, we can be assured that we 
will not break any external code in other packages by so 
doing.

\cutend*

% --------------------------------------------------------------------------------
\subsection{Subpackages}
\cutdef*{subsubsection}
\label{section:ref:subpackages}

Package declarations may be nested.  This can 
be useful for a variety of reasons, including 
namespace cleanliness and control of complexity:

\begin{verbatim}

    package a {

        foo = 21;

        package b {

            bar = "abc";
        };
    };

\end{verbatim}

Here {\tt foo} is externally accessible as {\tt a::foo} 
while {\tt bar} is externally accessible as  {\tt a::b::bar}.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Subapis}
\cutdef*{subsubsection}
\label{section:ref:subapis}

Packages may also declare nested APIs: 
be useful for a variety of reasons, including 
namespace cleanliness and control of complexity:

\begin{verbatim}

    package alpha {

        api Beta {

            bar: String;
        };

        package beta: Beta {

            bar = "abc";
        };
    };

\end{verbatim}

Here API Beta is externally accessible as {\tt alpha::Beta}, 
package beta is externally accessible as {\tt alpha::beta}, 
and {\tt bar} is externally accessible as  {\tt alpha::beta::bar}.

\cutend*

% --------------------------------------------------------------------------------
\subsection{Anonymous APIs}
\cutdef*{subsubsection}
\label{section:ref:anonymous-apis}

Often an API is small and used only once, at its 
place of definition, in which case there is little 
point in even giving it a name.  In this case the 
API name may simply be replaced by an underbar 
wildcared:

\begin{verbatim}

    package alpha {

        package beta: api { bar: String; } {

            bar = "abc";
        };
    };

\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{Generic Packages}
\cutdef*{subsubsection}
\label{section:ref:generic-packages}

Often a package must make an arbitrary choice among 
a number of available datatypes.  For example, a 
binary tree needs to know the type of its keys in 
order to keep the tree sorted, but the logic of the 
binary tree does not otherwise depend particularly upon 
the key type. 

In such a case, rather than coding  up separate 
versions of the tree for each key type of interest, 
it is more efficient to define a single generic 
package which can then be expanded at compiletime 
to produce the various specialized tree implementations 
needed.

A generic package is in essence a typed compiletime 
code macro which accepts a package as argument and 
returns a package as result:

\begin{verbatim}

    api  Key {

        Key;

        compare:  (Key, Key) -> Order;
    };

    generic package binary_tree (k: Key) {

        Binary_Tree
            = LEAF
            | NODE { key:   Key,

                     left_kid:  Binary_Tree,
                     right_kid: Binary_Tree
                   }
            ;

        fun make_tree () = ... ;
        fun insert_tree  (tree: Binary_Tree, key: Key) = ... ;
        fun contains_key (tree: Binary_Tree, key: Key) = ... ;
    };

    package tree_of_ints    = binary_tree (Key = int::Int;       compare = int::compare;);
    package tree_of_floats  = binary_tree (Key = float::Float;   compare = float::compare;);
    package tree_of_strings = binary_tree (Key = string::String; compare = string::compare;);
\end{verbatim}

Here we have defined a single generic package {\tt binary\_tree} which 
accepts as argument a package containing {\tt Key}, the type for the 
trees keys, and {\tt compare}, the function which compares two keys to 
see which is greater (or if they are equal).  (For expository brevity, 
we have omitted the bodies of the package functions.)

We have then generated three concrete specializations of this generic 
package, one each for trees with Int, Float and String keys.

Here the arguments

\begin{verbatim}
    (Key = int::Int;       compare = int::compare;)
    (Key = float::Float;   compare = float::compare;)
    (Key = string::String; compare = string::compare;)
\end{verbatim}

define anonymous packages as arguments for the generic package.

(This is not a general syntax for defining anonymous packages; 
it works only in this specific syntactic context.   A general 
syntax for anonymous packages is to again change the package 
name to an underbar:  {\tt package \_ \{ ... \}}.)

For an industrial-strength example of generic packages in action, see 
\ahrefloc{src/lib/src/red-black-set-g.pkg}{src/lib/src/red-black-set-g.pkg}, 
\ahrefloc{src/lib/src/string-set.pkg}{src/lib/src/string-set.pkg} and 
\ahrefloc{src/lib/src/string-key.pkg}{src/lib/src/string-key.pkg}.


\cutend*

