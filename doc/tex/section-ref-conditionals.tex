
% --------------------------------------------------------------------------------
\subsection{?? ::}
\cutdef*{subsubsection}
\label{section:ref:conditionals:what-else}

The simplest Mythryl conditional expression is {\tt ... ?? ... :: ...} which 
corresponds exactly to the C  {\tt ... ? ... : ...} conditional expression:

\begin{verbatim}
    linux$ my

    eval:  1 == 1 ?? "red" :: "green"

    "red"

    eval:  1 == 2 ?? "red" :: "green"

    "green"
\end{verbatim}

Unlike in C or Perl, Mythryl boolean expressions must evaluate to {\tt TRUE} or 
{\tt FALSE};  Mythryl does not allow you to use integer zero to 
mean {\tt FALSE} nor integer one to mean {\tt TRUE}.

\cutend*



% --------------------------------------------------------------------------------
\subsection{if else fi}
\cutdef*{subsubsection}
\label{section:ref:conditionals:if-else-fi}

The Mythryl if-else-fi is fairly conventional.  Unlike in C, it is 
an expression which returns either the value of its {\it then} or 
{\it else} branch, whichever is selected by the controlling Boolean 
expression.  To keep this well-typed, this means that both branches 
must evaluate to values of the same type.

For conciseness, the {\it then} and {\it else} branches of the 
Mythryl {\tt if} expression are implicit code blocks: each may 
contain an arbitrary sequence of statements, and takes on the 
value of the final statement in the sequence:

\begin{verbatim}
    linux$ my

    eval:  if (1 == 1) "red"; else "green"; fi;

    "red"

    eval:  if (1 == 2) "red"; else "green"; fi;

    "green"
\end{verbatim}

The conditional expression must be parenthesized 
unless it is a single variable, or a variable 
with a close-binding prefix, postfix or circumfix 
operator, typically a dereference.

The {\it else} clause may be dropped, in which case 
it takes on a default value of {\tt Void}, meaning 
that the {\tt then} clause must also have a {\tt Void} 
value:

\begin{verbatim}
    linux$ cat my-script 
    #!/usr/bin/mythryl

    if TRUE
       print "True.\n";
    fi;

    if FALSE
       print "False.\n";
    fi;

    linux$ ./my-script
    True.
\end{verbatim}

As usual, {\tt elif} may be used to construct 
a chain of tests and actions:

\begin{verbatim}
    linux$ cat my-script 
    #!/usr/bin/mythryl

    x = 2;

    if   (x == 1)  print "One.\n";
    elif (x == 2)  print "Two.\n";
    elif (x == 3)  print "Three.\n";
    else           print "Many.\n";
    fi;

    linux$ ./my-script
    Two.
\end{verbatim}

\cutend*


