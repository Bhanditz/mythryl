
% --------------------------------------------------------------------------------
\subsection{Preface}
\cutdef*{subsubsection}
\label{section:tut:familiar:preface}

\begin{quote}\begin{tiny}
        ``In the beginning we must simplify the subject,\newline
          ~~thus unavoidably falsifying it, and later we must\newline
         ~~sophisticate away the falsely simple beginning.''\newline
\newline
         ~~~~~~~~~~~~~~~~~~~~~~~~~~~~---{\em Maimonides}
\end{tiny}\end{quote}


On the first day of my first undergrad physics class, the 
prof told us, ``Everything we teach you in first year physics 
is a lie.  But sometimes one has to lie to get at the truth; 
until you understand these lies, you cannot understand the 
deeper truths behind them.''

This section is a lot like that.

Most of what we cover will appear to be familiar things done in familiar ways.

Almost none of it is.

But it will simplify things to pretend otherwise 
long enough to cover the material once;  afterward we will 
go back and look more deeply at what is {\em really} going on.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Hello, world!}
\cutdef*{subsubsection}

\begin{quote}\begin{tiny}
        ``The only way to learn a new programming language is by writing\newline
          programs in it.  The first program to write is the same for\newline
          all languages:  Print the words {\tt hello, world}''\newline
\newline
         ~~~~~~~~~~~~~~~~~~~~~~~~~~~~---{\em The C Programming Language}\newline
         ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Kernighan and Ritchie
\end{tiny}\end{quote}

C is used for writing major applications, including C compilers.  Perl is 
used for scripting.  Mythryl is a wide spectrum language, used for both 
writing compilers and for scripting.  There are a number of ways of 
running Mythryl code.  In this section we will focus on just two, 
interactive evaluation and scripting.

Interactive evaluation of Mythryl code is done by typing {\tt my} at the 
Linux prompt.  This starts up Mythryl in an incremental mode in which 
lines of code are read, compiled and executed  one at a time.  This can 
be an effective way of experimenting with individual Mythryl language 
features:

\begin{verbatim}
    linux$ my
    eval:  printf "Hello, world!\n";
    Hello, world!
    eval:  ^D
    linux$
\end{verbatim}

Type control-D to exit the interactive loop.

Mythryl scripting is done by using a text editor to create in an appropriate 
directory (folder) a text file (say "my-script") containing Mythryl source 
code which starts with the usual scripting shebang line:

\begin{verbatim}
    #!/usr/bin/mythryl
    printf "Hello, world!\n";
\end{verbatim}

Set the script executable and run it in the usual Linux fashion:

\begin{verbatim}
    linux$ chmod +x my-script
    linux$ ./my-script
    Hello, world!
    linux$ 
\end{verbatim}

There are also other ways of executing Mythryl code, such 
as from the Linux commandline:

\begin{verbatim}
    linux$ my -x '6!'
    720
    linux$ 
\end{verbatim}

We will not have use for these until we get to more advanced tutorials.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Constants}
\cutdef*{subsubsection}

Mythryl constants largely follow C conventions:

\begin{verbatim}
    linux$ my

    eval:  printf "%c %d %d %d %f %f\n" 'a' 12 012 0x12 1.2 1.0e2;
    a 12 10 18 1.200000 100.000000

    eval:  ^D
    linux$
\end{verbatim}

The Boolean constants are {\tt TRUE} and {\tt FALSE}:

\begin{verbatim}
    linux$ my

    eval:  big = TRUE;

    eval:  printf "It looks %s!\n" (big ?? "BIG" :: "TINY");
    It looks BIG!

    eval:  big = FALSE;

    eval:  printf "It looks %s!\n" (big ?? "BIG" :: "TINY");
    It looks TINY!

    eval:  ^D
    linux$
\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{Naming Values}
\cutdef*{subsubsection}

You assign a name to a value in Mythryl using an equal sign:

\begin{verbatim}
    #!/usr/bin/mythryl
    a = 10;
    b = 3;
    printf "a == %d\n" a;
    printf "b == %d\n" b;
\end{verbatim}

When run this produces

\begin{verbatim}
    linux$ ./my-script
    a == 10
    b == 3
    linux$
\end{verbatim}

This looks a lot like C variable assignment.  It is not, 
but for the moment it will do no harm to think of it as one.

\cutend*

% --------------------------------------------------------------------------------
\subsection{Arithmetic}
\cutdef*{subsubsection}

Mythryl arithmetic operations are written much as in C.

The most notable difference is that Mythryl is more sensitive to the 
presence or absence of white space.  For example {\tt a-b} and {\tt a 
-b} are the same in C, but in Mythryl the former dash designates subtraction 
and the latter dash unary negation:

\begin{verbatim}
    #!/usr/bin/mythryl
    a = 10;
    b = 3;
    printf "%d\n" (a-b);
    printf "%d %d\n" a -b;
    printf "*  %d\n" (a*b);
    printf "/  %d\n" (a/b);
    printf "+  %d\n" (a+b);
    printf "|  %d\n" (a|b);
    printf "^  %d\n" (a^b);
\end{verbatim}

When run this produces

\begin{verbatim}
    linux$ ./my-script
    7
    10 -3
    *  30
    /  3
    +  13
    |  11
    ^  9
    linux$
\end{verbatim}

(The last two are respectively inclusive-or and exclusive-or, taken from 
C;  if you are not familiar with them, don't worry about them.)

\cutend*

% --------------------------------------------------------------------------------
\subsection{Comparisons and Conditionals}
\cutdef*{subsubsection}

Mythryl comparison operators are nearly the same as in C.

The Mythryl {\tt if} statement is a bit different from that of C.  In 
particular, the {\tt if} statement ends with a mandatory matching {\tt 
fi} keyword.  This means that you do not need curly braces around a 
block of conditional code in Mythryl, which makes the code a bit more 
concise:

\begin{verbatim}
    #!/usr/bin/mythryl

    a = 10;
    b = 3;

    if   (a == b)  printf "a == b is TRUE.\n";
    else           printf "a == b is FALSE.\n";
    fi;

    if   (a <  b)  printf "a <  b is TRUE.\n";
    elif (a >  b)  printf "a >  b is TRUE.\n";
    elif (a == b)  printf "a == b is TRUE.\n";
    else           printf "a and b are INCOMPARABLE.\n";
    fi;

    if   (not (a <= b))   printf "a <= b is FALSE\n";
    fi;

    if (a >= b)  printf "a >= b is TRUE\n";
    else         printf "a >= b is FALSE\n";
    fi;

    if (a != b)  printf "a != b is TRUE\n";
                 printf "a != b is TRUE\n";
    else
                 printf "a != b is FALSE\n";
                 printf "a != b is FALSE\n";
    fi;
\end{verbatim}

When run this produces

\begin{verbatim}
    linux$ ./my-script
    a == b is FALSE.
    a >  b is TRUE.
    a <= b is FALSE.
    a >= b is TRUE.
    a != b is TRUE.
    a != b is TRUE.
    linux$
\end{verbatim}

Mythryl also supports a C-style trinary conditional expression. 
Unlike C, the regular Mythryl {\tt if} statement may also be 
used as a value-yielding expression.  (In fact, as we shall see, 
just about every Mythryl statement may be used as a value-yielding 
expression.)

\begin{verbatim}
    #!/usr/bin/mythryl

    a = 10;
    b = 3;

    printf "a == b is %s\n"   (a == b  ?? "TRUE" :: "FALSE");

    printf "a != b is %s\n"   if (a != b) "TRUE"; else "FALSE"; fi;

\end{verbatim}

When run this produces

\begin{verbatim}
    linux$ ./my-script
    a == b is FALSE
    a != b is TRUE
    linux$
\end{verbatim}

The final major Mythryl conditional statement is the {\tt case} statement. 
In its simplest form, it may be used much like the C {\tt switch} statement:


\begin{verbatim}
    #!/usr/bin/mythryl

    a = 3;

    case a
     1 => print "I\n";
     2 => print "II\n";
     3 => print "III\n";
     4 => print "IV\n";
     5 => print "V\n";
     6 => print "VI\n";
     7 => print "VII\n";
     8 => print "VIII\n";
     9 => print "IX\n";
    10 => print "X\n";
     _ => print "Gee, I dunno!\n";
    esac;
\end{verbatim}

Here the underline serves as a match-anything wildcard.  We will have more to 
say about this later.  For now, just think of it as an {\tt otherwise} clause.

When run the above produces
\begin{verbatim}
    linux$ ./my-script
    III
    linux$
\end{verbatim}

Once again, unlike the C {\tt switch} statement, the Mythryl {\tt case} statement 
may also be used as an expression, for its value:

\begin{verbatim}
    #!/usr/bin/mythryl

    a = 3;

    print    case a
             1 => "I\n";
             2 => "II\n";
             3 => "III\n";
             4 => "IV\n";
             5 => "V\n";
             6 => "VI\n";
             7 => "VII\n";
             8 => "VIII\n";
             9 => "IX\n";
            10 => "X\n";
             _ => "Gee, I dunno!\n";
            esac;
\end{verbatim}

This is prettier because now we only have to type the {\tt print} in once.

When run, the above version produces exactly the same results as the original version:

\begin{verbatim}
    linux$ ./my-script
    III
    linux$
\end{verbatim}


If you wish to have more than one statement per alternative in the Mythryl 
{\tt case} statement you must enclose them in curly braces.  Here is an 
example which combines the previous two examples:  It both prints a 
value within the {\tt case} statement and also returns a value.  (The 
last statement in a curly-brace code block is the value of that block.)

\begin{verbatim}
    #!/usr/bin/mythryl

    a = 3;

    print    case a
             1 => { print "I\n";             "I\n";             };
             2 => { print "II\n";            "II\n";            };
             3 => { print "III\n";           "III\n";           };
             4 => { print "IV\n";            "IV\n";            };
             5 => { print "V\n";             "V\n";             };
             6 => { print "VI\n";            "VI\n";            };
             7 => { print "VII\n";           "VII\n";           };
             8 => { print "VIII\n";          "VIII\n";          };
             9 => { print "IX\n";            "IX\n";            };
            10 => { print "X\n";             "X\n";             };
             _ => { print "Gee, I dunno!\n"; "Gee, I dunno!\n"; };
            esac;
\end{verbatim}

When run, this prints the value twice:

\begin{verbatim}
    linux$ ./my-script
    III
    III
    linux$
\end{verbatim}

We shall have more to say about {\tt case} statements later, but that is 
enough to get you started.


\cutend*


% --------------------------------------------------------------------------------
\subsection{Mythryl for Loops}
\cutdef*{subsubsection}

Explicit looping is not used nearly as frequently in Mythryl as it is in C, 
partly because arrays are not nearly as important a datastructure in Mythryl 
as they are in C, but in Mythryl you {\bf can} write

\begin{verbatim}
    #!/usr/bin/mythryl

    for (i = 0; i < 10; ++i) {
        printf "%d\n" i;
    };
\end{verbatim}

and when you run it, it will print out

\begin{verbatim}
    linux$  ./my-script
    0
    1
    2
    3
    4
    5
    6
    7
    8
    9
    linux$
\end{verbatim}

As we will explain later, that isn't doing at {\it all} what you think it is doing, 
but in the meantime you may find it useful all the same.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Defining functions}
\cutdef*{subsubsection}

Suppose we want to convert more than one number from Arab to Roman numerals. 
Repeating the whole {\tt case} statement from the previous examples each time 
would be absurd;  instead we want to define a function once, which we may 
then invoke by name as many times as we please:

\begin{verbatim}
    #!/usr/bin/mythryl

    fun to_roman( i ) = {

        case i
         1 => "I";
         2 => "II";
         3 => "III";
         4 => "IV";
         5 => "V";
         6 => "VI";
         7 => "VII";
         8 => "VIII";
         9 => "IX";
        10 => "X";
         _ => "Gee, I dunno!";
        esac;
    };

    printf "3 => %s\n" (to_roman 3);
    printf "5 => %s\n" (to_roman 5);
    printf "0 => %s\n" (to_roman 0);
\end{verbatim}

When run the above produces

\begin{verbatim}
    linux$ ./my-script
    3 => III
    5 => V
    0 => Gee, I dunno!
    linux$
\end{verbatim}

The above function declaration looks a lot like a conventional C function 
declaration.  It is not, but it will do no harm for the time being if you 
think of it that way.

Notice the {\tt =} between the parameter list and 
the body.  Later we will see that it is needed to support curried functions. 
For now, just remember not to leave it out!

Also notice the semicolon at the end of the function definition.  C is a 
bit irregular about when terminating semicolons are needed.  Mythryl is 
perfectly regular:  All complete statements end with a semicolon.

As with almost all modern languages (even contemporary Fortran!)  
Mythryl function definitions can be recursive:

\begin{verbatim}
    #!/usr/bin/mythryl

    fun factorial( i ) = {

        if (i == 1)   i;
        else          i * factorial( i - 1 );
        fi;
    };

    printf "factorial(3) => %d\n" (factorial(3));
    printf "factorial(5) => %d\n" (factorial(5));
\end{verbatim}

When run the above produces:

\begin{verbatim}
    linux$ ./my-script
    factorial(3) => 6
    factorial(5) => 120
    linux$
\end{verbatim}

Later we shall see more concise ways of writing the above 
code, but for now the above gets the job done just fine.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Lists and Strings}
\cutdef*{subsubsection}

Mythryl strings are a bit like C strings, but a lot more like Perl or 
Python strings.

Mythryl strings are a first-class type with a 
variety of predefined operations:

\begin{verbatim}
    linux$ my

    eval:  "abc" + "def";

    "abcdef"

    eval:  toupper("abc");

    "ABC"

    eval:  tolower("ABC");

    "abc"

    eval:  ^D
    linux$
\end{verbatim}


Mythryl lists are a teeny bit like those of Perl and a lot more like those of 
Python, Lisp or Ruby.  Lists are one of the conveniences which make programming 
in Mythryl much more pleasant that programming in C for many common sorts of 
tasks.  Lists are one of the Mythryl programmer's favorite datastructures. 
Any time you have an indeterminate number of similar things which you need 
to keep track of, you usually just throw them in a list. 

Mythryl lists are written with square brackets.  You need to put blanks around 
them to keep the Mythryl compiler from confusing them with arrays.

Useful operations on lists include {\tt @} which concatenates two lists to 
form a single list, {\tt strcat} which concatenates a list of strings to 
produce a single string, and {\tt reverse} which reverses a list:

\begin{verbatim}
    #!/usr/bin/mythryl

    a = ["abc", "def", "ghi"];
    b = ["jkl", "mno", "pqr"];

    printf "strcat a     == '%s'\n"  (strcat(a));
    printf "strcat b     == '%s'\n"  (strcat(b));
    printf "strcat a@b   == '%s'\n"  (strcat(a @ b ));
    printf "strcat( reverse(a) ) == '%s'\n"  (strcat(reverse(a)));
    printf "strcat( reverse(a@b) ) == '%s'\n"  (strcat(reverse(a@b)));
\end{verbatim}

When run the above produces:

\begin{verbatim}
    linux$ ./my-script
    strcat a     == 'abcdefghi'
    strcat b     == 'jklmnopqr'
    strcat a@b   == 'abcdefghijklmnopqr'
    strcat( reverse(a) ) == 'ghidefabc'
    strcat( reverse(a@b) ) == 'pqrmnojklghidefabc'
    linux$
\end{verbatim}

The {\tt map} function applies a given function to all elements of a 
list and returns a list of the results.  It is one of the most 
frequently used Mythryl functions:

\begin{verbatim}
    linux$ my

    eval:  map toupper ["abc", "def", "ghi"];

    ["ABC", "DEF", "GHI"]

    eval:  ^D
    linux$
\end{verbatim}

The {\tt apply} function is just like the {\tt map} function except that 
it constructs no return list --- the supplied function is applied only 
for side-effects:

\begin{verbatim}
    #!/usr/bin/mythryl

    apply print ["abc\n", "def\n", "ghi\n"];
\end{verbatim}

When run the above produces:

\begin{verbatim}
    linux$ ./my-script
    abc
    def
    ghi
    linux$
\end{verbatim}

The Mythryl {\tt head} and {\tt tail} functions return the first 
element of a list and the rest of the list.  They correspond to Lisp {\tt car} 
and {\tt cdr}.  The Mythryl infix operator '!' prepends a new element to a 
list, returning a new list.  It corresponds to the Lisp {\tt cons} function:

\begin{verbatim}
    linux$ my

    eval:  a = [ "abc", "def", "ghi" ];

    ["abc", "def", "ghi"]

    eval:  head(a);

    "abc"

    eval:  tail(a);

    ["def", "ghi"]

    eval:  "abc" ! ["def", "ghi"];

    ["abc", "def", "ghi"]

    eval:  ^D
    linux$ 
\end{verbatim}

The Mythryl {\tt explode} and {\tt implode} functions convert 
between strings and lists of characters:

\begin{verbatim}
    linux$ my

    eval:  explode("abcdef");

    ['a', 'b', 'c', 'd', 'e', 'f']

    eval:  implode( ['a', 'b', 'c', 'd', 'e', 'f'] );

    "abcdef"

    eval:  implode( reverse( explode( "abcdef" ) ) );

    "fedcba"

    eval:  ^D

    linux$
\end{verbatim}

The {\tt strsort} function sorts a list of strings, and 
{\tt struniqsort} does the same while dropping duplicates. 
The {\tt shuffle} function re-arranges the list elements 
into a pseudo-random order:


\begin{verbatim}
    linux$ my

    eval:  strsort( ["def", "abc", "ghi", "def", "abc", "ghi"] );

    ["abc", "abc", "def", "def", "ghi", "ghi"]

    eval:  struniqsort( ["def", "abc", "ghi", "def", "abc", "ghi"] );

    ["abc", "def", "ghi"]

    eval:  shuffle( ["abc", "def", "ghi", "jkl", "mno"] );

    ["ghi", "def", "abc", "jkl", "mno"]

    eval:  ^D
    linux$
\end{verbatim}

The {\tt length} function counts the number of elements in 
a list;  the {\tt strlen} function counts the number of 
characters in a string:

\begin{verbatim}
    linux$ my

    eval:  length( ["abc", "def", "ghi"] );

    3

    eval:  strlen( "abcdefghi" );

    9

    eval:  ^D

    linux$
\end{verbatim}


\cutend*

% --------------------------------------------------------------------------------
\subsection{Packages}
\cutdef*{subsubsection}
\label{section:tut:bare-essentials:packages}

Mythryl packages are a bit like {\tt C++} classes or namespaces.  They let 
you construct a hierarchy of namespaces so that not every variable 
need be dumped into the global namespace as in C.  Like {\tt C++}, Mythryl 
uses the {\tt package::function} syntax to access a function within an 
external package:

\begin{verbatim}
    #!/usr/bin/mythryl

    # Define a simple package:
    #
    package my_package {
        fun double(i) = { 2*i; };
    };

    # Invoke the function in the package:
    #
    printf "%d\n" (my_package::double(2));

    # Define a shorter name for the package:
    #
    package p = my_package;

    # Invoke the same function by the shorter package name:
    #
    printf "%d\n" (p::double(3));

    # Dump all identifiers in the package into
    # the current namespace:
    #
    include my_package;

    # Now we can call the same function
    # with no package qualifier at all:
    #
    printf "%d\n" (double(4));
\end{verbatim}

When you run the above, it will print out

\begin{verbatim}
    linux$ ./my-script
    4
    6
    8
    linux$
\end{verbatim}

We shall have a great deal more to say about packages later, 
but the above will do for the moment.

For our current purposes, writing small scripts for didactic and 
utilitarian purposes, we will not often have cause to create packages. 
We will be far more interested in taking advantage of various packages 
from the Mythryl standard library.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Regular Expressions}
\cutdef*{subsubsection}
\label{section:tut:bare-essentials:regex}

Mythryl regular expressions are patterned after those of Perl, the 
de facto standard.  Unlike Perl, Mythryl has no special compiler 
support for regular expression usage.  On the down side, this means 
that Mythryl regular expression syntax is not quite as compact as 
that of Perl.  On the up side, since the Mythryl regular expression support 
is implemented entirely in library code, you may easily write or use 
alternate regular expression libraries if you do not like the stock one. 
In fact, Mythryl ships with several.  (Some might say, several too many.) 

Matching a string against a regular expression may be done using the {\tt \verb|=~|} 
operator:

\begin{verbatim}
    #!/usr/bin/mythryl

    fun has_vowel( string ) = {
         #
         if (string =~ ./(a|e|i|o|u|y)/)  printf "'%s' contains a vowel.\n"          string;
         else                             printf "'%s' does not contain a vowel.\n"  string;
         fi;
    };

    has_vowel("mythryl");
    has_vowel("crwth");
\end{verbatim}

When run, the above prints out

\begin{verbatim}
    linux$ ./my-script
    'mythryl' contains a vowel.
    'crwth' does not contain a vowel.
    linux$
\end{verbatim}

Unlike Perl, Mythryl does not hardwire the meaning of the {\tt \verb|=~|} operator. 
We will cover defining such operators 
\ahrefloc{section:tut:delving-deeper:binary-operators}{later}.

Other than matching a string against a regular expression, 
the most frequently used regular expression operation is 
doing substitutions of matched substrings.

Here is how to replace all substrings matching a given 
regular expression by a given constant replacement string:


\begin{verbatim}
    linux$ my

    eval:  regex::replace_all ./f.t/ "FAT" "the fat father futzed";
    "the FAT FATher FATzed"
\end{verbatim}


\textbf{Important detail:} If you need to include a {\tt /} within 
a regular expression, you cannot do so by backslashing it; 
you must instead double it: 

\begin{verbatim}
    fun has_slash( string ) =   string =~ ./\//;      # Do NOT do this!  It will not work!
    fun has_slash( string ) =   string =~ .////;      # Do this instead.
    fun has_slash( string ) =   string =~ "/";        # Or this -- a regex is just a string, so string constants work fine.
    fun has_slash( string ) =   string =~ .</>;       # Or this -- .<foo> is just like ./foo/ except for the delimiter chars.
    fun has_slash( string ) =   string =~ .|/|;       # Or this -- .|foo| is just like ./foo/ except for the delimiter chars.
    fun has_slash( string ) =   string =~ .#/#;       # Or this -- .#foo# is just like ./foo/ except for the delimiter chars.
\end{verbatim}

The above discussion is far from exhausting the topic of regular 
expressions, but it is enough for the first go-around;  we will 
return to regular expressions \ahrefloc{section:tut:full-monte:regex}{later}.




\cutend*

% --------------------------------------------------------------------------------
\subsection{Mythryl foreach Loops}
\cutdef*{subsubsection}

The C-style {\tt for} loop is not used very heavily in Mythryl, but another 
form of loop construct, the {\tt foreach}, comes in very handy. 
The {\tt foreach} loop is not actually a compiler construct at all, just a library routine.  The 
{\tt foreach} loop iterates over the members of a list:

\begin{verbatim}
    #!/usr/bin/mythryl

    foreach ["abc", "def", "ghi"] .{
        printf "%s\n" #i;
    };
\end{verbatim}

Note the dot before the curly brace and the sharp before the {\tt i} 
loop variable.  This syntax looks a little odd at first blush. 
It will make more sense once we have discussed Mythryl {\it thunk} syntax. 

When run, the above does just what you probably expect:

\begin{verbatim}
    linux$ ./my-script
    abc
    def
    ghi
    linux$
\end{verbatim}

The {\tt foreach} loop is more common than the {\tt for} loop in  
Mythryl code primarily because lists are more common than arrays. 
Mythryl has a profusion of library routines 
which construct or transform lists.  For example the {\tt ..} operator 
constructs a list containing a sequence of integers:

\begin{verbatim}
    linux$ my

    eval:  1 .. 10;

    [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]

    eval:  foreach (1..10) .{ printf "%d\n" #i; };
    1
    2
    3
    4
    5
    6
    7
    8
    9
    10

    ()

    eval:  ^D

    linux$
\end{verbatim}

\cutend*


% --------------------------------------------------------------------------------
\subsection{Working With Files and Directories}
\cutdef*{subsubsection}

The easiest way to read a textfile is to use the {\tt file::as\_lines} 
library function, which returns the contents of the file as a list 
of lines:

\begin{verbatim}
    #!/usr/bin/mythryl

    foreach (file::as_lines "my-script") .{
        print #line;
    };
\end{verbatim}

When run, this script prints itself out, unsurprisingly enough:

\begin{verbatim}
    linux$ ./my-script
    #!/usr/bin/mythryl

    foreach (file::as_lines "my-script") .{
        print #line;
    };

    linux$
\end{verbatim}

Similarly, the easiest way to write a textfile is to use the 
{\tt file::from\_lines} library function:

\begin{verbatim}
    #!/usr/bin/mythryl

    file::from_lines
        "foo.txt"
        [ "abc\n", "def\n", "ghi\n" ];
\end{verbatim}

Running this from the commandline creates a three-line 
file named {\tt foo.txt}:

\begin{verbatim}
    linux$ ./my-script
    linux$ cat foo.txt
    abc
    def
    ghi
    linux$
\end{verbatim}

The easiest way to modify a textfile is just to combine 
the above two operations.  Suppose, for example, that like 
Will Strunk (of {\it The Elements of Style} fame), you 
detest the word ``utilize'' and believe replacing it with 
``use'' is always an improvement.

Use your favorite text editor to create a file named {\tt foo.txt} 
containing the following text:

\begin{verbatim}
    Will Strunk never used "utilize";
    he always utilized "use".
\end{verbatim}

Here is a script which will change "utilize" to "use" 
throughout that file: 

\begin{verbatim}
    #!/usr/bin/mythryl

    fun fix_line( line ) = {
        regex::replace_all ./utilize/ "use" line;
    };

    lines = file::as_lines "foo.txt";

    lines = map fix_line lines;

    file::from_lines "foo.txt" lines;
\end{verbatim}

And here it is in action:

\begin{verbatim}
    linux$ cat foo.txt
    Will Strunk never used "utilize";
    he always utilized "use".

    linux$ ./my-script
    linux$ cat foo.txt
    Will Strunk never used "use";
    he always used "use".

    linux$
\end{verbatim}

If you like one-liners, here is a one-line version of the above script:

\begin{verbatim}
    #!/usr/bin/mythryl
    file::from_lines "foo.txt" (map (regex::replace_all ./utilize/ "use") (file::as_lines "foo.txt"));
\end{verbatim}

The easiest way to get the names of the files in the current 
directory is to use {\tt dir::files}:

\begin{verbatim}
    linux$ my

    eval:  foreach (dir::files ".") .{ printf "%s\n" #filename; };
    my-script
    foo.txt

    eval:  ^D

    linux$
\end{verbatim}

The easiest way to get the names of all the files in the 
current directory or any directory under it is to replace 
{\tt dir::files} by {\\dir\_tree::files} in the above script.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Commandline Arguments}
\cutdef*{subsubsection}

The {\tt argv} function returns the Linux commandline arguments with which the 
script was invoked.  Thus, this script implements a poor man's version of the 
Linux {\tt echo} program:

\begin{verbatim}
    #!/usr/bin/mythryl
    apply (printf " %s") (argv());
    print "\n";
    exit 0;
\end{verbatim}

When run it prints out the arguments it was given:

\begin{verbatim}
    linux$ ./my-script a b c
     a b c
\end{verbatim}

\cutend*


% --------------------------------------------------------------------------------
\subsection{Summary}
\cutdef*{subsubsection}

We have not yet covered any of the Mythryl facilities which provide functionality 
beyond that of Perl or Ruby.

We have, however, covered enough of the language to do useful scripting.

You might want to take some time to play around with the facilities 
covered above and get some experience actually doing useful things 
with Mythryl before proceeding to the more in-depth tutorials.

\cutend*