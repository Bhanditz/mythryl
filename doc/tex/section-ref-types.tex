
% --------------------------------------------------------------------------------
\subsection{Overview}
\cutdef*{subsubsection}
\label{section:ref:types:overview}

The Mythryl type system differs from that of older 
languages like C++ and Java in two important ways: 

\begin{itemize}
\item {\bf Type inference}: The Mythryl compiler uses unification-driven 
      inference to deduce the types of variables and functions wherever 
      possible.  Consequently it is rarely actually necessary for the 
      programmer to explicitly specify types outside of API declarations. 
      This dramatically improves code compactness and readability.

\item {\bf Parametric Type Polymorphism}: Many functions and datatypes 
      do not actually care about the types of some of their arguments.

      Older languages nevertheless require them to be declared with 
      specific types, limiting re-use of such functions and datatypes.

      Mythryl's Hindley-Milner type system not only allows such values 
      to be explicitly declared as don't-cares, but can also almost always 
      automatically compute a most general type for such functions and 
      datastructures, substantially improving re-usability of both 
      functions and datatypes.
\end{itemize}

In C++ and Java it is routine to use type casts to defeat the 
compiler type checker;  this is frequently necessary in order 
to work around the limitations of their type systems.

The enhanced expressiveness of the Mythryl type system means 
it is almost never necessary to defeat the compiler typechecker 
in this way;  in fact, the language does not even define a type 
cast operator for doing so.  This leads to code which is enormously 
more typesafe, robust and maintainable than similar code written 
in legacy languages.  It is routine for C programs to dump core 
when first run after significant maintenance edits;  Mythryl 
programs typically run correctly again the first time they 
compile.


\cutend*

% --------------------------------------------------------------------------------
\subsection{Base Types}
\cutdef*{subsubsection}
\label{section:ref:types:base-types}

Primitive types defined by the system include:

\begin{verbatim}
    Bool
    Char
    Fate
    Control_Fate
    Exception
    Float
    Float64_Rw_Vector
    Int
    Int1
    Int2
    Integer
    List
    Ref
    Rw_Vector
    String
    Unt
    Unt1
    Unt2
    Unt8
    Unt8_Rw_Vector
    Vector
    Void
\end{verbatim}

For our purposes here, types such as {\tt Bool, Char, Int, Float} and {\tt String} 
may be taken as atomic, just unanalysed constituents of other types.

\cutend*

% --------------------------------------------------------------------------------
\subsection{Naming Types}
\cutdef*{subsubsection}
\label{section:ref:types:naming-types}

Types may be named much like values:

\begin{verbatim}
    Foo  = Int;
    Name = String;
    Vec  = Float64_Rw_Vector;
\end{verbatim}

Such statements do not create new types, just new names 
for existing types.  They may be used to improve readability, 
or as an aid to conciseness, assigning short local synonyms 
to types with long names.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Tuple Types}
\cutdef*{subsubsection}
\label{section:ref:types:tuple-types}

Tuple types are written much like tuple expressions 
and patterns, as comma-separated lists of types 
wrapped in parentheses:

\begin{verbatim}
    My_Tuple_Type = (Int, Float, String);
\end{verbatim}

Tuple types, like all types, may be nested arbitrarily:

\begin{verbatim}
    My_Nested_Type = (((Int, Float, String), (String, Float, Int)), Int);
\end{verbatim}

\cutend*


% --------------------------------------------------------------------------------
\subsection{Record Types}
\cutdef*{subsubsection}
\label{section:ref:types:record-types}

Record types are also written much like record expressions 
and patterns, as comma-separated list of types record elements. 
The record elements here are field names together with their 
types.  The field names are part of the type of a record; the 
order of the fields is not.

\begin{verbatim}
    My_Field_Type = { name: String, age: Int };
\end{verbatim}

Record types, like all types, may be nested arbitrarily:

\begin{verbatim}
    My_Complex_Record_Type = { name: String, state: (((Int, Float, String), (String, { id: String, weight: Float }, Int)), Int) };
\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{List Types}
\cutdef*{subsubsection}
\label{section:ref:types:list-types}

The length of a list is not part of its type, but the type 
of its elements is.  All elements of a list must be of the 
same type.

List types are written using the type 
constructor {\tt List} together with an argument giving 
the type of the list elements:

\begin{verbatim}
    Int_List    = List(Int);
    Float_List  = List(Float);
    String_List = List(String);
    Record_List = List(My_Complex_Record_Type);
\end{verbatim}

List types, like all types, may be nested arbitrarily:

\begin{verbatim}
    My_List_Of_Int_Lists_Type = List(List(Int));
\end{verbatim}

\cutend*


% --------------------------------------------------------------------------------
\subsection{Function Types}
\cutdef*{subsubsection}
\label{section:ref:types:function-types}

Mythryl functions take some input type and return some 
result type.  Their type is written as the two types, 
separated by an infix arrow:

\begin{verbatim}
    Int_Fun     = Int -> Int;
    Float_Fun   = Float -> Float;
    String_Fun  = String -> String;
    Complex_Fun = (Int, Float, String) -> (String, Float, Int);
\end{verbatim}

For obvious reasons, such types are often called {\it arrow types}.

A curried function is actually a function which returns another 
function.  A curried function which takes two strings and returns 
another thus has type:

\begin{verbatim}
    Curried_Fun = String -> (String -> String);
\end{verbatim}

The arrow type operator is defined to associate to the right, so 
usually such types are simply written as:

\begin{verbatim}
    Curried_Fun = String -> String -> String;
\end{verbatim}

\cutend*


% --------------------------------------------------------------------------------
\subsection{Ref Types}
\cutdef*{subsubsection}
\label{section:ref:types:ref-types}

Almost all Mythryl values are immutable once created;  in 
the jargon of functional programming, they are {\it pure}. 
In more mainstream nomenclature, they are {\it read-only}.

The two exceptions are:
\begin{itemize}
\item References
\item Read-write vectors and matrices.
\end{itemize}

The latter are a concession to the needs of matrix algorithms; 
they are not often used  in vanilla Mythryl coding.

For most practical purposes, the only Mythryl values which 
can be modified are references, which work much like C pointers:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    int_ptr = REF 0;

    printf "int_ptr = %d\n"  *int_ptr;

    int_ptr := 1;

    printf "int_ptr = %d\n"  *int_ptr;

    int_ptr := 2;

    printf "int_ptr = %d\n"  *int_ptr;

    linux$ ./my-script
    int_ptr = 0
    int_ptr = 1
    int_ptr = 2
\end{verbatim}

Here we are seeing true side-effects at work:

\begin{itemize}
\item The {\tt REF 0} expression constructs and returns a reference 
      cell initialized to zero. 
\item The {\tt *int\_ptr} expression returns the current value of that 
      reference cell. 
\item The {\tt int\_ptr := 2} expression stores a new value into that 
      reference cell, overwriting the previous value.  This is a 
      heap side-effect visible to any function or thread possessing 
      a pointer to the reference cell. 
\end{itemize}

The type of a reference cell depends on the type of its contents, 
and is declared using the {\tt Ref} type constructor:

\begin{verbatim}
    Int_Ref        = Ref(Int);
    Float_Ref      = Ref(Float);
    String_Ref     = Ref(String);

    Stringlist_Ref = Ref(List(String));
    Record_Ref     = Ref(My_Complex_Record_Type);
\end{verbatim}

\cutend*



% --------------------------------------------------------------------------------
\subsection{Enum Types}
\cutdef*{subsubsection}
\label{section:ref:types:enum-types}

Enum type declarations define a new type by enumeration:

\begin{verbatim}
    Color = RED | GREEN | BLUE;
\end{verbatim}

Variables of type {\tt Color} may take on only the values 
{\tt RED, GREEN} or {\tt BLUE}.

Every such declaration without exception creates a new 
type, not equal to any existing type, even if it is 
lexically identical to another such declaration.

The value keywords defined by such a declaration may have 
associated values:

\begin{verbatim}
    Binary_Tree
        = LEAF
        | NODE { key:   Float,

                 left_kid:  Binary_Tree,
                 right_kid: Binary_Tree
               }
        ;
\end{verbatim}

Here internal nodes on the tree carry a record  
containing a float key and two child pointers;  leaf nodes carry no value.

An instance of such a tree may be created by an expression such as

\begin{verbatim}
    my_tree
        =
        NODE
          { key       => 2.0,
            left_kid  => NODE { key => 1.0, left_kid => LEAF, right_kid => LEAF },
            right_kid => NODE { key => 3.0, left_kid => LEAF, right_kid => LEAF }
          };
\end{verbatim}

Here {\tt NODE} functions to construct records on the heap; 
consequently it is termed a {\it constructor}.  By extension 
all such tags declared by an enum are called constructors, 
even those like {\tt RED, GREEN, BLUE} and {\tt LEAF} which 
have no associated types and thus construct nothing much of 
interest on the heap.

Recursive datatypes such as {\tt Binary\_Tree} are usually 
processed via recursive functions, using {\tt case} expressions 
to handle the various alternatives.  Here is a little 
recursive function to print out such binary trees: 

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    Binary_Tree
        = LEAF
        | NODE { key:   Float,

                 left_kid:  Binary_Tree,
                 right_kid: Binary_Tree
               }
        ;

    my_tree
        =
        NODE
          { key       => 2.0,
            left_kid  => NODE { key => 1.0, left_kid => LEAF, right_kid => LEAF },
            right_kid => NODE { key => 3.0, left_kid => LEAF, right_kid => LEAF }
          };


    fun print_tree LEAF
            =>
            ();

        print_tree (NODE { key, left_kid, right_kid })
            =>
            {   print "(";
                print_tree left_kid;
                printf "%2.1f" key;
                print_tree right_kid;
                print ")";
            };
    end;

    print_tree  my_tree;
    print "\n";

    linux$ ./my-script
    ((1.0)2.0(3.0))
\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{Type Variables}
\cutdef*{subsubsection}
\label{section:ref:types:type-variables}
Sometimes a function does not really care about its types:

\begin{verbatim}
    fun swap (x,y) = (y,x);
\end{verbatim}

The function swap simply accepts a tuple of two values  
and reverses them;  it really doesn't care whether they 
the two values are ints, floats, strings, or gigabyte 
sized databases.

Declaring such a function as something like

\begin{verbatim}
    (Int, Int) -> (Int, Int)
\end{verbatim}

would waste most of its potential utility.

Mythryl uses type variables such as {\tt X, Y, Z} to 
represent such don't-care values, and will automatically 
infer for such a function a most general type of:

\begin{verbatim}
    (X, Y) -> (Y, X)
\end{verbatim}

Type variables are often used explicitly when defining 
datastructures, to mark don't-care slots.

For example, the typical sorted binary tree implementation cares about 
the types of its keys, because it must know how to compare them in 
order to implement {\it insert} correctly and in order to implement 
{\it find} and {\it delete} efficiently, but it cares not at all about 
the types of the values in the tree, which it merely stores and 
returns unexamined.

Thus, a typical binary tree datatype declaration looks like:

\begin{verbatim}
    Binary_Tree(X)
        = LEAF
        | NODE { key:   Float,
                 value: X,
                 left_kid:  Binary_Tree,
                 right_kid: Binary_Tree
               }
        ;
\end{verbatim}

A user of such a tree will often declare 
explicitly the type of value in use:

\begin{verbatim}
    Int_Valued_Tree = Binary_Tree(Int);
\end{verbatim}

Here {\tt Binary\_Tree} is serves as a compile-time function which  
constructs new types from old;  consequently it is termed a 
{\it type constructor}.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Type Constraint Expressions}
\cutdef*{subsubsection}
\label{section:ref:types:type-constraint-expressions}

The type constraint expression is used to declare (or restrict) the type of some expression. 
It takes the form

\begin{quotation}
~~~~{\it expression}: {\it type}
\end{quotation}

Typically such an expression gets wrapped in parentheses to make sure 
the lexical scope is as intended, but this is not required.

One typical use is to declare the argument types for a function.

For example the function

\begin{verbatim}
    fun add (x, y) = x + y;
\end{verbatim}

will default to doing integer addition, because there is no 
information available at compiletime from which to infer the 
types of the arguments, and integer is the default in such 
cases.

This can be overridden by writing

\begin{verbatim}
    fun add (x: Float, y: Float) = x + y;
\end{verbatim}

to force floating point addition, or

\begin{verbatim}
    fun add (x: String, y: String) = x + y;
\end{verbatim}

to force string concatenation.

Such declarations can also be just good documentation in cases 
where it may be unclear what type is involved or intended.

A type constraint expression is legal anywhere an expression is 
legal.  For example we might instead have written

\begin{verbatim}
    fun add (x, y) = (x: Float) + (y: Float);
\end{verbatim}

or

\begin{verbatim}
    fun add (x, y) = (x: String) + (y: String);
\end{verbatim}

One situation in which an explicit type declaration is frequently 
necessary is when setting a variable to an empty list:

\begin{verbatim}
    empty = [];
\end{verbatim}

Here the compiler has no way of knowing whether you have in mind 
a list of ints, floats, strings, or Library of Congresses.  It will 
probably guess wrong, resulting in odd error messages when you 
later use the variable.  Consequently, you will usualy instead write 
something like

\begin{verbatim}
    empty  =  []: List(String);
\end{verbatim}


\cutend*


% --------------------------------------------------------------------------------
\subsection{The Value Restriction}
\cutdef*{subsubsection}
\label{section:ref:types:the-value-restriction}

In general the Mythryl compiler attempts to deduce the most  
general type for each function.  Thus, for example, the function

\begin{verbatim}
    fun swap (x,y) = (y,x);
\end{verbatim}

could logically be assigned any one of a literally infinite 
number of possible types such as 

\begin{verbatim}
    (Int, Int) -> (Int, Int)
    (Float, Int) -> (Int, Float)
    ((String, Int), Int) -> (Int, (String, Int))
    (X, X) -> (X, X)
    (X, Y) -> (Y, X)
\end{verbatim}

Of these, the last is by far the most general;  it allows the 
function to be used in the most possible contexts subject to 
correctness constraints.  Thus, it is the most desirable from 
a code re-use point of view, in general.  (In a particular case, 
of course, the programmer may intend that it be used only on 
more restricted types, and may explicitly declare that.)

There are some cases in which a most general type cannot be 
reliably induced by the compiler.  For example, the problem 
of inferring a most general type for a set of mutually recursive 
functions is in general undecidable.  (That is a precise mathematical 
term which in practice means "impossible".)

There are other cases in which it would be unsound to generalize 
the type of an expression.  (By "generalize" we mean essentially 
"introduce type variables into".)

The rule univerally adopted in the functional programming world, 
and used by the Mythryl compiler, is called {\it the value restriction}, 
and says that type generalization is done only on expressions which 
involve no runtime side-effects --- expressions which are {\it values}.

In this sense, a function is a value --- it has no effect when defined, 
only when called.  A number, or a string is also a value.

Since type generalization is rarely relevant to expressions like numbers 
or strings, in practice the value restriction may be taken as saying that 
only functions are type generalized --- and only functions which are 
not members of mutually recursive sets of functions.

\cutend*



