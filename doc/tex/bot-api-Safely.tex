
A typical application of {\tt Safely} is to catch {\sc I/O} exceptions encountered 
during file {\sc I/O}, close any open file descriptors, and continue execution 
appropriately:

\begin{verbatim}
    # Return an alphabetically sorted list of directory entries,
    # e.g. [ "bar", "foo", "zot" ], skipping those starting with a dot:
    #
    fun file_names (directory_name: String)
        =
        file_list
        where

            # Collect everything in directory
            # as a list of strings:
            #
            file_list
                =
                safely::do
                    {
                      open_it  =>  .{ f::open_directory_stream  directory_name; },
                      close_it =>     f::close_directory_stream,
                      cleanup  =>     fn _ =  ()
                    }
                   .{   loop []
                        where
                            fun loop results
                                =
                                case (f::read_directory_entry  #directory_stream)
                                  
                                     NULL
                                         =>
                                         list_mergesort::sort  string::(>)  results;

                                     THE filename
                                         =>
                                         # Ignore everything but vanilla files:
                                         #
                                         if   (is_file filename)   loop (filename ! results);
                                         else                      loop results;
                                         fi;
                                esac;
                        end;
                    };

        end;
\end{verbatim}

This example processes a directory.  The {\tt open\_it} thunk is called before first execution 
of the main loop, the {\tt close\_it} thunk will be called at the end, and the {\tt cleanup} 
thunk argument provides an opportunity to do special recovery in case of an exception.

The main body thunk is called with the result of the {\tt open\_it} call, which in this case 
is {\tt #directory\_stream}.

This example is taken from \ahrefloc{src/lib/src/dir.pkg}{src/lib/src/dir.pkg}, which 
contains a number of additional examples.


