
% --------------------------------------------------------------------------------
\subsection{Whitespace sensitivity}
\cutdef*{subsubsection}
\label{section:ref:expressions:whitespace-sensitivity}

Mythryl is more sensitive to the presence or absence of 
whitespace than most contemporary languages.  In particular, 
Mythryl uses the presence or absence of whitespace around 
an operator to distinguish between prefix, infix, postfix 
and circumfix applications:

\begin{verbatim}
    f - g                # '-' is binary -- subtraction.
    f  -g                # '-' unary prefix -- negation:  f(-g).

    f | g | h            # '|' is infix: "f or g or h".
    f  |g|  h            # '|' is circumfix -- absolute value: "f(abs(g)(h)".

    f * g                # '-' is binary -- multiplication.
    f  *g                # '-' is unary -- dereference:  f(*g).

    f g ! h              # '!' is binary -- list construction: (f(g)) ! h
    f g!  h              # '!' is unary postfix -- factorial:  f(g!)(h).
\end{verbatim}

The amount or kind of whitespace does not matter;  only whether it is present or not.

Mythryl treats prefix, postfix and infix forms of a given operator 
as completely separate identifiers.  Thus, you may bind the infix 
form of {\tt *} to a new function without affecting its prefix 
interpretation.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Prefix, postfix, and circumfix operators}
\cutdef*{subsubsection}
\label{section:ref:expressions:prefix-postfix-and-circumfix-operators}

Within expressions Mythryl prefix, postfix and circumfix operators 
bind more tightly than any other syntactic construct in an expression.

The two major predefined prefix operators are unary {\tt -} and {\tt *}, 
which are by default respectiv bound to unary negation and unary 
dereference:

\begin{verbatim}
    linux$ my

    eval:  x = 4;

    4

    eval:  -x;

    -4

    eval:  x = REF 4;

    REF 4

    eval:  *x;

    4
\end{verbatim}

The only Mythryl postfix operator with a default binding is {\tt !}, 
bound to factorial:

\begin{verbatim}
    linux$ my

    eval:  7!

    5040
\end{verbatim}

There are currently no Mythryl circumfix operators with default bindings.

\cutend*

% --------------------------------------------------------------------------------
\subsection{Function application}
\cutdef*{subsubsection}
\label{section:ref:expressions:function-application}

Within Mythryl expressions, function application binds more tightly 
than anything but prefix, postfix and circumfix operators.

In particular, it binds more closely than infix operators.  For 
example {\tt sin 0.0+1.0} is {\tt (sin 0.0)+1.0} not {\tt sin (0.0+1.0)}:

\begin{verbatim}
    linux$ my

    eval:  sin 0.0+1.0

    1.0

    eval:  (sin 0.0)+1.0

    1.0

    eval:  sin (0.0+1.0)

    0.841470984808

\end{verbatim}

This can be a trap for the unwary newcomer!

Remember: {\it Function application binds more tightly than (almost) anything else!}

\cutend*

% --------------------------------------------------------------------------------
\subsection{Infix operators}
\cutdef*{subsubsection}
\label{section:ref:expressions:infix-operators}

Mythryl provides the usual arithmetic set of binary arithmetic operators. 
Unlike in C, however, these are not compiler-ordained operators but rather 
just default bindings established by the standard library, which may be 
easily redefined by the application programmer if desired.  Default 
bindings include:

\begin{verbatim}
    a+b           # Integer and floating point addition.
    a-b           # Integer and floating point subtraction.
    a*b           # Integer and floating point multiplication.
    a/b           # Integer and floating point division.
    a%b           # Integer modulus.
    a|b           # Integer bitwise-or.
    a&b           # Integer bitwise-and.
    a^b           # Integer bitwise-xor.
    a<<b          # Integer left-shift.
    a>>b          # Integer right-shift.
    a==b          # Equality comparison on equality types.
    a!=b          # Does-not-equal comparison on equality types.
    a<=b          # Less-than or equal.
    a<b           # Less-than.
    a>b           # Greater-than.
    a>=b          # Greater-than o equal.
\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{Tuple Expressions}
\cutdef*{subsubsection}
\label{section:ref:expressions:tuple-expressions}

A tuple is a sequence of values identified and accessed by number 
within the sequence.  Different values within a tuple may be of 
different types.  Tuples are the simplest and cheapest of Mythryl 
datastructures.  It is normal and encouraged for a Mythryl program 
to create and discard millions of tuples during a run;  the Mythryl 
compiler and runtime are optimised to support this.  At the 
implementation level, a tuple is just a sequence of values packed 
consecutively into a chunk of {\sc RAM}.

A tuple is constructed by listing a comma-separated sequence of 
values in parentheses, and accessed using the operators {\tt \#1, \#2, \#3 ... } 
to access the first, second and third slots (and so on):

\begin{verbatim}
    linux$ my

    eval:  t = (1, 2.0, "three");               # Construct a tuple.

    (1, 2.0, "three")

    eval:  #1 t;                                # Access first field in tuple.

    1

    eval:  #2 t;                                # Access second field in tuple.

    2.0

    eval:  #3 t;                                # Access third field in tuple.

    "three"
\end{verbatim}

In practice, tuple elements are usually accessed via pattern-matching:

\begin{verbatim}
    linux$ my

    eval:  t = (1, 2.0, "three");

    eval:  my (int, float, string) = t;         # Assign individual names to the tuple fields.

    eval:  int;

    1

    eval:  float;

    2.0

    eval:  string;

    "three"

\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{Record Expressions}
\cutdef*{subsubsection}
\label{section:ref:expressions:record-expressions}

Mythryl records are like tuples except that fields are 
accessed by name rather than by number.  Records are 
in fact just syntactic sugar for tuples --- the compiler 
converts records into tuples early in processing after 
which they are compiled identically.  Record labels 
occupy a separate namespace.  Syntactically, records 
are created using curly braces rather than parentheses:

\begin{verbatim}
    linux$ my

    eval:  r = { foo => 1, bar => 2.0, zot => "three" };        # Construct a record.

    eval:  r.foo;                                               # Access field 'foo'

    1

    eval:  r.bar;                                               # Access field 'bar'

    2.0

    eval:  r.zot;                                               # Access field 'zot'

    "three"

    eval:  .foo r;                                              # Access field 'foo', alternate syntax.

    1

    eval:  .bar r;                                              # Access field 'bar', alternate syntax.

    2.0

    eval:  .zot r;                                              # Access field 'zot', alternate syntax.

    "three"
\end{verbatim}

As with tuples, record fields are in practice usually accessed 
via pattern-matching:

\begin{verbatim}
    linux$ my

    eval:  r = { foo => 1, bar => 2.0, zot => "three" };        # Construct a record.

    eval:  my { foo => f, bar => b, zot => z } = r;             # Unpack it into f,b,z via pattern-matching.

    eval:  f;

    1

    eval:  b;

    2.0

    eval:  z;

    "three"
\end{verbatim}

Frequently a record is unpacked into variables with the same 
names as the record fields:

\begin{verbatim}
    linux$ my

    eval:  r = { foo => 1, bar => 2.0, zot => "three" };        # Construct a record.

    eval:  my { foo => foo, bar => bar, zot => zot } = r;       # Unpack it into foo, bar, zot via pattern-matching.

    eval:  foo;

    1

    eval:  bar;

    2.0

    eval:  zot;

    "three"

\end{verbatim}

Mythryl allows this case to be specially abbreviated: 

\begin{verbatim}
    linux$ my

    eval:  r = { foo => 1, bar => 2.0, zot => "three" };        # Construct a record.

    eval:  my { foo, bar, zot } = r;                            # Unpack it into foo, bar, zot via pattern-matching.

    eval:  foo;

    1

    eval:  bar;

    2.0

    eval:  zot;

    "three"

\end{verbatim}

A similar abbreviation is supported when creating a record:

\begin{verbatim}
    linux$ my

    eval:  foo = 1;                             # Name an integer value.

    1

    eval:  bar = 2.0;                           # Name a float value.

    2.0

    eval:  zot = "three";                       # Name a string value.

    "three"

    eval:  r = { foo, bar, zot };               # Abbreviated record creation syntax.

    eval:  r.foo;                               # Extract field 'foo' from record.

    1

    eval:  r.bar;                               # Extract field 'bar' from record.

    2.0

    eval:  r.zot;                               # Extract field 'zot' from record.

    "three"
\end{verbatim}

Mythryl records are exactly as cheap as Mythryl tuples, 
and as with tuples, it is common and encouraged for 
Mythryl programs to create and discard millions of 
records during a run.

\cutend*

% --------------------------------------------------------------------------------
\subsection{List Expressions}
\cutdef*{subsubsection}
\label{section:ref:expressions:list-expressions}

Like Lisp lists, Mythryl lists are implemented as a chain 
of paired value cells.  Consequently, accessing the n-th 
cell in a list takes O(N) time.

Unlike Lisp lists, Mythryl lists are strongly typed; all the elements 
of a Mythryl list must be of the same type.

Mythryl lists have properties complementary to those of 
Mythryl tuples and records:

\begin{itemize}
\item Tuples are fixed length; Lists may be any length.
\item Tuples are fixed at creation;  Lists may be incrementally grown and shrunk.
\item Tuples elements may be different types; List elements must all be the same type.
\end{itemize}

A complete Mythryl list may be constructed using square brackets.  The two 
primitive list access functions are {\tt head} which returns the first 
element in the list and {\tt tail} which returns the rest of the list:

\begin{verbatim}
    linux$ my

    eval:  x = [ "one", "two", "three" ];       # Construct a three-element list.

    ["one", "two", "three"]

    eval:  head x;                              # Access the first element.

    "one"

    eval:  x = tail x;                          # Get the rest of the list.

    ["two", "three"]

    eval:  head x;                              # Access first element of the rest of the list.

    "two"

    eval:  x = tail x;                          # Get the rest of second list.

    ["three"]

    eval:  head x;                              # Access first element of third list.

    "three"
\end{verbatim}

More commonly Mythryl lists are built up and processed incrementally 
using the {\tt !} constructor, which adds one element to the front 
of a list:

\begin{verbatim}
    linux$ my

    eval:  x = [];                              # Construct an empty list.

    []

    eval:  x = "three" ! x;                     # Prepend the string "three".

    ["three"]

    eval:  x = "two" ! x;                       # Prepend the string "two".

    ["two", "three"]

    eval:  x = "one" ! x;                       # Prepend the string "one".

    ["one", "two", "three"]

    eval:  my (foo ! x) = x;                    # Decompose string into head and tail parts.

    eval:  foo;                                 # Show head part.

    "one"

    eval:  x;                                   # Show tail part.

    ["two", "three"]

    eval:  my (foo ! x) = x;                    # Again decompose into head and tail parts.

    eval:  foo;                                 # Show new head part.

    "two"

    eval:  x;                                   # Show new tail part.

    ["three"]
\end{verbatim}

Prepending a value to an existing list is a constant-time operation (O(1));  a 
single new cell is created which holds the new value and points to the existing 
list.  Consequently lists can and frequently do share parts:

\begin{verbatim}
    linux$ my

    eval:  x = [ "one", "two", "three" ];

    ["one", "two", "three"]

    eval:  y = "zero" ! x;

    ["zero", "one", "two", "three"]

    eval:  z = "Zero" ! x;

    ["Zero", "one", "two", "three"]
\end{verbatim}

Here list {\tt x} is three cells long and lists {\tt y} and {\tt z} are 
each four cells long, but only a total of five cells of storage are 
used between the three of them.

This sharing can make lists quite economical in aggregate even though 
an individual list uses twice as much memory per elementary value 
stored as a tuple or record.

Lists are the standard Mythryl datastructure used to store and process 
a sequence of same-type values;  you should use them whenever you do not 
have a special reason to do otherwise.

(The most frequent reason not to use a list is when you need constant-time 
--- O(1) --- random access to sequence elements; in that case you will usually use 
a vector.  Occasionally you may use a vector just because it consumes 
half as much memory per elementary value stored as does a list.)

Because lists are used pervasively throughout most Mythryl programs, 
the Mythryl standard library provides many convenience functions for 
processing them.  Two of the most frequently used are those to compute 
the length of a list and to reverse a list:

\begin{verbatim}
    linux$ my

    eval:  x = [ "one", "two", "three" ];

    ["one", "two", "three"]

    eval:  list::length x;

    3

    eval:  reverse x;

    ["three", "two", "one"]

    eval:  
\end{verbatim}

Two more are the function {\tt apply}, which calls a given 
function once on each element of a list, and {\tt map} which 
is similar but constructs a new list containing the results 
of those calls:

\begin{verbatim}
    linux$ my

    eval:  x = [ "one", "two", "three" ];

    ["one", "two", "three"]

    eval:  apply print x;
    onetwothree

    eval:  map string::to_upper x;

    ["ONE", "TWO", "THREE"]
\end{verbatim}

Mythryl programmers habitually avoid the need for many 
explicit loops by using these two functions to iterate 
over lists, making their code shorter and simpler.

The infix operator {\tt @} is used to concatenate two lists. 
This involves making a copy of the the first list, and consequently 
takes time and space proportional to the length of the first list: 

\begin{verbatim}
    linux$ my

    eval:  [ "one", "two", "three" ] @ [ "four", "five", "six" ];

    ["one", "two", "three", "four", "five", "six"]
\end{verbatim}

The {\tt fold\_left} and {\tt fold\_right} operators are used to add, 
multiply, concatenate or otherwise pairwise-combine the contents of 
a list in order to produce a single result:

\begin{verbatim}
    linux$ my

    eval:  x = [ "one", "two", "three" ];

    ["one", "two", "three"]

    eval:  fold_forward string::(+) "" x;

    "threetwoone"

    eval:  fold_backward string::(+) "" x;

    "onetwothree"

\end{verbatim}

Here the empty strings are the initial value to be combined 
pairwise with the string elements.  The difference between 
the two functions is the order in which the list elements 
are processed.

The same functions may 
be used with integer, floating point or any other kind of 
value:

\begin{verbatim}
    linux$ my

    eval:  x = [ 1, 2, 3, 4 ];

    [1, 2, 3, 4]

    eval:  fold_forward int::(+) 0 x;

    10

    eval:  fold_forward int::(*) 1 x;

    24

    eval:  x = [ 1.0, 2.0, 3.0, 4.0 ];

    [1.0, 2.0, 3.0, 4.0]

    eval:  fold_forward float::(+) 0.0 x;

    10.0

    eval:  fold_forward float::(*) 1.0 x;

    24.0
\end{verbatim}

Note that the initial value needs to be zero when summing a list 
and one when computing the product of a list.

As with {\tt apply} and {\tt map}, {\tt fold\_left} and {\tt fold\_right} 
can save you the effort of writing many explicit loops, making your 
code shorter and simpler.

\cutend*
