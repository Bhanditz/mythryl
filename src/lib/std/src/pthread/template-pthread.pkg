## template-pthread.pkg
#
# Skeleton code for a persistent server pthread.
# The intention is to simplify construction of
# new server pthreads via clone-and-mutate.

# Compiled by:
#     src/lib/std/src/standard-core.sublib


stipulate
    package pth =  pthread;							# pthread			is from   src/lib/std/src/pthread.pkg
herein

    package template_pthread
    :       Template_Pthread							# Template_Pthread		is from   src/lib/std/src/pthread/template-pthread.api
    { 
	mutex   =  pth::make_mutex   (); 
	condvar =  pth::make_condvar ();  

	# One record for each request
	# supported by the server:
	#
	Do_This =  { input: Int,	reply: Int    -> Void };		# Edit to suit. The 'reply' functions will typically send
	Do_Echo =  { input: String,	reply: String -> Void };		# a do_something() request back to the client pthread.

	Request =  DO_THIS  Do_This						# Union of above record types, so that we can keep them all in one queue.
		|  DO_ECHO  Do_Echo
		; 

	request_queue =  REF ([]: List(Request)); 				# Queue of pending requests from client pthreads.

	fun request_queue_is_empty ()						# We cannot write just    fun request_queue_is_empty () =  (*request_queue == []);
	    =									# because Request is not an equality type. (The 'reply' fields are functions
	    case *request_queue    [] => TRUE;					# and Mythryl does not support comparison of functions for equality.)
	                           _  => FALSE;
	    esac;



	fun do_this' (r: Do_This)						# Internal fn -- will execute in context of server pthread.
	    =
	    {   # Edit to do actual work before replying.
		# This is the meat of the server:
		#
		r.reply r.input;
	    };

	fun do_echo' (r: Do_Echo)						# Internal fn -- will execute in context of server pthread.
	    =
	    r.reply r.input;



	###############################################
	# The rest of the file is mostly boilerplate:
	###############################################

	fun do_this  (request: Do_This)						# External fn -- will execute in context of client pthread.
	    = 
	    { 
		pth::acquire_mutex mutex;  
		    # 
		    request_queue :=  (DO_THIS request)  !  *request_queue; 
		    # 
		    pth::broadcast_condvar condvar;  
		    # 
		pth::release_mutex mutex;  
	    };           

	fun do_echo  (request: Do_Echo)						# External fn -- will execute in context of client pthread.
	    = 
	    { 
		pth::acquire_mutex mutex;  
		    # 
		    request_queue :=  (DO_ECHO request)  !  *request_queue; 
		    # 
		    pth::broadcast_condvar condvar;  
		    # 
		pth::release_mutex mutex;  
	    };           

	fun get_new_requests  () 
	    = 
	    { 
		pth::acquire_mutex mutex;  
		    # 
		    for (request_queue_is_empty()) {
			#
			pth::wait_on_condvar (condvar, mutex);
		    };

		    new_requests  = reverse  *request_queue; 			# 'reverse' to restore original request ordering.
		    # 
		    request_queue := []; 
		    # 
		pth::release_mutex  mutex;  
		# 
		new_requests; 
	    };           

	fun server_loop () 
	    = 
	    {   service_requests (get_new_requests()); 
		#
		server_loop (); 
	    } 
	   where 
		fun service_requests  [] 
			=> 
			(); 

		    service_requests  (request ! rest) 
			=> 
			{   service_request request; 
			    #
			    service_requests rest; 
			} 
			where 
			    fun service_request (DO_THIS r) =>  do_this' r; 
				service_request (DO_ECHO r) =>  do_echo' r;
			    end; 
			end;
		end; 
	    end; 

#                                                                my _ = 	# Commented out to conserve resources;
#        pth::spawn_pthread  server_loop;					# Uncomment these two lines for actual use.
    };

end;

## Code by Jeff Prothero: Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
