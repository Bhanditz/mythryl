## unt8.pkg

# Compiled by:
#     src/lib/std/src/standard-core.sublib

###                "Short words are best
###                 and the old words when
###                 short are best of all."
###
###                   -- Winston Churchill



package unt8_guts: (weak)  Unt {		# Unt	is from   src/lib/std/src/unt.api
    #						# inline_t	is from   src/lib/core/init/built-in.pkg
    package w8  =  inline_t::u8;		# "u8" == "8-bit unsigned int".
    package w31 =  inline_t::tu;		# "tu" == "tagged unsigned int": 31-bits on 32-bit architectures, 63-bit on 64-bit architectures.
    package lw  =  unt1_guts;			# unt1_guts	is from   src/lib/std/src/unt1-guts.pkg

    Unt = unt8::Unt;				#  31 bits 

    unt_size = 8;
    unt_size_w = 0u8;
    unt_shift = inline_t::tu::(-) (0u31, unt_size_w);		# 64-bit issue -- this will be 63 on 64-bit architectures.
    fun adapt op args = w8::bitwise_and (op args, 0uxFF);

    my to_int:    Unt -> Int = w8::to_int;
    my to_int_x:   Unt -> Int = w8::to_int_x;
    my from_int:  Int -> Unt = w8::from_int;

    my to_large_unt:  Unt -> large_unt::Unt = w8::to_large_unt;
    to_large_unt_x = w8::to_large_unt_x;
    from_large_unt = w8::from_large_unt;

    my to_large_int:   Unt -> large_int::Int = lw::to_large_int o to_large_unt;
    my to_large_int_x:  Unt -> large_int::Int = w8::to_large_int_x;
    my from_large_int: large_int::Int -> Unt = w8::from_large_int;


    # These should be inline functions             XXX BUGGO FIXME

    fun (<<) (w:  Unt, k)
        =
        if (inline_t::default_unt::(<=) (unt_size_w, k))
	    #
	    0u0;
	else
	    adapt w8::lshift (w, k);
	fi;

    fun (>>) (w:  Unt, k)
        =
        if (inline_t::default_unt::(<=) (unt_size_w, k))
	    #
	    0u0;
	else
	    w8::rshiftl (w, k);
	fi;

    fun (>>>) (w:  Unt, k)
        =
        if (inline_t::default_unt::(<=) (unt_size_w, k))
	    #
	    adapt w8::rshift (w8::lshift (w, unt_shift), 0u31);
	else
	    adapt w8::rshift (w8::lshift (w, unt_shift), inline_t::default_unt::(+) (unt_shift, k));
        fi;

    my bitwise_or:   (Unt, Unt) -> Unt = w8::bitwise_or;
    my bitwise_xor:  (Unt, Unt) -> Unt = w8::bitwise_xor;
    my bitwise_and:  (Unt, Unt) -> Unt = w8::bitwise_and;
    my bitwise_not:  Unt -> Unt = adapt w8::bitwise_not;

    my (*) : (Unt, Unt) -> Unt = (*);
    my (+) : (Unt, Unt) -> Unt = (+);
    my (-) : (Unt, Unt) -> Unt = (-);
    my (/) : (Unt, Unt) -> Unt = (/);
    my (%) : (Unt, Unt) -> Unt = (%);

    fun compare (w1, w2)
        =
	if   (w8::(<) (w1, w2))  LESS;
	elif (w8::(>) (w1, w2))  GREATER;
	else                     EQUAL;
        fi;

    my (>)  : (Unt, Unt) -> Bool = (>);
    my (>=) : (Unt, Unt) -> Bool = (>=);
    my (<)  : (Unt, Unt) -> Bool = (<);
    my (<=) : (Unt, Unt) -> Bool = (<=);

    my (-_) : Unt -> Unt = (-_);
    my min:  (Unt, Unt) -> Unt = w8::min;
    my max:  (Unt, Unt) -> Unt = w8::max;

    fun format radix
        =
        (num_format::format_unt radix) o to_large_unt; 

    to_string = format number_string::HEX;

    fun scan radix
        =
	scan
        where
            scan_large = num_scan::scan_word radix;

	    fun scan getc cs
                =
                case (scan_large getc cs)
		    #
		    THE (w, cs')
                        =>
                        if (inline_t::u1::(>) (w, 0u255))   raise exception OVERFLOW;
		        else				    THE (from_large_unt w, cs');
                        fi;

		    NULL => NULL;
                esac;
	end;

    from_string
        =
        pre_basis::scan_string (scan number_string::HEX);

};						#  package unt8_guts 





##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
