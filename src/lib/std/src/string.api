## string.api

# Compiled by:
#     src/lib/std/src/standard-core.sublib



###     "The string is a stark data package and
###      everywhere it is passed there is duplication.
###      It is a perfect vehicle for hiding information."
###
###                               -- Alan J Perlis


# This api is implemented in:
#     src/lib/std/src/string-guts.pkg

api String {
    #
    eqtype Char;
    eqtype String;

    maximum_vector_length:  Int;



    length_in_bytes:	 String -> Int;						# E.g.:   length "abcdef"                           -->  6
    length_in_chars:	 String -> Int;						# String should be 7-bit ascii or UTF-8. Returns number of bytes in string not matching 10xxxxxx.
    get_byte:		(String, Int) -> Int;					# E.g.:   string::get_byte         ("abcdef", 0)    -->  97
    get_byte_as_char:	(String, Int) -> Char;					# E.g.:   string::get_byte_as_char ("abcdef", 0)    -->  'a'
    get_char_as_int:	(String, Int) -> (Int, Int);				# First result is char starting at given byte offset in string, UTF-8 encoded using 1-6 bytes.  Second result is next byte offset to read (== original offset + char-length-in-bytes.) For UTF-8 encoding background see (e.g.) http://www.cl.cam.ac.uk/~mgk25/ucs/man-utf-8.html
    get_char_bytecount: (String, Int) -> Int;					# Returns number of bytes used to encode UTF-8 char at given byte offset in string.,
#   (_[]):		(String, Int) -> Char;  				# Note:   The (_[])   enables   'vec[index]'  notation;
    extract:		(String, Int, Null_Or( Int )) -> String;		# E.g.:   extract ("abcdef", 2, NULL)               -->   "cdef"
										# E.g.:   extract ("abcdef", 2, THE 1)              -->  "c"			# Int args are (offset, length).
    substring:		(String, Int, Int) -> String;				# E.g.:   substring ("abcdef", 1, 4);	            -->   "bcde"		# Int args are (offset, length).
    +  :		(String, String) -> String;				# E.g.:   "abc" + "def"                             -->   "abcdef"
    cat:		List( String ) -> String;				# E.g.:   cat                   ["an", "example"]   -->   "anexample"
    join:		String -> List( String ) -> String;			# E.g.:   join  " "             ["an", "example"]   -->   "an example"	
    join':		String -> String -> String -> List(String) -> String;	# E.g.:   join'  "("  ", "  ")" ["an", "example"]   -->   "(an, example)"
    from_char:		Char   -> String;					# E.g.:   from_char 'a'                             -->   "a"	
    implode:		List(Char) -> String;					# E.g.:   implode ['a', 'b', 'c']                   -->   "abc"
    explode:		String -> List(Char);					# E.g.:   explode "abc"                             -->   ['a', 'b', 'c']
    chomp:		String -> String;					# E.g.:   chomp "abc\n"                             -->   "abc"    (Drops trailing newline if present, else is a no-op.)
    map:		(Char -> Char) -> String -> String;			# E.g.:   string::map char::to_upper "abc"          -->   "ABC"
    repeat:		(String, Int) -> String;				# E.g.:   repeat ("abc", 2)			    -->   "abcabc"

    translate:		(Char -> String) -> String -> String;
    tokens:		(Char -> Bool) -> String -> List(String);		# E.g.:   string::tokens {. #c == ','; } "a,b,,c";  -->   ["a", "b", "c"]
    fields:		(Char -> Bool) -> String -> List(String);		# E.g.:   string::fields {. #c == ','; } "a,b,,c";  -->   ["a", "b", "", "c"]
    lines:                                String -> List(String);		# E.g.:   string::lines                  "a\nb\n\nc";  -->   ["a\n", "b\n", "\n", "c"]

    is_prefix:		String -> String -> Bool;				# Is String1 is a prefix of String2.
    is_substring:	String -> String -> Bool;
    is_suffix:		String -> String -> Bool;

    compare:		(String, String) -> Order;
    compare_sequences:	((Char, Char) -> Order) -> (String, String) -> Order;

    to_lower:		String -> String;					# "THIS_is_tExt" -> "this_is_text"
    to_upper:		String -> String;					# "THIS_is_tExt" -> "THIS_IS_TEXT"
    to_mixed:		String -> String;					# "THIS_is_tExt" -> "This_Is_Text"

    has_alpha:		String -> Bool;						# fun has_alpha string =   list::exists  char::is_alpha  (explode string);
    has_lower:		String -> Bool;						# fun has_upper string =   list::exists  char::is_upper  (explode string);
    has_upper:		String -> Bool;						# fun has_lower string =   list::exists  char::is_lower  (explode string);

    is_alpha:		String -> Bool;						# fun is_alpha  string =   length string > 0   and   list::all  char::is_alpha  (explode string);
    is_upper:		String -> Bool;						# fun is_upper  string =   length string > 0   and   list::all  char::is_upper  (explode string);
    is_lower:		String -> Bool;						# fun is_lower  string =   length string > 0   and   list::all  char::is_lower  (explode string);
    is_mixed:		String -> Bool;						# fun is_mixed  string =   is_alpha string  and  has_upper string  and  has_lower string;
    is_ascii:		String -> Bool;						# TRUE iff all bytes in string have high bit equal to zero.

    # For is_alpha/is_space/etc predicates on
    # individual chars in a string see:
    #
    #     src/lib/std/src/string-chartype.api

    <  : (String, String) -> Bool;
    <= : (String, String) -> Bool;
    >  : (String, String) -> Bool;
    >= : (String, String) -> Bool;

    from_string:	String -> Null_Or( String );
    to_string:		String ->          String;
    from_cstring:	String -> Null_Or( String );
    to_cstring:		String ->          String;

    byte_offset_of_ith_char: (String, Int) -> Null_Or(Int);			# Scan down utf-8 encoded string looking for byte offset of first byte of i-th char.  Return NULL if string contains less than 'i' chars.



    # The following should perhaps be in a separate edit-support pkg, but
    # for the moment keeping them in the string pkg is easy and convenient:

    utf8_to_ucs2:	String -> String;					# Return a string in which each char is encoded using exactly two bytes, most-significant first.  Intended primarily for use with  w2x::x::POLY_TEXT16  in  src/lib/x-kit/widget/xkit/app/guishim-imp-for-x.pkg

    expand_tabs_and_control_chars						# We need this to convert raw line String into something viewable on screen in   src/lib/x-kit/widget/edit/lineeditor.pkg
      :
      { text:		String,
	col:		Int,							# Screen col for first char of 'text'. (Normally 0 for left-justified string.)
	cursorcol:	Int
      }
      ->
      {	text:		String,
	col:		Int,							# Screen col for first char of any text following 'text'.  Useful when expanding multiple strings within a single line.
	#
	cursor_byteoffset_in_input:	Int,
	cursor_bytecount_in_input:	Int,
	#
	cursor_byteoffset_in_output:	Int,
	cursor_bytecount_in_output:	Int,
	#
	cursor_firstcol_on_screen:	Int,
	cursor_colcount_on_screen:	Int
      };
};


## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2014,
## released per terms of SMLNJ-COPYRIGHT.
