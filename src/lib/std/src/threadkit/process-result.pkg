## process-result.pkg
#
# Support functionality for
#
#     src/lib/src/lib/thread-kit/src/process-deathwatch.pkg

# Compiled by:
#     src/lib/std/standard.lib



stipulate
    package md1 =  oneshot_maildrop;					# oneshot_maildrop		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/oneshot-maildrop.pkg
    package mop =  mailop;						# mailop			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
herein

    # This is used only in process_deathwatch:
    #
    #     src/lib/src/lib/thread-kit/src/process-deathwatch.pkg
    #
    package process_result 
    :       Threadkit_Process_Result					# Threadkit_Process_Result	is from   src/lib/std/src/threadkit/threadkit-process-result.api
    {
	Result_Val(X)
	  #
	  = EXCEPTION  Exception
	  | RESULT     X
	  ;

	Threadkit_Process_Result(X)
	    =
	    md1::Oneshot_Maildrop( Result_Val(X) );

	fun make_threadkit_process_result ()
	    =
	    md1::make_oneshot_maildrop ();


	fun put           (iv, v ) =  md1::set (iv, RESULT    v );
	fun put_exception (iv, ex) =  md1::set (iv, EXCEPTION ex);


	fun wrap (RESULT    v ) =>  v;
	    wrap (EXCEPTION ex) =>  raise exception ex;
	end;


	fun get iv
	    =
	    wrap (md1::get iv);


	fun get_mailop  iv
	    =
	    mop::make_compound_mailop					# mop::make_compound_mailop is the plain name for   mop::(==>)
		#
		(md1::get' iv,  wrap);

    };
end;


## COPYRIGHT (c) 1996 AT&T Research.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
