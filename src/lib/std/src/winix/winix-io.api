## winix-io.api

# Compiled by:
#     src/lib/std/src/standard-core.sublib



# A sub-api of api Winix:
#
#     src/lib/std/src/winix/winix.api

stipulate
    package wty =  winix_types;					# winix_types			is from   src/lib/std/src/posix/winix-types.pkg
herein

    # This api is implemented in:
    #
    #     src/lib/std/src/posix/winix-io.pkg
    #
    api Winix_Io {
	#
	eqtype Iod;						# "Iod" == "I/O descriptor".
								# An Iod is an abstract descriptor for an OS entity that supports I/O
								# (e::g., file, tty device, socket, ...).
								# On posix, in practice it is an Int encoding a host-OS fd.


	Iod_Kind = FILE						# On posix defined by   psx::stat::is_file
		 | DIRECTORY					# On posix defined by   psx::stat::is_directory
		 | SYMLINK					# On posix defined by   psx::stat::is_symlink
		 | CHAR_DEVICE					# On posix defined by   psx::stat::is_char_dev
		 | BLOCK_DEVICE					# On posix defined by   psx::stat::is_block_dev
		 | PIPE						# On posix defined by   psx::stat::is_pipe
		 | SOCKET					# On posix defined by   psx::stat::is_socket
		 | OTHER					# Future-proofing.
		 ;

	hash:  Iod -> Unt;					# Return a hash value for the I/O descriptor. 

	compare:  (Iod, Iod) -> Order;				# Compare two I/O descriptors 

	iod_to_iodkind:  Iod -> wty::Iod_Kind;			# Return the kind of I/O descriptor: FILE|DIR|SYMLINK|TTY|PIPE|SOCKET|DEVICE|OTHER.
								# Existing code uses this only to check for TTY, mostly to select line-buffering vs block buffering.

	Wait_Request
	    =
	    { io_descriptor:    Iod,
	      readable:	    Bool,
	      writable:	    Bool,
	      oobdable:	    Bool				# Out-Of-Band-Data available on socket or PTY.
	    };
	    # Public representation of a polling operation on
	    # an I/O descriptor.

	Wait_Result	= Wait_Request;				# A synonym to clarify declarations.

	exception BAD_WAIT_REQUEST;

	# Block on a set of I/O descriptors until
	# an I/O opportunity arises or until
	# specified timeout expires.
	#
	# See also the 'wait_for_io_opportunity' operation in:   src/lib/std/src/socket/socket.api
	#
	wait_for_io_opportunity
	  :
	  { wait_requests:	List( Wait_Request ),
	    timeout:	Null_Or( time::Time )			# Timeout: NULL means wait forever, (THE time::zero_time) means do not block.
	  }
	  ->
	  List( Wait_Result );

	# Deprecated synonym for above, mainly so that unix folks
	# looking for 'select' in the function index will get led here:
	#
	select
	  :
	  { wait_requests:	List( Wait_Request ),
	    timeout:	Null_Or( time::Time )
	  }
	  ->
	  List( Wait_Result );

    };								# api Winix_Io 
end;



## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
