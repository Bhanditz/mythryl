## posix-id.pkg
#
# API for POSIX 1003.1 process dictionary submodule

# Compiled by:
#     src/lib/std/src/standard-core.sublib



stipulate
    package ci  =  mythryl_callable_c_library_interface;		# mythryl_callable_c_library_interface	is from   src/lib/std/src/unsafe/mythryl-callable-c-library-interface.pkg
#   package f8b =  eight_byte_float_guts;				# eight_byte_float_guts			is from   src/lib/std/src/eight-byte-float-guts.pkg
    package hi  =  host_int;						# host_int				is from   src/lib/std/src/posix-1003.1b/host-int.pkg
    package hug =  host_unt_guts;					# host_unt_guts				is from   src/lib/std/src/bind-sysword-32.pkg
    package i1w =  one_word_int;					# one_word_int				is from   src/lib/std/one-word-int.pkg
    package iwg =  one_word_int_guts;					# one_word_int_guts			is from   src/lib/std/src/one-word-int-guts.pkg
    package mig =  multiword_int_guts;					# multiword_int_guts			is from   src/lib/std/src/multiword-int-guts.pkg
    package pf  =  posix_file;						# posix_file				is from   src/lib/std/src/posix-1003.1b/posix-file.pkg
    package pp  =  posix_process;					# posix_process				is from   src/lib/std/src/posix-1003.1b/posix-process.pkg
    package tg  =  time_guts;						# time_guts				is from   src/lib/std/src/time-guts.pkg
    #
    fun cfun  fun_name
	=
	ci::find_c_function { lib_name => "posix_process_environment", fun_name };
herein

    # This package gets 'include'-ed in
    #
    #     src/lib/std/src/posix-1003.1b/posix-1003-1b.pkg
    #
    package posix_id {
	#
	Process_Id      = pp::Process_Id;

	User_Id         = pf::User_Id;
	Group_Id        = pf::Group_Id;
	File_Descriptor = pf::File_Descriptor;

	Sy_Int = hi::Int;

	fun uid_to_unt i = i;
	fun unt_to_uid i = i;

	fun gid_to_unt i = i;
	fun unt_to_gid i = i;

	my get_process_id        : Void -> Sy_Int = cfun "getpid";					# getpid	def in    src/c/lib/posix-process-environment/getpid.c
	my get_parent_process_id : Void -> Sy_Int = cfun "getppid";					# getppid	def in    src/c/lib/posix-process-environment/getppid.c

	fun get_process_id'        () = pp::PID (get_process_id ());
	fun get_parent_process_id' () = pp::PID (get_parent_process_id ());

	my get_user_id:           Void -> hi::Int = cfun "getuid";				# getuid	def in    src/c/lib/posix-process-environment/getuid.c
	my get_effective_user_id: Void -> hi::Int = cfun "geteuid";				# geteuid	def in    src/c/lib/posix-process-environment/geteuid.c

	my get_group_id           : Void -> hi::Int = cfun "getgid";				# getgid	def in    src/c/lib/posix-process-environment/getgid.c
	my get_effective_group_id : Void -> hi::Int = cfun "getegid";				# getegid	def in    src/c/lib/posix-process-environment/getegid.c

	fun get_user_id'           () =  (hug::from_int (get_user_id ()));
	fun get_effective_user_id' () =  (hug::from_int (get_effective_user_id ()));

	fun get_group_id'  ()          =  (hug::from_int (get_group_id           ()));
	fun get_effective_group_id' () =  (hug::from_int (get_effective_group_id ()));

	my set_user_id  : hi::Int -> Void = cfun "setuid";					# setuid	def in    src/c/lib/posix-process-environment/setuid.c
	my set_group_id : hi::Int -> Void = cfun "setgid";					# setgid	def in    src/c/lib/posix-process-environment/setgid.c

	fun set_user_id'  uid =  set_user_id  (hug::to_int uid);
	fun set_group_id' gid =  set_group_id (hug::to_int gid);

	my  get_group_ids : Void -> List( hi::Int ) =  cfun "getgroups";				# getgroups	def in    src/c/lib/posix-process-environment/getgroups.c
	#
	fun get_group_ids' ()
	    =
	    map
		hug::from_int
	       (get_group_ids ());

	my get_login:         Void -> String =  cfun "getlogin";					# getlogin	def in    src/c/lib/posix-process-environment/getlogin.c
	my get_process_group: Void -> Sy_Int =  cfun "getpgrp";						# getpgrp	def in    src/c/lib/posix-process-environment/getpgrp.c
	my set_session_id:    Void -> Sy_Int =  cfun "setsid";						# setsid	def in    src/c/lib/posix-process-environment/setsid.c

	my set_process_group_id: (Sy_Int, Sy_Int) -> Void =   cfun "setpgid";				# setpgid	def in    src/c/lib/posix-process-environment/setpgid.c
	#
	fun get_process_group' ()
	    =
	    pp::PID (get_process_group ());

	fun set_session_id' ()
	    =
	    pp::PID (set_session_id ());

	fun set_process_group_id'
		{ pid:   Null_Or( Process_Id ),
		  pgid:  Null_Or( Process_Id )
		}
	    =
	    set_process_group_id (convert pid, convert pgid)
	    where
		fun convert (THE (pp::PID pid)) =>  pid;
		    convert NULL                =>  0;
		end;
	    end;

	my uname:  Void ->  List( (String, String) ) =  cfun "uname";			# uname		def in    src/c/lib/posix-process-environment/uname.c

	sysconf = pp::sysconf;

	my time : Void -> i1w::Int =  cfun "time";					# time		def in    src/c/lib/posix-process-environment/time.c
	#
	time' = tg::from_seconds o iwg::to_multiword_int o time;



	# Times in clock ticks: 

	my times' : Void -> (i1w::Int, i1w::Int, i1w::Int, i1w::Int, i1w::Int) =   cfun "times";	# times		def in    src/c/lib/posix-process-environment/times.c

	ticks_per_sec
	    =
            int_guts::to_multiword_int (hug::to_int_x (sysconf "CLK_TCK"));

	ticks_to_time
	    =
	    case (mig::quot_rem (tg::fractions_per_second, ticks_per_sec))
		#
		(factor, 0)
                    =>
		    (fn ticks = tg::from_fractions (factor * iwg::to_multiword_int ticks));

	        _   =>
		    (fn ticks = tg::from_fractions
			             (mig::quot (tg::fractions_per_second
					   * iwg::to_multiword_int ticks,
					   ticks_per_sec)));
            esac;

	fun times ()
            =
            {   my (e, u, s, cu, cs)
                    =
                    times' ();

		{ elapsed =>  ticks_to_time  e,
		  utime   =>  ticks_to_time  u, 
		  stime   =>  ticks_to_time  s, 
		  cutime  =>  ticks_to_time  cu, 
		  cstime  =>  ticks_to_time  cs
                };
	    };

	my getenv:	String -> Null_Or( String ) = cfun "getenv";		# getenv		def in    src/c/lib/posix-process-environment/getenv.c
	my environment:	Void   ->    List( String ) = cfun "environ";		# environ		def in    src/c/lib/posix-process-environment/environ.c

	my ctermid:  Void -> String =   cfun "ctermid";				# ctermid		def in    src/c/lib/posix-process-environment/ctermid.c

	my ttyname' : Sy_Int -> String = cfun "ttyname";			# ttyname		def in    src/c/lib/posix-process-environment/ttyname.c
	#
	fun ttyname fd
	    =
	    ttyname' (pf::fd_to_int fd);

	my isatty' : Sy_Int -> Bool =   cfun "isatty";				# isatty		def in    src/c/lib/posix-process-environment/isatty.c
	#
	fun isatty fd
	    =
	    isatty' (pf::fd_to_int fd);

    };					#  package posix_id
end;



##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
