## winix-text-file-io-driver-for-posix.pkg
#
# Platform-specific textfile I/O for posix.
#
# This file gets combined with the platform-agnostic code in
# winix_text_file_for_os_g						# winix_text_file_for_os_g			is from   src/lib/std/src/io/winix-text-file-for-os-g.pkg
# to produce winix_text_file_for_posix					# winix_text_file_for_posix			is from   src/lib/std/src/posix/winix-text-file-for-posix.pkg
# which is also published as the 'file' package,			# file						is from   src/lib/std/src/posix/file.pkg
# the main standard.lib user textfile I/O resource.

# Compiled by:
#     src/lib/std/src/standard-core.sublib




stipulate
    package str =  string_guts;						# string_guts					is from   src/lib/std/src/string-guts.pkg
    package int =  int_guts;						# int_guts					is from   src/lib/std/src/bind-int-32.pkg
    package pos =  file_position_guts;					# file_position_guts				is from   src/lib/std/src/bind-position-31.pkg
    package psx =  posix_1003_1b;					# posix_1003_1b					is from   src/lib/std/src/posix-1003.1b/posix-1003-1b.pkg
    #
herein

    package winix_text_file_io_driver_for_posix
    : (weak)
    api {
	include Winix_Extended_File_Io_Driver_For_Os;			# Winix_Extended_File_Io_Driver_For_Os		is from   src/lib/std/src/io/winix-extended-file-io-driver-for-os.api

	stdin:   Void -> drv::Filereader;
	stdout:  Void -> drv::Filewriter;
	stderr:  Void -> drv::Filewriter;

	string_reader:  String -> drv::Filereader;

    } {
        package drv   = winix_base_text_file_io_driver_for_posix;	# winix_base_text_file_io_driver_for_posix	is from   src/lib/std/src/io/winix-base-text-file-io-driver-for-posix.pkg

	File_Descriptor =  psx::File_Descriptor;

	best_io_quantum = 4096;						# Reading/writing files 4K at a time should be fairly efficient.

	make_filereader =  psx::make_text_filereader;			# make_text_filereader		def in    src/lib/std/src/posix-1003.1b/posix-io.pkg
	make_filewriter =  psx::make_text_filewriter;			# make_text_filewriter		def in    src/lib/std/src/posix-1003.1b/posix-io.pkg

	fun announce s x y
	    =
	    {
    # print "Posix: "; print (s: String); print "\n"; 
		x y;
	    };

	fun open_for_read  name
	    =
	    make_filereader
		{ fd          =>  announce "openf" psx::openf (name, psx::O_RDONLY, psx::o::flags []),
		  name,
		  ok_to_block =>  TRUE
		};

	standard_mode
	    =
	    psx::s::flags
		[	#  mode 0666 
		  psx::s::irusr, psx::s::iwusr,
		  psx::s::irgrp, psx::s::iwgrp,
		  psx::s::iroth, psx::s::iwoth
		];
								# createf			def in    src/lib/std/src/posix-1003.1b/posix-file.pkg
	fun create_file (name, mode, flags)
	    =
	    announce "createf"   psx::createf (name, mode, flags, standard_mode);

	fun open_for_write  name
	    =
	    make_filewriter
		{ fd            =>  create_file (name, psx::O_WRONLY, psx::o::trunc),
		  name,
		  ok_to_block   =>  TRUE,
		  append_mode   =>  FALSE,
		  best_io_quantum
		};

	fun open_for_append  name
	    =
	    make_filewriter
		{ fd            =>  create_file (name, psx::O_WRONLY, psx::o::append),
		  name,
		  ok_to_block   =>  TRUE,
		  append_mode   =>  TRUE,
		  best_io_quantum
		};

	fun stdin ()
	    =
	    make_filereader
	      {
		fd		=>  psx::stdin,
		name		=>  "<stdin>",
		ok_to_block	=>  TRUE		#  Bug!  Should check!  XXX BUGGO FIXME
	      };

	fun stdout ()
	    =
	    make_filewriter
	      {
		fd		=>  psx::stdout,
		name		=>  "<stdout>",
		#
		ok_to_block	=>  TRUE,		# Bug!  Should check! XXX BUGGO FIXME
		append_mode	=>  FALSE,		# Bug!  Should check! XXX BUGGO FIXME
		best_io_quantum
	      };

	fun stderr ()
	    =
	    make_filewriter
	      {
		fd		=>  psx::stderr,
		name		=>  "<stderr>",
		#
		ok_to_block	=>  TRUE,		# Bug!  Should check! XXX BUGGO FIXME
		append_mode	=>  FALSE,		# Bug!  Should check! XXX BUGGO FIXME
		best_io_quantum
	      };

	fun string_reader src
	    =
	    {
		pos = REF 0;
		closed = REF FALSE;

		fun raise_exception_if_file_is_closed ()
		    =
		    if  *closed    raise exception io_exceptions::CLOSED_IO_STREAM;  fi;

		len = str::length src;
		plen = pos::from_int len;

		fun avail ()
		    =
		    len - *pos;

		fun read_ro n
		    =
		    {   p = *pos;
			m = int::min (n, len-p);

			raise_exception_if_file_is_closed ();
			pos := p+m;
			str::substring (src, p, m);	  # Could use unchecked operations here.
		      };

		fun read_rw asl
		    =
		    {   p = *pos;

			my (buf, i, n)
			    =
			    rw_vector_slice_of_chars::base asl;

			m = int::min (n, len-p);

			raise_exception_if_file_is_closed ();

			pos := p+m;

			rw_vector_slice_of_chars::copy_vec
			    { from => vector_slice_of_chars::make_slice (src, p, THE m),
			      to   => buf,
			      di   => i
			    };
			m;
		    };

		fun get_file_position ()
		    =
		    {   raise_exception_if_file_is_closed();
			pos::from_int *pos;
		    };

		fun set_file_position p
		    =
		    {   raise_exception_if_file_is_closed ();

			if  (p < 0
			or   p > plen
			)
			     raise exception SUBSCRIPT;
			else
			     pos := pos::to_int p;
			fi;
		    };

		drv::FILEREADER
		    {
		      name      => "<string>", 
		      best_io_quantum => len,
		      read_vector                => THE (read_ro),
		      read_rw_vector             => THE (read_rw),
		      read_vector_nonblocking    => THE (THE o read_ro),
		      read_rw_vector_nonblocking => THE (THE o read_rw),
		      blockx          => THE (raise_exception_if_file_is_closed),
		      can_readx       => THE (fn () = { raise_exception_if_file_is_closed(); TRUE;}),
		      avail           => THE o avail,
		      get_file_position    => THE get_file_position,
		      set_file_position    => THE set_file_position,
		      end_file_position    => THE (fn () = { raise_exception_if_file_is_closed(); plen;}),
		      verify_file_position => THE get_file_position,
		      close           => fn () = closed := TRUE,
		      io_descriptor   => NULL
		    };
	      };

    }; #  winix_text_file_io_driver_for_posix 
end;



##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
