## posix-binary-streamreaders-and-streamwriters.pkg

# Compiled by:
#     src/lib/std/src/standard-core.sublib


# This implements the UNIX version of the
# OS-specific binary primitive IO package.
#
# The Text IO version is implemented by a trivial
# translation of these operations -- see:
#
#     src/lib/std/src/posix/posix-text-streamreaders-and-streamwriters.pkg



stipulate
    package fpg =  file_position_guts;						# file_position_guts				is from   src/lib/std/src/bind-position-31.pkg
    package vec =  vector_of_one_byte_unts;					# vector_of_one_byte_unts			is from   src/lib/std/src/vector-of-one-byte-unts.pkg
    package pf  =  posix_1003_1b;						# posix_1003_1b					is from   src/lib/std/src/posix-1003.1b/posix-1003-1b.pkg
    package pio =  posix_1003_1b;
herein	

    package posix_binary_streamreaders_and_streamwriters
    : (weak)       Winix_Streamreaders_And_Streamwriters			# Winix_Streamreaders_And_Streamwriters		is from   src/lib/std/src/io/winix-streamreaders-and-streamwriters.api
    {
	#
	package    streamreaders_and_streamwriters
	 =  binary_streamreaders_and_streamwriters;				# binary_streamreaders_and_streamwriters	is from   src/lib/std/src/io/binary-streamreaders-and-streamwriters.pkg


	File_Descriptor
            =
            pf::File_Descriptor;

	to_fpi =  fpg::from_int;						# "fpi" == "File Position from Int", presumably.

	fun announce s x y
            =
	    {   # print "Posix: "; print (s: String); print "\n"; 
	        x y;
            };

	buffer_size_b = 4096;

	make_reader = pio::make_binary_streamreader;
	make_writer = pio::make_binary_streamwriter;

	fun open_for_read name
            =
            make_reader {
		fd => announce "openf" pf::openf (name, pf::O_RDONLY, pf::o::flags [] ),
		name,
		blocking_mode => TRUE
	      };

	standard_mode
            =
            pf::s::flags
                [	#  mode 0666 
		  pf::s::irusr, pf::s::iwusr,
		  pf::s::irgrp, pf::s::iwgrp,
		  pf::s::iroth, pf::s::iwoth
	        ];

	fun create_file (name, mode, flags)
            =
	    announce "createf" pf::createf (name, mode, flags, standard_mode);

	fun open_for_write name
            =
            make_writer {
		fd=>create_file (name, pf::O_WRONLY, pf::o::trunc),
		name,
		blocking_mode=>TRUE,
		append_mode=>FALSE,
		chunk_size=>buffer_size_b
	      };

	fun open_for_append name
            =
            make_writer {
		fd		  => create_file (name, pf::O_WRONLY, pf::o::append),
		name	  => name,
		blocking_mode => TRUE,
		append_mode	  => TRUE,
		chunk_size	  => buffer_size_b
	    };

    };   #  posix_binary_streamreaders_and_streamwriters 
end;



## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
