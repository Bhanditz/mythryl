## net-protocol-db.pkg

# Compiled by:
#     src/lib/std/src/standard-core.sublib

stipulate
    package ci  =  mythryl_callable_c_library_interface;		# mythryl_callable_c_library_interface	is from   src/lib/std/src/unsafe/mythryl-callable-c-library-interface.pkg
    #
    fun cfun  fun_name
	=
	ci::find_c_function { lib_name => "socket", fun_name };
herein

    package   net_protocol_db
    : (weak)  Net_Protocol_Db						# Net_Protocol_Db			is from   src/lib/std/src/socket/net-protocol-db.api
    {
	Entry = ENTRY { name:	    String,
			aliases:    List( String ),
			protocol:   Int
		      };

	stipulate
	    fun conc field' (ENTRY a)
		=
		field' a;
	herein

	    name     = conc .name;
	    aliases  = conc .aliases;
	    protocol = conc .protocol;

	end;

	# Protocol DB query functions 
	#
	stipulate
	    #
	    Protoent = (String, List(String), Int);

	    fun get NULL
		    =>
		    NULL;

		get (THE (name, aliases, protocol))
		    =>
		    THE (ENTRY { name, aliases, protocol } );
	    end;

	    get_prot_by_name'   = cfun "get_protocol_by_name"  : String -> Null_Or( Protoent );			# "get_protocol_by_name"		def in   src/c/lib/socket/get-protocol-by-name.c
	    get_prot_by_number' = cfun "get_protocol_by_number": Int    -> Null_Or( Protoent );			# "get_protocol_by_number"		def in   src/c/lib/socket/get-protocol-by-number.c

	herein
	    get_by_name   =  get o get_prot_by_name';
	    get_by_number =  get o get_prot_by_number';
	end;

    };
end;


## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
