## posix-io-unit-test.pkg
#
# Unit/regression test functionality for
#
#     src/lib/std/src/psx/posix-io.pkg

# Compiled by:
#     src/lib/test/unit-tests.lib

# Run by:
#     src/lib/test/all-unit-tests.pkg


stipulate
    include threadkit;										# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package psx =  posixlib;									# posixlib			is from   src/lib/std/src/psx/posixlib.pkg
#   package cpu =  cpu_bound_task_hostthreads;							# cpu_bound_task_hostthreads	is from   src/lib/std/src/hostthread/cpu-bound-task-hostthreads.pkg
#   package io  =   io_bound_task_hostthreads;							#  io_bound_task_hostthreads	is from   src/lib/std/src/hostthread/io-bound-task-hostthreads.pkg
#   package pth =  hostthread;									# hostthread			is from   src/lib/std/src/hostthread.pkg
#   package ts  =  thread_scheduler;								# thread_scheduler		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg
    package v1b =        vector_of_one_byte_unts;						#       vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
    package vbs =  vector_slice_of_one_byte_unts;						# vector_slice_of_one_byte_unts	is from   src/lib/std/src/vector-slice-of-one-byte-unts.pkg
#    package vsc =     vector_slice_of_chars;							#    vector_slice_of_chars	is from   src/lib/std/src/vector-slice-of-chars.pkg
    #
#    sleep = makelib::scripting_globals::sleep;

    (_[]) = v1b::(_[]);
herein

    package posix_io_unit_test {
	#
 	include unit_test;									# unit_test			is from   src/lib/src/unit-test.pkg
 
 	name =  "src/lib/std/src/psx/posix-io-unit-test.pkg";
 
 
	scratch_filename = "posix-io-unit-test--scratch-file.log";					# ".log" so that 'make clean' will remove it.


	fun apply_function_to_scratchfile_fd  function
	    =
	    {	file_descriptor =  psx::creat (scratch_filename, psx::mode_0644);
		#
		file_descriptor =  function  file_descriptor;

		psx::close  file_descriptor;

		psx::unlink  scratch_filename;
	    };

	fun apply_function_to_pipe_fds  function
	    =
	    {	(psx::make_pipe ()) ->   { infd, outfd }; 
		#
		(function  { infd, outfd })
		    ->
		    { infd, outfd };

		psx::close  infd;
		psx::close outfd;
	    };

	fun exercise__read_vector__and__write_vector ()
	    =
	    apply_function_to_scratchfile_fd  exercise__read_vector__and__write_vector'
	    where
		fun exercise__read_vector__and__write_vector'  file_descriptor
		    =
		    {
											log::note .{ "=>  exercise__read_vector__and__write_vector/TOP ();   -- posix-io-unit-test.pkg"; };
			a =  one_byte_unt::from_int  11;
			b =  one_byte_unt::from_int  13;
			c =  one_byte_unt::from_int  17;
			d =  one_byte_unt::from_int  19;

			vector_of_one_byte_unts
			    =
			    v1b::from_list   [ a, b, c, d ];

			slice_of_vector_of_one_byte_unts
			    =	
			    vbs::make_full_slice   vector_of_one_byte_unts;


			psx::write_vector (file_descriptor, slice_of_vector_of_one_byte_unts);

			psx::close file_descriptor;



			file_descriptor = psx::openf (scratch_filename, psx::O_RDONLY, psx::o::flags []);

			readback_vector =  psx::read_as_vector { file_descriptor, max_bytes_to_read => 8 };

			assert( v1b::length readback_vector == 4 );				# Length of [ a, b, c, d ] above.

			assert( readback_vector[0] == a );
			assert( readback_vector[1] == b );
			assert( readback_vector[2] == c );
			assert( readback_vector[3] == d );

											log::note .{ "=>  exercise__read_vector__and__write_vector/ZZZ ();   -- posix-io-unit-test.pkg"; };
			file_descriptor;
		    };
	    end;

	fun exercise__setfd__and__getfd ()
	    =
	    apply_function_to_pipe_fds  exercise__setfd__and__getfd'
	    where
		fun exercise__setfd__and__getfd' { infd, outfd }
		    =
		    {
											log::note .{ "=>  exercise__setfd__and__getfd/TOP ();   -- posix-io-unit-test.pkg"; };
			psx::setfd ( infd, psx::fd::cloexec  );
			psx::setfd (outfd, psx::fd::flags [] );

			assert( psx::getfd  infd  ==  psx::fd::cloexec  );
			assert( psx::getfd outfd  ==  psx::fd::flags [] );


			psx::setfd ( infd, psx::fd::flags [] );
			psx::setfd (outfd, psx::fd::cloexec  );

			assert( psx::getfd  infd  ==  psx::fd::flags [] );
			assert( psx::getfd outfd  ==  psx::fd::cloexec  );

											log::note .{ "=>  exercise__setfd__and__getfd/ZZZ ();   -- posix-io-unit-test.pkg"; };
			{ infd, outfd };
		    };
	    end; 

	fun exercise__setfl__and__getfl ()
	    =
	    apply_function_to_pipe_fds  exercise__setfl__and__getfl'
	    where
		fun exercise__setfl__and__getfl' { infd, outfd }
		    =
		    {
											log::note .{ "=>  exercise__setfl__and__getfl/TOP ();   -- posix-io-unit-test.pkg"; };
			include psx;
			include psx::flags;


			setfl ( infd, flags [ append, nonblock ] );
			setfl (outfd, flags [                  ] );

			assert(  #1 (getfl  infd)  ==  flags [ append, nonblock ] );
			assert(  #1 (getfl outfd)  ==  flags [                  ] );


			setfl ( infd, flags [                  ] );
			setfl (outfd, flags [ append, nonblock ] );

			assert(  #1 (getfl  infd)  ==  flags [                  ] );
			assert(  #1 (getfl outfd)  ==  flags [ append, nonblock ] );


			assert(  #2 (getfl  infd)  ==  O_RDONLY  );
			assert(  #2 (getfl outfd)  ==  O_WRONLY  );

											log::note .{ "=>  exercise__setfl__and__getfl/ZZZ ();   -- posix-io-unit-test.pkg"; };
			{ infd, outfd };
		    };
	    end;	


 	fun run ()
 	    =
 	    {   printf "\nDoing %s:\n" name;   
 		#
											log::note .{ "=>  run/TOP ();   -- posix-io-unit-test.pkg"; };
		exercise__read_vector__and__write_vector ();
		exercise__setfd__and__getfd ();
		exercise__setfl__and__getfl ();
 		#
 		summarize_unit_tests  name;
											log::note .{ "=>  run/ZZZ ();   -- posix-io-unit-test.pkg"; };
 	    };
    };
end;
