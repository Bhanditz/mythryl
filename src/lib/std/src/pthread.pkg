# pthread.pkg
#
# Mythryl-level interface to support for parallel computation
# via kernel threads (on Linux, Posix threads).
#
# We are the Mythryl side of the
#
#     src/c/lib/pthread/libmythryl-pthread.c
#
# interface to functionality defined in
#
#     src/c/h/runtime-pthread.h   
#
# and implemented in the platform-specific files
#
#     src/c/pthread/pthread-on-posix-threads.c				# pthread build on top of modern posix threads interface.
#     src/c/pthread/pthread-on-sgi.c					# pthread built on top of 1994-era SGI Irix interface.
#     src/c/pthread/pthread-on-solaris.c				# pthread build on top of 1994-era Solaris   interface.
#
# http://handle.dtic.mil/100.2/ADA255639

# Compiled by:
#     src/lib/std/src/standard-core.sublib

stipulate
    package ci  =  mythryl_callable_c_library_interface;		# mythryl_callable_c_library_interface	is from   src/lib/std/src/unsafe/mythryl-callable-c-library-interface.pkg
    package fat =  fate;						# fate					is from   src/lib/std/src/nj/fate.pkg
    #
    fun cfun fun_name
	=
	ci::find_c_function { lib_name => "pthread", fun_name };
herein

    package   pthread
    : (weak)  Pthread											# Pthread				is from   src/lib/std/src/pthread.api
    {
	# We allocate our mutex, condvar and barrier values
	# on the C heap because having the Mythryl garbage
	# collector moving them around seems like a really
	# really bad idea -- see	src/c/lib/pthread/libmythryl-pthread.c
	#
	# At the Mythryl level we represent them as C
	# addresses encoded as unsigned integer values:
	#
	Barrier = one_word_unt::Unt;
	Condvar = one_word_unt::Unt;
	Mutex   = one_word_unt::Unt;

	Try_Mutex_Result =  ACQUIRED_MUTEX | MUTEX_WAS_UNAVAILABLE;

	Pthread_Datum = Int;										# Dummy value to get things compiling first time around.
	Pthread_State =  PTHREAD_STATE (fat::Fate(Void), Pthread_Datum);

	exception MAKE_PTRHEAD;

	# Here we're looking up fns in
	# the table constructed in
	#
	#     src/c/lib/pthread/cfun-list.h
	#
	my spawn_pthread:	Pthread_State -> Void   =        cfun   "spawn_pthread"     ;
	my get_pthread_id:	Void -> Int             =        cfun   "get_pthread_id"      ;

	stipulate											# We need this little two-step because we'll get complaints
	    foo =  cfun   "pthread_exit";								# from the typechecker (due to the "value restriction")
	herein												# if we just do    my pthread_exit:   Void -> X =   cfun "release_pthread" here.
	    pthread_exit =        (unsafe::cast foo):	Void -> X ;
	end;

	make_mutex	=	cfun "mutex_make":		Void -> Mutex;
	free_mutex	=	cfun "mutex_free":		Mutex -> Void;
	set_mutex	=	cfun "mutex_init":		Mutex -> Void;
	clear_mutex	=	cfun "mutex_destroy":		Mutex -> Void;
	acquire_mutex	=	cfun "mutex_lock":		Mutex -> Void;
	release_mutex	=	cfun "mutex_unlock":		Mutex -> Void;
	try_mutex'	=	cfun "mutex_trylock":		Mutex -> Bool;				# This is not exported to clients -- we export try_mutex (below) instead.

	make_barrier	=	cfun "barrier_make":		Void -> Barrier;
	free_barrier	=	cfun "barrier_free":		Barrier -> Void;
	set_barrier'	=	cfun "barrier_init":		(Barrier, Int) -> Void;			# 'Int' is number of threads which must arrive at barrier before it will release them.
	clear_barrier	=	cfun "barrier_destroy":		Barrier -> Void;
	wait_on_barrier	=	cfun "barrier_wait":		Barrier -> Bool;			# Exactly one pthread waiting on barrier gets a TRUE return value.

	make_condvar	=	cfun "condvar_make":		Void -> Condvar;
	free_condvar	=	cfun "condvar_free":		Condvar -> Void;
	set_condvar	=	cfun "condvar_init":		Condvar -> Void;
	clear_condvar	=	cfun "condvar_destroy":		Condvar -> Void;
	wait_on_condvar	=	cfun "condvar_wait":		(Condvar, Mutex) -> Void;
	signal_condvar	=	cfun "condvar_signal":		Condvar -> Void;
	broadcast_condvar =	cfun "condvar_broadcast":	Condvar -> Void;

	fun try_mutex mutex						# Returning a Bool is too confusing, so we use a custom datatype for clarity.
	    =
	    if (try_mutex' mutex)   ACQUIRED_MUTEX;
	    else		    MUTEX_WAS_UNAVAILABLE;
	    fi;

	fun set_barrier { barrier, threads }
	    =
	    set_barrier' (barrier, threads);				# We do not actually need to repackage the record
									# as a tuple -- they have the same heap structure anyhow.

	fun active_pthreads () = 0;					# Dummy def to get things compiling first time around.

	initial_datum = 0;						# Dummy def to get things compiling first time around.

	fun get_datum () = 0;						# Dummy def to get things compiling first time around.
	fun set_datum d  = ();						# Dummy def to get things compiling first time around.



	fun try_lock l  = TRUE;						# Dummy def to get things compiling first time around.
	fun lock     l  = ();						# Dummy def to get things compiling first time around.
	fun unlock   l  = ();						# Dummy def to get things compiling first time around.

    };
end;

## Code by Jeff Prothero: Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
