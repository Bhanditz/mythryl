## export.pkg

# Compiled by:
#     src/lib/std/src/standard-core.sublib

stipulate
    package ci  =  mythryl_callable_c_library_interface;				# mythryl_callable_c_library_interface	is from   src/lib/std/src/unsafe/mythryl-callable-c-library-interface.pkg
    package rs  =  runtime_signals;							# runtime_signals			is from   src/lib/std/src/nj/runtime-signals.pkg
    package rt  =  runtime;								# runtime				is from   src/lib/core/init/runtime.pkg
    package wt  =  winix_types;								# winix_types				is from   src/lib/std/src/posix/winix-types.pkg
											# winix_types				is from   src/lib/std/src/win32/winix-types.pkg
    package wx  =  wrap_export;								# wrap_export				is from   src/lib/std/src/nj/wrap-export.pkg
herein
    package   export
    : (weak)  Export									# Export				is from   src/lib/std/src/nj/export.api
    {
        Fork_Result = AM_PARENT | AM_CHILD;
	#
	package process =   winix_process;

	fun cfun  fun_name
	    =
	    ci::find_c_function { lib_name => "heap", fun_name };
	    #
	    ###############################################################
	    # The functions in this package should be called only on a
	    # quiescient system with only one active posix thread, so
	    # our usual latency-minimization reasons for indirecting
	    # syscalls through other posix threads do not apply.
	    #
	    # Consequently I'm not taking the time and effort to switch it
	    # over from using find_c_function() to using find_c_function'().
	    # 				   -- 2012-04-21 CrT


	export_heap
	    =
	    cfun "export_heap"								# "export_heap"				def in    src/c/lib/heap/export-heap.c
	    :
	    String -> Bool;

	# We need the pair wrapper type to make sure that the second argument will
	# be fully wrapped when it is passed to the run-time system.
	# [also see wrap-export.pkg]
	#
	Cmdt =   wx::Pair( String,  List( String ) ) -> wt::process::Status;		# "Cmdt" may be "Command_Type"


	spawn_to_disk'
	    =
	    cfun "spawn_to_disk"							# "spawn_to_disk"			def in   src/c/lib/heap/export-fun.c
	    :
	    (String, Cmdt) -> Void;


	fun null_filename ()
	    =
	    raise exception rt::RUNTIME_EXCEPTION  ("empty heap file name", NULL);


	# Export the current Mythryl heap to the given file:
	#
	fun fork_to_disk ""
		=>
		null_filename ();

	    fork_to_disk file_name
		=>
		{   at::run_functions_scheduled_to_run  at::FORK_TO_DISK;
		    #
		    if (export_heap file_name)
			#		    
			at::run_functions_scheduled_to_run  at::STARTUP;
			AM_CHILD;
		    else
			AM_PARENT;
		    fi;
		 };
	end;

						    # inline_t		is from   src/lib/core/init/built-in.pkg
	fun spawn_to_disk ("", f)
		=>
		null_filename ();

	    spawn_to_disk (file_name, f)
		=>
		{   rs::mask_signals   rs::MASK_ALL;
		    #
		    at::run_functions_scheduled_to_run   at::SPAWN_TO_DISK;
		    #
		    rt::pervasive_package_pickle_list__global
			:=
			inline_t::cast ();
		    #
		    spawn_to_disk' (file_name, wx::wrap f);			# Never returns.
		};
	end;

    };										# package export 
end;



## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
