## plain-markup-buffer.pkg
#
# A simple (no textstyles) pretty-printing device for output to file outstreams.

# Compiled by:
#     src/lib/prettyprint/big/devices/prettyprint-devices.sublib

###                 "Teach us delight in simple things,
###                  and mirth that has no bitter springs."
###
###                                   -- Rudyard Kipling


stipulate
    package fil =  file__premicrothread;					# file__premicrothread	is from   src/lib/std/src/posix/file--premicrothread.pkg
herein

    api Plain_Markup_Buffer {
	#
	include Markup_Buffer;							# Markup_Buffer	is from   src/lib/prettyprint/big/src/markup-buffer.api

	make_markup_buffer
	    :
	    { dst:  fil::Output_Stream,
	      wid:  Int
	    }
	    -> Markup_Buffer;
    };
    

    package  plain_markup_buffer
    : (weak) Plain_Markup_Buffer
    {
	Markup_Buffer = MARKUP_BUFFER  {
	    dst:  fil::Output_Stream,
	    wid:  Int
	  };

	Textstyle = Void;            # No textstyle support 

	fun same_textstyle    _ = TRUE;
	fun push_textstyle    _ = ();
	fun pop_textstyle     _ = ();
	fun default_textstyle _ = ();

	make_markup_buffer = MARKUP_BUFFER;

	fun depth _ = NULL;							# Maximum printing depth (in terms of boxes) 

	# The width of the device 
	#
	fun line_width (MARKUP_BUFFER { wid, ... } ) = THE wid;

	fun text_width _ = NULL;						# The suggested maximum width of text on a line.

	fun space (MARKUP_BUFFER { dst, ... }, n)				# Append some number of spaces to the markup_buffer.
	    =
	    fil::write (dst, number_string::pad_left ' ' n "");

	fun newline (MARKUP_BUFFER { dst, ... } )				# Append a newline to the markup_buffer.
	    =
	    fil::write_one (dst, '\n');


	fun string (MARKUP_BUFFER { dst, ... }, s) = fil::write (dst, s);	# Append a string    in the current textstyle to the markup_buffer:
	fun char   (MARKUP_BUFFER { dst, ... }, c) = fil::write_one (dst, c);	# Append a character in the current textstyle to the markup_buffer:

	fun flush (MARKUP_BUFFER { dst, ... } ) = fil::flush dst;		# Process and write contents of markup_buffer, then reset it to empty.
    };
end;


## COPYRIGHT (c) 1997 Bell Labs, Lucent Technologies.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
