## prettyprint-tree-g.pkg
#
# This generic implements a declarative way to specify pretty-printing.
# This lets one package construct a first-cut prettyprint representation
# of a document and then another packages refine it before finally
# shipping it off for actual rendering.

# Compiled by:
#     src/lib/prettyprint/big/prettyprint-mill.lib



###               "The difference between the true hacker
###                and the mere power user is that the
###                limits of what a power user can achieve
###                are set by his tools, but the hacker is
###                master of his tools, and lives in a
###                world without limits.
###
###               "Every true hacker is necessarily a compiler
###                hacker at need, as well as an editor hacker,
###                kernel hacker, and so forth.
###
###               "True hackers are a rare breed.  For every
###                one of them, you will find a thousand
###                power users posing as hackers, more often
###                than not without even realizing the difference."
###
###					-- Willy Simons



generic package  prettyprint_tree_g   (
    #		 ==================
    #
    pp:  Base_Prettyprint_Mill								# Base_Prettyprint_Mill		is from   src/lib/prettyprint/big/src/base-prettyprint-mill.api
)
{
    Traitful_Text   =  pp::Traitful_Text;
    Texttraits	    =  pp::Texttraits;
    Left_Margin_Is  =  pp::pmt::Left_Margin_Is;

    # The Prettyprint_Tree type is
    # a concrete representation
    # of a prettyprint layout:

    Prettyprint_Tree
	= HORIZONTAL_BOX		(Left_Margin_Is, List( Prettyprint_Tree ))
	| VERTICAL_BOX			(Left_Margin_Is, List( Prettyprint_Tree ))
	| ALIGN_BOX 			(Left_Margin_Is, List( Prettyprint_Tree ))
	| WRAP_BOX			(Left_Margin_Is, List(Prettyprint_Tree))
	| TRAITFUL_TEXT	Traitful_Text
	| TEXT		String
	| TEXTTRAITS    (Texttraits, List(Prettyprint_Tree))
	| BREAK		{ spaces: Int,   indent_on_wrap: Int }
	| NEW_LINE
	| NBSPACE	Int
	| CONTROL	(pp::Prettyprint_Output_Stream -> Void)
	;

    # Prettyprint a prettyprint tree:
    #
    fun prettyprint_tree (pp, tree)
        =
	prettyprint tree
	where
            fun prettyprint (HORIZONTAL_BOX               (i, l)) =>    {   pp::begin_indented_horizontal_box	pp i;	prettyprint_list l;	pp::end_box pp;	};
		prettyprint (VERTICAL_BOX                 (i, l)) =>    {   pp::begin_indented_vertical_box	pp i;	prettyprint_list l;	pp::end_box pp;	};
		prettyprint (ALIGN_BOX			  (i, l)) =>    {   pp::begin_indented_align_box	pp i;	prettyprint_list l;	pp::end_box pp;	};
		prettyprint (WRAP_BOX                     (i, l)) =>    {   pp::begin_indented_wrap_box 	pp i;	prettyprint_list l;	pp::end_box pp;	};
		#
		prettyprint (TRAITFUL_TEXT tok	) =>     pp::traitful_text	 pp tok;
		prettyprint (TEXT s		) =>     pp::text		 pp s;
		prettyprint (TEXTTRAITS (tt, l)	) => {   pp::push_texttraits	(pp, tt);	prettyprint_list l;	pp::pop_texttraits pp;	};
		prettyprint (BREAK brk    	) =>     pp::break		 pp brk;
		prettyprint NEW_LINE              =>     pp::newline		 pp;
		prettyprint (NBSPACE n)           =>     pp::nonbreakable_spaces pp n;
		prettyprint (CONTROL control_fn)  =>     pp::control		 pp control_fn;
            end 

	    also
	    fun prettyprint_list []         	  =>  ();
		prettyprint_list (item ! rest)    =>  {   prettyprint item;	prettyprint_list rest;	};
            end;
	end;

    cut				    =  BREAK { spaces => 0, indent_on_wrap => 0 };
    #
    fun space n
	=
	BREAK { spaces => n, indent_on_wrap => 0 };


};



## COPYRIGHT (c) 2005 John Reppy (http://www.cs.uchicago.edu/~jhr)
## All rights reserved.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
