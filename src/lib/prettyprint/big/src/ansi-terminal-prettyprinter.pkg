## ansi-terminal-prettyprinter.pkg

# Compiled by:
#     src/lib/prettyprint/big/src/out/prettyprint-devices.sublib

stipulate
    package fil =  file__premicrothread;						# file__premicrothread				is from   src/lib/std/src/posix/file--premicrothread.pkg
    package out =  ansi_terminal_prettyprinter_output_stream;				# ansi_terminal_prettyprinter_output_stream	is from   src/lib/prettyprint/big/src/out/ansi-terminal-prettyprinter-output-stream.pkg
herein

    api Ansi_Terminal_Prettyprinter {
	#
	package ss:  api {								# "ss" == "styled string"
	    include Styled_String							# Styled_String					is from   src/lib/prettyprint/big/src/styled-string.api
		    where
			Textstyle == out::Textstyle;

	    styled_string:  (out::Textstyle, String) -> Styled_String;
	};

	include Prettyprinter								# Prettyprinter					is from   src/lib/prettyprint/big/src/prettyprinter.api
		where  Prettyprinter_Output_Stream == out::Prettyprinter_Output_Stream
		where  Textstyle                   == out::Textstyle
		where  Styled_String               == ss::Styled_String;

	make_ansi_terminal_prettyprinter
	  :
	  { output_stream:	fil::Output_Stream
	  }
	  -> List( Prettyprinter_Args )
	  -> Prettyprinter;
    };

    package  ansi_terminal_prettyprinter
    : (weak) Ansi_Terminal_Prettyprinter
    {
	package ss									# ss == "styled string".
	    =
	    package {
		Textstyle = out::Textstyle;
		Styled_String = STYLED_STRING  (Textstyle, String);
		#	
		styled_string = STYLED_STRING;
		#	
		fun string    (STYLED_STRING (textstyle, string)) = string;
		fun textstyle (STYLED_STRING (textstyle, string)) = textstyle;
		fun size      (STYLED_STRING (textstyle, string)) = string::length string;
	    };

	package pp
	    =
	    prettyprinter_g (								# prettyprinter_g				is from   src/lib/prettyprint/big/src/prettyprinter-g.pkg
		package ss  = ss;
		package out = out;
	    );

	include pp;

	fun make_ansi_terminal_prettyprinter  arg  pp_args
	    =
	    make_prettyprinter  (out::make_ansi_terminal_output_stream  arg)  pp_args;
    };
end;

## COPYRIGHT (c) 2005 John Reppy (http://www.cs.uchicago.edu/~jhr)
## All rights reserved.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
