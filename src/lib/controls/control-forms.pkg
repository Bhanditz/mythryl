## control-forms.pkg

# Compiled by:
#     src/lib/controls/controls.lib


stipulate
    package lst =  list;							# list				is from   src/lib/std/src/list.pkg
    package qht =  quickstring_hashtable;					# quickstring_hashtable		is from   src/lib/src/quickstring-hashtable.pkg
    package qs  =  quickstring;							# quickstring			is from   src/lib/src/quickstring.pkg
herein

    package control_forms {
	#
	#  Menu_Ranks are used for ordering help messages (lexical order) 
	#
	Menu_Slot =  List( Int );

	Control(X)
	    =
	    CONTROL
	      {
		name: qs::Quickstring,						# Name of the control.

		get:       Void -> X,						# Return the control's value.
		menu_slot: Menu_Slot,						# Positions control in the control menu hierarchy.
		obscurity: Int,							# Control's detail level; higher means more obscure.

		help:      String,						# Control's description.

		set:  Null_Or(X) -> Void -> Void 				# function to set the control's value;
										# it is delayed (error checking in 1st
										# stage, actual assignment in 2nd);
										# if the argument is NULL, then
										# the 2nd stage will restore the
										# value that was present during the
										# first stage.

	      }

	withtype Control_Set (X, Y)
	    =
	     qht::Hashtable
		 { control:  Control(X),
		   info:     Y
		 };

	Value_Converter(X)							# Conversion functions for control values.	    =
	  =
	  { type_name:    String,
	    from_string:  String -> Null_Or(X),
	    to_string:    X -> String
	  };

	fun menu_rank_gt  ranks
	    =
	    lst::collate  int::compare  ranks    ==    GREATER;
    };
end;


## COPYRIGHT (c) 2002 Bell Labs, Lucent Technologies
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
