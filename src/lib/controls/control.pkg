## control.pkg

# Compiled by:
#     src/lib/controls/controls.lib

stipulate
    package cf  =  control_forms;						# control_forms			is from   src/lib/controls/control-forms.pkg
    package qs  =  quickstring;							# quickstring			is from   src/lib/src/quickstring.pkg
herein

    package    control
    : (weak)   Control								# Control			is from   src/lib/controls/control.api
    {
	Menu_Rank	    =  cf::Menu_Rank;
	Control(X) 	    =  cf::Control(X);
	Value_Converter(X)  =  cf::Value_Converter(X);

	fun make_control { name, menu_rank, obscurity, help, control }
	    =
	    cf::CONTROL
	      {
		name =>  qs::from_string name,
		get  =>  fn () = *control,
		set  =>  fn THE v =>  (fn () =  control := v);
			    NULL  =>  { v = *control;   fn () = control := v; };
			 end,
		menu_rank,
		obscurity,
		help
	      };

	# This exception is raised to announce
	# that there is a syntax error in a
	# string representation of a control value:
	#
	exception
	    BAD_VALUE_SYNTAX  {
	      type_name:     String,
	      control_name:  String,
	      value:         String
	    };

	fun make_string_control
	      #
	      { type_name, from_string, to_string }
	      #
	      (cf::CONTROL c)
	    =
	    {   c ->   { name, get, set, menu_rank, obscurity, help };

		fun from_string' s
		    =
		    case (from_string s)
			#		  
			THE v => v;
			#		  
			NULL  =>
			    raise exception BAD_VALUE_SYNTAX { type_name,
							       control_name =>  qs::to_string name,
							       value        =>  s
							     };
		    esac;

		cf::CONTROL
		  {
		    name,
		    get => to_string o get,
		    set => set o null_or::map from_string',
		    menu_rank,
		    obscurity,
		    help
		  };
	    };

	fun name (cf::CONTROL { name, ... }  ) =  qs::to_string name;
	fun get  (cf::CONTROL { get, ... }   ) =  get ();
	fun set  (cf::CONTROL { set, ... }, v) =  set (THE v) ();
	fun set' (cf::CONTROL { set, ... }, v) =  set (THE v);

	fun info (cf::CONTROL { menu_rank, obscurity, help, ... } )
	    =
	    { menu_rank, obscurity, help };

	fun save_controller_state (cf::CONTROL { set, ... } )					# Generate a thunk containing current controller state, which when run will restore the controller to that state.
	    =
	    set NULL;

	fun menu_rank_gt
	      (
		cf::CONTROL { menu_rank => rank1, ... },
		cf::CONTROL { menu_rank => rank2, ... }
	      )
	    =
	    list::collate  int::compare (rank1, rank2);

    };
end;

## COPYRIGHT (c) 2002 Bell Labs, Lucent Technologies
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
