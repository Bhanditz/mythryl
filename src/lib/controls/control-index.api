## control-index.api

# Compiled by:
#     src/lib/controls/controls.lib





# A registry collects together string controls.
# It supports generation of help messages and
# initialization from the dictionary.

stipulate
    package ctl =  control;							# control		is from   src/lib/controls/control.pkg
herein

    api Control_Index {
	#
	Control_Index;

	Control_Info
	    =
	    { dictionary_name:  Null_Or( String ) };


	make:
	    { help:  String }				# Registry's description.
	    ->
	    Control_Index;



	note_control					# Register a control.
	    :
	    Control_Index
	    ->
	    { control:         ctl::Control( String ),
	      dictionary_name: Null_Or( String )
	    }
	    ->
	    Void;



	note_control_set				# Register a set of controls.
	    :
	    Control_Index
	    ->
	    { control_set:           control_set::Control_Set (String, X),
	      make_dictionary_name:  String -> Null_Or( String )
	    }
	    ->
	    Void;



	note_subindex					# Nest a registry inside another registry.
	    :
	    Control_Index
	    ->
	    { prefix:		Null_Or( String ),
	      menu_slot:        ctl::Menu_Slot,		# Positions within control menu hierarchy.
	      obscurity:  Int,				# registry's detail level; higher means more obscure.
	      reg:        Control_Index
	    }
	    ->
	    Void;



	find_control
	    :
	    Control_Index
	    ->
	    List( String )
	    ->
	    Null_Or( ctl::Control( String ) );



	set_up_controls_from_posix_environment
	    :			# Initialize the controls in the registry from the unix environment: 
	    Control_Index -> Void;

	Index_Tree
	    =
	    INDEX_TREE  {
	      path:         List( String ),
	      help:         String,
	      subregs:      List( Index_Tree ),
	      control_set:  List { control:	ctl::Control( String ),
				   info:	Control_Info
				 }
	    };

	controls:
	    (Control_Index,  Null_Or(Int))
	    ->
	    Index_Tree;

    };
end;

## COPYRIGHT (c) 2002 Bell Labs, Lucent Technologies
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
