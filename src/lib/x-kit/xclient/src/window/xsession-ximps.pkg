## xsession-ximps.pkg
#
# For the big picture see the imp dataflow diagram in
#
#     src/lib/x-kit/xclient/src/window/xclient-ximps.pkg
#
# xsession-ximps wraps up the ximps
#
#    inbuf_ximp;						# inbuf_ximp					is from   src/lib/x-kit/xclient/src/wire/inbuf-ximp.pkg
#    outbuf_ximp;						# outbuf_ximp					is from   src/lib/x-kit/xclient/src/wire/outbuf-ximp.pkg
#    xsequencer_ximp;						# xsequencer_ximp				is from   src/lib/x-kit/xclient/src/wire/xsequencer-ximp.pkg
#    decode_xpackets_ximp;					# decode_xpackets_ximp				is from   src/lib/x-kit/xclient/src/wire/decode-xpackets-ximp.pkg
#
# to look like a single logical ximp to the rest of
# the system.

# Compiled by:
#     src/lib/x-kit/xclient/xclient-internals.sublib





stipulate
    include threadkit;						# threadkit					is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    #
    package un  =  unt;						# unt						is from   src/lib/std/unt.pkg
    package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts			is from   src/lib/std/src/vector-of-one-byte-unts.pkg
    package w2v =  wire_to_value;				# wire_to_value					is from   src/lib/x-kit/xclient/src/wire/wire-to-value.pkg
    package xg  =  xgeometry;					# xgeometry					is from   src/lib/std/2d/xgeometry.pkg
    package xtr =  xlogger;					# xlogger					is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg

    package sok =  socket__premicrothread;			# socket__premicrothread			is from   src/lib/std/socket--premicrothread.pkg

    package op  =  outbuf;					# outbuf					is from   src/lib/x-kit/xclient/src/wire/outbuf.pkg
    package xew =  xerror_well;					# xerror_well					is from   src/lib/x-kit/xclient/src/wire/xerror-well.pkg
    package sp  =  xsequencer;					# xsequencer					is from   src/lib/x-kit/xclient/src/wire/xsequencer.pkg
    package xwp =  xevent_to_window;				# xevent_to_window				is from   src/lib/x-kit/xclient/src/window/xevent-to-window.pkg
    package xes =  xevent_sink;					# xevent_sink					is from   src/lib/x-kit/xclient/src/wire/xevent-sink.pkg
    package xt  =  xtypes;					# xtypes					is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
    package et  =  event_types;					# event_types					is from   src/lib/x-kit/xclient/src/wire/event-types.pkg

#   package ix  =  inbuf_ximp;					# inbuf_ximp					is from   src/lib/x-kit/xclient/src/wire/inbuf-ximp.pkg
#   package ox  =  outbuf_ximp;					# outbuf_ximp					is from   src/lib/x-kit/xclient/src/wire/outbuf-ximp.pkg
    package sx  =  xsequencer_ximp;				# xsequencer_ximp				is from   src/lib/x-kit/xclient/src/wire/xsequencer-ximp.pkg
    package dxx =  decode_xpackets_ximp;			# decode_xpackets_ximp				is from   src/lib/x-kit/xclient/src/wire/decode-xpackets-ximp.pkg

    package fx  =  font_index;					# font_index					is from   src/lib/x-kit/xclient/src/window/font-index.pkg
    package kx  =  keymap_ximp;					# keymap_ximp					is from   src/lib/x-kit/xclient/src/window/keymap-ximp.pkg
    package kp  =  keymap;					# keymap					is from   src/lib/x-kit/xclient/src/window/keymap.pkg
    package xwx =  xevent_to_window_ximp;			# xevent_to_window_ximp				is from   src/lib/x-kit/xclient/src/window/xevent-to-window-ximp.pkg
    package sox =  xsocket_ximps;				# xsocket_ximps					is from   src/lib/x-kit/xclient/src/wire/xsocket-ximps.pkg

    package dy  =  display;					# display					is from   src/lib/x-kit/xclient/src/wire/display.pkg

    # These are purely temporary debug kludges to force these to compile:
    #
    Keymap_Ximp_Exports = keymap_ximp::Exports;			
    Xevent_To_Window_Ximp_Exports
        =
	xevent_to_window_ximp::Exports;				# xevent_to_window_ximp				is from   src/lib/x-kit/xclient/src/window/xevent-to-window-ximp.pkg
herein


    # This impset is typically instantiated by:
    #
    #     src/lib/x-kit/xclient/src/window/xclient-ximps.pkg

    package   xsession_ximps
    :         Xsession_Ximps					# Xsession_Ximps				is from   src/lib/x-kit/xclient/src/wire/xsession-ximps.api
    {
	Imports  =  {										# Ports we use, provided by other imps.
		      window_property_xevent_sink:	xes::Xevent_Sink,			# We'll forward X server PropertyNotify events to this sink.
		      selection_xevent_sink:		xes::Xevent_Sink			# We'll forward X server SelectionNotify, SelectionRequest and SelectionClear events to this sink.
		    };

	Exports    =  {										# Ports we provide for use by other imps.
			xsequencer:		sp::Xsequencer,					# Requests from widget/application code.
		        xerror_well:		xew::Xerror_Well,				# Errors from the X server.
			keymap:			kp::Keymap,				# Requests from widget/application code.
			xevent_to_window:	xwp::Xevent_To_Window    			#
		      };

	Option = MICROTHREAD_NAME String;							# 

	Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);


	fun process_options (options: List(Option), { name })
	    =
	    {   my_name   = REF name;
		#
		apply  do_option  options
		where
		    fun do_option (MICROTHREAD_NAME n)  =   my_name := n;
		end;

		{ name => *my_name };
	    };


        ##########################################################################################
	# External entrypoints
	#
	fun make_xsession_ximps_egg												# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
	      (
		socket:		sok::Socket (X, sok::Stream(sok::Active)),							# Socket to use.
		xdisplay:	dy::Xdisplay,
		options:	List(Option)
	      )
	    =
	    {   (process_options (options, { name => "tmp" }))
		    ->
		    { name };


		me = 	{ keymap_egg		=>   kx::make_keymap_egg (xdisplay, []),
			  xevent_to_window_egg	=>  xwx::make_xevent_to_window_egg [],
			  xsocket_ximps_egg	=>  sox::make_xsocket_ximps_egg (socket, [])
			};

		fn () = {													# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
			    (me.keymap_egg	        ())   -> (keymap_exports,               keymap_egg'             );

			    (me.xevent_to_window_egg	())   -> (xevent_to_window_exports,	xevent_to_window_egg'	);
			    (me.xsocket_ximps_egg	())   -> (xsocket_ximps_exports,	xsocket_ximps_egg'	);

			    keymap			=  keymap_exports.keymap;
			    xsequencer			=  xsocket_ximps_exports.xsequencer;
			    xerror_well			=  xsocket_ximps_exports.xerror_well;
			    xevent_to_window		=  xevent_to_window_exports.xevent_to_window;

			    phase3
				=
				fn  ( imports:	Imports,									# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				      run_gun':	Run_Gun,	
				      end_gun':	End_Gun
				    )
				    =
				    {
					xsequencer	=  xsocket_ximps_exports.xsequencer;
					keymap		=  keymap_exports.keymap;
					xevent_sink	=  xevent_to_window_exports.xevent_sink;

					window_property_xevent_sink =  imports.window_property_xevent_sink;
					selection_xevent_sink	    =  imports.selection_xevent_sink;

					keymap_egg'	      (	{ xsequencer },
								run_gun', end_gun'
							      );

					xevent_to_window_egg' (	{ keymap,
								  window_property_xevent_sink,
								  selection_xevent_sink
								},
								run_gun', end_gun'
							      );

					xsocket_ximps_egg'    ( { xevent_sink },
								run_gun', end_gun'
							      );
					();
				    };

			    ({ xsequencer, xerror_well, keymap, xevent_to_window }, phase3);
			};
	    };
    };																	# package xsession_ximps
end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1990, 1991 by John H. Reppy.  See SMLNJ-COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2014,
## released per terms of SMLNJ-COPYRIGHT.
