## sample-ximp.pkg
#
# This file is intended purely for clone-and-mutate
# construction of new X imps ("ximps").
#
# For the big picture see the imp dataflow diagram in
#
#     src/lib/x-kit/xclient/src/window/xsession.pkg

# Compiled by:
#     src/lib/x-kit/xclient/xclient-internals.sublib





stipulate
    include threadkit;													# threadkit				is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
herein


    package   sample_ximp
    : (weak)  Sample_Ximp												# Sample_Ximp				is from   src/lib/x-kit/xclient/src/wire/sample-ximp.api
    {
	include sample_port;												# sample_port				is from   src/lib/x-kit/xclient/src/wire/sample-port.pkg
	#
	Starting_Gun = Mailop(Void);											# Purely for readability.

	Sample_Ximp_State = Ref( Void );										# Opaque wrapper for all nonephemeral mutable state maintained by ximp.

	Links = { int_sink: Int -> Void };

	Configport = Mailslot( {  links: Links,  me: Sample_Ximp_State,  starting_gun': Starting_Gun  } );

	Patronplea  =  STOP
		    ;
	Patronport = Mailslot( Patronplea );


	Sample_Plea  =  DO_SOMETHING Int
		    |  PASS_SOMETHING Oneshot_Maildrop( Int )
		    ;
	Sample_Q    = Mailqueue( Sample_Plea );


	Ports	= { configport:		Configport,
		    patronport:		Patronport,
		    sample_port:	Sample_Port
		  };


	fun run {   me:			Sample_Ximp_State,								# These four values will be statically globally visible
		    patronport:		Patronport,									# throughout the code body for the imp.
		    sample_q:		Sample_Q,
		    links:		Links,
		    to:			Replyqueue(Void)								# The name makes   foo::pass_something(imp) to .{ ... }   syntax read well.
		}
	    =
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (take_from_mailslot'  patronport  ==>  do_patronplea),
			    (take_from_mailqueue' sample_q    ==>  do_sample_plea)
			];

			loop ();
		    }	
		    where
			fun shut_down_sample_imp' ()
			    =
			    {
				thread_exit { success => TRUE };							# Will not return.	
			    };

			fun do_patronplea  STOP
			    =
			    shut_down_sample_imp' ();									# Will not return.	

			fun do_sample_plea  (DO_SOMETHING i)
			        =>
				{   links.int_sink i;									# Demonstrate use of links.
				};

			    do_sample_plea  (PASS_SOMETHING reply_oneshot)
				=>
				{   put_in_oneshot (reply_oneshot, 0);
				};
			end;
		    end;
	    end;	



	fun startup   (reply_oneshot:  Oneshot_Maildrop( Ports ))   ()							# Root fn of imp microthread.  Note currying.
	    =
	    {   configport  =  make_mailslot  ()	:  Configport;
		patronport  =  make_mailslot  ()	:  Patronport;

		sample_port = { do_something, pass_something };

		to          =  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, { configport, patronport, sample_port });				# Return value from make_sample_ximp().

		(take_from_mailslot  configport)									# Input args from configure_sample_ximp().
		    ->
		    { me, links, starting_gun' };

		block_until_mailop_fires  starting_gun';								# Wait for the starting gun.

		run { me, patronport, sample_q, links, to };								# Will not return.
	    }
	    where
		sample_q     =  make_mailqueue (get_current_microthread()):  Sample_Q;

		fun do_something (i: Int)										# External entrypoint.
		    =	
		    put_in_mailqueue  (sample_q, DO_SOMETHING i);


		fun pass_something  (replyqueue: Replyqueue(Void))  (reply_handler: Int -> Void)			# External entrypoint.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (sample_q, PASS_SOMETHING reply_oneshot);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };
	    end;


        ##########################################################################################
	# External entrypoints
	#
	fun make_sample_ximp_state ()											# External entrypoint.
	    =
	    REF ();

	fun make_sample_ximp  (name: String)										# External entrypoint.
	    =
	    {   reply_oneshot = make_oneshot_maildrop();
		#
		xlogger::make_thread  name  (startup  reply_oneshot);							# Note that startup() is curried.

		get_from_oneshot  reply_oneshot;
	    };

	fun configure_sample_ximp											# External entrypoint.
	      (
		configport:	Configport,
		me:		Sample_Ximp_State,
		links:		Links,
		starting_gun':	Starting_Gun
	      )
	    =
	    put_in_mailslot  (configport, { me, links, starting_gun' });

	fun shut_down_sample_ximp  (patronport: Patronport)								# External entrypoint.
	    =
	    put_in_mailslot  (patronport,  STOP);
    };															# package sample_ximp
end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1990, 1991 by John H. Reppy.  See SMLNJ-COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
