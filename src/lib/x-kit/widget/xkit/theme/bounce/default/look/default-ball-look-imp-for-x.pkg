## default-ball-look-for-x.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include package   threadkit;				# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ap  =  client_to_atom;				# client_to_atom		is from   src/lib/x-kit/xclient/src/iccc/client-to-atom.pkg
#   package au  =  authentication;				# authentication		is from   src/lib/x-kit/xclient/src/stuff/authentication.pkg
#   package cpm =  cs_pixmap;					# cs_pixmap			is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#   package cpt =  cs_pixmat;					# cs_pixmat			is from   src/lib/x-kit/xclient/src/window/cs-pixmat.pkg
#   package dy  =  display;					# display			is from   src/lib/x-kit/xclient/src/wire/display.pkg
#   package xet =  xevent_types;				# xevent_types			is from   src/lib/x-kit/xclient/src/wire/xevent-types.pkg
#   package w2x =  windowsystem_to_xserver;			# windowsystem_to_xserver	is from   src/lib/x-kit/xclient/src/window/windowsystem-to-xserver.pkg
#   package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
#   package fti =  font_index;					# font_index			is from	  src/lib/x-kit/xclient/src/window/font-index.pkg
#   package r2k =  xevent_router_to_keymap;			# xevent_router_to_keymap	is from   src/lib/x-kit/xclient/src/window/xevent-router-to-keymap.pkg
#   package mtx =  rw_matrix;					# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
#   package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package rgb =  rgb;						# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
#   package rop =  ro_pixmap;					# ro_pixmap			is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
#   package rw  =  root_window;					# root_window			is from   src/lib/x-kit/widget/lib/root-window.pkg
#   package rwv =  rw_vector;					# rw_vector			is from   src/lib/std/src/rw-vector.pkg
#   package sep =  client_to_selection;				# client_to_selection		is from   src/lib/x-kit/xclient/src/window/client-to-selection.pkg
#   package shp =  shade;					# shade				is from   src/lib/x-kit/widget/lib/shade.pkg
#   package sj  =  socket_junk;					# socket_junk			is from   src/lib/internet/socket-junk.pkg
#   package x2s =  xclient_to_sequencer;			# xclient_to_sequencer		is from   src/lib/x-kit/xclient/src/wire/xclient-to-sequencer.pkg
#   package tr  =  logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
#   package tsr =  thread_scheduler_is_running;			# thread_scheduler_is_running	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler-is-running.pkg
#   package u1  =  one_byte_unt;				# one_byte_unt			is from   src/lib/std/one-byte-unt.pkg
#   package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
#   package v2w =  value_to_wire;				# value_to_wire			is from   src/lib/x-kit/xclient/src/wire/value-to-wire.pkg
#   package wg  =  widget;					# widget			is from   src/lib/x-kit/widget/basic/widget.pkg
#   package wi  =  window;					# window			is from   src/lib/x-kit/xclient/src/window/window.pkg
#   package wme =  window_map_event_sink;			# window_map_event_sink		is from   src/lib/x-kit/xclient/src/window/window-map-event-sink.pkg
#   package wpp =  client_to_window_watcher;			# client_to_window_watcher	is from   src/lib/x-kit/xclient/src/window/client-to-window-watcher.pkg
#   package wy  =  widget_style;				# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
#   package e2s =  xevent_to_string;				# xevent_to_string		is from   src/lib/x-kit/xclient/src/to-string/xevent-to-string.pkg
#   package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
#   package xj  =  xsession_junk;				# xsession_junk			is from   src/lib/x-kit/xclient/src/window/xsession-junk.pkg
#   package xt  =  xtypes;					# xtypes			is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg

#   package g2w =  guiboss_to_windowsystem;			# guiboss_to_windowsystem	is from   src/lib/x-kit/widget/theme/guiboss-to-windowsystem.pkg
    package bl  =  ball_look;					# ball_look			is from   src/lib/x-kit/widget/theme/bounce/look/ball-look.pkg
    package bs  =  ball_state;					# ball_state			is from   src/lib/x-kit/widget/space/bounce/ball/ball-state.pkg
    package bsi =  ball_state_imp;				# ball_state_imp		is from   src/lib/x-kit/widget/space/bounce/ball/ball-state-imp.pkg
    package bwe =  bounce_widget_egg;				# bounce_widget_egg		is from   src/lib/x-kit/widget/space/bounce/bounce-widget-egg.pkg
    package gd  =  gui_displaylist;				# gui_displaylist		is from   src/lib/x-kit/widget/theme/gui-displaylist.pkg
    package iul =  issue_unique_look_id;			# issue_unique_look_id		is from   src/lib/x-kit/widget/gui/issue-unique-look-id.pkg
    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
    #
    package b2l =  bounce_to_look;				# bounce_to_look		is from   src/lib/x-kit/widget/space/bounce/bounce-to-look.pkg
    package l2b =  look_to_bounce;				# look_to_bounce		is from   src/lib/x-kit/widget/space/bounce/look-to-bounce.pkg
    package iul =  issue_unique_look_id;			# issue_unique_look_id		is from   src/lib/x-kit/widget/gui/issue-unique-look-id.pkg
    #
    package xg  =  xgeometry;					# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
    #
    tracefile   =  "widget-unit-test.trace.log";
herein

    package default_ball_look_imp_for_x
    :               Ball_Look_Imp											# Ball_Look_Imp		is from   src/lib/x-kit/widget/theme/bounce/ball-look-imp.api
    {
	#
	include package   ball_look;											# ball_look		is from   src/lib/x-kit/widget/theme/bounce/look/ball-look.pkg
	#
	Ball_Look_State
	  =
	  { id:		iul::Id,
	    state:	Ref( bs::Ball_State_Needs )									# Opaque wrapper for all nonephemeral mutable state maintained by look.
	  };

	Imports =  bwe::Imports;
	Exports =  bwe::Exports;

	Me_Slot = Mailslot( { imports:			Imports,
			      me:			Ball_Look_State,
			      ball_look_arg:		Ball_Look_Arg,
			      run_gun':			Run_Gun,
			      end_gun':			End_Gun,
			      look_endstate_oneshot:	Null_Or(Oneshot_Maildrop( Ball_Look_Arg )),
			      ball_look_callbacks:	List(( bl::Ball_Look -> Void		)),
			      widget_control_callbacks:	List(b2l::Bounce_To_Look -> Void)
			    }
			  );

	Runstate  =   {													# These values will be statically globally visible throughout the code body for the imp.
			me:			Ball_Look_State,							# State which is preserved across imp shutdown/restart cycles.
		        ball_look_arg:		Ball_Look_Arg,
			imports:		Imports,								# Imps to which we send requests.
			to:			Replyqueue,								# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
			end_gun':		End_Gun,								# We shut down the microthread when this fires.
			look_endstate_oneshot:	Null_Or(Oneshot_Maildrop( Ball_Look_Arg ))
		      };

	Ball_Q    = Mailqueue( Runstate -> Void );

	fun run (
		  ball_q:			Ball_Q,									# 
		  #
		  runstate as
		  {													# These values will be statically globally visible throughout the code body for the imp.
		    me:				Ball_Look_State,							# State which is preserved across imp shutdown/restart cycles.
		    ball_look_arg:		Ball_Look_Arg,
		    imports:			Imports,								# Imps to which we send requests.
		    to:				Replyqueue,								# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		    end_gun':			End_Gun,								# We shut down the microthread when this fires.
		    look_endstate_oneshot:	Null_Or(Oneshot_Maildrop( Ball_Look_Arg ))
		  }
		)
	    =
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (end_gun'                    ==>  shut_down_ball_imp'),
			    (take_from_mailqueue' ball_q ==>  do_ball_plea)
			];

			loop ();
		    }	
		    where
			fun do_ball_plea thunk
			    =
			    thunk runstate;

			fun shut_down_ball_imp' ()
			    =
			    {
				case look_endstate_oneshot								# Pass our state back to guiboss to allow later impnet restart without state loss.
				    #
				    NULL	=> ();
				    THE oneshot => put_in_oneshot (oneshot, ball_look_arg);				# 
				esac;

				thread_exit { success => TRUE };							# Will not return.	
			    };

		    end;
	    end;	



	fun startup													# Root fn of imp microthread.
	      ( id:		iul::Id,
		reply_oneshot:  Oneshot_Maildrop( (Me_Slot, Exports, bs::Ball_State_Needs -> Void) )
	      )
	      ()													# Note currying.
	    =
	    {   me_slot  =  make_mailslot  ()	:  Me_Slot;
		#
		bounce_to_look		=  { id, do_something, pass_something, pass_draw_done_flag };
		gui_to_look		=  { id, start_of_frame };
		ball_look		=  { id, do_something, pass_something };
		
		exports			=  { bounce_to_look, gui_to_look };

		to          =  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, (me_slot, exports, note_new_state));					# Return value from make_ball_look().

		(take_from_mailslot  me_slot)										# Input args from configure_default_ball_look_for_x().
		    ->
		    { me, ball_look_arg, imports, run_gun', end_gun', look_endstate_oneshot,
		      widget_control_callbacks,
		           ball_look_callbacks
		    };

		apply   {. #callback ball_look;		}   ball_look_callbacks;					# Pass our port to everyone who asked for it.
		apply   {. #callback bounce_to_look;	}   widget_control_callbacks;					# Pass our port to everyone who asked for it.

		block_until_mailop_fires  run_gun';									# Wait for the starting gun.

		run (ball_q, { me, ball_look_arg, imports, to, end_gun', look_endstate_oneshot });			# Will not return.
	    }
	    where
		ball_q     =  make_mailqueue (get_current_microthread()):  Ball_Q;


		fun note_new_state (state: bs::Ball_State_Needs)							# PUBLIC.
		    =	
		    put_in_mailqueue  (ball_q,
			#
			\\ ({ me, ... }: Runstate)
			    =
			    me.state := state										# Do we need to notify guiboss that we need to redraw?
		    );


		#######################################################################
		# gui_to_look fns:

		fun start_of_frame							# We get this call at the start of every frame from   src/lib/x-kit/widget/gui/guiboss-imp.pkg
		      {
			frame_number:		Int,					# 1,2,3,... Purely for convenience of look-imp, guiboss-imp makes no use of this.
			site:			xg::Box,				# Window rectangle in which to draw.
			visible:		Bool,					# If FALSE, widget is not visible on screen, so look-imp may be able to avoid updating foreground and background.
			duration_in_seconds:	Float					# If state has changed look-imp should call note_changed_widget_foreground() before this time is up. Also useful for motionblur.
		      }
		    =
		    put_in_mailqueue  (ball_q,
			#
			\\ ({ me, imports, ... }: Runstate)
			    =
			    {   background_box =  site;
				foreground_box =  xg::box::make_nested_box (background_box, 4);
				#
				background = gd::COLOR (r8::rgb8_blue, [ gd::POLY_FILL_BOX [ background_box ]]);
				foreground = gd::COLOR (r8::rgb8_red,  [ gd::POLY_FILL_BOX [ foreground_box ]]);

				id = me.id;

				imports.look_to_gui.note_changed_widget_foreground { id, foreground };
				imports.look_to_gui.note_changed_widget_background { id, background };
			    }
		    );


		#######################################################################
		# bounce_to_look fns:

		fun do_something (i: Int)										# PUBLIC.
		    =	
		    put_in_mailqueue  (ball_q,
			#
			\\ ({ imports, ... }: Runstate)
			    =
			    imports.int_sink i										# Demonstrate use of imports.
		    );


		fun pass_something  (replyqueue: Replyqueue)  (reply_handler: Int -> Void)				# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (ball_q,
			    #
			    \\ (_: Runstate)
				=
				put_in_oneshot (reply_oneshot, 0)
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };

		fun pass_draw_done_flag  (replyqueue: Replyqueue)  (reply_handler: Void -> Void)			# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Void );
			#
			put_in_mailqueue  (ball_q,
			    #
			    \\ (_: Runstate)
				=
				put_in_oneshot (reply_oneshot, ())
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };
	    end;


	fun process_options
	    ( options: List(Ball_Look_Option),
	      #
	      { name,
		id,
		ball_look_callbacks,
		widget_control_callbacks
	      }
	    )
	    =
	    {   my_name				=  REF name;
		my_id				=  REF id;
		my_widget_control_callbacks	=  REF widget_control_callbacks;
		my_ball_look_callbacks		=  REF      ball_look_callbacks;
		#
		apply  do_option  options
		where
		    fun do_option (MICROTHREAD_NAME        n) =>   my_name			:=  n;
			do_option (ID      	           i) =>   my_id                   	:=  i;
			#
			do_option (WIDGET_CONTROL_CALLBACK c) =>   my_widget_control_callbacks	:=  c ! *my_widget_control_callbacks;
			do_option (BALL_LOOK_CALLBACK      c) =>   my_ball_look_callbacks	:=  c ! *my_ball_look_callbacks;
		    end;
		end;

		{ name			    =>  *my_name,
		  id 			    =>  *my_id,
		  ball_look_callbacks       =>  *my_ball_look_callbacks,
		  widget_control_callbacks  =>  *my_widget_control_callbacks
		};
	    };


        ##########################################################################################
	# External entrypoints
	#
	fun make_ball_egg												# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
	      (
#		appwindow:		g2w::Appwindow,
		ball_arg:		bs::Ball_Arg,
		ball_look_arg:		Ball_Look_Arg,
# XXX SUCKO FIXME look_to_bounce is not yet used anywhere.
		look_to_bounce:		l2b::Look_To_Bounce,
		state_endstate_oneshot:	Null_Or(Oneshot_Maildrop( bs::Ball_Arg )),					# guiboss uses this to capture final state of state-imp when endgun fires.
		 look_endstate_oneshot: Null_Or(Oneshot_Maildrop( Ball_Look_Arg ))					# guiboss uses this to capture final state of  look-imp when endgun fires.
	      )
	    =
	    {
		ball_look_arg -> (		    ball_look_options);
		ball_arg      -> (ball_state_needs, ball_state_options);

		(process_options
		  ( ball_look_options,
		    { name			=>  "ball_look",
		      id			=>  iul::id_zero,
		      ball_look_callbacks	=>  [],
		      widget_control_callbacks	=>  []
		    }
		) )
		    ->
		    { name,
		      id, 
		      ball_look_callbacks,
		      widget_control_callbacks
		    };
	
		my (id, ball_look_options)
		    =
		    if (iul::id_to_int(id) == 0)
			id = iul::issue_unique_id();										# Allocate unique imp id.
			(id, ID id ! ball_look_options);								# Make our id stable across stop/restart cycles.
		    else
			(id, ball_look_options);									# Nop.
		    fi;

		ball_look_arg = (ball_look_options);

		me = { id, state => REF ball_state_needs };

		ball_state_egg =  bsi::make_ball_state_egg  ball_arg  state_endstate_oneshot;

		\\ () = {   reply_oneshot										# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
				=
				make_oneshot_maildrop ()
				:    Oneshot_Maildrop( (Me_Slot, Exports, bs::Ball_State_Needs -> Void) );

			    xlogger::make_thread  name  (startup  (id, reply_oneshot));					# Note that startup() is curried.

			    (get_from_oneshot  reply_oneshot) -> (me_slot, exports, note_new_state);

			    (ball_state_egg ()) -> (ball_state_exports, ball_state_egg');

			    fun phase3											# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				(
				  imports:	Imports,
				  run_gun':	Run_Gun,	
				  end_gun':	End_Gun
				)
				=
				{
				    ball_state_imports = { state_sink => note_new_state };

				    ball_state_egg' (ball_state_imports, run_gun', end_gun');

				    put_in_mailslot  (me_slot, { me, ball_look_arg, imports, run_gun', end_gun', look_endstate_oneshot,
								 ball_look_callbacks,
								 widget_control_callbacks
							       }
						     );
				};

			    (exports, phase3);
			};
	    };


	fun pprint_ball_look_arg
	      (pp:		pp::Prettyprint_Mill)
	      (ball_look_arg:	Ball_Look_Arg)
	    =
	    {
		ball_look_arg
		  ->
		  ( options:	List(Ball_Look_Option)
		  );

		pp.box {.
		    pp.txt " [";
		    pp::seq {. pp.txt ", "; }
			    pprint_option
			    options
			    ;	
		    pp.txt " ]";
		};
	    }
	    where
		fun pprint_option option
		    =
		    case option
			#
			MICROTHREAD_NAME name		=>  {  pp.lit (sprintf "MICROTHREAD_NAME \"%s\"" name);		};
			ID			id	=>  {  pp.lit (sprintf "ID %d" (iul::id_to_int id)        );	};
			WIDGET_CONTROL_CALLBACK _	=>  {  pp.lit          "WIDGET_CONTROL_CALLBACK (callback)";	};
			BALL_LOOK_CALLBACK _		=>  {  pp.lit          "BALL_LOOK_CALLBACK (callback)";	};
		    esac;
	    end;
    };

end;
