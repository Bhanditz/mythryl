## default-node-look-for-x.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include threadkit;						# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ap  =  atom_port;					# atom_port			is from   src/lib/x-kit/xclient/src/iccc/atom-port.pkg
#   package au  =  authentication;				# authentication		is from   src/lib/x-kit/xclient/src/stuff/authentication.pkg
#   package cpm =  cs_pixmap;					# cs_pixmap			is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#   package cpt =  cs_pixmat;					# cs_pixmat			is from   src/lib/x-kit/xclient/src/window/cs-pixmat.pkg
#   package dy  =  display;					# display			is from   src/lib/x-kit/xclient/src/wire/display.pkg
#   package et  =  event_types;					# event_types			is from   src/lib/x-kit/xclient/src/wire/event-types.pkg
#   package exp =  xserver;					# xserver			is from   src/lib/x-kit/xclient/src/window/xserver.pkg
#   package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
#   package fti =  font_index;					# font_index			is from	  src/lib/x-kit/xclient/src/window/font-index.pkg
#   package kp  =  keymap;					# keymap			is from   src/lib/x-kit/xclient/src/window/keymap.pkg
#   package mtx =  rw_matrix;					# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
#   package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package rgb =  rgb;						# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
#   package rop =  ro_pixmap;					# ro_pixmap			is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
#   package rw  =  root_window;					# root_window			is from   src/lib/x-kit/widget/lib/root-window.pkg
#   package rwv =  rw_vector;					# rw_vector			is from   src/lib/std/src/rw-vector.pkg
#   package s2p =  xevent_to_window;				# xevent_to_window		is from   src/lib/x-kit/xclient/src/window/xevent-to-window.pkg
#   package sep =  selection_port;				# selection_port		is from   src/lib/x-kit/xclient/src/window/selection-port.pkg
#   package shp =  shade;					# shade				is from   src/lib/x-kit/widget/lib/shade.pkg
#   package sj  =  socket_junk;					# socket_junk			is from   src/lib/internet/socket-junk.pkg
#   package sp  =  xsequencer;					# xsequencer			is from   src/lib/x-kit/xclient/src/wire/xsequencer.pkg
#   package tr  =  logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
#   package tsr =  thread_scheduler_is_running;			# thread_scheduler_is_running	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler-is-running.pkg
#   package u1  =  one_byte_unt;				# one_byte_unt			is from   src/lib/std/one-byte-unt.pkg
#   package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
#   package v2w =  value_to_wire;				# value_to_wire			is from   src/lib/x-kit/xclient/src/wire/value-to-wire.pkg
#   package wg  =  widget;					# widget			is from   src/lib/x-kit/widget/basic/widget.pkg
#   package wi  =  window;					# window			is from   src/lib/x-kit/xclient/src/window/window.pkg
#   package wme =  window_map_event_sink;			# window_map_event_sink		is from   src/lib/x-kit/xclient/src/window/window-map-event-sink.pkg
#   package wpp =  window_watcher;				# window_watcher		is from   src/lib/x-kit/xclient/src/window/window-watcher.pkg
#   package wy  =  widget_style;				# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
#   package x2s =  xevent_to_string;				# xevent_to_string		is from   src/lib/x-kit/xclient/src/to-string/xevent-to-string.pkg
#   package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
#   package xg  =  xgeometry;					# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
#   package xj  =  xsession_junk;				# xsession_junk			is from   src/lib/x-kit/xclient/src/window/xsession-junk.pkg
#   package xt  =  xtypes;					# xtypes			is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg

    package aw  =  appwindow;					# appwindow			is from   src/lib/x-kit/widget/theme/appwindow.pkg
    package nl  =  node_look;					# node_look			is from   src/lib/x-kit/widget/theme/packed/look/node-look.pkg
    package nli =  node_look_imp;				# node_look_imp			is from   src/lib/x-kit/widget/theme/canvas/look/node-look-imp.pkg
    package nsi =  node_state_imp;				# node_state_imp		is from   src/lib/x-kit/widget/space/canvas/node/node-state-imp.pkg
    package cwc =  canvas_widget_control;			# canvas_widget_control		is from   src/lib/x-kit/widget/space/canvas/canvas-widget-control.pkg
    package cwe =  canvas_widget_egg;				# canvas_widget_egg		is from   src/lib/x-kit/widget/space/canvas-widget-egg.pkg
    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    #
    tracefile   =  "widget-unit-test.trace.log";
herein

    package default_node_look_imp_for_x
    :               Node_Look_Imp											# Node_Look_Imp	is from   src/lib/x-kit/widget/theme/canvas/node-look-imp.api
    {
	#
	include node_look;												# node_look		is from   src/lib/x-kit/widget/theme/canvas/node-look.pkg
	#
	Node_Look_State = Ref( nsi::Node_State_Needs );									# Opaque wrapper for all nonephemeral mutable state maintained by look.

	Imports =  cwe::Imports;
	Exports =  cwe::Exports;

	Me_Slot = Mailslot( { imports:			Imports,
			      me:			Node_Look_State,
			      run_gun':			Run_Gun,
			      end_gun':			End_Gun,
			      endstate_oneshot:		Null_Or(Oneshot_Maildrop( nsi::Node_Arg )),
			      node_look_callbacks:	List( ( nl::Node_Look -> Void		 )),
			      widget_control_callbacks:	List(cwc::Canvas_Widget_Control -> Void)
			    }
			  );
	Node_Look_Option == nli::Node_Look_Option;
	Node_Look_Arg	 =  nli::Node_Look_Arg;

	Node_Look_Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);

	Runstate =  {													# These values will be statically globally visible throughout the code body for the imp.
		      me:		Node_Look_State,								# State which is preserved across imp shutdown/restart cycles.
		      imports:		Imports,									# Imps to which we send requests.
		      to:		Replyqueue,									# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		      end_gun':		End_Gun,									# We shut down the microthread when this fires.
		      endstate_oneshot:	Null_Or(Oneshot_Maildrop( nsi::Node_Arg ))
		    };

	Node_Q    = Mailqueue( Runstate -> Void );


	fun run ( node_q:		Node_Q,										# 
		  #
		  runstate as
		  {													# These values will be statically globally visible throughout the code body for the imp.
		    me:			Node_Look_State,								# State which is preserved across imp shutdown/restart cycles.
		    imports:		Imports,									# Imps to which we send requests.
		    to:			Replyqueue,									# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		    end_gun':		End_Gun,									# We shut down the microthread when this fires.
		    endstate_oneshot:	Null_Or(Oneshot_Maildrop( nsi::Node_Arg ))
		  }
		)
	    =
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (end_gun'                     ==>  shut_down_node_imp'),
			    (take_from_mailqueue' node_q  ==>  do_node_plea)
			];

			loop ();
		    }	
		    where
			fun do_node_plea thunk
			    =
			    thunk runstate;

			fun shut_down_node_imp' ()
			    =
			    {
# XXX BUGGO NOT YET USING endstate_oneshot:
				thread_exit { success => TRUE };							# Will not return.	
			    };
		    end;
	    end;	



	fun startup   (reply_oneshot:  Oneshot_Maildrop( (Me_Slot, Exports, nsi::Node_State_Needs -> Void) ))   ()	# Root fn of imp microthread.  Note currying.
	    =
	    {   me_slot  =  make_mailslot  ()	:  Me_Slot;
		#
		canvas_widget_control	=  { do_something, pass_something, pass_draw_done_flag };
		node_look		=  { do_something, pass_something };
		exports			=  { canvas_widget_control };

		to          =  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, (me_slot, exports, note_new_state));					# Return value from make_node_look().

		(take_from_mailslot  me_slot)										# Input args from configure_default_node_look_for_x().
		    ->
		    { me, imports, run_gun', end_gun', endstate_oneshot,
                      widget_control_callbacks,
		           node_look_callbacks
                    };

		apply   {. #callback node_look;			}        node_look_callbacks;				# Pass our port to everyone who asked for it.
		apply   {. #callback canvas_widget_control;	}   widget_control_callbacks;				# Pass our port to everyone who asked for it.

		block_until_mailop_fires  run_gun';									# Wait for the starting gun.

		run (node_q, { me, imports, to, end_gun', endstate_oneshot });						# Will not return.
	    }
	    where
		node_q     =  make_mailqueue (get_current_microthread()):  Node_Q;


		fun note_new_state (state: nsi::Node_State_Needs)							# External entrypoint.
		    =	
		    put_in_mailqueue  (node_q,
			#
			fn ({ me, ... }: Runstate)
			    =
			    me := state											# Do we need to notify gui-boss that we need to redraw?
		    );

		fun do_something (i: Int)										# External entrypoint.
		    =	
		    put_in_mailqueue  (node_q,
			#
			fn ({ imports, ... }: Runstate)
			    =
			    imports.int_sink i										# Demonstrate use of imports.
		    );


		fun pass_something  (replyqueue: Replyqueue)  (reply_handler: Int -> Void)				# External entrypoint.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (node_q,
			    #
			    fn (_: Runstate)
				=
				put_in_oneshot (reply_oneshot, 0)
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };

		fun pass_draw_done_flag  (replyqueue: Replyqueue)  (reply_handler: Void -> Void)			# External entrypoint.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Void );
			#
			put_in_mailqueue  (node_q,
			    #
			    fn (_: Runstate)
				=
				put_in_oneshot (reply_oneshot, ())
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };
	    end;


	fun process_options
            ( options:	List(Node_Look_Option),
	      #
	      { name,
		node_look_callbacks,
		widget_control_callbacks
	      }
	    )
	    =
	    {   my_name				=  REF name;
		my_widget_control_callbacks	=  REF widget_control_callbacks;
		my_node_look_callbacks		=  REF      node_look_callbacks;
		#
		apply  do_option  options
		where
		    fun do_option (MICROTHREAD_NAME        n) =>   my_name			:=  n;
			do_option (WIDGET_CONTROL_CALLBACK c) =>   my_widget_control_callbacks	:=  c ! *my_widget_control_callbacks;
			do_option (NODE_LOOK_CALLBACK      c) =>   my_node_look_callbacks	:=  c ! *my_node_look_callbacks;
		    end;
		end;

		{ name			    =>  *my_name,
		  node_look_callbacks       =>  *my_node_look_callbacks,
		  widget_control_callbacks  =>  *my_widget_control_callbacks
		};
	    };


        ##########################################################################################
	# External entrypoints
	#
	fun make_node_egg												# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
	      (
#		appwindow:		aw::Appwindow,
		node_arg:		nsi::Node_Arg,
		options:		List(Node_Look_Option),
		endstate_oneshot:	Null_Or(Oneshot_Maildrop( nsi::Node_Arg ))
	      )
	    =
	    {   (process_options
		  ( options,
		    { name			=>  "node_look",
		      node_look_callbacks	=>  [],
		      widget_control_callbacks	=>  []
		    }
		) )
		    ->
		    { name,
		      node_look_callbacks,
		      widget_control_callbacks
		    };
	
		node_arg -> (node_state_needs, node_state_options);

		me = REF node_state_needs;

		node_state_egg =  nsi::make_node_state_egg  node_arg  endstate_oneshot;

		fn () = {   reply_oneshot										# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
				=
				make_oneshot_maildrop ()
				:    Oneshot_Maildrop( (Me_Slot, Exports, nsi::Node_State_Needs -> Void) );

			    xlogger::make_thread  name  (startup  reply_oneshot);					# Note that startup() is curried.

			    (get_from_oneshot  reply_oneshot) -> (me_slot, exports, note_new_state);

			    (node_state_egg ()) -> (node_state_exports, node_state_egg');

			    fun phase3											# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				(
				  imports:	Imports,
				  run_gun':	Run_Gun,	
				  end_gun':	End_Gun
				)
				=
				{   node_state_imports = { state_sink => note_new_state };
				    #
				    node_state_egg' (node_state_imports, run_gun', end_gun');

				    put_in_mailslot  (me_slot, { me, imports, run_gun', end_gun', endstate_oneshot,
								 node_look_callbacks,
								 widget_control_callbacks
							       }
						     );
				};

			    (exports, phase3);
			};
	    };


	fun pprint_node_look_arg
	      (pp:		pp::Prettyprint_Mill)
	      (node_look_arg:	Node_Look_Arg)
	    =
	    {
		node_look_arg
		  ->
		  ( options:	List(Node_Look_Option)
		  );

		pp.box {.
		    pp.txt " [";
		    pp::seq {. pp.txt ", "; }
			    pprint_option
			    options
			    ;	
		    pp.txt " ]";
		};
	    }
	    where
		fun pprint_option option
		    =
		    case option
			#
			MICROTHREAD_NAME name		=>  {  pp.lit (sprintf "MICROTHREAD_NAME \"%s\"" name);		};
			WIDGET_CONTROL_CALLBACK _	=>  {  pp.lit          "WIDGET_CONTROL_CALLBACK (callback)";	};
			NODE_LOOK_CALLBACK _		=>  {  pp.lit          "NODE_LOOK_CALLBACK (callback)";	};
		    esac;
	    end;
    };

end;
