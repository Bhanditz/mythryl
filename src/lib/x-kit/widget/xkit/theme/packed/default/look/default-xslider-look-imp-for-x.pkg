## default-xslider-look-for-x.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include threadkit;						# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ap  =  atom_port;					# atom_port			is from   src/lib/x-kit/xclient/src/iccc/atom-port.pkg
#   package au  =  authentication;				# authentication		is from   src/lib/x-kit/xclient/src/stuff/authentication.pkg
#   package cpm =  cs_pixmap;					# cs_pixmap			is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#   package cpt =  cs_pixmat;					# cs_pixmat			is from   src/lib/x-kit/xclient/src/window/cs-pixmat.pkg
#   package dy  =  display;					# display			is from   src/lib/x-kit/xclient/src/wire/display.pkg
#   package et  =  event_types;					# event_types			is from   src/lib/x-kit/xclient/src/wire/event-types.pkg
#   package exp =  widget_to_xserver;				# widget_to_xserver		is from   src/lib/x-kit/xclient/src/window/widget-to-xserver.pkg
#   package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
#   package fti =  font_index;					# font_index			is from	  src/lib/x-kit/xclient/src/window/font-index.pkg
#   package kp  =  keymap;					# keymap			is from   src/lib/x-kit/xclient/src/window/keymap.pkg
#   package mtx =  rw_matrix;					# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
#   package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package rgb =  rgb;						# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
#   package rop =  ro_pixmap;					# ro_pixmap			is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
#   package rw  =  root_window;					# root_window			is from   src/lib/x-kit/widget/lib/root-window.pkg
#   package rwv =  rw_vector;					# rw_vector			is from   src/lib/std/src/rw-vector.pkg
#   package s2p =  xevent_to_window;				# xevent_to_window		is from   src/lib/x-kit/xclient/src/window/xevent-to-window.pkg
#   package sep =  selection_port;				# selection_port		is from   src/lib/x-kit/xclient/src/window/selection-port.pkg
#   package shp =  shade;					# shade				is from   src/lib/x-kit/widget/lib/shade.pkg
#   package sj  =  socket_junk;					# socket_junk			is from   src/lib/internet/socket-junk.pkg
#   package sp  =  xclient_to_sequencer;			# xclient_to_sequencer		is from   src/lib/x-kit/xclient/src/wire/xclient-to-sequencer.pkg
#   package tr  =  logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
#   package tsr =  thread_scheduler_is_running;			# thread_scheduler_is_running	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler-is-running.pkg
#   package u1  =  one_byte_unt;				# one_byte_unt			is from   src/lib/std/one-byte-unt.pkg
#   package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
#   package v2w =  value_to_wire;				# value_to_wire			is from   src/lib/x-kit/xclient/src/wire/value-to-wire.pkg
#   package wg  =  widget;					# widget			is from   src/lib/x-kit/widget/basic/widget.pkg
#   package wi  =  window;					# window			is from   src/lib/x-kit/xclient/src/window/window.pkg
#   package wme =  window_map_event_sink;			# window_map_event_sink		is from   src/lib/x-kit/xclient/src/window/window-map-event-sink.pkg
#   package wpp =  window_watcher;				# window_watcher		is from   src/lib/x-kit/xclient/src/window/window-watcher.pkg
#   package wy  =  widget_style;				# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
#   package x2s =  xevent_to_string;				# xevent_to_string		is from   src/lib/x-kit/xclient/src/to-string/xevent-to-string.pkg
#   package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
#   package xg  =  xgeometry;					# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
#   package xj  =  xsession_junk;				# xsession_junk			is from   src/lib/x-kit/xclient/src/window/xsession-junk.pkg
#   package xt  =  xtypes;					# xtypes			is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg

    package aw  =  appwindow;					# appwindow			is from   src/lib/x-kit/widget/theme/appwindow.pkg
    package iui =  issue_unique_id;				# issue_unique_id		is from   src/lib/src/issue-unique-id.pkg
    package pwe =  packed_widget_egg;				# packed_widget_egg		is from   src/lib/x-kit/widget/space/packed-widget-egg.pkg
    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package ss  =  slider_state;				# slider_state			is from   src/lib/x-kit/widget/space/packed/slider/slider-state.pkg
    package ssi =  slider_state_imp;				# slider_state_imp		is from   src/lib/x-kit/widget/space/packed/slider/slider-state-imp.pkg
    package xl  =  xslider_look;				# xslider_look			is from   src/lib/x-kit/widget/theme/packed/look/xslider-look.pkg
    #
    package l2p =  look_to_packed;				# look_to_packed		is from   src/lib/x-kit/widget/space/packed/look-to-packed.pkg
    package p2l =  packed_to_look;				# packed_to_look		is from   src/lib/x-kit/widget/space/packed/packed-to-look.pkg
    #
    package xg  =  xgeometry;					# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
    #
    tracefile   =  "widget-unit-test.trace.log";
herein

    package default_xslider_look_imp_for_x
    :               Xslider_Look_Imp											# Xslider_Look_Imp	is from   src/lib/x-kit/widget/theme/packed/xslider-look-imp.api
    {
	#
	include xslider_look;												# xslider_look		is from   src/lib/x-kit/widget/theme/packed/xslider-look.pkg
	#
	Xslider_Look_State
	  =
	  { id:		Id,
	    state:	Ref( ss::Slider_State_Needs )									# Opaque wrapper for all nonephemeral mutable state maintained by look.
	  };

	Imports =  pwe::Imports;
	Exports =  pwe::Exports;

	Xslider_Look_Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);

	Me_Slot = Mailslot( { imports:			Imports,
			      me:			Xslider_Look_State,
			      xslider_look_arg:		Xslider_Look_Arg,
			      run_gun':			Run_Gun,
			      end_gun':			End_Gun,
			      look_endstate_oneshot:	Null_Or(Oneshot_Maildrop( Xslider_Look_Arg )),
			      xslider_look_callbacks:	List(          xl::Xslider_Look -> Void),
			      widget_control_callbacks:	List(p2l::Packed_To_Look -> Void)
			    }
			  );

	Runstate =  {													# These values will be statically globally visible throughout the code body for the imp.
		      me:			Xslider_Look_State,							# State which is preserved across imp shutdown/restart cycles.
		      xslider_look_arg:		Xslider_Look_Arg,
		      imports:			Imports,								# Imps to which we send requests.
		      to:			Replyqueue,								# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		      end_gun':			End_Gun,								# We shut down the microthread when this fires.
		      look_endstate_oneshot:	Null_Or(Oneshot_Maildrop( Xslider_Look_Arg ))
		    };

	Xslider_Q    = Mailqueue( Runstate -> Void );

	fun run ( xslider_q:			Xslider_Q,								# 
		  #
		  runstate as
		  {													# These values will be statically globally visible throughout the code body for the imp.
		    me:				Xslider_Look_State,							# State which is preserved across imp shutdown/restart cycles.
		    xslider_look_arg:		Xslider_Look_Arg,
		    imports:			Imports,								# Imps to which we send requests.
		    to:				Replyqueue,								# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		    end_gun':			End_Gun,								# We shut down the microthread when this fires.
		    look_endstate_oneshot:	Null_Or(Oneshot_Maildrop( Xslider_Look_Arg ))
		  }
		)
	    =
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (end_gun'                        ==>  shut_down_xslider_imp'),
			    (take_from_mailqueue' xslider_q  ==>  do_xslider_plea)
			];

			loop ();
		    }	
		    where
			fun do_xslider_plea thunk
			    =
			    thunk runstate;

			fun shut_down_xslider_imp' ()
			    =
			    {
				case look_endstate_oneshot								# Pass our state back to gui-boss to allow later impnet restart without state loss.
				    #
				    NULL	=> ();
				    THE oneshot => put_in_oneshot (oneshot, xslider_look_arg);				# 
				esac;

				thread_exit { success => TRUE };							# Will not return.	
			    };
		    end;
	    end;	



	fun startup													# Root fn of imp microthread.
	      ( id:		Id,
		reply_oneshot:  Oneshot_Maildrop( (Me_Slot, Exports, ss::Slider_State_Needs -> Void) )
	      )
	      ()													# Note currying.
	    =
	    {   me_slot  =  make_mailslot  ()	:  Me_Slot;
		#
		packed_to_look		=  { id, do_something, pass_something, pass_draw_done_flag };
		gui_to_look		=  { id, start_of_frame };
		xslider_look		=  { id, do_something, pass_something };

		exports			=  { gui_to_look, packed_to_look };

		to          =  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, (me_slot, exports, note_new_state));					# Return value from make_xslider_look().

		(take_from_mailslot  me_slot)										# Input args from configure_default_xslider_look_for_x().
		    ->
		    { me, xslider_look_arg, imports, run_gun', end_gun', look_endstate_oneshot,
		      widget_control_callbacks,
		        xslider_look_callbacks
		    };

		apply   {. #callback xslider_look;   }   xslider_look_callbacks;					# Pass our port to everyone who asked for it.
		apply   {. #callback packed_to_look; }   widget_control_callbacks;					# Pass our port to everyone who asked for it.

		block_until_mailop_fires  run_gun';									# Wait for the starting gun.

		run (xslider_q, { me, xslider_look_arg, imports, to, end_gun', look_endstate_oneshot });		# Will not return.
	    }
	    where
		xslider_q     =  make_mailqueue (get_current_microthread()):  Xslider_Q;


		#######################################################################
		# gui_to_look fns:

		fun start_of_frame							# We get this call at the start of every frame from   src/lib/x-kit/widget/gui/gui-boss-imp.pkg
		      {
			frame_number:		Int,					# 1,2,3,... Purely for convenience of look-imp, gui-boss-imp makes no use of this.
			site:			xg::Box,				# Window rectangle in which to draw.
			visible:		Bool,					# If FALSE, widget is not visible on screen, so look-imp may be able to avoid updating foreground and background.
			duration_in_seconds:	Float					# If state has changed look-imp should call note_changed_widget_foreground() before this time is up. Also useful for motionblur.
		      }
		    =
		    put_in_mailqueue  (xslider_q,
			#
			fn ({ imports, ... }: Runstate)
			    =
			    ()
		    );



		#######################################################################
		# packed_to_look fns:

		fun note_new_state (state: ss::Slider_State_Needs)							# External entrypoint.
		    =	
		    put_in_mailqueue  (xslider_q,
			#
			fn ({ me, ... }: Runstate)
			    =
			    me.state := state										# Do we need to notify gui-boss that we need to redraw?
		    );


		fun do_something (i: Int)										# External entrypoint.
		    =	
		    put_in_mailqueue  (xslider_q,
			#
			fn ({ imports, ... }: Runstate)
			    =
			    imports.int_sink i										# Demonstrate use of imports.
		    );


		fun pass_something  (replyqueue: Replyqueue)  (reply_handler: Int -> Void)				# External entrypoint.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (xslider_q,
			    #
			    fn (_: Runstate)
				=
				put_in_oneshot (reply_oneshot, 0)
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };

		fun pass_draw_done_flag  (replyqueue: Replyqueue)  (reply_handler: Void -> Void)			# External entrypoint.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Void );
			#
			put_in_mailqueue  (xslider_q,
			    #
			    fn (_: Runstate)
				=
				put_in_oneshot (reply_oneshot, ())
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };
	    end;


	fun process_options
	    ( options: List(Xslider_Look_Option),
	      #
	      { name,
		id,
	        xslider_look_callbacks,
	        widget_control_callbacks
	      }
	    )
	    =
	    {   my_name				=  REF name;
		my_id				=  REF id;
		my_xslider_look_callbacks	=  REF xslider_look_callbacks;
		my_widget_control_callbacks	=  REF widget_control_callbacks;
		#
		apply  do_option  options
		where
		    fun do_option (MICROTHREAD_NAME        n) =>   my_name			:=  n;
			do_option (ID      	           i) =>   my_id                   	:=  i;
			#
			do_option (WIDGET_CONTROL_CALLBACK c) =>   my_widget_control_callbacks	:=  c ! *my_widget_control_callbacks;
			do_option (XSLIDER_LOOK_CALLBACK   c) =>   my_xslider_look_callbacks	:=  c ! *my_xslider_look_callbacks;
		    end;
		end;

		{ name			   =>  *my_name,
		  id 			   =>  *my_id,
		  xslider_look_callbacks   =>  *my_xslider_look_callbacks,
		  widget_control_callbacks =>  *my_widget_control_callbacks
		};
	    };


        ##########################################################################################
	# External entrypoints
	#
	fun make_xslider_egg												# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
	      (
#		appwindow:		aw::Appwindow,
		slider_arg:		ss::Slider_Arg,
		xslider_look_arg:	Xslider_Look_Arg,
# XXX SUCKO FIXME look_to_packed is not yet used anywhere.
		look_to_packed:		l2p::Look_To_Packed,
		state_endstate_oneshot:	Null_Or(Oneshot_Maildrop( ss::Slider_Arg)),					# gui-boss uses this to capture final state of state-imp when endgun fires.
		 look_endstate_oneshot: Null_Or(Oneshot_Maildrop(Xslider_Look_Arg))					# gui-boss uses this to capture final state of  look-imp when endgun fires.
	      )
	    =
	    {
		slider_arg       -> (slider_state_needs, slider_state_options);
		xslider_look_arg -> (		         xslider_look_options);

		(process_options
		  ( xslider_look_options,
		    { name			=>  "xslider_look",
		      id			=>  id_zero,
		      xslider_look_callbacks	=>  [],
		      widget_control_callbacks	=>  []
		    }
		) )
		    ->
		    { name,
		      id,
		      xslider_look_callbacks,
		      widget_control_callbacks
		    };
	
		my (id, xslider_look_options)
		    =
		    if (id_to_int(id) == 0)
			id = issue_unique_id();										# Allocate unique imp id.
			(id, ID id ! xslider_look_options);								# Make our id stable across stop/restart cycles.
		    else
			(id, xslider_look_options);											# Nop.
		    fi;

		xslider_look_arg = (xslider_look_options);

		me = { id, state => REF slider_state_needs };

		slider_state_egg =  ssi::make_slider_state_egg  slider_arg  state_endstate_oneshot;

		fn () = {   reply_oneshot										# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
				=
				make_oneshot_maildrop ()
				:    Oneshot_Maildrop( (Me_Slot, Exports, ss::Slider_State_Needs -> Void) );

			    xlogger::make_thread  name  (startup  (id, reply_oneshot));					# Note that startup() is curried.

			    (get_from_oneshot  reply_oneshot) -> (me_slot, exports, note_new_state);

			    (slider_state_egg ()) -> (slider_state_exports, slider_state_egg');

			    fun phase3											# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				(
				  imports:	Imports,
				  run_gun':	Run_Gun,	
				  end_gun':	End_Gun
				)
				=
				{   slider_state_imports = { state_sink => note_new_state };
				    #
				    slider_state_egg' (slider_state_imports, run_gun', end_gun');

				    put_in_mailslot  (me_slot, { me, xslider_look_arg, imports, run_gun', end_gun', look_endstate_oneshot,
							         xslider_look_callbacks,
							         widget_control_callbacks
							       }
						     );
				};

			    (exports, phase3);
			};
	    };

	fun pprint_xslider_look_arg
	      (pp:			pp::Prettyprint_Mill)
	      (xslider_look_arg:	Xslider_Look_Arg)
	    =
	    {
		xslider_look_arg
		  ->
		  ( options:	List(Xslider_Look_Option)
		  );

		pp.box {.
		    pp.txt " [";
		    pp::seq {. pp.txt ", "; }
			    pprint_option
			    options
			    ;	
		    pp.txt " ]";
		};
	    }
	    where
		fun pprint_option option
		    =
		    case option
			#
			MICROTHREAD_NAME name		=>  {  pp.lit (sprintf "MICROTHREAD_NAME \"%s\"" name);		};
			ID			id	=>  {  pp.lit (sprintf "ID %d" (id_to_int id)        );		};
			WIDGET_CONTROL_CALLBACK _	=>  {  pp.lit          "WIDGET_CONTROL_CALLBACK (callback)";	};
			XSLIDER_LOOK_CALLBACK _		=>  {  pp.lit          "XSLIDER_LOOK_CALLBACK (callback)";	};
		    esac;
	    end;
    };

end;
