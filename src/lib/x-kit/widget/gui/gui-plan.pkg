## gui-plan.pkg
#
# A stopped_gui is an abstract gui specification.  Client
# code constructs a stopped_gui to describe a desired gui
# and hands it to guiboss-imp to run.
#
# When guiboss::restart_gui is called, guiboss converts
# the stopped_gui it is given into a running_gui tree which
# contains running widget imps and records all runtime-
# relevant information.
#
# When guiboss::stop_gui is called, the runtime running_gui
# is converted back into a stopped_gui which preserves the
# state of the GUI at shutdown time, for possible later
# resumption via guiboss::restart_gui.

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    package os  =  guiboss_to_bounce_space;			# guiboss_to_bounce_space		is from   src/lib/x-kit/widget/space/bounce/guiboss-to-bounce-space.pkg
    package osi =  bounce_space_imp;				# bounce_space_imp			is from   src/lib/x-kit/widget/space/bounce/bounce-space-imp.pkg

    package cs  =  guiboss_to_canvas_space;			# guiboss_to_canvas_space		is from   src/lib/x-kit/widget/space/canvas/guiboss-to-canvas-space.pkg
    package csi =  canvas_space_imp;				# canvas_space_imp			is from   src/lib/x-kit/widget/space/canvas/canvas-space-imp.pkg

    package ps  =  guiboss_to_packed_space;			# guiboss_to_packed_space		is from   src/lib/x-kit/widget/space/packed/guiboss-to-packed-space.pkg
    package psi =  packed_space_imp;				# packed_space_imp			is from   src/lib/x-kit/widget/space/packed/packed-space-imp.pkg

    package pwe =  packed_widget_egg;				# packed_widget_egg			is from   src/lib/x-kit/widget/space/packed/packed-widget-egg.pkg

#   package ks  =  label_look_state;				# label_look_state			is from   src/lib/x-kit/widget/look/label-look-state.pkg
    #
    package c2b =  client_to_ball_state;			# client_to_ball_state			is from   src/lib/x-kit/widget/space/bounce/client-to-ball-state.pkg
    package c2n =  client_to_node_state;			# client_to_node_state			is from   src/lib/x-kit/widget/space/canvas/client-to-node-state.pkg

    package bli =  ball_look_imp;				# ball_look_imp				is from   src/lib/x-kit/widget/xkit/theme/bounce/default/look/ball-look-imp.pkg
    package nli =  node_look_imp;				# node_look_imp				is from   src/lib/x-kit/widget/xkit/theme/canvas/default/look/node-look-imp.pkg
    #
    package bs  =  ball_state;					# ball_state				is from   src/lib/x-kit/widget/space/bounce/ball/ball-state.pkg
    package bsi =  ball_state_imp;				# ball_state_imp			is from   src/lib/x-kit/widget/space/bounce/ball/ball-state-imp.pkg

    package ns  =  node_state;					# node_state				is from   src/lib/x-kit/widget/space/canvas/node/node-state.pkg
    package nsi =  node_state_imp;				# node_state_imp			is from   src/lib/x-kit/widget/space/canvas/node/node-state-imp.pkg

#   package blx =  ball_look_imp;				# ball_look_imp				is from   src/lib/x-kit/widget/xkit/theme/packed/default/look/ball-look-imp.pkg
#   package nlx =  node_look_imp;				# node_look_imp				is from   src/lib/x-kit/widget/xkit/theme/packed/default/look/node-look-imp.pkg


    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill		is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg

    package blh =  bounce_space_layout_hint;			# bounce_space_layout_hint		is from   src/lib/x-kit/widget/space/bounce/bounce-space-layout-hint.pkg
    package clh =  canvas_space_layout_hint;			# canvas_space_layout_hint		is from   src/lib/x-kit/widget/space/canvas/canvas-space-layout-hint.pkg
    package plh =  packed_space_layout_hint;			# packed_space_layout_hint		is from   src/lib/x-kit/widget/space/packed/packed-space-layout-hint.pkg

    package gt  = gui_types;					# gui_types				is from   src/lib/x-kit/widget/gui/gui-types.pkg

    package xg  =  xgeometry;					# xgeometry				is from   src/lib/std/2d/xgeometry.pkg
    package xt  =  xtypes;					# xtypes				is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
    package r8  =  rgb8;					# rgb8					is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package xtr =  xlogger;					# xlogger				is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg

    package err =  error_message;				# error_message				is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    #
herein

    package gui_plan
    {
	package ball {
	    #
	    Value = c2b::Value;										# Bool.

	    Is_Bad_Value =  Value -> Null_Or( (Void -> String, Null_Or (Void -> Value) ));		# Given a Value, return NULL if it is valid, else a thunk returning a diagnostic
													# string explaining why it is not, plus optionally thunk computing 'nearby' valid value.
													# usable in place of the invalid value.
	    Ball_Option
	      #
	      = MICROTHREAD_NAME		String							# 
	      | ID				Id							# Unique ID for imp, issued by issue_unique_id::issue_unique_id().
	      | IS_BAD_VALUE			Is_Bad_Value
	      | CHANGE_CALLBACK			Value -> Void						# We'll call each of these callbacks each time our value changes.
	      | CLIENT_TO_STATE_CALLBACK	c2b::Client_To_State -> Void				# Client code registers this callback to get a port to us once we start up.
	      | LOOK_ARG			bli::Bounce_Look_Arg	
	      ;

	    Ball_Arg
	      =
	      {	value:				Value,
		options:			List( Ball_Option )
	      };

	    fun pprint_ball_arg
		  (pp:		pp::Prettyprint_Mill)
		  (ball_arg:	Ball_Arg)
		=
		{
		    ball_arg
		      ->
		      { value:			Value,
			options:		List( Ball_Option )
		      };

		    pp.box {.
			pp.txt (sprintf " { value => %s," (c2b::value_to_string value));
			pp.txt " options => [";
			pp::seq {. pp.txt ", "; }
				pprint_option
				options
				;	
			pp.txt " ]";
			pp.txt " }";
		    };
		}
		where
		    fun pprint_option option
			=
			case option
			    #
			    MICROTHREAD_NAME name	=>  {  pp.lit (sprintf "MICROTHREAD_NAME \"%s\"" name);		};
			    ID			id	=>  {  pp.lit (sprintf "ID %d" (id_to_int id)        );		};
			    IS_BAD_VALUE _		=>  {  pp.lit          "IS_BAD_VALUE (\\)";			};
			    CHANGE_CALLBACK _		=>  {  pp.lit          "CHANGE_CALLBACK (callback)";		};
			    CLIENT_TO_STATE_CALLBACK _	=>  {  pp.lit          "CLIENT_TO_STATE_CALLBACK (callback)";	};
			    LOOK_ARG _			=>  {  pp.lit          "LOOK_ARG ...";				};	# XXX SUCKO FIXME should do a better job here.
			esac;
		end;
	};

	package node {
	    #
	    Value = c2n::Value;										# Bool.

	    Is_Bad_Value =  Value -> Null_Or( (Void -> String, Null_Or (Void -> Value) ));		# Given a Value, return NULL if it is valid, else a thunk returning a diagnostic
													# string explaining why it is not, plus optionally thunk computing 'nearby' valid value.
													# usable in place of the invalid value.
	    Node_Option
	      #
	      = MICROTHREAD_NAME		String							# 
	      | ID				Id							# Unique ID for imp, issued by issue_unique_id::issue_unique_id().
	      | IS_BAD_VALUE			Is_Bad_Value
	      | CHANGE_CALLBACK			Value -> Void						# We'll call each of these callbacks each time our value changes.
	      | CLIENT_TO_STATE_CALLBACK	c2n::Client_To_State -> Void				# Client code registers this callback to get a port to us once we start up.
	      | LOOK_ARG			nli::Canvas_Look_Arg	
	      ;

	    Node_Arg
	      =
	      {	value:				Value,
		options:			List( Node_Option )
	      };

	    fun pprint_node_arg
		  (pp:		pp::Prettyprint_Mill)
		  (node_arg:	Node_Arg)
		=
		{
		    node_arg
		      ->
		      { value:			Value,
			options:		List( Node_Option )
		      };

		    pp.box {.
			pp.txt (sprintf " { value => %s," (c2n::value_to_string value));
			pp.txt " options => [";
			pp::seq {. pp.txt ", "; }
				pprint_option
				options
				;	
			pp.txt " ]";
			pp.txt " }";
		    };
		}
		where
		    fun pprint_option option
			=
			case option
			    #
			    MICROTHREAD_NAME name	=>  {  pp.lit (sprintf "MICROTHREAD_NAME \"%s\"" name);		};
			    ID			id	=>  {  pp.lit (sprintf "ID %d" (id_to_int id)        );		};
			    IS_BAD_VALUE _		=>  {  pp.lit          "IS_BAD_VALUE (\\)";			};
			    CHANGE_CALLBACK _		=>  {  pp.lit          "CHANGE_CALLBACK (callback)";		};
			    CLIENT_TO_STATE_CALLBACK _	=>  {  pp.lit          "CLIENT_TO_STATE_CALLBACK (callback)";	};
			    LOOK_ARG _			=>  {  pp.lit          "LOOK_ARG ...";				};	# XXX SUCKO FIXME should do a better job here.
			esac;
		end;
	};


	#
	Bounce_Widget
	    #
	    = BALL		ball::Ball_Arg

	also
	Canvas_Widget
	    #
	    = PACKED_SPACE	(ps::Packed_Space_Arg,  Packed_Widget)
	    #
	    | NODE		node::Node_Arg

	also
	Packed_Widget
	    #
	    = ROW		 List( (Packed_Widget, List(plh::Packed_Space_Layout_Hint)) )
	    | COL		 List( (Packed_Widget, List(plh::Packed_Space_Layout_Hint)) )
	    #	
	    | WIDGET		gt::Start_Packed_Widget_Fn
	    #
	    | CANVAS_SPACE	(cs::Canvas_Space_Arg,  List((Canvas_Widget, List(clh::Canvas_Space_Layout_Hint))))
	    | BOUNCE_SPACE	(os::Bounce_Space_Arg,  List((Bounce_Widget, List(blh::Bounce_Space_Layout_Hint))))
	    #
	    | NULL_PACKED_WIDGET									# Note[1]
	    ;

	Gui_Plan =  (ps::Packed_Space_Arg,  Packed_Widget);

	fun pprint_gui_plan (gui_plan: Gui_Plan)							# "pprint" == "prettyprint".
	    =
	    pp::with_standard_prettyprint_mill
		#
		(err::default_plaint_sink ())	[]
		#
		(\\ pp:   pp::Prettyprint_Mill
		    =
		    do_gui_plan  gui_plan
		    where
			fun do_gui_plan  gui_plan
			    =
			    do_packed_space  gui_plan

			also
			fun do_packed_space
			      ( packed_space_arg:	ps::Packed_Space_Arg,
				packed_widget:		Packed_Widget
			      )
			    =
			    {   pp.box {.
				    do_packed_space_arg  packed_space_arg;
				    do_packed_widget     packed_widget;
				};
				pp.newline();
			    }

			also
			fun do_bounce_space
			      (
				bounce_space_arg:	os::Bounce_Space_Arg,
				bounce_widgets:		List(  (Bounce_Widget, List(blh::Bounce_Space_Layout_Hint))  )
			      )
			    =
			    {   pp.box {.
				    do_bounce_space_arg  bounce_space_arg;
				    do_bounce_widgets    bounce_widgets;
				};
				pp.newline();
			    }

			also
			fun do_bounce_space_arg  (bounce_space_arg:	os::Bounce_Space_Arg)
			    =
			    {
				osi::pprint_bounce_space_arg  pp  bounce_space_arg;
				pp.newline();
			    }


			also
			fun do_bounce_space_layout_hints  (layout_hints: List(blh::Bounce_Space_Layout_Hint))
			    =
			    {   title = "";
				#
				fun do_hint (hint:  blh::Bounce_Space_Layout_Hint)
				    =
				    osi::pprint_bounce_space_layout_hint  pp  hint;

				pp::list  pp  do_hint  title  layout_hints;
			    }

			also
			fun do_bounce_widgets  (bounce_widgets:		List(  (Bounce_Widget, List(blh::Bounce_Space_Layout_Hint))  ))
			    =
			    {
				pp.box' 0 -1 {.
				    pp.lit  "[";
				    pp.ind 2;
				    pp.txt " ";

				    fun do_widget (bounce_widget: Bounce_Widget,  layout_hints: List(blh::Bounce_Space_Layout_Hint))
					=
					pp.box {.
					    do_bounce_widget			bounce_widget;
					    pp.endlit ",";
					    pp.txt " ";
					    do_bounce_space_layout_hints	layout_hints;
					};

				    pp::seq
					{.   pp.endlit ",";   pp.txt " ";   }	# Inter-element separator.
					do_widget				# Print one list element.
					bounce_widgets;				# List of elements.

				    pp.ind 0;
				    pp.txt " ";
				    pp.lit "]";
				};
			    }

			also
			fun do_bounce_widget  (bounce_widget:	Bounce_Widget)
			    =
			    case bounce_widget
				#
			        BALL	(arg: ball::Ball_Arg)
				    =>
				    {
					pp.box {.
					    pp.lit  "BALL (";		pp.txt " ";
					    ball::pprint_ball_arg  pp  arg;
#					    pp.txt ", ";
#					    blx::pprint_bounce_look_arg  pp  ball_look_arg;
					    pp.lit  ")";
					};
					pp.newline();
				    };
			    esac

			also
			fun do_canvas_space
			      (
				canvas_space_arg:	cs::Canvas_Space_Arg,
				canvas_widgets:		List(  (Canvas_Widget, List(clh::Canvas_Space_Layout_Hint))  )
			      )
			    =
			    {   pp.box {.
				    do_canvas_space_arg  canvas_space_arg;
				    do_canvas_widgets    canvas_widgets;
				};
				pp.newline();
			    }

			also
			fun do_canvas_space_arg  (canvas_space_arg:	cs::Canvas_Space_Arg)
			    =
			    {
				csi::pprint_canvas_space_arg  pp  canvas_space_arg;
				pp.newline();
			    }

			also
			fun do_canvas_space_layout_hints  (layout_hints: List(clh::Canvas_Space_Layout_Hint))
			    =
			    {   title = "";
				#
				fun do_hint (hint:  clh::Canvas_Space_Layout_Hint)
				    =
				    csi::pprint_canvas_space_layout_hint  pp  hint;

				pp::list  pp  do_hint  title  layout_hints;
			    }

			also
			fun do_canvas_widgets  (canvas_widgets:		List(  (Canvas_Widget, List(clh::Canvas_Space_Layout_Hint))  ))
			    =
			    {
				pp.box' 0 -1 {.
				    pp.lit  "[";
				    pp.ind 2;
				    pp.txt " ";


				    fun do_widget (canvas_widget: Canvas_Widget,  layout_hints: List(clh::Canvas_Space_Layout_Hint))
					=
					pp.box {.
					    do_canvas_widget			canvas_widget;
					    pp.endlit ",";
					    pp.txt " ";
					    do_canvas_space_layout_hints	layout_hints;
					};

				    pp::seq
					{.   pp.endlit ",";   pp.txt " ";   }	# Inter-element separator.
					do_widget				# Print one list element.
					canvas_widgets;				# List of elements.

				    pp.ind 0;
				    pp.txt " ";
				    pp.lit "]";
				};
			    }

			also
			fun do_canvas_widget  (canvas_widget:	Canvas_Widget)
			    =
			    case canvas_widget
				#
			        NODE	(arg:	node::Node_Arg)
				    =>
				    {
					pp.box {.
					    pp.lit  "NODE (";		pp.txt " ";
					    node::pprint_node_arg  pp  arg;
#					    pp.txt ", ";
#					    nlx::pprint_canvas_look_arg  pp  node_look_arg;
					    pp.lit  ")";
					};
					pp.newline();
				    };

			        PACKED_SPACE  packed_space
				    =>
				    {
					pp.lit  "PACKED_SPACE ";
					pp.newline();
					do_packed_space packed_space;
					pp.newline();
				    };
			    esac

			also
			fun do_packed_space_arg  (packed_space_arg:	ps::Packed_Space_Arg)
			    =
			    {
				psi::pprint_packed_space_arg  pp  packed_space_arg;
				pp.newline();
			    }

			also
			fun do_packed_space_layout_hints  (layout_hints: List(plh::Packed_Space_Layout_Hint))
			    =
			    {   title = "";
				#
				fun do_hint (hint:  plh::Packed_Space_Layout_Hint)
				    =
				    psi::pprint_packed_space_layout_hint  pp  hint;

				pp::list  pp  do_hint  title  layout_hints;
			    }

			also
			fun do_packed_widget  (packed_widget:	Packed_Widget)
			    =
			    case packed_widget
				#
			        ROW	(widgets:	 List( (Packed_Widget, List(plh::Packed_Space_Layout_Hint)) ))
				    =>
				    {
					pp.box' 0 -1 {.
					    pp.lit  "ROW [";
					    pp.ind 2;
					    pp.txt " ";

					    fun do_widget (packed_widget: Packed_Widget,  layout_hints: List(plh::Packed_Space_Layout_Hint))
						=
						pp.box {.
						    do_packed_widget			packed_widget;
						    pp.endlit ",";
						    pp.txt " ";
						    do_packed_space_layout_hints	layout_hints;
						};

					    pp::seq
						{.   pp.endlit ",";   pp.txt " ";   }	# Inter-element separator.
						do_widget				# Print one list element.
						widgets;				# List of elements.

					    pp.ind 0;
					    pp.txt " ";
					    pp.lit "]";
					};
				    };

			        COL	(a:	 List( (Packed_Widget, List(plh::Packed_Space_Layout_Hint)) ))
				    =>
				    {
					pp.lit  "COL";
					pp.newline();
				    };

				WIDGET _
				    =>
				    {
					pp.box {.
					    pp.lit  "WIDGET _";
					};
					pp.newline();
				    };

			        CANVAS_SPACE (canvas_space:	(cs::Canvas_Space_Arg,  List((Canvas_Widget, List(clh::Canvas_Space_Layout_Hint)))))
				    =>
				    {
					pp.lit  "CANVAS_SPACE";
					do_canvas_space  canvas_space;
					pp.newline();
				    };

			        BOUNCE_SPACE (bounce_space:	(os::Bounce_Space_Arg,  List((Bounce_Widget, List(blh::Bounce_Space_Layout_Hint)))))
				    =>
				    {
					pp.lit  "BOUNCE_SPACE";
					do_bounce_space  bounce_space;
					pp.newline();
				    };
			      
			        NULL_PACKED_WIDGET
				    =>
				    {
					pp.lit  "NULL_PACKED_WIDGET";
					pp.newline();
				    };
			    esac;
		    end
		);
    };

end;


####################################################################################
# Note[1]
#    We need a NULL_PACKED_WIDGET but not a NULL_CANVAS_WIDGET or NULL_BOUNCE_WIDGET
#    because a Packed_Widget is a mandatory part of Gui_Plan, but Bounce_Widget and
#    Canvas_Widget are always optional (an empty list is always permissible).