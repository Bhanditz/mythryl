## gui-plan.pkg
#
# A gui_plan is the most abstract gui representation;
# it holds widget eggs which preserve widget state
# information when the widgets are not running.
#
# When a gui is run, gui_boss converts the gui_plan
# it is given into a gui_tree tree which contains
# running widget imps in place of the widget eggs.
#
# The gui_tree tree in turn is then converted into
# a gui_tree which assigns each widget a specific
# pixel-rectangle on a specific window in which
# to draw itself.

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    package bs  =  bounce_space;				# bounce_space			is from   src/lib/x-kit/widget/space/bounce/bounce-space.pkg
    package boi =  bounce_space_imp;				# bounce_space_imp		is from   src/lib/x-kit/widget/space/bounce/bounce-space-imp.pkg

    package cs  =  canvas_space;				# canvas_space			is from   src/lib/x-kit/widget/space/canvas/canvas-space.pkg
    package cai =  canvas_space_imp;				# canvas_space_imp		is from   src/lib/x-kit/widget/space/canvas/canvas-space-imp.pkg

    package ps  =  packed_space;				# packed_space			is from   src/lib/x-kit/widget/space/packed/packed-space.pkg
    package pai =  packed_space_imp;				# packed_space_imp		is from   src/lib/x-kit/widget/space/packed/packed-space-imp.pkg

    package pwe =  packed_widget_egg;				# packed_widget_egg		is from   src/lib/x-kit/widget/space/packed/packed-widget-egg.pkg

#   package ks  =  label_look_state;				# label_look_state		is from   src/lib/x-kit/widget/look/label-look-state.pkg
    #
    package ls  =  label_state;					# label_state			is from   src/lib/x-kit/widget/space/packed/label/label-state.pkg
    package lli =  label_look_imp;				# label_look_imp		is from   src/lib/x-kit/widget/theme/packed/look/label-look-imp.pkg
    package lsi =  label_state_imp;				# label_state_imp		is from   src/lib/x-kit/widget/space/packed/label/label-state-imp.pkg

    package bli =  ball_look_imp;				# ball_look_imp			is from   src/lib/x-kit/widget/theme/bounce/look/ball-look-imp.pkg
    package bsi =  ball_state_imp;				# ball_state_imp		is from   src/lib/x-kit/widget/space/bounce/ball/ball-state-imp.pkg

    package nli =  node_look_imp;				# node_look_imp			is from   src/lib/x-kit/widget/theme/canvas/look/node-look-imp.pkg
    package nsi =  node_state_imp;				# node_state_imp		is from   src/lib/x-kit/widget/space/canvas/node/node-state-imp.pkg

    package kli =  knob_look_imp;				# knob_look_imp			is from   src/lib/x-kit/widget/theme/packed/look/knob-look-imp.pkg


    package xli =  xslider_look_imp;				# xslider_look_imp;		is from   src/lib/x-kit/widget/theme/packed/look/xslider-look-imp.pkg
    package yli =  yslider_look_imp;				# yslider_look_imp		is from   src/lib/x-kit/widget/theme/packed/look/yslider-look-imp.pkg

    package ssi =  slider_state_imp;				# slider_state_imp		is from   src/lib/x-kit/widget/space/packed/slider/slider-state-imp.pkg

    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg

    package xg  =  xgeometry;					# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
    package xt  =  xtypes;					# xtypes			is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
    package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg

    package err =  error_message;				# error_message			is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    #
herein

    package gui_plan
    {
	#
	Bounce_Widget
	    #
	    = BALL		(bsi::Ball_Arg, bli::Ball_Look_Arg)
	    #
	    | NULL_BOUNCE_WIDGET

	also
	Canvas_Widget
	    #
	    = PACKED_SPACE	(pai::Packed_Space_Arg,  Packed_Widget)
	    #
	    | NODE		(nsi::Node_Arg, nli::Node_Look_Arg)
	    #
	    | NULL_CANVAS_WIDGET

	also
	Packed_Widget
	    #
	    = ROW		 List( (Packed_Widget, List(ps::Layout_Hint)) )
	    | COL		 List( (Packed_Widget, List(ps::Layout_Hint)) )
	    #	
	    | LABEL		(lsi::Label_Arg,  lli::Label_Look_Arg)
	    | KNOB		(ssi::Slider_Arg, kli::Knob_Look_Arg)
	    | XSLIDER		(ssi::Slider_Arg, xli::Xslider_Look_Arg)
	    | YSLIDER		(ssi::Slider_Arg, yli::Yslider_Look_Arg)
	    #
	    | CANVAS_SPACE	(cai::Canvas_Space_Arg,  List((Canvas_Widget, List(cs::Layout_Hint))))
	    | BOUNCE_SPACE	(boi::Bounce_Space_Arg,  List((Bounce_Widget, List(bs::Layout_Hint))))
	    #
	    | NULL_PACKED_WIDGET
	    ;

	Gui_Plan =  (pai::Packed_Space_Arg,  Packed_Widget);

	fun pprint_gui_plan (gui_plan: Gui_Plan)							# "pprint" == "prettyprint".
	    =
	    pp::with_standard_prettyprint_mill
		#
		(err::default_plaint_sink ())	[]
		#
		(fn pp:   pp::Prettyprint_Mill
		    =
		    do_gui_plan  gui_plan
		    where
			fun do_gui_plan
			      ( packed_space_arg:		pai::Packed_Space_Arg,
				packed_widget:		Packed_Widget
			      )
			    =
			    {   pp.box {.
				    do_packed_space_arg  packed_space_arg;
				    do_packed_widget     packed_widget;
				};
				pp.newline();
			    }

			also
			fun do_packed_space_arg  (packed_space_arg:	pai::Packed_Space_Arg)
			    =
			    {
				pp.lit  "Not printing gui_plan packed_space arg";
				pp.newline();
			    }

			also
			fun do_label_look_arg  (arg:	lli::Label_Look_Arg)
			    =
			    case arg	[] =>  pp.lit "[]"    ;
					_  =>  pp.lit "[...]" ;
			    esac

			also
			fun do_packed_widget  (packed_widget:	Packed_Widget)
			    =
			    case packed_widget
				#
			        ROW	(widgets:	 List( (Packed_Widget, List(ps::Layout_Hint)) ))
				    =>
				    {
					pp.box' 0 -1 {.
					    pp.lit  "ROW [";
					    pp.ind 2;
					    pp.txt " ";


					    fun do_widget (packed_widget: Packed_Widget,  layout_hints: List(ps::Layout_Hint))
						=
						do_packed_widget  packed_widget;

					    pp::seq
						{.   pp.endlit ",";   pp.txt " ";   }	# Inter-element separator.
						do_widget				# Print one list element.
						widgets;				# List of elements.

					    pp.ind 0;
					    pp.txt " ";
					    pp.lit "]";
					};
				    };

			        COL	(a:	 List( (Packed_Widget, List(ps::Layout_Hint)) ))
				    =>
				    {
					pp.lit  "COL";
					pp.newline();
				    };

			        LABEL	( label_arg:		lsi::Label_Arg,
					  label_look_arg:	lli::Label_Look_Arg
					)
				    =>
				    {
					pp.box {.
					    pp.lit  "LABEL (";		pp.txt " ";
					    lsi::pprint_label_arg  pp  label_arg;
					    pp.txt ", ";
# NEEDS WORK
					    do_label_look_arg  label_look_arg;
					    pp.lit  ")";
					};
					pp.newline();
				    };

			        KNOB	(a:	(ssi::Slider_Arg, kli::Knob_Look_Arg))
				    =>
				    {
					pp.lit  "KNOB";
					pp.newline();
				    };

			        XSLIDER	(a:	(ssi::Slider_Arg, xli::Xslider_Look_Arg))
				    =>
				    {
					pp.lit  "XSLIDER";
					pp.newline();
				    };

			        YSLIDER	(a:	(ssi::Slider_Arg, yli::Yslider_Look_Arg))
				    =>
				    {
					pp.lit  "YSLIDER";
					pp.newline();
				    };
			      
			        CANVAS_SPACE (a:	(cai::Canvas_Space_Arg,  List((Canvas_Widget, List(cs::Layout_Hint)))))
				    =>
				    {
					pp.lit  "CANVAS_SPACE";
					pp.newline();
				    };

			        BOUNCE_SPACE (a:	(boi::Bounce_Space_Arg,  List((Bounce_Widget, List(bs::Layout_Hint)))))
				    =>
				    {
					pp.lit  "BOUNCE_SPACE";
					pp.newline();
				    };
			      
			        NULL_PACKED_WIDGET
				    =>
				    {
					pp.lit  "NULL_PACKED_WIDGET";
					pp.newline();
				    };
			    esac;
		    end
		);
    };

end;
