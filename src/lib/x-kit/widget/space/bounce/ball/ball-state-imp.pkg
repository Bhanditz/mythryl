## ball-state-imp.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include package   threadkit;				# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ap  =  client_to_atom;				# client_to_atom		is from   src/lib/x-kit/xclient/src/iccc/client-to-atom.pkg
#   package au  =  authentication;				# authentication		is from   src/lib/x-kit/xclient/src/stuff/authentication.pkg
#   package cpm =  cs_pixmap;					# cs_pixmap			is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#   package cpt =  cs_pixmat;					# cs_pixmat			is from   src/lib/x-kit/xclient/src/window/cs-pixmat.pkg
#   package dy  =  display;					# display			is from   src/lib/x-kit/xclient/src/wire/display.pkg
#   package xet =  xevent_types;				# xevent_types			is from   src/lib/x-kit/xclient/src/wire/xevent-types.pkg
#   package w2x =  windowsystem_to_xserver;			# windowsystem_to_xserver	is from   src/lib/x-kit/xclient/src/window/windowsystem-to-xserver.pkg
#   package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
#   package fti =  font_index;					# font_index			is from	  src/lib/x-kit/xclient/src/window/font-index.pkg
#   package r2k =  xevent_router_to_keymap;			# xevent_router_to_keymap	is from   src/lib/x-kit/xclient/src/window/xevent-router-to-keymap.pkg
#   package mtx =  rw_matrix;					# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
#   package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package rgb =  rgb;						# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
#   package rop =  ro_pixmap;					# ro_pixmap			is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
#   package rw  =  root_window;					# root_window			is from   src/lib/x-kit/widget/lib/root-window.pkg
#   package rwv =  rw_vector;					# rw_vector			is from   src/lib/std/src/rw-vector.pkg
#   package sep =  selection_port;				# selection_port		is from   src/lib/x-kit/xclient/src/window/selection-port.pkg
#   package shp =  shade;					# shade				is from   src/lib/x-kit/widget/lib/shade.pkg
#   package sj  =  socket_junk;					# socket_junk			is from   src/lib/internet/socket-junk.pkg
#   package x2s =  xclient_to_sequencer;			# xclient_to_sequencer		is from   src/lib/x-kit/xclient/src/wire/xclient-to-sequencer.pkg
#   package tr  =  logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
#   package tsr =  thread_scheduler_is_running;			# thread_scheduler_is_running	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler-is-running.pkg
#   package u1  =  one_byte_unt;				# one_byte_unt			is from   src/lib/std/one-byte-unt.pkg
#   package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
#   package v2w =  value_to_wire;				# value_to_wire			is from   src/lib/x-kit/xclient/src/wire/value-to-wire.pkg
#   package wg  =  widget;					# widget			is from   src/lib/x-kit/widget/basic/widget.pkg
#   package wi  =  window;					# window			is from   src/lib/x-kit/xclient/src/window/window.pkg
#   package wme =  window_map_event_sink;			# window_map_event_sink		is from   src/lib/x-kit/xclient/src/window/window-map-event-sink.pkg
#   package wpp =  window_watcher;				# window_watcher		is from   src/lib/x-kit/xclient/src/window/window-watcher.pkg
#   package wy  =  widget_style;				# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
#   package e2s =  xevent_to_string;				# xevent_to_string		is from   src/lib/x-kit/xclient/src/to-string/xevent-to-string.pkg
#   package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
#   package xg  =  xgeometry;					# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
#   package xj  =  xsession_junk;				# xsession_junk			is from   src/lib/x-kit/xclient/src/window/xsession-junk.pkg
#   package xt  =  xtypes;					# xtypes			is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg
    #
    package bs  =  ball_state;					# ball_state			is from   src/lib/x-kit/widget/space/bounce/ball/ball-state.pkg
    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package pwe =  bounce_widget_egg;				# bounce_widget_egg		is from   src/lib/x-kit/widget/space/bounce/bounce-widget-egg.pkg
    #
    tracefile   =  "widget-unit-test.trace.log";
herein

    package ball_state_imp
    :       Ball_State_Imp												# Ball_State_Imp		is from   src/lib/x-kit/widget/space/bounce/ball/ball-state-imp.api
    {
	include package   ball_state;											# ball_state			is from   src/lib/x-kit/widget/space/bounce/ball/ball-state.pkg
	#
	Imports = {													# Ports we use, provided by other imps.
                    state_sink: Ball_State_Needs -> Void
		  };

	Exports	= {													# Ports we provide for use by other imps.
		    ball_state:	Ball_State
		  };

	Ball_State_Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);

	Ball_State_State
	  =
	  { id:		Id,
	    state:	Ref( Ball_State_Needs )
	  };

	Me_Slot = Mailslot( { imports:			Imports,
			      me:			Ball_State_State,
			      options:			List(Ball_State_Option),
			      run_gun':			Run_Gun,
			      end_gun':			End_Gun,
			      endstate_oneshot:		Null_Or(Oneshot_Maildrop(bs::Ball_Arg)),				# When end_gun fires we save our state in this and exit.
			      change_callbacks:		Ref(List(Ball_State_Needs -> Void)),
			      ball_state_callbacks:	List(Ball_State -> Void)
			    }
			  );


	Runstate =    {	me:			Ball_State_State,									# State which is preserved across imp shutdown/restart cycles.
			options:		List(Ball_State_Option),
			imports:		Imports,										# Imps to which we send requests.
			to:			Replyqueue,										# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
			end_gun':		End_Gun,										# We shut down the microthread when this fires.
			endstate_oneshot:	Null_Or(Oneshot_Maildrop(bs::Ball_Arg)),							# When end_gun fires we save our state in this and exit.
			change_callbacks:	Ref(List(Ball_State_Needs -> Void))							# We call these each time our state changes.
		      };

	Ball_Q =  Mailqueue( Runstate -> Void );

	fun run	( ball_q:		Mailqueue( Runstate -> Void ),							# 
		  runstate as
		    {													# These values will be statically globally visible throughout the code body for the imp.
		      me:		Ball_State_State,								# State which is preserved across imp shutdown/restart cycles.
		      options:		List(Ball_State_Option),
		      imports:		Imports,									# Imps to which we send requests.
		      to:		Replyqueue,									# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		      end_gun':		End_Gun,									# We shut down the microthread when this fires.
		      endstate_oneshot:	Null_Or(Oneshot_Maildrop(bs::Ball_Arg)),						# When end_gun fires we save our state in this and exit.
		      change_callbacks:	Ref(List(Ball_State_Needs -> Void))						# We call these each time our state changes.
		    }
		)
	    =
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (end_gun'                     ==>  shut_down_ball_imp'),
			    (take_from_mailqueue' ball_q  ==>  do_ball_plea)
			];

			loop ();
		    }	
		    where
			fun do_ball_plea thunk
			    =
			    thunk runstate;

			fun shut_down_ball_imp' ()
			    =
			    {
				case endstate_oneshot									# Pass our state back to guiboss to allow later impnet restart without state loss.
				    #
				    NULL	=> ();
				    THE oneshot =>  put_in_oneshot (oneshot, (*me.state, options));			# 
				esac;

				thread_exit { success => TRUE };							# Will not return.	
			    };
		    end;
	    end;	



	fun startup   (id: Id,  reply_oneshot:  Oneshot_Maildrop( (Me_Slot, Exports) ))   ()				# Root fn of imp microthread.  Note currying.
	    =
	    {   me_slot  =  make_mailslot  ()	:  Me_Slot;
		#
		ball_state	=  { id, subscribe_to_changes, pass_value, set_state };
		exports		=  { ball_state };

		to		=  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, (me_slot, exports));							# Return value from ball_egg'().

		(take_from_mailslot  me_slot)										# Imports from ball_egg'().
		    ->
		    { me, options, imports, run_gun', end_gun', endstate_oneshot, change_callbacks, ball_state_callbacks };

		apply   {. #callback ball_state; }   ball_state_callbacks;						# Pass our port to everyone who asked for it.
		apply   {. #callback  *me.state; }      *change_callbacks;						# Pass our initial state to everyone who is change-subscribed.

		block_until_mailop_fires  run_gun';									# Wait for the starting gun.

		run (ball_q, { me, options, imports, to, end_gun', endstate_oneshot, change_callbacks });		# Will not return.
	    }
	    where
		ball_q     =  make_mailqueue (get_current_microthread()):  Ball_Q;


		fun subscribe_to_changes  callback									# PUBLIC.
		    =
		    put_in_mailqueue
		      (
			ball_q,
			#
			\\ ({ change_callbacks, ... }: Runstate)
			    =
			    change_callbacks :=  callback ! *change_callbacks
		      );	


		fun pass_value  (replyqueue: Replyqueue)  (reply_handler: Ball_State_Needs -> Void)			# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Ball_State_Needs );
			#
			put_in_mailqueue
			  (
			    ball_q,
			    #
			    \\ ({ me, ... }: Runstate)
				=
				{
				    put_in_oneshot (reply_oneshot, *me.state);
				}
			  );

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun set_state (state: Ball_State_Needs)									# PUBLIC.
		    =	
		    put_in_mailqueue
		      (
			ball_q,
			#
			\\ ({ me, change_callbacks, ... }: Runstate)
			    =
			    {   me.state := state;									# Change the text we display.
				apply   {. #callback state; }   *change_callbacks;					# Update all listeners with our new state.
			    }	
			);


	    end;


	fun process_options (options: List(Ball_State_Option), { name, id, change_callbacks, ball_state_callbacks })
	    =
	    {   my_name			=  REF name;
		my_id			=  REF id;
		#
		my_change_callbacks	=      change_callbacks;		# Comes with REF pre-installed.
		my_ball_state_callbacks	=  REF ball_state_callbacks;
		#
		apply  do_option  options
		where
		    fun do_option (MICROTHREAD_NAME    n) =>   my_name                 :=  n;
			do_option (ID                  i) =>   my_id                   :=  i;
			#
			do_option (CHANGE_CALLBACK     c) =>   my_change_callbacks     :=  c ! *my_change_callbacks;
			do_option (BALL_STATE_CALLBACK c) =>   my_ball_state_callbacks :=  c ! *my_ball_state_callbacks;
		    end;
		end;

		{ name 			=>   *my_name,
		  id 			=>   *my_id,
		  change_callbacks	=>    my_change_callbacks,
		  ball_state_callbacks	=>   *my_ball_state_callbacks
		};
	    };

        ##########################################################################################
	# PUBLIC.
	#
	fun make_ball_state_egg												# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
		( needs:		     Ball_State_Needs,
		  options:		List(Ball_State_Option)
		)
		(endstate_oneshot:	Null_Or(Oneshot_Maildrop(bs::Ball_Arg)))					# When end_gun fires we save our state in this and exit.
	    =
	    {   (process_options
		  ( options,
		    { name			=>  "ball_state",
		      id			=>  id_zero,
		      change_callbacks		=>  REF([]),
		      ball_state_callbacks	=>  []
		    }
		) )
		    ->
		    { name,
		      id, 
		      change_callbacks,
		      ball_state_callbacks
		    };

		my (id, options)
		    =
		    if (id_to_int(id) == 0)
			id = issue_unique_id();										# Allocate unique imp id.
			(id, ID id ! options);										# Make our id stable across stop/restart cycles.
		    else
			(id, options);
		    fi;

		me = { id, state => REF needs };

		\\ () = {   reply_oneshot = make_oneshot_maildrop():  Oneshot_Maildrop( (Me_Slot, Exports) );		# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
			    #
			    xlogger::make_thread  name  (startup  (id, reply_oneshot));					# Note that startup() is curried.

			    (get_from_oneshot  reply_oneshot) -> (me_slot, exports);

			    fun phase3											# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				(
				  imports:	Imports,
				  run_gun':	Run_Gun,	
				  end_gun':	End_Gun
				)
				=
				{   change_callbacks :=  imports.state_sink ! *change_callbacks;
				    #
				    put_in_mailslot  (me_slot, { me, options, imports, run_gun', end_gun', endstate_oneshot, change_callbacks, ball_state_callbacks });
				};

			    (exports, phase3);
			};
	    };


	fun pprint_ball_arg
	      (pp:		pp::Prettyprint_Mill)
	      (ball_arg:	bs::Ball_Arg)
	    =
	    {
		ball_arg ->   ( { position:	Xyz,
				  velocity:	Xyz
			        }:		Ball_State_Needs,
				options:	List(Ball_State_Option)
			      );

		pp.box {.
		    pp.lit (sprintf "{ position => { x => %g, y => %g, z => %g }, velocity => { x => %g, y => %g, z => %g } }" position.x position.y position.z velocity.x velocity.y velocity.z );
		    pp.endlit ",";
		    pp.txt " [";
		    pp::seq {. pp.txt ", "; }
			    pprint_option
			    options
			    ;	
		    pp.txt " ]";
		    pp.lit ")";
		};
	    }
	    where
		fun pprint_option option
		    =
		    case option
			#
			MICROTHREAD_NAME name	=>  {  pp.lit (sprintf "MICROTHREAD_NAME \"%s\"" name );	};
			ID               id	=>  {  pp.lit (sprintf "ID %d" (id_to_int id)         );	};
			CHANGE_CALLBACK _	=>  {  pp.lit          "CHANGE_CALLBACK (callback)"    ;	};
			BALL_STATE_CALLBACK _	=>  {  pp.lit          "BALL_STATE_CALLBACK (callback)";	};
		    esac;
	    end;
    };

end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2015,
## released per terms of SMLNJ-COPYRIGHT.
