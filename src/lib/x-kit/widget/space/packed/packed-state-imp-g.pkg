## packed-state-imp-g.pkg
#
# Generic version of a packed-space widget state-imp
# for simple types like Float and String.
#
# The state-imp is responsible for maintaining the
# core state of a widget;  the look-imp is then
# responsible for displaying it.
#
# In general client app code talks to the state-imps while
# the gui-boss-imp talks to the look-imps.
#
# Each state-imp notifies its look-imp when it changes state;
# the look-imp interprets mouse and keyboard input and tells
# the state-imp what new state value to assume.
#
# See also:
#
#     src/lib/x-kit/widget/space/packed/packed-space-imp.pkg
#     src/lib/x-kit/widget/gui/gui-boss-imp.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include threadkit;						# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ap  =  atom_port;					# atom_port			is from   src/lib/x-kit/xclient/src/iccc/atom-port.pkg
#   package au  =  authentication;				# authentication		is from   src/lib/x-kit/xclient/src/stuff/authentication.pkg
#   package cpm =  cs_pixmap;					# cs_pixmap			is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#   package cpt =  cs_pixmat;					# cs_pixmat			is from   src/lib/x-kit/xclient/src/window/cs-pixmat.pkg
#   package dy  =  display;					# display			is from   src/lib/x-kit/xclient/src/wire/display.pkg
#   package et  =  event_types;					# event_types			is from   src/lib/x-kit/xclient/src/wire/event-types.pkg
#   package w2x =  widget_to_xserver;				# widget_to_xserver		is from   src/lib/x-kit/xclient/src/window/widget-to-xserver.pkg
#   package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
#   package fti =  font_index;					# font_index			is from	  src/lib/x-kit/xclient/src/window/font-index.pkg
#   package kp  =  keymap;					# keymap			is from   src/lib/x-kit/xclient/src/window/keymap.pkg
#   package mtx =  rw_matrix;					# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
#   package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package rgb =  rgb;						# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
#   package rop =  ro_pixmap;					# ro_pixmap			is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
#   package rw  =  root_window;					# root_window			is from   src/lib/x-kit/widget/lib/root-window.pkg
#   package rwv =  rw_vector;					# rw_vector			is from   src/lib/std/src/rw-vector.pkg
#   package s2p =  xevent_to_window;				# xevent_to_window		is from   src/lib/x-kit/xclient/src/window/xevent-to-window.pkg
#   package sep =  selection_port;				# selection_port		is from   src/lib/x-kit/xclient/src/window/selection-port.pkg
#   package shp =  shade;					# shade				is from   src/lib/x-kit/widget/lib/shade.pkg
#   package sj  =  socket_junk;					# socket_junk			is from   src/lib/internet/socket-junk.pkg
#   package x2s =  xclient_to_sequencer;			# xclient_to_sequencer		is from   src/lib/x-kit/xclient/src/wire/xclient-to-sequencer.pkg
#   package tr  =  logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
#   package tsr =  thread_scheduler_is_running;			# thread_scheduler_is_running	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler-is-running.pkg
#   package u1  =  one_byte_unt;				# one_byte_unt			is from   src/lib/std/one-byte-unt.pkg
#   package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
#   package v2w =  value_to_wire;				# value_to_wire			is from   src/lib/x-kit/xclient/src/wire/value-to-wire.pkg
#   package wg  =  widget;					# widget			is from   src/lib/x-kit/widget/basic/widget.pkg
#   package wi  =  window;					# window			is from   src/lib/x-kit/xclient/src/window/window.pkg
#   package wme =  window_map_event_sink;			# window_map_event_sink		is from   src/lib/x-kit/xclient/src/window/window-map-event-sink.pkg
#   package wpp =  window_watcher;				# window_watcher		is from   src/lib/x-kit/xclient/src/window/window-watcher.pkg
#   package wy  =  widget_style;				# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
#   package e2s =  xevent_to_string;				# xevent_to_string		is from   src/lib/x-kit/xclient/src/to-string/xevent-to-string.pkg
#   package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
#   package xg  =  xgeometry;					# xgeometry			is from   src/lib/std/2d/xgeometry.pkg
#   package xj  =  xsession_junk;				# xsession_junk			is from   src/lib/x-kit/xclient/src/window/xsession-junk.pkg
#   package xt  =  xtypes;					# xtypes			is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg
    #
    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package pwe =  packed_widget_egg;				# packed_widget_egg		is from   src/lib/x-kit/widget/space/packed/packed-widget-egg.pkg
    #
    tracefile   =  "widget-unit-test.trace.log";
herein

    # This generic is invoked in:
    #
    #     src/lib/x-kit/widget/space/packed/float-state-imp.pkg
    #     src/lib/x-kit/widget/space/packed/string-state-imp.pkg
    #
    generic package packed_state_imp_g (
	#
	package ss						# client_to_float_state		is from   src/lib/x-kit/widget/space/packed/client-to-float-state.pkg
        :							# client_to_string_state	is from   src/lib/x-kit/widget/space/packed/client-to-string-state.pkg
	    api {
		eqtype Value;
		#
		Is_Bad_Value =  Value -> Null_Or( (Void -> String, Null_Or (Void -> Value) ));		# Given a Value, return NULL if it is valid, else a thunk returning a diagnostic
													# string explaining why it is not, plus optionally thunk computing 'nearby' valid value.
													# usable in place of the invalid value.

		Client_To_State										# Port used by client code to communicate with state imp.
		  =
		  { id:				Id,							# Unique id to facilitate storing ball_state instances in indexed datastructures like red-black trees.
		    #
		    set_value:			Value -> Void,
		    pass_value:	  Replyqueue -> (Value -> Void) -> Void,
		    subscribe_to_changes:	(Value -> Void) -> Void
		  };

		Needs
		  =
		  { value:			Value
		  };

		Option
		  #
		  = MICROTHREAD_NAME		String							# 
		  | ID				Id							# Unique ID for imp, issued by issue_unique_id::issue_unique_id().
		  | IS_BAD_VALUE		Is_Bad_Value
		  | CHANGE_CALLBACK		Value -> Void						# We'll call each of these callbacks each time our value changes.
		  | CLIENT_TO_STATE_CALLBACK	Client_To_State -> Void					# Client code registers this callback to get a port to us once we start up.
		  ;

		Make_Arg = (Needs, List(Option));

		value_to_string: Value -> String;
	    }												# package client_to_float_state;
	;
    )
#    : 	    Float_State_Imp									# Float_State_Imp		is from   src/lib/x-kit/widget/space/packed/float-state-imp.api

:   api {
	#
	Exports	= {										# Ports we provide for use by other imps.
		    client_to_state:		ss::Client_To_State
		  };

	Imports = {										# Ports we use, provided by other imps.
                    state_sink:			ss::Value -> Void
                  };

	Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);

        make_egg
	    :
	    ss::Make_Arg
         -> Null_Or(Oneshot_Maildrop(ss::Make_Arg))						# The optional oneshot is used by gui-boss to capture final state of imp when endgun fires.
	 -> Egg
	    ;

	pprint_make_arg:	pp::Prettyprint_Mill -> ss::Make_Arg -> Void;
    }

    {
	include ss;													# 
	#
	Imports = {													# Ports we use, provided by other imps.
                    state_sink: Value -> Void
		  };

	Float_State_State
	  =
	  { id:			Id,
	    state:		Ref( Needs ),
	    is_bad_value:	Is_Bad_Value
	  };

	Exports	= {													# Ports we provide for use by other imps.
		    client_to_state:	Client_To_State
		  };

	Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);

	Me_Slot = Mailslot( { imports:			Imports,
			      me:			Float_State_State,
			      options:			List(Option),
			      run_gun':			Run_Gun,
			      end_gun':			End_Gun,
			      endstate_oneshot:		Null_Or(Oneshot_Maildrop(Make_Arg)),				# When end_gun fires we save our state in this and exit.
			      change_callbacks:		Ref(List(Value -> Void)),
			      client_to_state_callbacks:	List(Client_To_State -> Void)
			    }
			  );

	Runstate = {													# These values will be statically globally visible throughout the code body for the imp.
		    me:				Float_State_State,							# State which is preserved across imp shutdown/restart cycles.
		    options:			List(Option),
		    imports:			Imports,								# Imps to which we send requests.
		    to:				Replyqueue,								# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		    end_gun':			End_Gun,								# We shut down the microthread when this fires.
		    endstate_oneshot:		Null_Or(Oneshot_Maildrop(Make_Arg)),					# When end_gun fires we save our state in this and exit.
		    change_callbacks:		Ref(List(Value -> Void))						#
		   };

	Slider_Q    = Mailqueue( Runstate -> Void );

	fun run (
		  slider_q:			Slider_Q,								# 
		  #
		  runstate as
		  {													# These values will be statically globally visible throughout the code body for the imp.
		    me:			        Float_State_State,							# State which is preserved across imp shutdown/restart cycles.
		    options:			List(Option),
		    imports:			Imports,								# Imps to which we send requests.
		    to:				Replyqueue,								# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		    end_gun':			End_Gun,								# We shut down the microthread when this fires.
		    endstate_oneshot:		Null_Or(Oneshot_Maildrop(Make_Arg)),					# When end_gun fires we save our state in this and exit.
		    change_callbacks:		Ref(List(Value -> Void))					#
		  }
		)
	    =
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (end_gun'                        ==>  shut_down_slider_imp'),
			    (take_from_mailqueue' slider_q   ==>  do_slider_plea)
			];

			loop ();
		    }	
		    where
			fun do_slider_plea  thunk
			    =
			    thunk runstate;


			fun shut_down_slider_imp' ()
			    =
			    {
				case endstate_oneshot									# Pass our state back to gui-boss to allow later impnet restart without state loss.
				    #
				    NULL	=> ();
				    THE oneshot => put_in_oneshot (oneshot, (*me.state, options));			# 
				esac;

				thread_exit { success => TRUE };							# Will not return.	
			    };
		    end;
	    end;	



	fun startup   (id: Id,  reply_oneshot:  Oneshot_Maildrop( (Me_Slot, Exports) ))   ()				# Root fn of imp microthread.  Note currying.
	    =
	    {   me_slot  =  make_mailslot  ()	:  Me_Slot;
		#
		client_to_state	=  { id, subscribe_to_changes, pass_value, set_value };
		exports		=  { client_to_state };

		to          =  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, (me_slot, exports));							# Return value from slider_egg'().

		(take_from_mailslot  me_slot)										# Imports from slider_egg'().
		    ->
		    { me, options, imports,
		      run_gun', end_gun',
		      endstate_oneshot, change_callbacks, client_to_state_callbacks
		    };

		apply   {. #callback client_to_state;   }   client_to_state_callbacks;					# Pass our port to everyone who asked for it.
		apply   {. #callback (*me.state).value; }           *change_callbacks;					# Pass our initial state to everyone who is change-subscribed.

		block_until_mailop_fires  run_gun';									# Wait for the starting gun.

		run (slider_q, { me, options, imports, to, end_gun', endstate_oneshot, change_callbacks });		# Will not return.
	    }
	    where
		slider_q     =  make_mailqueue (get_current_microthread()):  Slider_Q;


		fun subscribe_to_changes   callback									# PUBLIC.
		    =	
		    put_in_mailqueue  (slider_q,
			#
			fn ({ change_callbacks, ... }: Runstate)
			    =
			    change_callbacks :=  callback ! *change_callbacks
		    );


		fun pass_value  (replyqueue: Replyqueue)  (reply_handler: Value -> Void)			# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Value );
			#
			put_in_mailqueue  (slider_q,
			    #
			    fn ({ me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, (*me.state).value)
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };


		fun set_value (f: Value)										# PUBLIC.
		    =	
		    put_in_mailqueue  (slider_q,
			#
			fn ({ me as { id, state => REF { value }, is_bad_value }, change_callbacks, ... }: Runstate)
			    =
			    {
#				f = if (f < min)	min;								# Make sure our new value is not below min end of slider.
#				    else		f;
#				    fi;
#
#				f = if (f > max)	max;								# Make sure our new value is not above max end of slider.
#				    else		f;
#				    fi;

				case (is_bad_value f)
				    #
				    NULL =>
					if (f != value)
					    #
					    state = { value => f };
					    me.state := state;								# Note new state of slider.

					    apply   {. #callback f; }   *change_callbacks;				# Tell everyone who cares what our new state is.
					fi;
				    _ => ();										# For the moment we're ignoring the diagnostic. XXX BUGGO FIXME.
				esac;
			    }
		    );
	    end;


	fun process_options (options: List(Option), { name, id, is_bad_value, change_callbacks, client_to_state_callbacks })
	    =
	    {   my_name		=  REF name;
		my_id		=  REF id;
		#
		my_is_bad_value	=  REF is_bad_value;
		#
		my_change_callbacks		=     change_callbacks;		# Comes with REF pre-installed.
		my_client_to_state_callbacks	= REF client_to_state_callbacks;
		#
		apply  do_option  options
		where
		    fun do_option (MICROTHREAD_NAME 	  n)  =>  my_name          :=  n;
			do_option (ID                     i) =>   my_id            :=  i;
		        #
			do_option (IS_BAD_VALUE	    	  f)  =>  my_is_bad_value  :=  f;
		        #
			do_option (CHANGE_CALLBACK        c)  =>  my_change_callbacks       :=  c ! *my_change_callbacks;
			do_option (CLIENT_TO_STATE_CALLBACK  c)  =>  my_client_to_state_callbacks :=  c ! *my_client_to_state_callbacks;
		    end;
		end;

		{ name		=>  *my_name,
		  id 		=>  *my_id,
		  #
		  is_bad_value	=>  *my_is_bad_value,
		  #
		  change_callbacks	  =>   my_change_callbacks,
		  client_to_state_callbacks  =>  *my_client_to_state_callbacks
		};
	    };

        ##########################################################################################
	# PUBLIC.
	#
	fun make_egg											# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
	        ( needs:	     Needs,
		  options:	List(Option)
		)
		(endstate_oneshot:	 Null_Or(Oneshot_Maildrop(Make_Arg)))						# When end_gun fires we save our state in this and exit.
	    =
	    {   (process_options
		  ( options,
		    { name		=> "float_state",
		      id		=>  id_zero,
		      #	
		      is_bad_value	=>  fn (v: Value) = NULL,
		      #	
		      change_callbacks		=> REF([]),
		      client_to_state_callbacks	=> []
		    }
		) )
		  ->
		  { name,
		    id, 
		    #
		    is_bad_value,
		    #
		    change_callbacks,
		    client_to_state_callbacks
		  };
	
		my (id, options)
		    =
		    if (id_to_int(id) == 0)
			id = issue_unique_id();									# Allocate unique imp id.
			(id, ID id ! options);										# Make our id stable across stop/restart cycles.
		    else
			(id, options);
		    fi;

		me =  { id,
                        state =>  REF { value => needs.value },
			is_bad_value
		      };

		fn () = {   reply_oneshot = make_oneshot_maildrop():  Oneshot_Maildrop( (Me_Slot, Exports) );		# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
			    #
			    xlogger::make_thread  name  (startup  (id, reply_oneshot));					# Note that startup() is curried.

			    (get_from_oneshot  reply_oneshot) -> (me_slot, exports);

			    fun phase3											# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				(
				  imports:	Imports,
				  run_gun':	Run_Gun,	
				  end_gun':	End_Gun
				)
				=
				{   change_callbacks :=  imports.state_sink ! *change_callbacks;
				    #
				    put_in_mailslot  (me_slot, { me, options, imports, run_gun', end_gun', endstate_oneshot, change_callbacks, client_to_state_callbacks });
				};

			    (exports, phase3);
			};
	    };


	fun pprint_make_arg
	      (pp:		pp::Prettyprint_Mill)
	      (make_arg:	Make_Arg)
	    =
	    {
		make_arg ->     ( { value }:	Needs,
				   options:		List(Option)
			        );

		pp.box {.
		    pp.lit (sprintf "({ value => %s }" (ss::value_to_string value));
		    pp.endlit ",";
		    pp.txt " [";
		    pp::seq {. pp.txt ", "; }
			    pprint_option
			    options
			    ;	
		    pp.txt " ]";
		    pp.lit ")";
		};
	    }
	    where
		fun pprint_option option
		    =
		    case option
			#
			MICROTHREAD_NAME name	=>  {  pp.lit (sprintf "MICROTHREAD_NAME \"%s\"" name);		};
			ID               id	=>  {  pp.lit (sprintf "ID %d" (id_to_int id)         );	};
			IS_BAD_VALUE f	        =>  {  pp.lit (sprintf "IS_BAD_VALUE => {...}");		};
			CHANGE_CALLBACK _	=>  {  pp.lit          "CHANGE_CALLBACK (callback)";		};
			CLIENT_TO_STATE_CALLBACK _	=>  {  pp.lit          "CLIENT_TO_STATE_CALLBACK (callback)";	};
		    esac;
	    end;
    };

end;
