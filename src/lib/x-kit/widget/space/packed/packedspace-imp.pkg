## packedspace-imp.pkg
#
# For the big picture see the imp dataflow diagrams in
#
#     src/lib/x-kit/xclient/src/window/xclient-ximps.pkg
#

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include package   threadkit;				# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ap  =  client_to_atom;				# client_to_atom		is from   src/lib/x-kit/xclient/src/iccc/client-to-atom.pkg
#   package au  =  authentication;				# authentication		is from   src/lib/x-kit/xclient/src/stuff/authentication.pkg
#   package cpm =  cs_pixmap;					# cs_pixmap			is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
#   package cpt =  cs_pixmat;					# cs_pixmat			is from   src/lib/x-kit/xclient/src/window/cs-pixmat.pkg
#   package dy  =  display;					# display			is from   src/lib/x-kit/xclient/src/wire/display.pkg
#   package xet =  xevent_types;				# xevent_types			is from   src/lib/x-kit/xclient/src/wire/xevent-types.pkg
#   package w2x =  windowsystem_to_xserver;			# windowsystem_to_xserver	is from   src/lib/x-kit/xclient/src/window/windowsystem-to-xserver.pkg
#   package fil =  file__premicrothread;			# file__premicrothread		is from   src/lib/std/src/posix/file--premicrothread.pkg
#   package fti =  font_index;					# font_index			is from	  src/lib/x-kit/xclient/src/window/font-index.pkg
#   package r2k =  xevent_router_to_keymap;			# xevent_router_to_keymap	is from   src/lib/x-kit/xclient/src/window/xevent-router-to-keymap.pkg
#   package mtx =  rw_matrix;					# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
#   package r8  =  rgb8;					# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
#   package rgb =  rgb;						# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
#   package rop =  ro_pixmap;					# ro_pixmap			is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
#   package rw  =  root_window;					# root_window			is from   src/lib/x-kit/widget/lib/root-window.pkg
#   package rwv =  rw_vector;					# rw_vector			is from   src/lib/std/src/rw-vector.pkg
#   package sep =  client_to_selection;				# client_to_selection		is from   src/lib/x-kit/xclient/src/window/client-to-selection.pkg
#   package shp =  shade;					# shade				is from   src/lib/x-kit/widget/lib/shade.pkg
#   package sj  =  socket_junk;					# socket_junk			is from   src/lib/internet/socket-junk.pkg
#   package tr  =  logger;					# logger			is from   src/lib/src/lib/thread-kit/src/lib/logger.pkg
#   package tsr =  thread_scheduler_is_running;			# thread_scheduler_is_running	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler-is-running.pkg
#   package u1  =  one_byte_unt;				# one_byte_unt			is from   src/lib/std/one-byte-unt.pkg
#   package v1u =  vector_of_one_byte_unts;			# vector_of_one_byte_unts	is from   src/lib/std/src/vector-of-one-byte-unts.pkg
#   package v2w =  value_to_wire;				# value_to_wire			is from   src/lib/x-kit/xclient/src/wire/value-to-wire.pkg
#   package wg  =  widget;					# widget			is from   src/lib/x-kit/widget/old/basic/widget.pkg
#   package wi  =  window;					# window			is from   src/lib/x-kit/xclient/src/window/window.pkg
#   package wme =  window_map_event_sink;			# window_map_event_sink		is from   src/lib/x-kit/xclient/src/window/window-map-event-sink.pkg
#   package wpp =  client_to_window_watcher;			# client_to_window_watcher	is from   src/lib/x-kit/xclient/src/window/client-to-window-watcher.pkg
#   package wy  =  widget_style;				# widget_style			is from   src/lib/x-kit/widget/lib/widget-style.pkg
#   package e2s =  xevent_to_string;				# xevent_to_string		is from   src/lib/x-kit/xclient/src/to-string/xevent-to-string.pkg
#   package xc  =  xclient;					# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
#   package xj  =  xsession_junk;				# xsession_junk			is from   src/lib/x-kit/xclient/src/window/xsession-junk.pkg
#   package xt  =  xtypes;					# xtypes			is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
#   package xtr =  xlogger;					# xlogger			is from   src/lib/x-kit/xclient/src/stuff/xlogger.pkg

    package g2d =  geometry2d;					# geometry2d			is from   src/lib/std/2d/geometry2d.pkg

#   package b2g =  guiboss_to_gadget;				# guiboss_to_gadget		is from   src/lib/x-kit/widget/gui/guiboss-to-gadget.pkg
    package b2s =  bouncespace_to_sprite;			# bouncespace_to_sprite		is from   src/lib/x-kit/widget/space/bounce/bouncespace-to-sprite.pkg
    package c2o =  canvasspace_to_object;			# canvasspace_to_object		is from   src/lib/x-kit/widget/space/canvas/canvasspace-to-object.pkg
    package p2w =  packedspace_to_widget;			# packedspace_to_widget		is from   src/lib/x-kit/widget/space/packed/packedspace-to-widget.pkg
    package g2p =  gadget_to_pixmap;				# gadget_to_pixmap		is from   src/lib/x-kit/widget/theme/gadget-to-pixmap.pkg

    package w2p =  widget_to_packedspace;			# widget_to_packedspace		is from   src/lib/x-kit/widget/space/packed/widget-to-packedspace.pkg
    package s2g =  space_to_gui;				# space_to_gui			is from   src/lib/x-kit/widget/gui/space-to-gui.pkg

    package gt  =  guiboss_types;				# guiboss_types			is from   src/lib/x-kit/widget/gui/guiboss-types.pkg

    package blh =  bouncespace_layout_hint;			# bouncespace_layout_hint	is from   src/lib/x-kit/widget/space/bounce/bouncespace-layout-hint.pkg
    package clh =  canvasspace_layout_hint;			# canvasspace_layout_hint	is from   src/lib/x-kit/widget/space/canvas/canvasspace-layout-hint.pkg
    package plh =  packedspace_layout_hint;			# packedspace_layout_hint	is from   src/lib/x-kit/widget/space/packed/packedspace-layout-hint.pkg

    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    #
    tracefile   =  "widget-unit-test.trace.log";

    nb = log::note_on_stderr;					# log				is from   src/lib/std/src/log.pkg
herein

    package packedspace_imp
    :       Packedspace_Imp												# Packedspace_Imp		is from   src/lib/x-kit/widget/space/packed/packedspace-imp.api
    {
	#
#	include package   guiboss_to_packedspace;									# guiboss_to_packedspace			is from   src/lib/x-kit/widget/space/packed/guiboss-to-packedspace.pkg
	#
	Packedspace_State												# Holds all nonephemeral mutable state maintained by shape.
	  =
	  { id:		Id,
	    state:	Ref( Void )
	  };

	Imports = {													# Ports we use, provided by other imps.
		    int_sink:			Int -> Void,
		    space_to_gui:		s2g::Space_To_Gui							# Our channel to	src/lib/x-kit/widget/gui/guiboss-imp.pkg
		  };

	Exports	= {													# Ports we provide for use by other imps.
		    widget_to_packedspace:	w2p::Widget_To_Packedspace,
		    guiboss_to_packedspace:	gt::Guiboss_To_Packedspace
		  };

	Packedspace_Egg =  Void -> (Exports,   (Imports, Run_Gun, End_Gun) -> Void);

	Me_Slot = Mailslot  ( { imports:		Imports,
				me:			Packedspace_State,
				options:		List(gt::Packedspace_Option),
				run_gun':		Run_Gun,
				end_gun':		End_Gun,
				endstate_oneshot:	Null_Or(Oneshot_Maildrop(gt::Packedspace_Arg)),			# When end_gun fires we save our state in this and exit.
				callback:		Null_Or(gt::Guiboss_To_Packedspace -> Void)
                              }
                            );

	Runstate =  {													# These values will be statically globally visible throughout the code body for the imp.
		      me:		Packedspace_State,								# State which is preserved across imp shutdown/restart cycles.
		      options:		List(gt::Packedspace_Option),
		      imports:		Imports,									# Imps to which we send requests.
		      to:		Replyqueue,									# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		      end_gun':		End_Gun,									# We shut down the microthread when this fires.
		      endstate_oneshot:	Null_Or(Oneshot_Maildrop(gt::Packedspace_Arg))					# When end_gun fires we save our state in this and exit.
		    };

	Packedspace_Q    = Mailqueue( Runstate -> Void );


	fun re_site_packed_widget_tree											# Assign to each widget in given widget-tree a pixel-rectangle on which to draw itself, in window coordinates.
	        ( site:			g2d::Box,									# This is the available window rectangle to divide between our widgets.
		  packed_widget:	gt::Rg_Packed_Widget,								# This is the tree of widgets -- possibly a single leaf widget.
		  space_to_gui:		s2g::Space_To_Gui,								# We use this to tell guiboss-imp what site we have assigned to each widget.
		  backing_pixmap:	s2g::Backing_Pixmap
		)
	    =
	    {	compute_size_preferences_for_row_and_col_widgets (      packed_widget);
		#
		assign_sites_to_all_widgets (site, backing_pixmap, packed_widget);
	    }
	    where
		#
		fun compute_size_preferences_for_row_and_col_widgets
		      #	
		      (packed_widget:	gt::Rg_Packed_Widget)								# This is the tree of widgets -- possibly a single leaf widget.
		    =
		    case packed_widget
			#
			gt::RG_ROW r
			    =>
			    {   do_row (r.widgets, 0, 0, 1.0, 1.0)
				where
				    fun do_row ([], pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut)
					    =>
					    {	r.pixels_high_min := pixels_high_min;
						r.pixels_wide_min := pixels_wide_min;
						#
						r.pixels_high_cut := pixels_high_cut;
						r.pixels_wide_cut := pixels_wide_cut;

					        { pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut };
					    };

					do_row (({ widget, cooked_hints, raw_hints }: gt::Row_Widget) ! rest,  pixels_high_min', pixels_wide_min', pixels_high_cut', pixels_wide_cut')
					    =>
					    {   (compute_size_preferences_for_row_and_col_widgets widget)							# This widget may be a nested ROW or COL:  If so, process it recursively.
						    ->
						    { pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut };


						pixels_high_min = max (pixels_high_min,  pixels_high_min');
						pixels_high_cut = max (pixels_high_cut,  pixels_high_cut');
						#
						pixels_wide_min =     (pixels_wide_min + pixels_wide_min');
						pixels_wide_cut = max (pixels_wide_cut,  pixels_wide_cut');


						do_row (rest,  pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut);
					    };
				    end;
				end;
			    };

			gt::RG_COL r
			    =>
			    {   do_col (r.widgets, 0, 0, 1.0, 1.0)
				where
				    fun do_col ([], pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut)
					    =>
					    {	r.pixels_high_min := pixels_high_min;
						r.pixels_wide_min := pixels_wide_min;
						#
						r.pixels_high_cut := pixels_high_cut;
						r.pixels_wide_cut := pixels_wide_cut;

					        { pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut };
					    };

					do_col (({ widget, cooked_hints, raw_hints }: gt::Col_Widget) ! rest,  pixels_high_min', pixels_wide_min', pixels_high_cut', pixels_wide_cut')
					    =>
					    {   (compute_size_preferences_for_row_and_col_widgets widget)							# This widget may be a nested ROW or COL:  If so, process it recursively.
						    ->
						    { pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut };


						pixels_high_min =     (pixels_high_min + pixels_high_min');
						pixels_high_cut = max (pixels_high_cut,  pixels_high_cut');
						#
						pixels_wide_min = max (pixels_wide_min,  pixels_wide_min');
						pixels_wide_cut = max (pixels_wide_cut,  pixels_wide_cut');


						do_col (rest,  pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut);
					    };
				    end;
				end;
			    };

			gt::RG_SCROLLABLE_VIEW r
			    =>
			    {												# Supply default layout parameters.  User can override by providing WIDGET_PIXELS_CUT and WIDGET_PIXELS_MIN hints in ROW and COL specs -- see  src/lib/x-kit/widget/gui/gui-plan.pkg
				{ pixels_high_min =>  0,
				  pixels_wide_min =>  0,
				  #
				  pixels_high_cut =>  1.0,
				  pixels_wide_cut =>  1.0
				};
			    };

			gt::RG_TABBED_VIEWS r
			    =>
			    {												# Supply default layout parameters.  User can override by providing WIDGET_PIXELS_CUT and WIDGET_PIXELS_MIN hints in ROW and COL specs -- see  src/lib/x-kit/widget/gui/gui-plan.pkg
				{ pixels_high_min =>  0,
				  pixels_wide_min =>  0,
				  #
				  pixels_high_cut =>  1.0,
				  pixels_wide_cut =>  1.0
				};
			    };

			gt::RG_WIDGET { packedspace_to_widget => { pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut, ... }, ... }
			    =>
			    { pixels_high_min, pixels_wide_min, pixels_high_cut, pixels_wide_cut };

			gt::RG_CANVASSPACE r
			    =>
			    {
msg = sprintf "re_site_packed_widget_tree/pass1/CANVASSPACE unimplemented";
nb {. msg; };
raise exception FAIL msg;
			    };
			gt::RG_BOUNCESPACE r
			    =>
			    {
msg = sprintf "re_site_packed_widget_tree/pass1/BOUNCESPACE unimplemented";
nb {. msg; };
raise exception FAIL msg;
			    };

			gt::RG_NULL_PACKED_WIDGET /* r */
			    =>
			    { pixels_wide_min => 0, pixels_high_min => 0, pixels_wide_cut => 0.0, pixels_high_cut => 0.0 };
		    esac;


		fun wide_min_for_widget ({ widget => gt::RG_WIDGET { packedspace_to_widget => { pixels_wide_min => i, ... }, ... }, ... }: gt::Row_Widget) =>   i;
		    wide_min_for_widget ({ widget => gt::RG_ROW                               { pixels_wide_min => i, ... },        ... }: gt::Row_Widget) =>  *i;
		    wide_min_for_widget ({ widget => gt::RG_COL                               { pixels_wide_min => i, ... },        ... }: gt::Row_Widget) =>  *i;
		    wide_min_for_widget _										                                =>   0;
		end;

		fun high_min_for_widget ({ widget => gt::RG_WIDGET { packedspace_to_widget => { pixels_high_min => i, ... }, ... }, ... }: gt::Col_Widget) =>   i;
		    high_min_for_widget ({ widget => gt::RG_ROW                               { pixels_high_min => i, ... },        ... }: gt::Col_Widget) =>  *i;
		    high_min_for_widget ({ widget => gt::RG_COL                               { pixels_high_min => i, ... },        ... }: gt::Col_Widget) =>  *i;
		    high_min_for_widget  _										                                =>   0;
		end;

		fun wide_cut_for_widget ({ widget => gt::RG_WIDGET { packedspace_to_widget => { pixels_wide_cut => f, ... }, ... }, ... }: gt::Row_Widget) =>   f;
		    wide_cut_for_widget ({ widget => gt::RG_ROW                               { pixels_wide_cut => f, ... },        ... }: gt::Row_Widget) =>  *f;
		    wide_cut_for_widget ({ widget => gt::RG_COL                               { pixels_wide_cut => f, ... },        ... }: gt::Row_Widget) =>  *f;
		    wide_cut_for_widget _										                                => 1.0;
		end;

		fun high_cut_for_widget ({ widget => gt::RG_WIDGET { packedspace_to_widget => { pixels_high_cut => f, ... }, ... }, ... }: gt::Col_Widget) =>   f;
		    high_cut_for_widget ({ widget => gt::RG_ROW                               { pixels_high_cut => f, ... },        ... }: gt::Col_Widget) =>  *f;
		    high_cut_for_widget ({ widget => gt::RG_COL                               { pixels_high_cut => f, ... },        ... }: gt::Col_Widget) =>  *f;
		    high_cut_for_widget  _										                                => 1.0;
		end;

		fun assign_sites_to_all_widgets
		      ( site:		g2d::Box,									# This is the available window rectangle to divide between our widgets.
		        backing_pixmap:	s2g::Backing_Pixmap,								# This is the pixmap on which our site is located.
			packed_widget:	gt::Rg_Packed_Widget								# This is the tree of widgets -- possibly a single leaf widget.
		      )
		    =
		    case packed_widget
			#
			gt::RG_ROW r
			    =>
			    {
				r.site    :=  site;									# Remember this widget's assigned site on its home pixmap.
				#
				min_widths =    int::sum  (map  wide_min_for_widget  r.widgets);			# Compute total pixels needed by fixed-width widgets.  Fixed-width widgets get first call on available space; variable-width widgets divide up what is left.
				total_cut  =  float::sum  (map  wide_cut_for_widget  r.widgets);			# Sum cut values of all widgets. Each variable-width widget will get widget.share/total_cut of sharable_pixels.  Which might be zero.

				total_cut  =	if (total_cut > 0.0)	total_cut;
						else			1.0;						# Prevent divide-by-zero in subsequent logic.
						fi;

				site -> { row, col, high, wide };

				sharable_pixels										# Compute pixels remaining after all fixed-width widgets have been given their cut.
				    =
				    if (wide > min_widths)	float::from_int  (wide - min_widths);			#
				    else			0.0;							# No pixels left after giving fixed-width widgets their allotments.
				    fi;

				assign_sites_to_widgets (r.widgets, col)
				where
				    fun assign_sites_to_widgets ([], col)
					    =>
					    ();

					assign_sites_to_widgets ((w as ({ widget, cooked_hints, raw_hints }: gt::Row_Widget)) ! rest,  col)
					    =>
					    {   wide_min =  case cooked_hints.min
								#
								THE i => {
									    i;						# gui-plan option overrides value on widget.
									 };
								NULL  => {
									    wide_min_for_widget  w;
									 };
							    esac;

						wide_cut =  case cooked_hints.cut
								#
								THE f => {
									     f;						# gui-plan option overrides value on widget.
									 };
								NULL  => {
									     wide_cut_for_widget  w;
									 };
							    esac;

						pixels_for_this_widget	=                wide_min
									+ float::round ((wide_cut / total_cut) * sharable_pixels);

						site = { row, col, high, wide => pixels_for_this_widget };

						assign_sites_to_all_widgets (site, backing_pixmap, widget);		# This widget may be a nested ROW, COL or SCROLLABLE_VIEW:  If so, process it recursively.

						assign_sites_to_widgets  (rest, col + pixels_for_this_widget);		# Do remaining widgets in this ROW.
					    };
				    end;
				end;
			    };

			gt::RG_COL r
			    =>
			    {
				r.site    :=  site;									# Remember this widget's assigned site on its home pixmap.
				#
				min_heights =    int::sum  (map  high_min_for_widget  r.widgets);			# Compute total pixels needed by fixed-height widgets.  Fixed-height widgets get first call on available space; variable-height widgets divide up what is left.
				total_cut   =  float::sum  (map  high_cut_for_widget  r.widgets);			# Sum cut values of all widgets. Each variable-height widget will get widget.share/total_cut of sharable_pixels.  Which might be zero.

				total_cut  =	if (total_cut > 0.0)	total_cut;
						else			1.0;						# Prevent divide-by-zero in subsequent logic.
						fi;

				site -> { row, col, high, wide };

				sharable_pixels										# Compute pixels remaining after all fixed-height widgets have been given their cut.
				    =
				    if (high > min_heights)	float::from_int  (high - min_heights);			#
				    else			0.0;							# No pixels left after giving fixed-height widgets their allotments.
				    fi;

				assign_sites_to_widgets (r.widgets, row)
				where
				    fun assign_sites_to_widgets ([], row)
					    =>
					    ();

					assign_sites_to_widgets  ((w as ({ widget,  cooked_hints, raw_hints }: gt::Col_Widget))  !  rest,  row)
					    =>
					    {   high_min =  case cooked_hints.min
								#
								THE i => i;						# gui-plan option overrides value on widget.
								NULL  => high_min_for_widget  w;
							    esac;

						high_cut =  case cooked_hints.cut
								#
								THE f => f;						# gui-plan option overrides value on widget.
								NULL  => high_cut_for_widget  w;
							    esac;

						pixels_for_this_widget	=                high_min
									+ float::round ((high_cut / total_cut) * sharable_pixels);

						site = { row, col, wide, high => pixels_for_this_widget };

						assign_sites_to_all_widgets (site, backing_pixmap, widget);		# This widget may be a nested ROW, COL or SCROALLBLE_VIEW:  If so, process it recursively.

						assign_sites_to_widgets  (rest, row + pixels_for_this_widget);		# Do remaining widgets in this COL.
					    };
				    end;
				end;
			    };

			gt::RG_SCROLLABLE_VIEW r
			    =>
			    {
				r.site    :=  site;									# Remember this widget's assigned site on its home pixmap.
				#


				# Now to do recursive layout of this view:

				subwidget	=  r.widget;
				pixmap_size	=  r.gadget_to_rw_pixmap.size;

				subsite = { row  => 0,  high => pixmap_size.high,					# Initially we have the entire backing_pixmap for the view available for assignment to widgets.
					    col  => 0,  wide => pixmap_size.wide
					  };


				subpixmap = r.backing_pixmap;

				assign_sites_to_all_widgets (subsite, subpixmap, subwidget);				# Recursively lay out the view's widget-tree on its pixmap.
			    };

			gt::RG_TABBED_VIEWS r
			    =>
			    {
				r.site    :=  site;									# Remember this widget's assigned site on its home pixmap.
				#
				apply   do_widget   r.widgets								# 
					#										# 
					where
					    fun do_widget
						  { widget              => subwidget:	gt::Rg_Packed_Widget,
						    gadget_to_rw_pixmap => view_pixmap:	g2p::Gadget_To_Rw_Pixmap,
						    backing_pixmap	=> subpixmap:	s2g::Backing_Pixmap		# 
						  }
						=
						{   # Now to do recursive layout of this view:
						    #
						    subsite = { row  => 0,  high => r.pixmap_size.high,			# Initially we have the entire backing_pixmap for the view available for assignment to widgets.
								col  => 0,  wide => r.pixmap_size.wide
							      };

						    assign_sites_to_all_widgets (subsite, subpixmap, subwidget);	# Recursively lay out the view's widget-tree on its pixmap.
						};
					end;
			    };

			gt::RG_WIDGET (r as { guiboss_to_gadget => gt::GUIBOSS_TO_GADGET guiboss_to_gadget, ... })
			    =>
			    {
				r.site    :=  site;									# Remember this widget's assigned site on its home pixmap.
				#
				space_to_gui.note_widget_site
				  {
				    id			=>  guiboss_to_gadget.id,
				    backing_pixmap	=>  backing_pixmap,
				    site
				  };
			    };

			gt::RG_CANVASSPACE r
			    =>
			    {
				r.site    :=  site;									# Remember this widget's assigned site on its home pixmap.
msg = sprintf "re_site_packed_widget_tree/pass2/CANVASSPACE unimplemented";
nb {. msg; };
raise exception FAIL msg;
			    };
			gt::RG_BOUNCESPACE r
			    =>
			    {
				r.site    :=  site;									# Remember this widget's assigned site on its home pixmap.
msg = sprintf "re_site_packed_widget_tree/pass2/BOUNCESPACE unimplemented";
nb {. msg; };
raise exception FAIL msg;
			    };

			gt::RG_NULL_PACKED_WIDGET /* r */
			    =>
			    ();
		    esac;	
    end;


	fun run   ( packedspace_q:	Packedspace_Q,									# 
		    #
		    runstate as
		    {													# These values will be statically globally visible throughout the code body for the imp.
		      me:		Packedspace_State,								# State which is preserved across imp shutdown/restart cycles.
		      options:		List(gt::Packedspace_Option),
		      imports:		Imports,									# Imps to which we send requests.
		      to:		Replyqueue,									# The name makes   foo::pass_something(imp) to {. ... }   syntax read well.
		      end_gun':		End_Gun,									# We shut down the microthread when this fires.
		      endstate_oneshot:	Null_Or(Oneshot_Maildrop(gt::Packedspace_Arg))					# When end_gun fires we save our state in this and exit.
		    }
		  )
	    =
	    {
		loop ();
	    }
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (end_gun'                     ==>  shut_down_label_imp'),
			    (take_from_mailqueue' packedspace_q ==>  do_label_plea)
			];

			loop ();
		    }	
		    where
			fun do_label_plea thunk
			    =
			    thunk runstate;

			fun shut_down_label_imp' ()
			    =
			    {
				case endstate_oneshot									# Pass our state back to guiboss to allow later impnet restart without state loss.
				    #
				    NULL	=> ();
				    THE oneshot =>  put_in_oneshot (oneshot, options)		;			# 
				esac;

				thread_exit { success => TRUE };							# Will not return.	
			    };
		    end;
	    end;	



	fun startup   (id: Id,   reply_oneshot:  Oneshot_Maildrop( (Me_Slot, Exports) ))   ()				# Root fn of imp microthread.  Note currying.
	    =
	    {   me_slot  =  make_mailslot  ()	:  Me_Slot;
		#
		widget_to_packedspace	=                            { id, look_changed							};
		guiboss_to_packedspace	= gt::GUIBOSS_TO_PACKEDSPACE { id, do_something, pass_something, pass_re_siting_done_flag	};

		exports =  { widget_to_packedspace, guiboss_to_packedspace };

		to          =  make_replyqueue();
		#
		put_in_oneshot (reply_oneshot, (me_slot, exports));							# Return value from packedspace_egg'().

		(take_from_mailslot  me_slot)										# Imports from packedspace_egg'().
		    ->
		    { me, options, imports, run_gun', end_gun', endstate_oneshot, callback };

		block_until_mailop_fires  run_gun';									# Wait for the starting gun.

		case callback   THE callback => callback guiboss_to_packedspace;							# Tell application how to contact us.
				NULL	     => ();
		esac;

		run (packedspace_q, { me, options, imports, to, end_gun', endstate_oneshot });				# Will not return.
	    }
	    where
		packedspace_q     =  make_mailqueue (get_current_microthread()):  Packedspace_Q;


		#######################################################################
		# widget_to_packedspace fns:

		fun look_changed (id: Id)										# PUBLIC.
		    =	
		    put_in_mailqueue  (packedspace_q,
			#
			\\ ({ imports, ... }: Runstate)
			    =
			    ()												# Demonstrate use of imports.
		    );


		#######################################################################
		# guiboss_to_packedspace fns:

		fun do_something (i: Int)										# PUBLIC.
		    =	
		    put_in_mailqueue  (packedspace_q,
			#
			\\ ({ me, imports, ... }: Runstate)
			    =
			    imports.int_sink i										# Demonstrate use of imports.
		    );

		fun pass_re_siting_done_flag										# PUBLIC.
		        ( site:			g2d::Box,
			  topwindow_pixmap:	s2g::Backing_Pixmap,
			  packed_widget:	gt::Rg_Packed_Widget
			)
			(replyqueue:		Replyqueue)
			(reply_handler: 	Void -> Void)
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Void );
			#
			put_in_mailqueue  (packedspace_q,
			    #
			    \\ ({ me, imports, ... }: Runstate)
				=
				{
				    re_site_packed_widget_tree
				      (
					site,
					packed_widget,
					imports.space_to_gui,
					topwindow_pixmap
				      );

				    put_in_oneshot (reply_oneshot, ());							# Signal that widget re-siting is now complete.
				}
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };

		fun pass_something  (replyqueue: Replyqueue)  (reply_handler: Int -> Void)				# PUBLIC.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (packedspace_q,
			    #
			    \\ ({ me, ... }: Runstate)
				=
				put_in_oneshot (reply_oneshot, 0)
			);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };
	    end;


	fun process_options
	    (
	      options:		List(gt::Packedspace_Option),
	      #
	      { name,
		id,
		callback
	      }
	    )
	    =
	    {   my_name		=  REF name;
		my_id		=  REF id;
		my_callback	=  REF callback;
		#
		apply  do_option  options
		where
		    fun do_option (gt::PS_MICROTHREAD_NAME	n) =>  my_name		:=  n;
			do_option (gt::PS_ID			i) =>  my_id		:=  i;
			do_option (gt::PS_CALLBACK 		c) =>  my_callback	:=  THE c;
		    end;
		end;

		{ name     =>  *my_name,
		  id 	   =>  *my_id,
		  callback =>  *my_callback
		};
	    };

        ##########################################################################################
	# PUBLIC.
	#
	fun make_packedspace_egg
		(options:		List(gt::Packedspace_Option))							# PUBLIC. PHASE 1: Construct our state and initialize from 'options'.
		(endstate_oneshot:	Null_Or(Oneshot_Maildrop(gt::Packedspace_Arg)))					# When end_gun fires we save our state in this and exit.
	    =
	    {   (process_options
                  ( options,
		  #
		    { name	=> "packedspace",
		      id	=>  id_zero,
		      callback	=>  NULL
		    } 
		) )
		    ->
		    { name,
		      id, 
		      callback
		    };

		my (id, options)
		    =
		    if (id_to_int(id) == 0)
			id = issue_unique_id();										# Allocate unique imp id.
			(id, gt::PS_ID id ! options);									# Make our id stable across stop/restart cycles.
		    else
			(id, options);
		    fi;

		me = { id, state => REF () };

		\\ () = {   reply_oneshot = make_oneshot_maildrop():  Oneshot_Maildrop( (Me_Slot, Exports) );		# PUBLIC. PHASE 2: Start our microthread and return our Exports to caller.
			    #
			    xlogger::make_thread  name  (startup  (id, reply_oneshot));					# Note that startup() is curried.

			    (get_from_oneshot  reply_oneshot) -> (me_slot, exports);

			    fun phase3											# PUBLIC. PHASE 3: Accept our Imports, then wait for Run_Gun to fire.
				( imports:	Imports,
				  run_gun':	Run_Gun,	
				  end_gun':	End_Gun
				)
				=
				{
				    put_in_mailslot  (me_slot, { me, options, imports, run_gun', end_gun', endstate_oneshot, callback });
				};

			    (exports, phase3);
			};
	    };

    };

end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2014,
## released per terms of SMLNJ-COPYRIGHT.
