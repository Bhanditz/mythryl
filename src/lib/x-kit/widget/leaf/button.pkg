## button.pkg
#
# See also:
#     src/lib/x-kit/widget/leaf/button.pkg
#     src/lib/x-kit/widget/leaf/diamondbutton.pkg
#     src/lib/x-kit/widget/leaf/roundbutton.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib





###                    "If you have ambition, you might not achieve anything,
###                     but without ambition, you are almost certain
###                     not to achieve anything."
###
###                                              -- Whitfield Diffie 


# This package gets used in:
#
#     

stipulate
    include package   threadkit;						# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    include package   geometry2d;						# geometry2d			is from   src/lib/std/2d/geometry2d.pkg
    #
#   package xc  =  xclient;							# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
    #
#   package d3  =  three_d;							# three_d			is from   src/lib/x-kit/widget/old/lib/three-d.pkg

    package evt =  gui_event_types;						# gui_event_types		is from   src/lib/x-kit/widget/gui/gui-event-types.pkg
#   package b2g =  guiboss_to_gadget;						# guiboss_to_gadget		is from   src/lib/x-kit/widget/gui/guiboss-to-gadget.pkg
#   package g2g =  gadget_to_guiboss;						# gadget_to_guiboss		is from   src/lib/x-kit/widget/gui/gadget-to-guiboss.pkg
    package g2p =  gadget_to_pixmap;						# gadget_to_pixmap		is from   src/lib/x-kit/widget/theme/gadget-to-pixmap.pkg
    package gd  =  gui_displaylist;						# gui_displaylist		is from   src/lib/x-kit/widget/theme/gui-displaylist.pkg
    package gt  =  guiboss_types;						# guiboss_types			is from   src/lib/x-kit/widget/gui/guiboss-types.pkg
    package wt  =  widget_theme;						# widget_theme			is from   src/lib/x-kit/widget/theme/widget/widget-theme.pkg
    package wti =  widget_theme_imp;						# widget_theme_imp		is from   src/lib/x-kit/widget/xkit/theme/widget/default/widget-theme-imp.pkg
    package r8  =  rgb8;							# rgb8				is from   src/lib/x-kit/xclient/src/color/rgb8.pkg
    package r64 =  rgb;								# rgb				is from   src/lib/x-kit/xclient/src/color/rgb.pkg
    package w2p =  widget_to_widgetspace;					# widget_to_widgetspace		is from   src/lib/x-kit/widget/space/widget/look-to-widget.pkg
    package wi  =  widget_imp;							# widget_imp			is from   src/lib/x-kit/widget/xkit/theme/widget/default/look/widget-imp.pkg
    package g2d =  geometry2d;							# geometry2d			is from   src/lib/std/2d/geometry2d.pkg
    package g2j =  geometry2d_junk;						# geometry2d_junk		is from   src/lib/std/2d/geometry2d-junk.pkg
    package mtx =  rw_matrix;							# rw_matrix			is from   src/lib/std/src/rw-matrix.pkg
    package pp  =  standard_prettyprint_mill;					# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package gws =  guiboss_to_windowsystem;					# guiboss_to_windowsystem	is from   src/lib/x-kit/widget/theme/guiboss-to-windowsystem.pkg

    package iul =  issue_unique_look_id;					# issue_unique_look_id		is from   src/lib/x-kit/widget/gui/issue-unique-look-id.pkg

    nb =  log::note_on_stderr;							# log				is from   src/lib/std/src/log.pkg
herein

    package button
    :	    Button								# Button			is from   src/lib/x-kit/widget/leaf/button.api
    {
	package p {								# "p" for "position".
	    #
	    Text_Position	= TEXT_AT_LEFT
				| TEXT_AT_RIGHT
				| TEXT_IN_CENTER
				;
	};
	package t {								# "t" for "type".
	    #
	    Button_Type		= MOMENTARY_CONTACT
				| PUSH_ON_PUSH_OFF
				| IGNORE_MOUSECLICKS
				;
	};

	Font_Type = ROMAN_FONT
		  | ITALIC_FONT
		  | BOLD_FONT
		  ;
	

	App_To_Button
	  =
	  { get_active:			Void -> Bool,
	    get_state:			Void -> Bool,
	    #
	    get_button_relief:		Void -> wt::Relief,			# 
	    get_button_type:		Void -> t::Button_Type,			# 
	    #
	    get_button_text:		Void -> Null_Or(String),
	    get_button_on_text:		Void -> Null_Or(String),
	    get_button_off_text:	Void -> Null_Or(String),
	    #
	    get_button_image:		Void -> Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),
	    get_button_on_image:	Void -> Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),
	    get_button_off_image:	Void -> Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),

	    set_button_text:		Null_Or(String) -> Void,
	    set_button_on_text:		Null_Or(String) -> Void,
	    set_button_off_text:	Null_Or(String) -> Void,
	    #
	    set_button_image:		Null_Or(mtx::Rw_Matrix( r8::Rgb8 )) -> Void,
	    set_button_on_image:	Null_Or(mtx::Rw_Matrix( r8::Rgb8 )) -> Void,
	    set_button_off_image:	Null_Or(mtx::Rw_Matrix( r8::Rgb8 )) -> Void,
	    #
	    set_active_to:		Bool -> Void,
	    set_state_to:		Bool -> Void,				# Also calls gadget_to_guiboss.needs_redraw_gadget_request(id);
	    set_button_relief_to:	wt::Relief -> Void			# Also calls gadget_to_guiboss.needs_redraw_gadget_request(id);
	  };


	Redraw_Request_Fn_Arg
	    =
	    REDRAW_REQUEST_FN_ARG
	      {
		id:				iul::Id,			# Unique id.
		frame_number:			Int,				# 1,2,3,... Purely for convenience of widget, guiboss-imp makes no use of this.
		site:				g2d::Box,			# Window rectangle in which to draw.
		visible:			Bool,				# If FALSE, widget is not visible on screen, so look-imp may be able to avoid updating foreground and background.
		duration_in_seconds:		Float,				# If state has changed look-imp should call note_changed_gadget_foreground() before this time is up. Also useful for motionblur.
		gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		this_gadget_mode:		gt::Gadget_Mode,
		theme:				wt::Widget_Theme,
		#
		default_redraw_request_fn:	Redraw_Request_Fn,
		#
		button_state:			Bool,				# Is the button ON or OFF?
		button_type:			t::Button_Type,			# Is the button push-on-push-off or momentary-contact?
		button_relief:			wt::Relief,			# Is the button outline a slope, a ridge, or a flat band?

		text_position:			Null_Or(p::Text_Position),
		text:				Null_Or(String),
		on_text:			Null_Or(String),
		off_text:			Null_Or(String),

		image:				Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),
		on_image:			Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),
		off_image:			Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),

		fonts:				List(String),
		font_type:			Null_Or(Font_Type),
		font_size:			Null_Or(Int),

		no_box:				Bool,
		margin:				Int,
		thick:				Int
	      }
	withtype
	Redraw_Request_Fn
	  =
	  Redraw_Request_Fn_Arg
	  ->
	  { displaylist:		gd::Gui_Displaylist,
	    point_in_gadget:		Null_Or(g2d::Point -> Bool),		# 
	    pixels_high_min:		Int,
	    pixels_wide_min:		Int
	  }
	  ;



	Mousebutton_Fn_Arg
	    =
	    MOUSEBUTTON_FN_ARG							# Needs to be a sumtype because of recursive reference in default_mousebutton_fn.
	      { id:				iul::Id,			# Unique id.
		event:				gt::Mousebutton_Event,		# MOUSEBUTTON_PRESS or MOUSEBUTTON_RELEASE.
		button:				evt::Mousebutton,		# Which mousebutton was pressed/released.
		point:				g2d::Point,			# Where the mouse was.
		site:				g2d::Box,			# Widget's assigned area in window coordinates.
		modifier_keys_state:		evt::Modifier_Keys_State,	# State of the modifier keys (shift, ctrl...).
		mousebuttons_state:		evt::Mousebuttons_State,	# State of mouse buttons as a bool record.
		gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		theme:				wt::Widget_Theme,
		#
		default_mousebutton_fn:		Mousebutton_Fn,
		#
		button_state:			Bool,				# Is the button ON or OFF?
		button_type:			    t::Button_Type,		# Is the button push-on-push-off or momentary-contact?
		button_relief:			Ref(wt::Relief),		# Is the button outline a slope, a ridge, or a flat band?
		#
		initial_state:			Bool,				# Original state of button.
		note_state:			Bool -> Void,			# Change state of button. This takes care of notifying our state-watchers.
		needs_redraw_gadget_request:	Void -> Void			# Notify guiboss-imp that this button needs to be redrawn (i.e., sent a redraw_gadget_request()).
	      }
	withtype
	Mousebutton_Fn = Mousebutton_Fn_Arg -> Void;



	Mouse_Drag_Fn_Arg
	    =
	    MOUSE_DRAG_FN_ARG
	      {
		id:				iul::Id,			# Unique id.
		event_point:			g2d::Point,
		start_point:			g2d::Point,
		last_point:			g2d::Point,
		site:				g2d::Box,			# Widget's assigned area in window coordinates.
		phase:				gt::Drag_Phase,	
		modifier_keys_state:	        evt::Modifier_Keys_State,	# State of the modifier keys (shift, ctrl...).
		mousebuttons_state:		evt::Mousebuttons_State,	# State of mouse buttons as a bool record.
		gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		theme:				wt::Widget_Theme,
		#
		default_mouse_drag_fn:		Mouse_Drag_Fn,
		#
		button_state:			Bool,				# Is the button ON or OFF?
		button_type:			    t::Button_Type,		# Is the button push-on-push-off or momentary-contact?
		button_relief:			Ref(wt::Relief),		# Is the button outline a slope, a ridge, or a flat band?
		#
		initial_state:			Bool,				# Original state of button.
		note_state:			Bool -> Void,			# Change state of button. This takes care of notifying our state-watchers.
		needs_redraw_gadget_request:	Void -> Void			# Notify guiboss-imp that this button needs to be redrawn (i.e., sent a redraw_gadget_request()).
	      }
	withtype
	Mouse_Drag_Fn =  Mouse_Drag_Fn_Arg -> Void;



	Mouse_Transit_Fn_Arg							# Note that buttons are always all up in a mouse-transit event -- otherwise it is a mouse-drag event.
	    =
	    MOUSE_TRANSIT_FN_ARG
	      {
		id:				iul::Id,			# Unique id.
		event_point:			g2d::Point,
		site:				g2d::Box,			# Widget's assigned area in window coordinates.
		transit:			gt::Gadget_Transit,		# Mouse is entering (CAME) or leaving (LEFT) widget, or moving (MOVE) across it.
		modifier_keys_state:	        evt::Modifier_Keys_State,	# State of the modifier keys (shift, ctrl...).
		gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		theme:				wt::Widget_Theme,
		#
		default_mouse_transit_fn:	Mouse_Transit_Fn,
		#
		button_state:			Bool,				# Is the button ON or OFF?
		button_type:			    t::Button_Type,		# Is the button push-on-push-off or momentary-contact?
		button_relief:			Ref(wt::Relief),		# Is the button outline a slope, a ridge, or a flat band?
		#
		initial_state:			Bool,				# Original state of button.
		note_state:			Bool -> Void,			# Change state of button. This takes care of notifying our state-watchers.
		needs_redraw_gadget_request:	Void -> Void			# Notify guiboss-imp that this button needs to be redrawn (i.e., sent a redraw_gadget_request()).
	      }
	withtype
	Mouse_Transit_Fn =  Mouse_Transit_Fn_Arg -> Void;



	Key_Event_Fn_Arg
	    =
	    KEY_EVENT_FN_ARG
	      {
		id:				iul::Id,			# Unique id.
		key_event:			gt::Key_Event,			# KEY_PRESS or KEY_RELEASE
		keycode:			evt::Keycode,			# Keycode of the depressed key.
		keysym:				evt::Keysym,			# Keysym  of the depressed key.
		string:				String,				# Ascii  for the depressed key.
		event_point:			g2d::Point,
		site:				g2d::Box,			# Widget's assigned area in window coordinates.
		modifier_keys_state:	        evt::Modifier_Keys_State,	# State of the modifier keys (shift, ctrl...).
		mousebuttons_state:		evt::Mousebuttons_State,	# State of mouse buttons as a bool record.
		gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		theme:				wt::Widget_Theme,
		#
		default_key_event_fn:		Key_Event_Fn,
		#
		button_state:			Bool,				# Is the button ON or OFF?
		button_type:			    t::Button_Type,		# Is the button push-on-push-off or momentary-contact?
		button_relief:			Ref(wt::Relief),		# Is the button outline a slope, a ridge, or a flat band?
		#
		initial_state:			Bool,				# Original state of button.
		note_state:			Bool -> Void,			# Change state of button. This takes care of notifying our state-watchers.
		needs_redraw_gadget_request:	Void -> Void			# Notify guiboss-imp that this button needs to be redrawn (i.e., sent a redraw_gadget_request()).
	      }
	withtype
	Key_Event_Fn =  Key_Event_Fn_Arg -> Void;



	Option	= PIXELS_SQUARE		Int
		#
		| PIXELS_HIGH_MIN	Int
		| PIXELS_WIDE_MIN	Int
		#
		| PIXELS_HIGH_CUT	Float
		| PIXELS_WIDE_CUT	Float
		#
		| INITIAL_STATE		Bool
		| INITIALLY_ACTIVE	Bool
		#
		| MOMENTARY_CONTACT						# State is non-default (opposite of INITIAL_STATE) only between mouse downclick and upclick.
		| PUSH_ON_PUSH_OFF						# Mouse downclicks toggle state between TRUE and FALSE.
		| IGNORE_MOUSECLICKS						# Mouseclicks to not affect state.
		#
		| RELIEF		wt::Relief				# Should button boundary be drawn flat, raised, sunken, ridged or grooved?
		| MARGIN		Int					# How many pixels to inset button relative to its assigned window site.  Default is 4.
		| THICK			Int					# Thickness of lines (well, polygons) forming button.  Default is 5.
		| NO_BOX							# Do not draw a box around button.
		#
		| TEXT_AT_LEFT
		| TEXT_AT_RIGHT
		| TEXT_IN_CENTER
		#
		| TEXT			String					# Text to draw inside button.  Default is "".
		| ON_TEXT		String					# Text to draw inside button when switch is ON.   Default is TEXT else "".
		| OFF_TEXT		String					# Text to draw inside button when switch is OFF.  Default is TEXT else "".
		#
		| FONTS			List(String)				# Override theme font:  Font to use for text label, e.g. "-*-courier-bold-r-*-*-20-*-*-*-*-*-*-*".  We'll use the first font in list which is found on X server, else "9x15" (which X guarantees to have).
		#
		| ROMAN								# Show any text in plain  font from widget-theme.  This is the default.
		| ITALIC							# Show any text in italic font from widget-theme.
		| BOLD								# Show any text in bold   font from widget-theme.  NB: Text is either bold or italic, not both.
		#
		| FONT_SIZE		Int					# Show any text in this pointsize.  Default is 12.
		#
		| IMAGE			mtx::Rw_Matrix( r8::Rgb8 )		# Image to show to left of text (if any).                    Default is to draw nothing.
		| ON_IMAGE		mtx::Rw_Matrix( r8::Rgb8 )		# Image to show to left of text (if any) when switch is ON.  Default is to use IMAGE value if specified, else to draw nothing.
		| OFF_IMAGE		mtx::Rw_Matrix( r8::Rgb8 )		# Image to show to left of text (if any) when switch is ONF. Default is to use IMAGE value if specified, else to draw nothing.
		#
		| REDRAW_REQUEST_FN	Redraw_Request_Fn			# Application-specific handler for widget redraw.
		| MOUSEBUTTON_FN	Mousebutton_Fn				# Application-specific handler for mousebutton clicks.
		| MOUSE_DRAG_FN		Mouse_Drag_Fn				# Application-specific handler for mouse drags.
		| MOUSE_TRANSIT_FN	Mouse_Transit_Fn			# Application-specific handler for mouse crossings.
		| KEY_EVENT_FN		Key_Event_Fn				# Application-specific handler for keyboard input.
		#
		| STATE_WATCHER		(Bool -> Void)				# Widget's current state              will be sent to these fns each time state changes.
		| PORT_WATCHER		(Null_Or(App_To_Button) -> Void)	# Widget's app port                   will be sent to these fns at widget startup.
		| SITE_WATCHER		(Null_Or(g2d::Box) -> Void)		# Widget's site in window coordinates will be sent to these fns each time it changes.
		| POPUP_WATCHER  Null_Or( (g2d::Box, gt::Gui_Plan)
					  ->
					  (g2d::Box, gt::Client_To_Guiwindow)
					)
					-> Void
		;								# To help prevent deadlock, watcher fns should be fast and nonblocking, typically just setting a var or entering something into a mailqueue.
		
	fun process_options
	    ( options: List(Option),
	      #
	      {	button_type,
		#
		relief,
		margin,
		thick,
		no_box,
		#
		text_position,
		text,
		on_text,
		off_text,
		#
		fonts,
		font_type,
		font_size,
		#
		image,
		on_image,
		off_image,
		#
		redraw_request_fn,
		mousebutton_fn,
		mouse_drag_fn,
		mouse_transit_fn,
		key_event_fn,
		#
		initial_state,
		initially_active,
		#
		widget_options,
	        #
		port_watchers,
		state_watchers,
		popup_watchers,
		site_watchers
	      }
	    )
	    =
	    {   my_button_type		=  REF  button_type;
		#
		my_relief		=  REF  relief;
		my_margin		=  REF  margin;
		my_thick		=  REF  thick;
		my_no_box		=  REF  no_box;
		#
		my_text_position	=  REF  text_position;
		my_text			=  REF  text;
		my_on_text		=  REF  on_text;
		my_off_text		=  REF  off_text;
		#
		my_fonts		=  REF  fonts;
		my_font_type		=  REF  font_type;
		my_font_size		=  REF  font_size;
		#
		my_image		=  REF  image;
		my_on_image		=  REF  on_image;
		my_off_image		=  REF  off_image;
		#
		my_redraw_request_fn	=  REF  redraw_request_fn;
		my_mousebutton_fn	=  REF  mousebutton_fn;
		my_mouse_drag_fn	=  REF  mouse_drag_fn;
		my_mouse_transit_fn	=  REF  mouse_transit_fn;
		my_key_event_fn		=  REF  key_event_fn;
		#
		my_initial_state	=  REF  initial_state;
		my_initially_active	=  REF  initially_active;
		#
		my_widget_options	=  REF  widget_options;
		#
		my_port_watchers	=  REF  port_watchers;
		my_state_watchers	=  REF  state_watchers;
		my_site_watchers	=  REF  site_watchers;
		my_popup_watchers	=  REF  popup_watchers;
		#

		apply  do_option  options
		where
		    fun do_option (INITIAL_STATE		b) =>   my_initial_state	:=  b;
			do_option (INITIALLY_ACTIVE		b) =>   my_initially_active	:=  b;
			#
			do_option (MOMENTARY_CONTACT		 ) =>   my_button_type		:=  t::MOMENTARY_CONTACT;
			do_option (PUSH_ON_PUSH_OFF		 ) =>   my_button_type		:=  t::PUSH_ON_PUSH_OFF;
			do_option (IGNORE_MOUSECLICKS		 ) =>   my_button_type		:=  t::IGNORE_MOUSECLICKS;
			#
			do_option (RELIEF			r) =>   my_relief		:=  r;
			do_option (MARGIN			i) =>   my_margin		:=  i;
			do_option (THICK			i) =>   my_thick		:=  i;
			do_option (NO_BOX			 ) =>   my_no_box		:=  TRUE;
			#
			do_option (TEXT_AT_LEFT			 ) =>   my_text_position	:=  THE p::TEXT_AT_LEFT;
			do_option (TEXT_AT_RIGHT		 ) =>   my_text_position	:=  THE p::TEXT_AT_RIGHT;
			do_option (TEXT_IN_CENTER		 ) =>   my_text_position	:=  THE p::TEXT_IN_CENTER;
			#
			do_option (TEXT				t) =>   my_text			:=  THE t;
			do_option (ON_TEXT			t) =>   my_on_text		:=  THE t;
			do_option (OFF_TEXT			t) =>   my_off_text		:=  THE t;
			#
			do_option (FONTS			t) =>   my_fonts		:=  t;
			#
			do_option (ROMAN			 ) =>   my_font_type		:=  THE ROMAN_FONT;
			do_option (ITALIC			 ) =>   my_font_type		:=  THE ITALIC_FONT;
			do_option (BOLD				 ) =>   my_font_type		:=  THE BOLD_FONT;
			#
			do_option (FONT_SIZE			i) =>   my_font_size		:=  THE i;
			#
			do_option (IMAGE			i) =>	my_image		:=  THE i;
			do_option (ON_IMAGE			i) =>	my_on_image		:=  THE i;
			do_option (OFF_IMAGE			i) =>	my_off_image		:=  THE i;
			#
			do_option (REDRAW_REQUEST_FN		f) =>   my_redraw_request_fn	:=      f;
			do_option (MOUSEBUTTON_FN		f) =>   my_mousebutton_fn	:=      f;
			do_option (MOUSE_DRAG_FN		f) =>	my_mouse_drag_fn	:=  THE f;
			do_option (MOUSE_TRANSIT_FN		f) =>	my_mouse_transit_fn	:=      f;
			do_option (KEY_EVENT_FN			f) =>	my_key_event_fn		:=  THE f;
			#
			do_option (PORT_WATCHER			c) =>	my_port_watchers	:=  c ! *my_port_watchers;
			do_option (STATE_WATCHER		c) =>	my_state_watchers	:=  c ! *my_state_watchers;
			do_option (SITE_WATCHER			c) =>	my_site_watchers	:=  c ! *my_site_watchers;
			do_option (POPUP_WATCHER		c) =>	my_popup_watchers	:=  c ! *my_popup_watchers;
			#
			#
			do_option (PIXELS_HIGH_MIN		i) =>   my_widget_options	:=  (wi::PIXELS_HIGH_MIN i) ! *my_widget_options;
			do_option (PIXELS_WIDE_MIN		i) =>   my_widget_options	:=  (wi::PIXELS_WIDE_MIN i) ! *my_widget_options;
			#
			do_option (PIXELS_HIGH_CUT		f) =>   my_widget_options	:=  (wi::PIXELS_HIGH_CUT f) ! *my_widget_options;
			do_option (PIXELS_WIDE_CUT		f) =>   my_widget_options	:=  (wi::PIXELS_WIDE_CUT f) ! *my_widget_options;
			#
			do_option (PIXELS_SQUARE		i) =>   my_widget_options	:=  (wi::PIXELS_HIGH_MIN   i)
												!   (wi::PIXELS_WIDE_MIN   i)
												!   (wi::PIXELS_HIGH_CUT 0.0)
												!   (wi::PIXELS_WIDE_CUT 0.0)
												!   *my_widget_options;
		    end;
		end;

		{ button_type		=>  *my_button_type,
		  #
		  relief		=>  *my_relief,
		  margin		=>  *my_margin,
		  thick			=>  *my_thick,
		  no_box		=>  *my_no_box,
		  #
		  text_position		=>  *my_text_position,
		  text			=>  *my_text,
		  on_text		=>  *my_on_text,
		  off_text		=>  *my_off_text,
		  #
		  fonts			=>  *my_fonts,
		  font_type		=>  *my_font_type,
		  font_size		=>  *my_font_size,
		  #
		  image			=>  *my_image,
		  on_image		=>  *my_on_image,
		  off_image		=>  *my_off_image,
		  #
		  redraw_request_fn	=>  *my_redraw_request_fn,
		  mousebutton_fn	=>  *my_mousebutton_fn,
		  mouse_drag_fn		=>  *my_mouse_drag_fn,
		  mouse_transit_fn	=>  *my_mouse_transit_fn,
		  key_event_fn		=>  *my_key_event_fn,
		  #
		  initial_state		=>  *my_initial_state,
		  initially_active	=>  *my_initially_active,
		  #
		  widget_options	=>  *my_widget_options,
		  #
		  port_watchers		=>  *my_port_watchers,
		  state_watchers	=>  *my_state_watchers,
		  popup_watchers	=>  *my_popup_watchers,
		  #	
		  site_watchers		=>  *my_site_watchers
		};
	    };


	fun default_redraw_request_fn								# Handle a guiboss request to redraw ourself.
	    (
	      REDRAW_REQUEST_FN_ARG
		{
		  id:				iul::Id,					# Unique id.
		  frame_number:			Int,						# 1,2,3,... Purely for convenience of widget, guiboss-imp makes no use of this.
		  site:				g2d::Box,					# Window rectangle in which to draw.
		  visible:			Bool,						# If FALSE, widget is not visible on screen, so look-imp may be able to avoid updating foreground and background.
		  duration_in_seconds:		Float,						# If state has changed look-imp should call note_changed_gadget_foreground() before this time is up. Also useful for motionblur.
		  gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		  widget_to_widgetspace:	w2p::Widget_To_Widgetspace,
		  this_gadget_mode:		gt::Gadget_Mode,
		  theme:			wt::Widget_Theme,
		  #
		  default_redraw_request_fn:	Redraw_Request_Fn,
		  #
		  button_state:			Bool,						# Is the button ON or OFF?
		  button_type:			t::Button_Type,					# Is the button push-on-push-off or momentary-contact?
		  button_relief:		wt::Relief,					# Is the button outline a slope, a ridge, or a flat band?

		  text_position:		Null_Or(p::Text_Position),
		  text:				Null_Or(String),
		  on_text:			Null_Or(String),
		  off_text:			Null_Or(String),

		  image:			Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),
		  on_image:			Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),
		  off_image:			Null_Or(mtx::Rw_Matrix( r8::Rgb8 )),

		  fonts:			List(String),
		  font_type:			Null_Or(Font_Type),
		  font_size:			Null_Or(Int),

		  no_box:			Bool,
		  margin:			Int,
		  thick:			Int
		}
	    )
	    =
	    {	(*theme.current_gadget_colors { gadget_is_on => button_state, gadget_mode  => this_gadget_mode })
		    ->
		    (p: wt::Gadget_Palette);

		background_box =  site;
		background     = [ gd::COLOR (p.surround_color,  [ gd::FILLED_BOXES [ background_box ]]) ];

		inner_box = g2d::box::make_nested_box (background_box, margin);			# 

		fun pick_image_to_use ()
		    =
		    if button_state
			#
			case on_image
			    #
			    THE i =>  THE i;							# Button is ON so use "ON" image.
			    NULL  =>  image;							# Button is ON but no "ON" image so use plain image (or none).
			esac;
		    else
			case off_image
			    #
			    THE i =>  THE i;							# Button is OFF so use "OFF" image.
			    NULL  =>  image;							# Button is OFF but no "OFF" image so use plain image (or none).
			esac;
		    fi;

		fun pick_text_to_use ()
		    =
		    if button_state
			#
			case on_text
			    #
			    THE t =>  THE t;							# Button is ON so use "ON" text.
			    NULL  =>  text;							# Button is ON but no "ON" text so use plain text (or none).
			esac;
		    else
			case off_text
			    #
			    THE t =>  THE t;							# Button is OFF so use "OFF" text.
			    NULL  =>  text;							# Button is OFF but no "OFF" text so use plain text (or none).
			esac;
		    fi;

		image_to_use =  pick_image_to_use ();
		text_to_use  =  pick_text_to_use  ();

		fun pick_text_position_to_use ()
		    =
		    case text_position
			#
			THE p => p;								# If programmer explicitly specified a position, use that.
			#
			NULL => case image_to_use
				    #
				    THE _ =>  p::TEXT_AT_LEFT;					# If we have an image, position it at the left and text right next to it.
				    NULL  =>  p::TEXT_IN_CENTER;				# By default, center the text on the button.
				esac;       
		    esac;

		text_position_to_use =  pick_text_position_to_use ();

		fun image_displaylist (image:  mtx::Rw_Matrix(r8::Rgb8))
		    =
		    {   # Figure where to place 'image' within 'inner_box'.
			# If there is no text we'll center the image within inner_box,
			# otherwise we'll left-justify it to make room for text to its right:
			#
			to_point
			    =
			    case text_to_use
				#
				THE _ =>    {	col  = inner_box.col
						     + thick								# Indent image by thickness of box.
						     + 10								# Indent by an extra 10 to provide some esthetic whitespace.
						     ; 
						row  = inner_box.row + ((inner_box.high - image.rows) / 2);		# 
						#
						{ row, col };
					    };

				NULL =>	    {	col  = inner_box.col + ((inner_box.wide - image.cols) / 2);		# 
						row  = inner_box.row + ((inner_box.high - image.rows) / 2);		# 
						#
						{ row, col };
					    };
			    esac;

			ilist = [ gd::IMAGE { from_box =>   NULL,				# Use entire image.
					      from     =>   image,
					      to_point
					    }
				];

			ilist;
		    };

		fun get_fontnames ()
		    =
		    {   font_size_to_use
			    =
			    case font_size	THE i => i;
						NULL  => *theme.default_font_size;
			    esac;

			fontname_to_use
			    =
			    case font_type  THE ROMAN_FONT  =>  *theme.get_roman_fontname  font_size_to_use;
					    THE ITALIC_FONT =>  *theme.get_italic_fontname font_size_to_use;
					    THE BOLD_FONT   =>  *theme.get_bold_fontname   font_size_to_use;
					    NULL            =>  *theme.get_roman_fontname  font_size_to_use;
			    esac;

			fontnames =  fonts  @  [ fontname_to_use, "9x15" ];

			fontnames;
		    };


		fun get_text_dimensions (text: String)
		    =
		    {   g =  wti::get__guiboss_to_topwindow  theme;
			#
			font = g.get_font (get_fontnames ());

			{ font_ascent      =>  font.font_height.ascent,
			  font_descent     =>  font.font_height.descent,
			  length_in_pixels =>  font.string_length_in_pixels text
			};
		    };

		fun text_displaylist
		      (
			text:		String,
			text_box:	g2d::Box
		      )
		    =
		    {	text_dimensions =  get_text_dimensions  text;
			#
			fontnames =  get_fontnames ();

			case text_position_to_use
			    #
			    p::TEXT_IN_CENTER
				=>
				{   (g2d::box::midpoint text_box)
				        ->
					{ row, col };

				    row =  row - text_dimensions.font_descent + ((text_dimensions.font_ascent + text_dimensions.font_descent) / 2); 

				    draw_point = { row, col };

				    [ gd::COLOR ( p.text_color, 
						  [ gd::FONT ( fontnames,
							       [ gd::DRAW_TEXT  ( gd::CENTERED_ON_POINT,
										  [ gd::TEXT (draw_point, text) ]
										)
							       ]
							     )
						  ]
						)
				    ];
				};

			    p::TEXT_AT_LEFT
				=>
				{   box_corners =   g2d::box::box_corners  text_box;
				    #
				    (g2d::point::mean [ box_corners.upper_left, box_corners.lower_left ])
				        ->
					{ row, col };

				    # Indent text a bit from image and also
				    # center it properly vertically -- most
				    # fonts have ascent > descent:
				    #
				    col =  col + 10;
				    row =  row - text_dimensions.font_descent + ((text_dimensions.font_ascent + text_dimensions.font_descent) / 2); 
				    #
				    draw_point = { row, col };

				    [ gd::COLOR ( p.text_color, 
						  [ gd::FONT ( fontnames,
							       [ gd::DRAW_TEXT  ( gd::TO_RIGHT_OF_POINT,
										  [ gd::TEXT (draw_point, text) ]
										)
							       ]
							     )
						  ]
						)
				    ];
				};

			    p::TEXT_AT_RIGHT									# 
				=>
				{   box_corners =   g2d::box::box_corners  text_box;
				    #
				    (g2d::point::mean [ box_corners.upper_right, box_corners.lower_right ])
					->
					{ row, col };

				    # Indent text a bit from image and also
				    # center it properly vertically -- most
				    # fonts have ascent > descent:
				    #
				    col =  col - 10 - text_dimensions.length_in_pixels;
				    row =  row - text_dimensions.font_descent + ((text_dimensions.font_ascent + text_dimensions.font_descent) / 2); 
				    #
				    draw_point = { row, col };

				    [ gd::COLOR ( p.text_color, 
						  [ gd::FONT ( fontnames,
							       [ gd::DRAW_TEXT  ( gd::TO_RIGHT_OF_POINT,
										  [ gd::TEXT (draw_point, text) ]
										)
							       ]
							     )
						  ]
						)
				    ];
				};
			esac;
		    };





		
		# Maybe include an enclosing box in button foreground:
		#
		foreground =    case no_box		FALSE =>  [ gd::COLOR (p.body_color, [ gd::FILLED_POLYGON (g2d::box::to_points inner_box) ]) ]		# Interior of button. We draw this first because 3D outline occupies same bounding box.
						  		  @
			 					 *theme.pictureframe p { box => inner_box, thick, relief => button_relief };			# 3-D outline for button.

							TRUE  =>  [];
				esac;	

		# Maybe incorporate image into button foreground:
		#
		foreground =    case image_to_use	THE i =>  foreground @ (image_displaylist i);
							NULL  =>  foreground;
				esac;

		text_indent =   case image_to_use	THE i =>  i.cols + 10;
							NULL  =>            0;
				esac;

		text_box =    { row  =>  inner_box.row,
				col  =>  inner_box.col  + text_indent,
				high =>  inner_box.high,
				wide =>  inner_box.wide - text_indent
			      };

		# Maybe incorporate text into button foreground:
		#
		foreground
		    =
		    case text_to_use
			#
			NULL  => foreground;
			#
			THE t =>    {
					foreground @ (text_displaylist (t, text_box));
				    };
		    esac;

		fun point_in_gadget (point: g2d::Point)
		    =
		    g2d::point::in_box (point, inner_box);

		point_in_gadget = THE point_in_gadget;


		{ displaylist => background @ foreground,
		  point_in_gadget,
		  pixels_high_min => 0,
		  pixels_wide_min => 0
		};
	    };

	fun default_mousebutton_fn
	    (
	      MOUSEBUTTON_FN_ARG
	      { id:				iul::Id,						# Unique id.
		event:				gt::Mousebutton_Event,					# MOUSEBUTTON_PRESS or MOUSEBUTTON_RELEASE.
		button:				evt::Mousebutton,
		point:				g2d::Point,
		site:				g2d::Box,						# Widget's assigned area in window coordinates.
		modifier_keys_state:		evt::Modifier_Keys_State,				# State of the modifier keys (shift, ctrl...).
		mousebuttons_state:		evt::Mousebuttons_State,				# State of mouse buttons as a bool record.
		gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		theme:				wt::Widget_Theme,
		#
		default_mousebutton_fn:		Mousebutton_Fn_Arg -> Void,
		#
		button_state:			Bool,
		button_type:			    t::Button_Type,
		button_relief:			Ref(wt::Relief),
		#
		initial_state:			Bool,
		note_state:			Bool -> Void,
		needs_redraw_gadget_request:	Void -> Void
	      }:				Mousebutton_Fn_Arg
	    )
	    =
	    {
		case event
		    #
		    gt::MOUSEBUTTON_PRESS
			=>
			if (button_type != t::IGNORE_MOUSECLICKS)	
			    #
			    note_state  (not button_state);
			    needs_redraw_gadget_request ();
			fi;

		    gt::MOUSEBUTTON_RELEASE
			=>
			if (button_type == t::MOMENTARY_CONTACT)
			    #
			    note_state  initial_state;
			    needs_redraw_gadget_request ();
			fi;
		esac;

		();
	    };

	fun default_mouse_transit_fn
	    (
	      MOUSE_TRANSIT_FN_ARG
		{
		  id:				iul::Id,						# Unique id.
		  event_point:			g2d::Point,
		  site:				g2d::Box,						# Widget's assigned area in window coordinates.
		  transit:			gt::Gadget_Transit,					# Mouse is entering (CAME) or leaving (LEFT) widget, or moving (MOVE) across it.
		  modifier_keys_state:	        evt::Modifier_Keys_State,				# State of the modifier keys (shift, ctrl...).
		  gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		  widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		  theme:				wt::Widget_Theme,
		  #
		  default_mouse_transit_fn:	Mouse_Transit_Fn,
		  #
		  button_state:			Bool,							# Is the button ON or OFF?
		  button_type:			    t::Button_Type,					# Is the button push-on-push-off or momentary-contact?
		  button_relief:		Ref(wt::Relief),					# Is the button outline a slope, a ridge, or a flat band?
		  #
		  initial_state:		Bool,							# Original state of button.
		  note_state:			Bool -> Void,						# Change state of button. This takes care of notifying our state-watchers.
		  needs_redraw_gadget_request:	Void -> Void						# Notify guiboss-imp that this button needs to be redrawn (i.e., sent a redraw_gadget_request()).
		}
	    )
	    =
	    case transit
		#
		gt::CAME =>  needs_redraw_gadget_request ();						# So button will lighten when mouse enters it.
		gt::LEFT =>  needs_redraw_gadget_request ();						# So button will revert  when mosue leaves it.
		_	     =>  ();
	    esac;

	fun with (options: List(Option))								# PUBLIC
	    =
	    {
		reliefref	= REF wt::RAISED;							# 
		#
		textref		= REF (NULL: Null_Or(String));
		ontextref	= REF (NULL: Null_Or(String));
		offtextref	= REF (NULL: Null_Or(String));
		#
		imageref	= REF (NULL: Null_Or(mtx::Rw_Matrix( r8::Rgb8 )));
		onimageref	= REF (NULL: Null_Or(mtx::Rw_Matrix( r8::Rgb8 )));
		offimageref	= REF (NULL: Null_Or(mtx::Rw_Matrix( r8::Rgb8 )));

	        (process_options
		  (
		    options,
		    #
		    { button_type	=>      t::PUSH_ON_PUSH_OFF,
		      #	
		      relief		=>  *reliefref,	
		      margin		=>  4,
		      thick		=>  5,
		      no_box		=>  FALSE,
		      #
		      text_position	=>  (NULL: Null_Or(p::Text_Position)),
		      text		=>  *textref,
		      on_text		=>  *ontextref,
		      off_text		=>  *offtextref,
		      #
		      fonts		=>  [],
		      font_type		=>  (NULL: Null_Or(Font_Type)),
		      font_size		=>  (NULL: Null_Or(Int)),
		      #
		      image		=>  *imageref,
		      on_image		=>  *onimageref,
		      off_image		=>  *offimageref,
		      #
		      redraw_request_fn	=>  default_redraw_request_fn,
		      mousebutton_fn	=>  default_mousebutton_fn,
		      mouse_drag_fn	=>  NULL,
		      mouse_transit_fn	=>  default_mouse_transit_fn,
		      key_event_fn	=>  NULL,
		      #
		      initial_state	=>  FALSE,
		      initially_active	=>  TRUE,
		      #
		      widget_options	=>  [],
		      #
		      port_watchers	=>  [],
		      state_watchers	=>  [],
		      popup_watchers	=>  [],
		      site_watchers	=>  []
		    }
		) )
		    ->
		    {												# These values are globally visible to the subsequenc fns, which can lock them in as needed.
		      button_type,
		      #
		      relief,
		      margin,
		      thick,
		      no_box,
		      #
		      text_position,
		      text,
		      on_text,
		      off_text,
		      #
		      fonts,
		      font_type,
		      font_size,
		      #
		      image,
		      on_image,
		      off_image,
		      #
		      redraw_request_fn,
		      mousebutton_fn,
		      mouse_drag_fn,
		      mouse_transit_fn,
		      key_event_fn,
		      #
		      initial_state,
		      initially_active,
		      #
		      widget_options,
		      #
		      port_watchers,
		      state_watchers,
		      popup_watchers,
		      #
		      #
		      site_watchers
		    };

		reliefref	:= relief;
		#
		textref		:= text;
		ontextref	:= on_text;
		offtextref	:= off_text;
		#
		imageref	:= image;
		onimageref	:= on_image;
		offimageref	:= off_image;

		###############################
		# Top of state variable section
		#

		gadget_to_guiboss__global
		    =
		    REF (NULL:  Null_Or((gt::Gadget_To_Guiboss, iul::Id)));

		fun note_changed_gadget_activity (is_active: Bool)
		    =
		    case (*gadget_to_guiboss__global)
			#
			THE (gt::GADGET_TO_GUIBOSS gadget_to_guiboss, id)	=>  gadget_to_guiboss.note_changed_gadget_activity { id, is_active };
			NULL							=>  ();
		    esac;

		fun needs_redraw_gadget_request ()
		    =
		    case (*gadget_to_guiboss__global)
			#
			THE (gt::GADGET_TO_GUIBOSS gadget_to_guiboss, id)	=>  gadget_to_guiboss.needs_redraw_gadget_request(id);
			NULL							=>  ();
		    esac;


		last_known_site
		    =
		    REF ( { col => -1,  wide => -1,
			    row => -1,  high => -1
			  }:				g2d::Box
			);

		button_state  =  REF initial_state;


		button_active
		    =
		    REF initially_active;


		exception SAVED_STATE { last_known_site:	g2d::Box,					# Here we're doing the usual hack of using Exception as an extensible datatype -- nothing to do with actually raising or trapping exceptions.
					button_state:		Bool,
					button_active:		Bool
				      };	


		fun note_site (site: g2d::Box)
		    =
		    if(*last_known_site != site)
			last_known_site := site;
			#
			apply notify_watcher site_watchers
			    where
				fun notify_watcher site_watcher
				    =
				    site_watcher (THE site);
			    end;
		    fi;

		fun note_state (state: Bool)
		    =
		    if(*button_state != state)
			button_state := state;
			#
			apply notify_watcher state_watchers
			    where
				fun notify_watcher state_watcher
				    =
				    state_watcher state;
			    end;
		    fi;

		Make_Popup
		    =
		    ( g2d::Box,											# Requested site for popup.
		      gt::Gui_Plan										# GUI to run in popup.
		    )
		      -> ( g2d::Box,										# Actual site for popup.
			   gt::Client_To_Guiwindow								# Port allowing popup to be closed down etc.
			 );

		fun note_popup_maker (make_popup: Make_Popup)
		    =
		    apply notify_watcher popup_watchers
			where
			    fun notify_watcher popup_watcher
				=
				popup_watcher  (THE make_popup);
			end;

		#
		# End of state variable section
		###############################


		#####################
		# Top of port section
		#
		# Here we implement our App_To_Button port:

		fun set_active_to (is_active: Bool)
		    =
		    {   button_active :=  is_active;
			#
			note_changed_gadget_activity  is_active;
		    };

		fun set_state_to (state: Bool)
		    =
		    {   note_state state;
			#
			needs_redraw_gadget_request ();
		    };

		fun set_button_relief_to (relief: wt::Relief)
		    =
		    {
			reliefref := relief;
			#
			needs_redraw_gadget_request ();
		    };

		fun get_active ()
		    =
		    *button_active;

		fun get_state ()
		    =
		    *button_state;

		fun get_button_relief ()
		    =
		    *reliefref;

		fun get_button_type ()
		    =
		    button_type;


		fun get_button_text      () =  *textref;
		fun get_button_on_text   () =  *ontextref;
		fun get_button_off_text  () =  *offtextref;

		fun get_button_image     () =  *imageref;
		fun get_button_on_image  () =  *onimageref;
		fun get_button_off_image () =  *offimageref;

		fun set_button_text      t  =  textref    := t;
		fun set_button_on_text   t  =  ontextref  := t;
		fun set_button_off_text  t  =  offtextref := t;

		fun set_button_image     i =  imageref    := i;
		fun set_button_on_image  i =  onimageref  := i;
		fun set_button_off_image i =  offimageref := i;


		app_to_button
		  =
		  { get_active,
		    get_state,
		    get_button_relief,
		    get_button_type,
		    #
		    get_button_text,
		    get_button_on_text,
		    get_button_off_text,

		    get_button_image,
		    get_button_on_image,
		    get_button_off_image,

		    set_button_text,
		    set_button_on_text,
		    set_button_off_text,

		    set_button_image,
		    set_button_on_image,
		    set_button_off_image,

		    set_active_to,
		    set_state_to,
		    set_button_relief_to
		  }
		  : App_To_Button
		  ;

		#
		# End of port section
		#####################


		###############################
		# Top of widget hook fn section
		#
		# These fns get called by widget_imp logic, ultimately						# widget_imp		is from   src/lib/x-kit/widget/xkit/theme/widget/default/look/widget-imp.pkg
		# in response to user mouseclicks and keypresses etc:

		fun startup_fn
		    { 
		      id:				iul::Id,						# Unique id.
		      gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		      widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		      saved_widget_state:		Null_Or(Exception)
		    }
		    =
		    {	gadget_to_guiboss__global
			    :=  
			    THE (gadget_to_guiboss, id);

			case saved_widget_state
			    #
			    THE (SAVED_STATE r)									# Widget is being restarted after an earlier run.
				=>
				{ last_known_site   := r.last_known_site;					# Restore saved state.
				  button_state	    := r.button_state;
				  button_active	    := r.button_active;
				};

			    THE _ =>										# This shouldn't happen; guiboss_imp must have mistakenly given us another widget's saved state. (Type system cannot prevent this.)
				{   msg = "Wrong saved state! -- startup_fn in button.pkg";
				    log::fatal           msg;							# log			is from   src/lib/std/src/log.pkg
				    raise exception FAIL msg;							# Previous won't return, but type does not (yet?) indicate that properly.
				};

			    NULL => ();										# No saved state: We must be newly created in a fresh session.
			esac;

			apply   notify_watcher  port_watchers							# We do this here rather than (say) above this fn because we don't want the port in circulation until we're running.
				where
				    fun notify_watcher  port_watcher
					=
					port_watcher  (THE app_to_button);
				end;
			();
		    };

		fun shutdown_fn ()										# Return to widget_imp an exception packaging up our state; this will be returned to guiboss_imp, saved in the
		    =												# Paused_Gui tree, and passed to our startup_fn when/if gui is restarted. This exception will never be raised;
		    {	apply   notify_watcher  port_watchers							# 
				where
				    fun notify_watcher  port_watcher
					=
					port_watcher  NULL;
				end;

			apply notify_watcher site_watchers
			    where
				fun notify_watcher site_watcher
				    =
				    site_watcher NULL;
			    end;

			apply notify_watcher popup_watchers
			    where
				fun notify_watcher popup_watcher
				    =
				    popup_watcher NULL;
			    end;

			THE (SAVED_STATE  { last_known_site	=> *last_known_site,				# Exception is being used here purely as an extensible datatype.
					    button_state	=> *button_state,
					    button_active	=> *button_active
					  }	
			    );
		    };
	
		fun initialize_gadget_fn
		    {
		      id:				iul::Id,						# Unique id.
		      site:				g2d::Box,						# Window rectangle in which to draw.
		      gadget_to_guiboss:		gt::Gadget_To_Guiboss,
		      widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
		      theme:				wt::Widget_Theme,
		      pass_font:			List(String) -> Replyqueue
								     -> (evt::Font -> Void) -> Void,		# Nonblocking version of next, for use in imps.
		       get_font:			List(String) ->	 evt::Font,				# Accepts a list of font names which are tried in order.
		      make_rw_pixmap:			g2d::Size -> g2p::Gadget_To_Rw_Pixmap,
		      #
		      make_popup:			(g2d::Box, gt::Gui_Plan)
							    -> ( g2d::Box,
                                                                 gt::Client_To_Guiwindow
                                                               )
		    }
		    =
		    {	note_site site;
			note_popup_maker make_popup;
# rw_pixmap = make_rw_pixmap { wide => 100, high => 100 };
# nb {. "initialize_gadget_fn setting a scratch_rw_pixmap refcell... -- button.pkg"; };
# scratch_rw_pixmap := THE rw_pixmap;
# rw_pixmap.draw_displaylist [ gd::COLOR (r64::yellow, [ gd::FILLED_BOXES [ { row => 0, col => 0, high => 100, wide => 100 } ] ] ) ];
			();
		    };

		fun redraw_request_fn_wrapper
		    {
		      id:							iul::Id,			# Unique id.
		      frame_number:						Int,				# 1,2,3,... Purely for convenience of widget-imp, guiboss-imp makes no use of this.
		      site:							g2d::Box,			# Window rectangle in which to draw.
		      visible:							Bool,				# If FALSE, widget is not visible on screen, so widget-imp may be able to avoid updating foreground and background.
		      duration_in_seconds:					Float,				# If state has changed widget-imp should call redraw_gadget() before this time is up. Also useful for motionblur.
		      gadget_to_guiboss as gt::GADGET_TO_GUIBOSS g2g:		gt::Gadget_To_Guiboss,
		      widget_to_widgetspace:					w2p::Widget_To_Widgetspace,
		      this_gadget_mode:						gt::Gadget_Mode,
		      theme:							wt::Widget_Theme
		    }
		    =
		    {	note_site site;
			#
			redraw_request_fn_arg
			    =
			    REDRAW_REQUEST_FN_ARG
			      { id,
				frame_number,
				site,
				visible,
				duration_in_seconds,
				gadget_to_guiboss,
				widget_to_widgetspace,
				this_gadget_mode,
				theme,
				#
				default_redraw_request_fn,	
				#
				button_state	=> *button_state,
				button_type,
				button_relief	=> *reliefref,

				text_position,
				text		=> *textref,
				on_text		=> *ontextref,
				off_text	=> *offtextref,

				image		=> *imageref,
				on_image	=> *onimageref,
				off_image	=> *offimageref,

				fonts,
				font_type,
				font_size,

				no_box,
				margin,
				thick
			      };

			(redraw_request_fn  redraw_request_fn_arg)
			    ->
			    { displaylist,
			      point_in_gadget,
			      pixels_high_min,
			      pixels_wide_min
			    };

			g2g.redraw_gadget { id, displaylist, point_in_gadget };
		    };


		fun mousebutton_fn_wrapper									# This a callback we hand to   src/lib/x-kit/widget/xkit/theme/widget/default/look/widget-imp.pkg
		      {
			id:				iul::Id,						# Unique id.
			event:				gt::Mousebutton_Event,					# MOUSEBUTTON_PRESS or MOUSEBUTTON_RELEASE.
			button:				evt::Mousebutton,
			point:				g2d::Point,
			site:		 		g2d::Box,						# Widget's assigned area in window coordinates.
			modifier_keys_state:	        evt::Modifier_Keys_State,				# State of the modifier keys (shift, ctrl...).
			mousebuttons_state:		evt::Mousebuttons_State,				# State of mouse buttons as a bool record.
			gadget_to_guiboss:		gt::Gadget_To_Guiboss,
			widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
			theme:				wt::Widget_Theme
		      }
		    = 
		    {	note_site  site;
			#
			mousebutton_fn_arg
			    =
			    MOUSEBUTTON_FN_ARG
			      {
				id,
				event,
				button,
				point,
				site,
				modifier_keys_state,
				mousebuttons_state,
				gadget_to_guiboss,
				widget_to_widgetspace,
				theme,
				#
				default_mousebutton_fn,
				#
				button_state	=> *button_state,						# We don't pass the refcell here because we want client code to make state changes via note_state(), which will properly notify all state-watchers.
				button_type,
				button_relief	=>  reliefref,
				#
				initial_state,
				note_state,
				needs_redraw_gadget_request
			      };

			mousebutton_fn  mousebutton_fn_arg;
		    };

		fun mouse_drag_fn_wrapper									# This a callback we hand to   src/lib/x-kit/widget/xkit/theme/widget/default/look/widget-imp.pkg
		    (
		      {	id:				iul::Id,						# Unique id.
			event_point:			g2d::Point,
			start_point:			g2d::Point,
			last_point:			g2d::Point,
			site:				g2d::Box,						# Widget's assigned area in window coordinates.
			phase:				gt::Drag_Phase,	
			modifier_keys_state:	        evt::Modifier_Keys_State,				# State of the modifier keys (shift, ctrl...).
			mousebuttons_state:		evt::Mousebuttons_State,				# State of mouse buttons as a bool record.
			gadget_to_guiboss:		gt::Gadget_To_Guiboss,
			widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
			theme:				wt::Widget_Theme
		      }
		    )
		    = 
		    {	note_site  site;
			#
			mouse_drag_fn_arg
			    =
			    MOUSE_DRAG_FN_ARG
			      {
				id,
				event_point,
				start_point,
				last_point,
				site,
				phase,
				modifier_keys_state,
				mousebuttons_state,
				gadget_to_guiboss,
				widget_to_widgetspace,
				theme,
				#
				default_mouse_drag_fn =>  \\ _ = (),						# Default drag behavior for buttons is to do absolutely nothing.
				#
				button_state	=> *button_state,						# We don't pass the refcell here because we want client code to make state changes via note_state(), which will properly notify all state-watchers.
				button_type,
				button_relief	=>  reliefref,
				#
				initial_state,
				note_state,
				needs_redraw_gadget_request
			      };

			case mouse_drag_fn
			    #
			    THE mouse_drag_fn =>   mouse_drag_fn  mouse_drag_fn_arg;
			    NULL	      =>   ();								# We do not expect this case to happen: If mouse_drag_fn is NULL mouse_drag_fn_wrapper should not have been registered with widget-imp so we should never get called.
			esac;
		    };

		fun mouse_transit_fn_wrapper
		      #
		      ( arg as
			{
			  id:				iul::Id,						# Unique id.
			  event_point:			g2d::Point,
			  site:		 		g2d::Box,						# Widget's assigned area in window coordinates.
			  transit:			gt::Gadget_Transit,					# Mouse is entering (CAME) or leaving (LEFT) widget, or moving (MOVE) across it.
			  modifier_keys_state:	        evt::Modifier_Keys_State,				# State of the modifier keys (shift, ctrl...).
			  gadget_to_guiboss:		gt::Gadget_To_Guiboss,
			  widget_to_widgetspace:	w2p::Widget_To_Widgetspace,
			  theme:			wt::Widget_Theme
			}
		      )	
		    = 
		    {	note_site site;
			#
			mouse_transit_fn_arg
			    =
			    MOUSE_TRANSIT_FN_ARG
			      {
				id,
				event_point,
				site,
				transit,
				modifier_keys_state,
				gadget_to_guiboss,
				widget_to_widgetspace,
				theme,
				#
				default_mouse_transit_fn =>  \\ _ = (),						# Default transit behavior for buttons is to do absolutely nothing.
				#
				button_state	=> *button_state,						# We don't pass the refcell here because we want client code to make state changes via note_state(), which will properly notify all state-watchers.
				button_type,
				button_relief	=>  reliefref,
				#
				initial_state,
				note_state,
				needs_redraw_gadget_request
			      };

			mouse_transit_fn  mouse_transit_fn_arg;

			();
		    };

		fun key_event_fn_wrapper
		      {
			id:				iul::Id,						# Unique id.
			key_event:			gt::Key_Event,						# KEY_PRESS or KEY_RELEASE.
			keycode:			evt::Keycode,						# Keycode of the depressed key.
			keysym:				evt::Keysym,						# Keysym  of the depressed key.
			string:				String,							# Ascii  for the depressed key.
			event_point:			g2d::Point,
			site:		 		g2d::Box,						# Widget's assigned area in window coordinates.
			modifier_keys_state:	        evt::Modifier_Keys_State,				# State of the modifier keys (shift, ctrl...).
			mousebuttons_state:		evt::Mousebuttons_State,				# State of mouse buttons as a bool record.
			gadget_to_guiboss:		gt::Gadget_To_Guiboss,
			widget_to_widgetspace:		w2p::Widget_To_Widgetspace,
			theme:				wt::Widget_Theme
		      }
		    = 
		    {	note_site site;
			#
			key_event_fn_arg
			    =
			    KEY_EVENT_FN_ARG
			      {
				id,
				key_event,
				keycode,
				keysym,
				string,
				event_point,
				site,
				modifier_keys_state,
				mousebuttons_state,
				gadget_to_guiboss,
				widget_to_widgetspace,
				theme,
				#
				default_key_event_fn =>  \\ _ = (),						# Default key event behavior for buttons is to do absolutely nothing.
				#
				button_state	=> *button_state,						# We don't pass the refcell here because we want client code to make state changes via note_state(), which will properly notify all state-watchers.
				button_type,
				button_relief	=>  reliefref,
				#
				initial_state,
				note_state,
				needs_redraw_gadget_request
			      };

			case key_event_fn
			    #
			    THE key_event_fn =>   key_event_fn  key_event_fn_arg;
			    NULL	     =>   ();								# We do not expect this case to happen: If key_event_fn is NULL key_event_fn_wrapper should not have been registered with widget-imp so we should never get called.
			esac;

		       ();
		    };


		#
		# End of widget hook fn section
		###############################

		widget_options
		    =
		    case mouse_drag_fn
			#
			THE _ =>  (wi::MOUSE_DRAG_FN mouse_drag_fn_wrapper)       ! widget_options;		# Register for drag events only if we are going to use them.
			NULL  =>					            widget_options;
		    esac;

		widget_options
		    =
		    case key_event_fn
			#
			THE _ =>  (wi::KEY_EVENT_FN key_event_fn_wrapper)	  ! widget_options;		# Register for key events only if we are going to use them.
			NULL  =>						    widget_options;
		    esac;

		widget_options
		  =
		  [ wi::STARTUP_FN			startup_fn,						# We always register for these five because our base behavior depends on them.
		    wi::SHUTDOWN_FN			shutdown_fn,
		    wi::INITIALIZE_GADGET_FN		initialize_gadget_fn,
		    wi::REDRAW_REQUEST_FN		redraw_request_fn_wrapper,
		    wi::MOUSEBUTTON_FN			mousebutton_fn_wrapper,
		    wi::MOUSE_TRANSIT_FN		mouse_transit_fn_wrapper
		  ]
		  @
		  widget_options
		  ;

		make_widget_fn =  wi::make_widget_start_fn  widget_options;

		gt::WIDGET   make_widget_fn;									# So caller can write   gui_plan = gt::ROW [ button::with [...], button::with [...], ... ];
	    };													# PUBLIC
    };
end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1994 by AT&T Bell Laboratories  See SMLNJ-COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2014,
## released per terms of SMLNJ-COPYRIGHT.
