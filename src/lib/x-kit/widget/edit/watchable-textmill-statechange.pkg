## watchable-textmill-statechange.pkg
#
# Textpane state information stored in textmill							# textmill			is from   src/lib/x-kit/widget/edit/textmill.pkg
# without revealing the relevant types to textmill.
#
# See also:
#     src/lib/x-kit/widget/edit/textpane.pkg
#     src/lib/x-kit/widget/edit/millboss-imp.pkg
#     src/lib/x-kit/widget/edit/textmill.pkg
#     src/lib/x-kit/widget/edit/keystroke-macro-junk.pkg

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include package   threadkit;								# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package mt  =  millboss_types;								# millboss_types		is from   src/lib/x-kit/widget/edit/millboss-types.pkg

    nb = log::note_on_stderr;									# log				is from   src/lib/std/src/log.pkg
herein

    package watchable_textmill_statechange							# 
    {
	Watchable_Textmill_Statechange
	  =	
	  { note_watcher:	(Id, (Id, mt::Textmill_Statechange) -> Void) -> Void, 		# First 'Id' is watcher's (caller's) -- identifies this particular watcher for drop_watcher.  Second 'Id' is server's, identifiers the particular server for watcher.
	    drop_watcher:	Id -> Void							# 'Id' must match that given to note_watcher (or texteditor_imp::WATCHER).
	  };				 

	exception  WATCHABLE_TEXTMILL_STATECHANGE  Watchable_Textmill_Statechange;		# We'll never 'raise' this exception:  It is purely a datastructure to hide the Watchable_Textmill_Statechange type from millboss-imp.pkg, in the interests of good modularity.
	#
	#
	fun maybe_decrypt__watchable_textmill_statechange  (watchable:  mt::Watchable):  Fail_Or( Watchable_Textmill_Statechange )
	    =
	    case watchable.data
		#
		WATCHABLE_TEXTMILL_STATECHANGE
		watchable_textmill_statechange
		    =>
		    WORK watchable_textmill_statechange;

		_   =>  FAIL (sprintf "maybe_decrypt__watchable_textmill_statechange:  Unknown Watchable value, outer_type='%s', inner_type='%s' info='%s'  --watchable-textmill-statechange.pkg"
					watchable.outer_type
					watchable.inner_type
					watchable.info
			     );
	    esac;	

	fun decrypt__watchable_textmill_statechange  (watchable:  mt::Watchable):   Watchable_Textmill_Statechange
	    =
	    case watchable.data
		#
		WATCHABLE_TEXTMILL_STATECHANGE
		watchable_textmill_statechange
		    =>
		    watchable_textmill_statechange;

		_   =>	{   msg = (sprintf "maybe_decrypt__watchable_textmill_statechange:  Unknown Watchable value, outer_type='%s', inner_type='%s' info='%s'  --watchable-textmill-statechange.pkg"
					watchable.outer_type
					watchable.inner_type
					watchable.info
			          );
			    log::fatal msg;							# Won't return.
			    raise exception DIE msg;						# Just to keep compiler happy.
			};
	    esac;	


	fun encrypt__watchable_textmill_statechange  (watchable_textmill_statechange:  Watchable_Textmill_Statechange):  mt::Watchable
	    =
	    { id	 =>  issue_unique_id (),
	      outer_type =>  "watchable_textmill_statechange::Watchable_Textmill_Statechange",
	      inner_type =>  "millboss_types::Textmill_Statechange",
	      info	 =>  "Wrapped by watchable_textmill_statechange::encrypt__watchable_textmill_statechange.",
	      data	 =>  WATCHABLE_TEXTMILL_STATECHANGE watchable_textmill_statechange
	    };		 
    };

end;





##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2014,
## released per terms of SMLNJ-COPYRIGHT.
