## gadget-to-editboss.pkg
#
# Here we define the port which
#
#     src/lib/x-kit/widget/edit/editboss-imp.pkg
#
# exports to
#
#     src/lib/x-kit/widget/edit/textedit.pkg
#     src/lib/x-kit/widget/edit/textpane.pkg
#     src/lib/x-kit/widget/edit/screenline.pkg
#
# and company.

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib



stipulate
    include package   threadkit;								# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
#   package ebt =  editboss_types;								# editboss_types		is from   src/lib/x-kit/widget/edit/editboss-types.pkg

    package g2d =  geometry2d;									# geometry2d			is from   src/lib/std/2d/geometry2d.pkg

    package nl  =  red_black_numbered_list;							# red_black_numbered_list	is from   src/lib/src/red-black-numbered-list.pkg
    package iuw =  issue_unique_widget_id;							# issue_unique_widget_id	is from   src/lib/x-kit/widget/gui/issue-unique-widget-id.pkg

    package e2p =  textedit_to_textpane;							# textedit_to_textpane		is from   src/lib/x-kit/widget/edit/textedit-to-textpane.pkg
    package l2p =  screenline_to_textpane;							# screenline_to_textpane	is from   src/lib/x-kit/widget/edit/screenline-to-textpane.pkg
    package p2e =  textpane_to_textedit;							# textpane_to_textedit		is from   src/lib/x-kit/widget/edit/textpane-to-textedit.pkg
    package p2l =  textpane_to_screenline;							# textpane_to_screenline	is from   src/lib/x-kit/widget/edit/textpane-to-screenline.pkg

    package b2l =  editboss_to_screenline;							# editboss_to_screenline	is from   src/lib/x-kit/widget/edit/editboss-to-screenline.pkg
    package b2e =  editboss_to_textedit;							# editboss_to_textedit		is from   src/lib/x-kit/widget/edit/editboss-to-textedit.pkg
    package b2p =  editboss_to_textpane;							# editboss_to_textpane		is from   src/lib/x-kit/widget/edit/editboss-to-textpane.pkg
    package t2t =  textpane_to_textbuffer;							# textpane_to_textbuffer	is from   src/lib/x-kit/widget/edit/textpane-to-textbuffer.pkg

    package gt  =  guiboss_types;								# guiboss_types			is from   src/lib/x-kit/widget/gui/guiboss-types.pkg
    package ct  =  cutbuffer_types;								# cutbuffer_types		is from   src/lib/x-kit/widget/edit/cutbuffer-types.pkg
herein

    # This port is implemented in:
    #
    #     src/lib/x-kit/widget/edit/editboss-imp.pkg
    #
    package gadget_to_editboss {
	#
	Gadget_To_Editboss									# guiboss provides this interface to gadgets via the guiboss_types Gadget_To_Guiboss interface.  This is the toplevel application code access pathway to texteditor functionality.
	  =											# This was in a separate guiboss-to-editboss.pkg file:  Moved it here to resolve a package-cycle problem.
	  { id:				Id,							# Unique id to facilitate storing gadget_to_editboss instances in indexed datastructures like red-black trees.
	    #
	    get_textbuffer:		String -> Null_Or( t2t::Textpane_To_Textbuffer ),	
	    get_or_make_textbuffer:	t2t::Textbuffer_Arg -> t2t::Textpane_To_Textbuffer,
	    #
	    get_or_make_filebuffer:	{ dirname:		String,
					  textbuffer_arg:	t2t::Textbuffer_Arg
					}
					-> t2t::Textpane_To_Textbuffer,

	    get_cutbuffer_contents:	Void -> ct::Cutbuffer_Contents,				# O(1) nonblocking.
	    set_cutbuffer_contents:	ct::Cutbuffer_Contents -> Void,

	    note_screenline:		(b2l::Editboss_To_Screenline, p2l::Textpane_To_Screenline) -> Void,
	    #
	    note_textpane:		(b2p::Editboss_To_Textpane, e2p::Textedit_To_Textpane, l2p::Screenline_To_Textpane)  -> Void,
	    note_textedit:		(b2e::Editboss_To_Textedit, p2e::Textpane_To_Textedit                             )  -> Void,

	    drop_screenline:		{ screenline_id:	iuw::Id,
	    				  textedit_id:		iuw::Id,
	    				  textpane_id:		iuw::Id
					}
					-> Void,
	    #
	    drop_textpane:		{ textpane_id:		iuw::Id,
	    				  textedit_id:		iuw::Id
					}
					-> Void,

	    drop_textedit:		{ textedit_id:		iuw::Id
					}
					-> Void
	  };

	exception GADGET_TO_EDITBOSS Gadget_To_Editboss;


	fun find__gadget_to_editboss
	      (
		gadget_to_guiboss:	gt::Gadget_To_Guiboss,
		caller:			String
	      )
	    =
	    case (gadget_to_guiboss.find_global "gadget_to_editboss::GADGET_TO_EDITBOSS")
		#
		THE gadget_to_editboss
		    =>
		    case gadget_to_editboss.val
			#
			GADGET_TO_EDITBOSS gadget_to_editboss
			    =>
			    gadget_to_editboss;

			_ =>    {   msg =  sprintf "Expected Crypt of GADGET_TO_EDITBOSS but got Crypt of { key=>\"%s\", doc=>\"%s\" }  -- gadget_to_editboss::find__gadget_to_editboss called by %s" gadget_to_editboss.key gadget_to_editboss.doc caller;				# "Impossible."
				    log::fatal msg;
				    raise exception FAIL msg;
				};
		    esac;

		NULL =>	{   msg =  sprintf "GADGET_TO_EDITBOSS global not registered with guiboss_imp in time?! -- gadget_to_editboss::find__gadget_to_editboss called by %s" caller;				# "Impossible."
			    log::fatal msg;
			    raise exception FAIL msg;
			};
	    esac;
    };
end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2015,
## released per terms of SMLNJ-COPYRIGHT.
