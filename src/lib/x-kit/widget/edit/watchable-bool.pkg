## watchable-bool.pkg
#

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include package   threadkit;						# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package mt  =  millboss_types;						# millboss_types		is from   src/lib/x-kit/widget/edit/millboss-types.pkg

    nb = log::note_on_stderr;							# log				is from   src/lib/std/src/log.pkg
herein

    package watchable_bool							# 
    {
	Watchable_Bool
	  =	
	  { note_watcher:	(Id, (Id, Bool) -> Void) -> Void, 		# First 'Id' is watcher's (caller's) -- identifies this particular watcher for drop_watcher.  Second 'Id' is server's, identifiers the particular server for watcher.
	    drop_watcher:	Id -> Void					# 'Id' must match that given to note_watcher (or texteditor_imp::WATCHER).
	  };				 

	exception  WATCHABLE_BOOL  Watchable_Bool;				# We'll never 'raise' this exception:  It is purely a datastructure to hide the Watchable_Bool type from millboss-imp.pkg, in the interests of good modularity.
	#
	#
	fun maybe_unwrap__watchable_bool  (watchable:  mt::Watchable):  Fail_Or( Watchable_Bool )
	    =
	    case watchable.data
		#
		WATCHABLE_BOOL
		watchable_bool
		    =>
		    WORK watchable_bool;

		_   =>  FAIL (sprintf "maybe_unwrap__watchable_bool:  Unknown Watchable value, outer_type='%s', inner_type='%s' info='%s'  --watchable-bool.pkg"
					watchable.outer_type
					watchable.inner_type
					watchable.info
			     );
	    esac;	

	fun unwrap__watchable_bool  (watchable:  mt::Watchable):   Watchable_Bool
	    =
	    case watchable.data
		#
		WATCHABLE_BOOL
		watchable_bool
		    =>
		    watchable_bool;

		_   =>	{   msg = (sprintf "maybe_unwrap__watchable_bool:  Unknown Watchable value, outer_type='%s', inner_type='%s' info='%s'  --watchable-bool.pkg"
					watchable.outer_type
					watchable.inner_type
					watchable.info
			          );
			    log::fatal msg;							# Won't return.
			    raise exception DIE msg;						# Just to keep compiler happy.
			};
	    esac;	


	fun wrap__watchable_bool  (watchable_bool:  Watchable_Bool):  mt::Watchable
	    =
	    { id	 =>  issue_unique_id (),
	      outer_type =>  "watchable_bool::Watchable_Bool",
	      inner_type =>  "Bool",
	      info	 =>  "Wrapped by watchable_bool::wrap__watchable_bool.",
	      data	 =>  WATCHABLE_BOOL watchable_bool
	    };		 
    };

end;





##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2014,
## released per terms of SMLNJ-COPYRIGHT.
