## floats-millout.pkg
#

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib


stipulate
    include package   threadkit;								# threadkit			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package mt  =  millboss_types;								# millboss_types		is from   src/lib/x-kit/widget/edit/millboss-types.pkg

#   package im  =  int_red_black_map;								# int_red_black_map		is from   src/lib/src/int-red-black-map.pkg
#   package is  =  int_red_black_set;								# int_red_black_set		is from   src/lib/src/int-red-black-set.pkg
    package sm  =  string_map;									# string_map			is from   src/lib/src/string-map.pkg

    nb = log::note_on_stderr;									# log				is from   src/lib/std/src/log.pkg
herein

    package floats_millout									# 
    {
	Floats
	  =
	  { units:		Null_Or(String),
	    floats:		sm::Map(Float)
	  };

	Floats_Millout
	  =	
	  { note_watcher:	(Id, (Id, Floats) -> Void) -> Void, 				# First 'Id' is watcher's (caller's) -- identifies this particular watcher for drop_watcher.  Second 'Id' is server's, identifiers the particular server for watcher.
	    drop_watcher:	Id -> Void							# 'Id' must match that given to note_watcher (or texteditor_imp::WATCHER).
	  };				 

	exception  FLOATS_MILLOUT  Floats_Millout;						# We'll never 'raise' this exception:  It is purely a datastructure to hide the Floats_Millout type from millboss-imp.pkg, in the interests of good modularity.
	#
	#
	fun maybe_unwrap__floats_millout  (watchable:  mt::Millout):  Fail_Or( Floats_Millout )
	    =
	    case watchable.crypt
		#
		FLOATS_MILLOUT
		floats_millout
		    =>
		    WORK floats_millout;

		_   =>  FAIL (sprintf "maybe_unwrap__floats_millout:  Unknown Millout value, port_type='%s', data_type='%s' info='%s'  --floats-millout.pkg"
					watchable.port_type 
					watchable.data_type 
					watchable.info
			     );
	    esac;	

	fun unwrap__floats_millout  (watchable:  mt::Millout):   Floats_Millout
	    =
	    case watchable.crypt
		#
		FLOATS_MILLOUT
		floats_millout
		    =>
		    floats_millout;

		_   =>	{   msg = (sprintf "maybe_unwrap__floats_millout:  Unknown Millout value, port_type='%s', data_type='%s' info='%s'  --floats-millout.pkg"
					watchable.port_type 
					watchable.data_type 
					watchable.info
			          );
			    log::fatal msg;							# Won't return.
			    raise exception DIE msg;						# Just to keep compiler happy.
			};
	    esac;	


	fun wrap__floats_millout  (floats_millout:  Floats_Millout):  mt::Millout
	    =
	    { id	 =>  issue_unique_id (),
	      port_type  =>  "floats_millout::Floats_Millout",
	      data_type  =>  "floats_millout::Floats",
	      info	 =>  "Wrapped by floats_millout::wrap__floats_millout.",
	      crypt	 =>  FLOATS_MILLOUT floats_millout
	    };		 
    };

end;





##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Original code by Jeff Prothero Copyright (c) 2014-2014,
## released per terms of SMLNJ-COPYRIGHT.
