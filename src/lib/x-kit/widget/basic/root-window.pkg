## root-window.pkg
#
# This widget represents the root window on an X screen
# -- the one on which the wallpaper is drawn.  X stores
# various things like the X resource database as properties
# on the root window.

# Compiled by:
#     src/lib/x-kit/widget/xkit-widget.sublib




###              "Deep in their roots, all flowers keep the light."
###
###                                    -- Theodore Roethke


# See also:
#
#     src/lib/x-kit/widget/basic/root-window-old.api

stipulate
    include threadkit;						# threadkit		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
    #
    package xt  =  xtypes;					# xtypes		is from   src/lib/x-kit/xclient/src/wire/xtypes.pkg
    package xc  =  xclient;					# xclient		is from   src/lib/x-kit/xclient/xclient.pkg
    package ii  =  image_ximp;					# image_ximp		is from   src/lib/x-kit/widget/lib/image-ximp.pkg
    package ip  =  image_port;					# image_port		is from   src/lib/x-kit/widget/lib/image-port.pkg
    package pxc =  ro_pixmap_ximp;				# ro_pixmap_ximp	is from   src/lib/x-kit/widget/lib/ro-pixmap-ximp.pkg
    package rpp =  ro_pixmap_port;				# ro_pixmap_port	is from   src/lib/x-kit/widget/lib/ro-pixmap-port.pkg
    package si  =  shade_ximp;					# shade_ximp		is from   src/lib/x-kit/widget/lib/shade-ximp.pkg
    package shp =  shade_port;					# shade_port		is from   src/lib/x-kit/widget/lib/shade-port.pkg
    package wa  =  widget_attribute;				# widget_attribute	is from   src/lib/x-kit/widget/lib/widget-attribute.pkg
    package wy  =  widget_style;				# widget_style		is from   src/lib/x-kit/widget/lib/widget-style.pkg
    package xrs =  cursors;					# cursors		is from   src/lib/x-kit/xclient/src/window/cursors.pkg
    package rop =  ro_pixmap;					# ro_pixmap		is from   src/lib/x-kit/xclient/src/window/ro-pixmap.pkg
    package cpm =  cs_pixmap;					# cs_pixmap		is from   src/lib/x-kit/xclient/src/window/cs-pixmap.pkg
    package rgb =  rgb;						# rgb			is from   src/lib/x-kit/xclient/src/color/rgb.pkg
    package wp  =  window_property;				# window_property	is from   src/lib/x-kit/xclient/src/iccc/window-property.pkg
herein

    package root_window						# Why is this not ": Root_Window" ???   XXX BUGGO FIXME
								# Root_Window	is from   src/lib/x-kit/widget/basic/root-window.api
    {


	# Root chunk, corresponding to display/screen pair.
	#  server = ""          => "unix:0.0"
	#         = ":d"        => "unix:d.0"
	#         = "host:d"    => "host:d.0"
	#         = "host:d.s"  => "host:d.s"
	# where host is an internet address (e.g., "128.84.254.97") or "unix".
	#
	# At present, screen is always the default screen.

	Style = wy::Style;

	Root_Window
	    =
	    ROOT_WINDOW
	      { screen:		xsession_junk::Screen,
		id:		Ref( Void ),			# Here we are just taking advantage of the fact that all REFs are distinct.
		#						# We should eventually convert this to a proper small-int id -- eventually
		#						# one wants to use the id as a key for lookup.  -- 2013-07-21 CrT
		make_shade:	rgb::Rgb -> shp::Shades,
		make_tile:	String -> rop::Ro_Pixmap,
		#
		style:		Style,
		next_widget_id:	Void -> Int
	      };

	init_images
	    =
	    [ (quark::quark "lightGray", standard_clientside_pixmaps::light_gray),
	      (quark::quark "gray",      standard_clientside_pixmaps::gray      )
	    ];

###################
# XXX SUCKO FIXME
# This function is hugely redundant
# with make_root_window in   src/lib/x-kit/widget/lib/run-in-x-window.pkg
###################
	fun make_root_window						# Called (mainly) from   make_root_window  in   src/lib/x-kit/widget/lib/run-in-x-window-old.pkg
	      ( server:         	String,				# Typically from DISPLAY environment variable.
		xauthentication:	Null_Or( xt::Xauthentication ),	# Ultimately from ~/.Xauthority.
		run_gun':		Run_Gun,
		end_gun':		End_Gun
	      )
	    =
	    {

		screen = xsession_junk::default_screen_of
                             (xsession_junk::open_xsession (run_gun', end_gun', server, xauthentication));
		#
		widget_id_slot = make_mailslot ();

		fun widget_id_loop i
		    =
		    {   put_in_mailslot  (widget_id_slot,  i);
			#
			widget_id_loop (i+1);
		    };



		(ii::make_image_ximp_state ()) ->   image_ximp_state;
		(ii::make_image_ximp    "img") ->   (image_ximp_configstate, image_ximp_exports);

		(pxc::make_ro_pixmap_ximp_state ()) ->   ro_pixmap_ximp_state;
		(pxc::make_ro_pixmap_ximp "ropmap") ->  (ro_pixmap_ximp_configstate, ro_pixmap_ximp_exports);

		(si::make_shade_ximp_state ()) ->   shade_ximp_state;
		(si::make_shade_ximp "ropmap") ->  (shade_ximp_configstate, shade_ximp_exports);

		image_port     =      image_ximp_exports.image_port;
		ro_pixmap_port =  ro_pixmap_ximp_exports.ro_pixmap_port;
		shade_port     =      shade_ximp_exports.shade_port;

		ii::configure_image_ximp      (    image_ximp_configstate,     image_ximp_state, { }, run_gun', end_gun');
		pxc::configure_ro_pixmap_ximp (ro_pixmap_ximp_configstate, ro_pixmap_ximp_state, { }, run_gun', end_gun', screen, image_port.get_image);
		si::configure_shade_ximp      (    shade_ximp_configstate,     shade_ximp_state, { }, run_gun', end_gun', screen);


#		fire_run_gun ();


		fun make_tile name
		    =
		    case (ro_pixmap_port.get_ro_pixmap name)
			#
			THE tile => tile;
			NULL     => {   msg = (sprintf "make_tile: failed to find tile '%s'  -- root-window.pkg" name);
					log::fatal msg;
					raise exception FAIL msg;
				    };
		    esac;

		fun make_shade rgb
		    =
		    case (shade_port.get_shades rgb)
			#
			THE shades =>   shades;
			#
			NULL       =>   {   msg = "make_shade: failed to create shades  -- root-window.pkg";
					    log::fatal msg;
					    raise exception FAIL msg;
				        };
		    esac;

		make_thread "widget_id factory" .{  widget_id_loop  0;  };

		ROOT_WINDOW
		  { id         =>  REF (), 
		    screen, 
		    style      =>  wy::empty_style { screen, tilef => make_tile }, 
		    make_tile,

		    make_shade,

		    next_widget_id =>  fn () =  take_from_mailslot  widget_id_slot		# Gets used (only) in widget::make_widget, in  src/lib/x-kit/widget/basic/widget.pkg
		  };
	      };

	fun screen_of   (ROOT_WINDOW { screen, ... } ) =  screen;
	fun xsession_of (ROOT_WINDOW { screen, ... } ) =  xsession_junk::xsession_of_screen  screen;

	fun delete_root_window root
	    =
	    xsession_junk::close_xsession (xsession_of root);

	fun same_root     (ROOT_WINDOW { id, ... }, ROOT_WINDOW { id=>id', ... } )
	    =
	    id == id';

	fun shades    (ROOT_WINDOW { make_shade, ... } ) c =  make_shade c;
	fun ro_pixmap (ROOT_WINDOW { make_tile,  ... } ) s =  make_tile s;

	fun color_of
	    (ROOT_WINDOW { screen, ... } )
	    color_spec
	    =
	    xc::get_color  color_spec;

	fun open_font     (ROOT_WINDOW { screen, ... } )
	    =
	    xsession_junk::find_font (xsession_junk::xsession_of_screen screen);

	fun get_standard_xcursor (ROOT_WINDOW { screen, ... } ) =  xrs::get_standard_xcursor (xsession_junk::xsession_of_screen  screen);
	fun ring_bell            (ROOT_WINDOW { screen, ... } ) =  xsession_junk::ring_bell  (xsession_junk::xsession_of_screen  screen);

	fun    size_of_screen    (ROOT_WINDOW { screen, ... } ) =  xsession_junk::size_of_screen screen;
	fun mm_size_of_screen    (ROOT_WINDOW { screen, ... } ) =  xsession_junk::mm_size_of_screen screen;
	fun   depth_of_screen    (ROOT_WINDOW { screen, ... } ) =  xsession_junk::depth_of_screen screen;

	fun style_of (ROOT_WINDOW { style, ... } ) =   style;

	fun is_monochrome (ROOT_WINDOW { screen, ... } )
	    = 
	    xsession_junk::display_class_of_screen screen == xt::STATIC_GRAY    and 
	    xsession_junk::depth_of_screen         screen == 1;

	fun style_from_strings (ROOT_WINDOW { screen, make_tile, ... }, sl)
	    =
	    wy::style_from_strings ( { screen, tilef=>make_tile }, sl);

	fun strings_from_style sty    =  wy::strings_from_style sty;
	fun merge_styles (sty1, sty2) =  wy::merge_styles (sty1, sty2);

	fun style_from_xrdb root
	    =
	    {   xsession = xsession_of  root;
		screen   = xsession_junk::default_screen_of  xsession;
		stl      = wp::xrdb_of_screen screen;

		# (file::print ("XRDB strings:\n"$(string::join "\n" stl)$"\n"));
		style_from_strings (root, stl);
	    };

	Opt_Name = wy::Opt_Name;
	Arg_Name = wy::Arg_Name;
	Opt_Kind = wy::Opt_Kind;
	Opt_Spec = wy::Opt_Spec;
	Opt_Db   = wy::Opt_Db;

						    # widget_attribute	is from   src/lib/x-kit/widget/lib/widget-attribute-old.pkg
	Value = wa::Value;


	fun parse_command (o_spec) sl
	    =
	    wy::parse_command (o_spec) sl;


	fun find_named_opt (o_db: wy::Opt_Db) (o_nam: wy::Opt_Name) (ROOT_WINDOW { screen, make_tile, ... } )
	    =
	    wy::find_named_opt o_db o_nam { screen, tilef=>make_tile };


	fun style_from_opt_db (ROOT_WINDOW { screen, make_tile, ... }, o_db)
	    =
	    wy::style_from_opt_db ( { screen, tilef=>make_tile }, o_db);


	fun find_named_opt_strings (o_db: wy::Opt_Db) (o_nam: wy::Opt_Name)
	    =
	    wy::find_named_opt_strings o_db o_nam;


	fun help_string_from_opt_spec (o_spec)
	    =
	    wy::help_string_from_opt_spec  o_spec;

    };							# package root_window 

end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1994 by AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
