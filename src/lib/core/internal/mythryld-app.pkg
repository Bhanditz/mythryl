##  mythryld-app.pkg 
#
# Start-of-execution for the mythryld executable,
# which is to say the entire Mythryl interactive
# compiler/etc system.

# Compiled by:
#     src/lib/core/internal/interactive-system.lib


stipulate
    package mcb =  mythryl_compiler;								# mythryl_compiler		is from   src/lib/core/compiler/set-mythryl_compiler-to-mythryl_compiler_for_intel32_posix.pkg
    #												# mythryl_compiler_g		is from   src/lib/compiler/toplevel/compiler/mythryl-compiler-g.pkg
    #
    package wix =  winix;									# winix				is from   src/lib/std/winix.pkg
    #
herein

    package   mythryld_app
    :         Mythryld_App									# Mythryld_App			is from   src/lib/core/internal/mythryld-app.api
    {
	fun open_main_log ()
	    =
	    {
		fd = file::open_for_append "main.log~";
		file::write       (fd, "src/lib/core/internal/make-mythryld-executable.pkg: START\n"); 
		file::flush        fd;
		file::close_output fd;
	    };

	# We are invoked (only) from:
	#     src/lib/core/internal/make-mythryld-executable.pkg
	#
        fun main  do_all_requested_compiles							# do_all_requested_compiles	is ultimately   run_commandline	from   src/app/makelib/main/makelib-g.pkg
            =
	    {
		#################################################
		#                                              
		#         START OF THE Mythryld WORLD            
		#                                              
		#  Congratulations!  You've found the start-   
		#  of-execution for the mythryld executable --   
		#  the equivalent of main() in a C program.    
		#################################################

		open_main_log ();

		# When our mythryl.c wrapper invokes us to run a script,
		# it sets MYTHRYL_SCRIPT=<stdin> in the environment.				# See start_subprocess()	in    src/c/o/mythryl.c
		#
		# This is our cue to disable interactive prompts,
		# which otherwise generate unexpected and ugly clutter.
		#
		# In this case, we also skip processing commandline
		# arguments, in deference to the script logic:
		#
		#
		case (wix::process::get_env "MYTHRYL_SCRIPT")
		    #               
		    THE "<stdin>" =>   mythryl_parser::interactive := FALSE;
		    _		  =>   do_all_requested_compiles ();				# Typically does not return.
		esac;


		mcb::rpl::read_eval_print_from_script ();							# See src/lib/compiler/toplevel/interact/read-eval-print-loops-g.pkg
	    };
    };
end;
