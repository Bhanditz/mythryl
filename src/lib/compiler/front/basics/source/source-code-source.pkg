## source-code-source.pkg
#
# Here we record where the source code
# for a given compilation unit came from.
#
# Typically this will be a "foo.pkg" file
# in the host filesystem, but it might have
# been typed in interactively at the Mythryl
# prompt or such.
#
# We also track some related useful information
# such as where to send error messages generated
# while compiling the source code.


# Compiled by:
#     src/lib/compiler/front/basics/basics.sublib




###			"Trust the Source, Luke."


stipulate
    package cp  =  control_print;					# control_print		is from   src/lib/compiler/front/basics/print/printcontrol.pkg
    package iox =  io_exceptions;					# io_exceptions		is from   src/lib/std/src/io/io-exceptions.pkg
    package lnd =  line_number_db;					# line_number_db	is from   src/lib/compiler/front/basics/source/line-number-db.pkg
    package pp  =  prettyprint;						# prettyprint		is from   src/lib/prettyprint/big/src/prettyprint.pkg
herein

    package   source_code_source
    : (weak)  Source_Code_Source					# Source_Code_Source	is from   src/lib/compiler/front/basics/source/source-code-source.api
    {
	Input_Source
	  =
	  { line_number_db:  lnd::Sourcemap,
	    file_opened: String,
	    saw_errors:  Ref( Bool ),
	    #
	    error_consumer:  pp::Device,
	    interactive:     Bool,					# 
	    source_stream:   file::Input_Stream				# file			is from   src/lib/std/src/posix/file.pkg
	  };

	fun say (msg:  String)
	    =
	    cp::say msg;


	lexer_initial_position
	    =
	    2;								#  Position of first char according to mythryl-lex :(

	fun make_source
		( file_name,	# Filename for source_stream, else "<Input_Stream>" or such.
		  line_num,
		  source_stream,
		  interactive,
		  error_consumer
		)
	    =
	    { source_stream,
	      interactive,
	      error_consumer,
	      file_opened =>  file_name,
	      saw_errors  =>  REF FALSE,
	      line_number_db  =>  lnd::newmap (   lexer_initial_position, 
						     { file_name,
						       line   =>  line_num,
						       column =>  1
						      }
						 )
	    };

	fun close_source ( { interactive=>TRUE, ... } : Input_Source)
		=>
		();

	    close_source ( { source_stream, ... } )
		=>
		{   # Apply say ["[closing ", (Pathnames::trim fileName), "]\n"];
		    #
		    file::close_input  source_stream			# file			is from   src/lib/std/src/posix/file.pkg
		    except
			iox::IO _ =  ();
		};
	end;

	fun filepos ( { line_number_db, ... }: Input_Source)  pos
	    = 
	    {   (lnd::filepos line_number_db  pos)
		    ->
		    { file_name, line, column };

		(file_name, line, column);
	    };

    };                                      #  package source_code_source 
end;


## COPYRIGHT (c) 1996 Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
