## unify-and-generalize-types-g.pkg 

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

# The center of the typer (type inference/checking engine) is
#
#     src/lib/compiler/front/typer/main/type-package-language-g.pkg
#
# -- see it for a higher-level overview.



#                   "You get what you give:  Unloved code is ugly code."


stipulate
    package ds  =  deep_syntax;					# deep_syntax			is from   src/lib/compiler/front/typer-stuff/deep-syntax/deep-syntax.pkg
    package err =  error_message;				# error_message			is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    package lnd =  line_number_db;				# line_number_db		is from   src/lib/compiler/front/basics/source/line-number-db.pkg
    package syx =  symbolmapstack;				# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
herein

    api Unify_And_Generalize_Types {
	#
	unify_and_generalize_types
	    :
	    { symbolmapstack:		syx::Symbolmapstack,
	      declaration:		ds::Declaration,

	      outside_all_lets:		Bool,

	      error_function:		err::Error_Function,
	      source_code_region:	lnd::Source_Code_Region
	    }
	    -> ds::Declaration
	    ;

	 debugging:  Ref(  Bool );
    };
end;

#  Genericized to factor out dependencies on highcode... 

stipulate
    package cos =  compile_statistics;			# compile_statistics			is from   src/lib/compiler/front/basics/stats/compile-statistics.pkg
    package ctt =  core_type_types;			# core_type_types			is from   src/lib/compiler/front/typer-stuff/types/core-type-types.pkg
    package ds  =  deep_syntax;				# deep_syntax				is from   src/lib/compiler/front/typer-stuff/deep-syntax/deep-syntax.pkg
    package err =  error_message;			# error_message				is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    package id  =  inlining_data;			# inlining_data				is from   src/lib/compiler/front/typer-stuff/basics/inlining-data.pkg
    package ip  =  inverse_path;			# inverse_path				is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package mtt =  more_type_types;			# more_type_types			is from   src/lib/compiler/front/typer/types/more-type-types.pkg
    package pds =  prettyprint_deep_syntax;		# prettyprint_deep_syntax		is from   src/lib/compiler/front/typer/print/prettyprint-deep-syntax.pkg
    package pp  =  standard_prettyprint_mill;		# standard_prettyprint_mill		is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package ppt =  prettyprint_type;			# prettyprint_type			is from   src/lib/compiler/front/typer/print/prettyprint-type.pkg
    package rol =  resolve_overloaded_literals;		# resolve_overloaded_literals		is from   src/lib/compiler/front/typer/types/resolve-overloaded-literals.pkg
    package rov =  resolve_overloaded_variables;	# resolve_overloaded_variables		is from   src/lib/compiler/front/typer/types/resolve-overloaded-variables.pkg
    package sta =  stamp;				# stamp					is from   src/lib/compiler/front/typer-stuff/basics/stamp.pkg
    package sy  =  symbol;				# symbol				is from   src/lib/compiler/front/basics/map/symbol.pkg
    package syx =  symbolmapstack;			# symbolmapstack			is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
    package tc  =  typer_control;			# typer_control				is from   src/lib/compiler/front/typer/basics/typer-control.pkg
    package td  =  typer_debugging;			# typer_debugging			is from   src/lib/compiler/front/typer/main/typer-debugging.pkg
    package tdt =  type_declaration_types;		# type_declaration_types		is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
    package tyj =  type_junk;				# type_junk				is from   src/lib/compiler/front/typer-stuff/types/type-junk.pkg
    package uds =  unparse_deep_syntax;			# unparse_deep_syntax			is from   src/lib/compiler/front/typer/print/unparse-deep-syntax.pkg
    package uj  =  unparse_junk;			# unparse_junk				is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
    package uty =  unparse_type;			# unparse_type				is from   src/lib/compiler/front/typer/print/unparse-type.pkg
    package uyt =  unify_types;				# unify_types				is from   src/lib/compiler/front/typer/types/unify-types.pkg
    package vac =  variables_and_constructors;		# variables_and_constructors		is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg

    Pp = pp::Pp;
herein

    # This generic is invoked (only) from:
    #
    #     src/lib/compiler/front/semantic/types/unify-and-generalize-types.pkg
    #
    generic package   unify_and_generalize_types_g   (
	#             ============================

		 inlining_data_says_it_is_pure:  id::Inlining_Data -> Bool;				# pure_info			from   src/lib/compiler/front/semantic/basics/inlining-junk.pkg
		 inlining_data_to_my_type:       id::Inlining_Data -> Null_Or( tdt::Typoid );		# inlining_data_to_my_type	from   src/lib/compiler/front/semantic/modules/generics-expansion-junk-parameter.pkg
	    )

    : (weak) Unify_And_Generalize_Types									# Unify_And_Generalize_Types	is from   src/lib/compiler/front/typer/types/unify-and-generalize-types-g.pkg
    {
	stipulate
	    #
	    Symbolmapstack =  syx::Symbolmapstack;
	    Error_Function =  err::Error_Function;

	    --> = mtt::(-->);

	    # The following is support for tracking the
	    # lexical context in which type variables
	    # appear.  We are interested in whether
	    # type variables are free or bound and also
	    # whether we are inside some let/stipulate
	    # construct or at top level.
	    #
	    # The two critical things we track are:
	    #
	    #
	    # outside_all_lets:
	    #
	    #     TRUE iff we are not lexicallly			# NB: A 'stipulate' statement is a 'let'
	    #     within the scope of any "let"			# construct, also { ... } code blocks and
	    #     construct.					# 'if'-statement 'then' and 'else' clauses.
	    #
	    #     We need this because (for example)
	    #     it is an error not to generalize			# Generalization is discussed below.
	    #     a type variable a top level but
	    #     it is ok not to generalize one in
	    #     a 'let' due to the "value restriction".
	    #
	    #
	    # fn_nesting:
	    #
	    #     This is the number of fun/fn			# 'lambdas', in fp jargon.
	    #     definitions lexically enclosing us.
	    #     This numbering starts at 0.
	    #     
	    #     We need this to support type agnosticism -- "polymorphism".
	    #     
	    #     Mythryl (and ML generally) implement
	    #     what is sometimes called "let-polymorphism",
	    #     in which (canonically) functions defined
	    #     in 'let' constructs have their type variables
	    #     'generalized' to allow them to match different
	    #     types in different invocations.  For example
	    #         {
	    #             ...
	    #             fun swap (a, b) = (b, a);
	    #             ...
	    #             p0 = swap ( 1 ,  2 );
	    #             p1 = swap ('1', '2');
	    #             p2 = swap ("1", "2");
	    #             ...
	    #         };
	    #     
	    #     Here swap() is operating indifferently		# "Don't-care polymorphism" and
	    #     upon pairs of ints, chars and strings.		# "parametric polymorphism" are two
	    #     							# more names for this kind of
	    #     Making this typecheck requires a special		# typeagnosticism.
	    #     kludge.  Here's how it works:
	    #     
	    #       o Instead of assigning 'swap' a normal		# In fp jargon this is called a "type scheme".
	    #	      type, we assign it a template for a		# We represent them using TYPESCHEME records and
	    #         type, with holes where all the type		# we represent the 'holes' with TYPESCHEME_ARG,
	    #	      variables should be.				# both from    src/lib/compiler/front/typer-stuff/types/types.pkg
	    #     
	    #       o Each time we come to a call to 'swap'		# This copy-and-complete is implemented by
	    #         we generate a fresh type for it by		#     tyj::instantiate_if_typescheme()
	    #	      making a copy of the template and			# from
	    #	      filling in all the holes with fresh		#     src/lib/compiler/front/typer-stuff/types/type-junk.pkg
	    #	      type variables.					#
	    #     
	    #     This way type inference is free to deduce
	    #     a different type for each call to 'swap'.
	    #     
	    #     We refer to the process of replacing a
	    #     type by such a type template as
	    #     "type generalization".
	    #
	    #     We implement this below, in
	    #
	    #         generalize_pattern()
	    #         generalize_type()
	    #
	    #     However when doing such type variable
	    #     generalization we must NOT generalize
	    #     any type variables inherited from			# For a more extended discussion
	    #     enclosing scopes because the inherited		# see Benjamin C Pierce's
	    #     type variables encode important type			# "Types and Programming Languages" 
	    #     constraints which will be lost if we			# Chapter 22, in particular  
	    #     generalize them, allowing incorrect			# page 333 rule 3.
	    #     code to typecheck.	
	    #
	    #     We implement this restriction by
	    #
	    #      1) Tracking our current fn nesting level
	    #         as we do syntactic dagwalks.
	    #
	    #      2) Tagging every type variable with
	    #         the outermost fn nesting level
	    #         mentioning it.  (We refine this
	    #	  during the type unification pass.)
	    #
	    #      3) Generalizing only those variables
	    #	  defined at a deeper nesting level
	    #	  than our current dagwalk fn nesting
	    #         level, which is to say variables
	    #	  introduced by 'let' constructs
	    #	  nested within the current fun/fn
	    #	  (if any), rather than inherited from
	    #	  scopes outside the 'let'.
	    #
	    #     Free type variables not declared by the user
	    #     are in general given a lexical nesting depth
	    #     of 'infinity', represented by an arbitrary		# E.g.:   tyj::make_meta_typevar_and_type  tdt::infinity;
	    #     integer larger than any expected real lexical
	    #     nesting depth.
	    #
	    #     USER_TYPEVAR typevars
	    #     are created with fn_nesting == infinity;		# By  make_user_typevar()  in  src/lib/compiler/front/typer-stuff/types/type-junk.pkg 
	    #     this nesting depth can be reduced
	    #     via unification.
	    #
	    #     When we instantiate a typeagnostic
	    #     type body we set all the META
	    #     variables to fn_nesting == "infinity".		# "infinity" being a kilomyriad, which
	    #								# is to say 10000000. See types.pkg. :-)
	    #
	    #
	    # 8/18/92:
	    #     Cleaned up syntax_treewalk_lexical_context
	    #     "state machine" some and fixed bug #612.
	    #

	    Syntax_Treewalk_Lexical_Context
		=
		{
		  fn_nesting:        Int,
		  outside_all_lets:  Bool
		};


	    # When we start a typechecking a dagwalk
	    # at the root of a syntax tree, this is
	    # our initial type variable context:
	    #
	    root_syntax_treewalk_lexical_context
		=
		{
		  fn_nesting       => 0,
		  outside_all_lets => TRUE
		};


	    # Note that we have entered the lexical scope
	    # of a 'let' equivalent construct.
	    # These include 'stipulate's, { ... } codeblocks and
	    # if-statement 'then' and 'else' clauses:
	    #
	    fun enter_let_scope { fn_nesting, outside_all_lets }
		=
		{ 
		  outside_all_lets => FALSE,
		  fn_nesting
		};


	    # Note that we have entered the lexical scope
	    # of a fun/fn or equivalent construct:
	    #
	    fun enter_fn_scope  { fn_nesting, outside_all_lets }
		=
		{
																if *uty::debugging
																    printf "enter_fn_scope bumping fn_nesting from %d to %d\n" fn_nesting (fn_nesting + 1);
																fi;
		    {
		      fn_nesting =>  fn_nesting + 1,
		      outside_all_lets
		    };
		};

	herein 

	    #  Debug support: 

	    say         =   control_print::say;

#	    debugging   =   tc::unify_and_generalize_types_g_debugging;		#  REF FALSE 
#	    internals   =   REF FALSE;
debugging =  log::debugging;
internals =  log::internals;

	    generalize_mutually_recursive_functions
		=
		tc::generalize_mutually_recursive_functions;			#  REF FALSE
	    #
	    fun if_debugging_say (msg: String)
		=
		if *debugging
		    say msg;
		    say "\n";
		fi;

	    debug_print   =   (fn x =  td::debug_print debugging x);
	    #
	    fun bug msg   =   err::impossible("TypeCheck: " + msg);
	    #
	    fun print_callstack
		(msg:    String)
		(callstack:  List(String))
		=
		{   printf "%s:  callstack(%d) == " msg (list::length callstack);
		    apply  .{ printf " -> %s" #string; }  (reverse callstack);
		    printf "\n";
		};

	    is_value      =   tyj::is_value { inlining_data_says_it_is_pure };

	    infix my 9  sub ;
	    infix my    --> ;

	    print_depth = control_print::print_depth;
	    #
	    fun ref_new_valcon (tdt::VALCON { name, is_constant, form, typoid, signature, is_lazy } )
		= 
		tdt::VALCON
		  {
		    typoid => mtt::ref_pattern_typoid,
		    name,
		    is_constant,
		    form,
		    signature,
		    is_lazy
		  };

	    exception NOT_THERE;
	    #
	    fun message (   msg,    mode: unify_types::Unify_Fail   )
		=
		string::cat [ msg, " [", unify_types::fail_message mode, "]" ];




	    # Here is the heart of the compiler's type inference engine.
	    #
	    # This is also where we implement type agnosticism
	    # by generalizing USER_TYPEVAR and META_TYPEVAR
	    # types to TYPESCHEME_ARG types whenever permitted
	    # by the "value restriction" as implemented by is_value() in
	    #
	    #     src/lib/compiler/front/typer-stuff/types/type-junk.pkg
	    #
	    # We get called from two spots in typecheck_declaration'() in
	    #
	    #     src/lib/compiler/front/typer/main/type-package-language-g.pkg
	    #
	    # We delegate actual type unification to unify_types() in
	    #
	    #     src/lib/compiler/front/typer/types/unify-types.pkg
	    #
	    # A light overview of Hindley-Milner type inference may be found here:
	    #     http://en.wikipedia.org/wiki/Type_inference
	    #
	    # A more detailed treatment may be found in the
	    #     Types and Programming Languages
	    # text by Benjamin C Pierce, chapter 22.
	    #
	    fun unify_and_generalize_types											# External entrypoint. (The only one in this file.)
		{
		  declaration:         ds::Declaration,
		  symbolmapstack:      Symbolmapstack,
		  #
		  outside_all_lets:    Bool,
		  error_function:      Error_Function,
		  source_code_region:  ds::Source_Code_Region
		}
		: ds::Declaration
		= 
		{
																if_debugging_unparse_declaration     ("\nunify_and_generalize_types: MID, just before calling do_declaration: declaration unparse     is:    ",  declaration     );
																if_debugging_prettyprint_declaration ("\nunify_and_generalize_types: MID, just before calling do_declaration: declaration prettyprint is:    ", (declaration,100));
		    declaration
			=
			do_declaration (
			    #
			    declaration,
			    outside_all_lets  ??                   root_syntax_treewalk_lexical_context
					      ::  enter_let_scope  root_syntax_treewalk_lexical_context,
			    source_code_region,
			    []
			);
																if_debugging_unparse_declaration     ("\nunify_and_generalize_types [unify-and-generalize-types-g.pkg]: XYZZY pre-overload-resolution declaration unparse     is:\n",  declaration);
																if_debugging_prettyprint_declaration ("\nunify_and_generalize_types [unify-and-generalize-types-g.pkg]: XYZZY pre-overload-resolution declaration prettyprint is:\n", (declaration,100));

																if_debugging_say ("\nunify_and_generalize_types: calling resolve_all_overloaded_literals  ... [unify-and-generalize-types-g.pkg]\n");
		    resolve_all_overloaded_literals (); 									if_debugging_say ("\nunify_and_generalize_types: calling resolve_all_overloaded_variables ... [unify-and-generalize-types-g.pkg]\n");
		    resolve_all_overloaded_variables  symbolmapstack;
		    
																if_debugging_unparse_declaration     ("\nunify_and_generalize_types [unify-and-generalize-types-g.pkg]: BOT PLUXX. pre-hack    declaration unparse     is:\n",  declaration);
																if_debugging_prettyprint_declaration ("\nunify_and_generalize_types [unify-and-generalize-types-g.pkg]: BOT PLUXX. pre-hack    declaration prettyprint is:\n", (declaration,100));
#		    drop_resolved_typevars_in_declaration  declaration;


																if_debugging_unparse_declaration     ("\nunify_and_generalize_types [unify-and-generalize-types-g.pkg]: BOT PLUGH. transformed declaration unparse     is:\n",  declaration);
																if_debugging_prettyprint_declaration ("\nunify_and_generalize_types [unify-and-generalize-types-g.pkg]: BOT PLUGH. transformed declaration prettyprint is:\n", (declaration,100));
																if_debugging_say "\n^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^";
																if_debugging_say   "============= unify_and_generalize_types/BOTTOM =============\n";
		    declaration;
		}
		where
		    (rol::make_overloaded_literal_resolver  ()                      ) ->   { note_overloaded_literal,  resolve_all_overloaded_literals   };
		    (rov::make_overloaded_variable_resolver inlining_data_to_my_type) ->   { note_overloaded_variable, resolve_all_overloaded_variables  };

		    prettyprint_declaration         = pds::prettyprint_declaration (syx::empty, NULL);
		    prettyprint_expression          = pds::prettyprint_expression  (syx::empty, NULL);
		    prettyprint_pattern             = pds::prettyprint_pattern      syx::empty;

		    prettyprint_typoid              = ppt::prettyprint_typoid               symbolmapstack;

		    unparse_typoid                  = uty::unparse_typoid                   symbolmapstack;
		    unparse_typevar_ref             = uty::unparse_typevar_ref              symbolmapstack;
		    unparse_pattern                 = uds::unparse_pattern                  symbolmapstack;
		    unparse_expression              = uds::unparse_expression              (symbolmapstack, NULL);
		    unparse_rule                    = uds::unparse_rule                    (symbolmapstack, NULL);
		    unparse_named_value             = uds::unparse_named_value             (symbolmapstack, NULL);
		    unparse_recursively_named_value = uds::unparse_recursively_named_value (symbolmapstack, NULL);

		    unparse_declaration
			= 
			(fn stream
			    =
			    fn d
				=
				uds::unparse_declaration
					(symbolmapstack, NULL)
					stream
					(d, *print_depth)
			);
		    #
/* */		    fun if_debugging_unparse_declaration (msg, declaration)
			=
			if *debugging
			    if *internals  td::with_internals (fn () =  td::debug_print debugging (msg, unparse_declaration, declaration));
			    else					td::debug_print debugging (msg, unparse_declaration, declaration) ;
			    fi;
			fi;
		    #
/* */		    fun if_debugging_unparse_typoid (msg, type)
			=
			if *debugging
			    if *internals   td::with_internals (fn () =  td::debug_print debugging (msg, unparse_typoid, type));
			    else                                         td::debug_print debugging (msg, unparse_typoid, type) ;
			    fi;
			fi;
		    #
/* */		    fun if_debugging_prprint_typoid (msg, type)
			=
			if *debugging
			    if *internals   td::with_internals (fn () =  td::debug_print debugging (msg, prettyprint_typoid, type));
			    else                                         td::debug_print debugging (msg, prettyprint_typoid, type) ;
			    fi;
			fi;
		    #
/* */		    fun if_debugging_unparse_typevar_ref  (msg, typevar_ref)
			= 
			if *debugging		# Without this 'if' (and the matching one in unify_types), compiling the compiler takes 5X as long! :-)
			    if *internals    td::with_internals (fn () =  td::debug_print debugging (msg, unparse_typevar_ref, typevar_ref));
			    else                                          td::debug_print debugging (msg, unparse_typevar_ref, typevar_ref) ;
			    fi;
			fi;
		    #
/* */		    fun if_debugging_unparse_pattern (msg, pattern)
			=
			if *debugging
			    if *internals   td::with_internals (fn () =  td::debug_print debugging (msg, unparse_pattern, pattern));
			    else                                         td::debug_print debugging (msg, unparse_pattern, pattern) ;
			    fi;
			fi;
		    #
/* */		    fun if_debugging_unparse_expression (msg, expression)
			=
			if *debugging	
			    if *internals   td::with_internals (fn () =  td::debug_print debugging (msg, unparse_expression, expression));
			    else					 td::debug_print debugging (msg, unparse_expression, expression) ;
			    fi;
			fi;

		    #
/* */		    fun if_debugging_prettyprint_expression (msg, expression)
			=
			if *debugging	
			    if *internals   td::with_internals (fn () =    td::debug_print debugging (msg, prettyprint_expression, expression));
			    else					   td::debug_print debugging (msg, prettyprint_expression, expression) ;
			    fi;
			fi;
		    #
/* */		    fun if_debugging_prettyprint_pattern (msg, pattern)
			=
			if *debugging	
			    if *internals   td::with_internals  (fn () =	td::debug_print debugging (msg, prettyprint_pattern, pattern));
			    else						td::debug_print debugging (msg, prettyprint_pattern, pattern) ;
			    fi;
			fi;
		    #
/* */		    fun if_debugging_prettyprint_declaration (msg, declaration)
			=
			if *debugging	
			    if *internals   td::with_internals   (fn () =  td::debug_print debugging (msg, prettyprint_declaration, declaration));
			    else					   td::debug_print debugging (msg, prettyprint_declaration, declaration) ;
			    fi;
			fi;


		    # This is a simple wrapper for unify_types(),
		    # used all through this file.
		    #
		    # 'typoid1' and 'typoid2' are the only
		    # arguments of consequence;  the rest
		    # are just diagnostic printing support:
		    #
		    fun unify_types_and_handle_errors
			  {
			    typoid1, name1,			# typoid1: tdt::Typoid,   name1:  String
			    typoid2, name2,			# typoid2: tdt::Typoid,   name2:  String

			    message => m,
			    source_code_region,

			    unparse_phrase,			# prettyprint::String -> (X, Int) -> Void
			    phrase_name,			# String
			    phrase,				# X; X here and above is one of deep syntax Case_Pattern, Expression, ...
			    callstack
			  }
			=
			{
																# More annoying than helpful:
																#			if *debugging
																#                            printf "unify-and-generalize-types-g.pkg:\
																#                                   \ unify_types_and_handle_errors: calling unify_types name1 %s name2 %s message %s\n" name1 name2 m;
																#                        fi;

			    uyt::unify_types
			      (
				name1, name1,
				typoid1, typoid2,
				"unify_types_and_handle_errors" ! callstack
			      );

			    TRUE;
			}
			except
			    uyt::UNIFY_TYPES mode
				=
				{   error_function source_code_region
					err::ERROR
					(message (m, mode))
					(fn pp
					    =
					    {   uty::reset_unparse_type();

						len1   = size name1;
						len2   = size name2;

						blanks = "                                   ";

						pad1   = substring (blanks, 0, int::max (0, len2-len1));
						pad2   = substring (blanks, 0, int::max (0, len2-len1));

						m = if (m=="")
							 name1 + " and " + name2 + " don't agree";
						    else m;
						    fi;

						if (name1 != "")
						    pp.newline(); 
						    pp.lit (name1 + ": " + pad1);
						    unparse_typoid  pp  typoid1;
						fi; 

						if (name2 != "")
						    pp.newline(); 
						    pp.lit (name2 + ": " + pad2);
						    unparse_typoid pp typoid2;
						fi;

						if (phrase_name != "")
						    pp.newline();
						    pp.lit ("in " + phrase_name + ":");
						    pp::break pp { blanks=>1, indent_on_wrap=>2 };
						    unparse_phrase pp (phrase,*print_depth);
						fi;
					    }
					);
				    FALSE;
				};

																if_debugging_say "\n============= unify_and_generalize_types/TOP ============= [unify-and-generalize-types-g.pkg]";
																if_debugging_say   "vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv\n";
																if_debugging_say ("unify_and_generalize_types: outside_all_lets = " + bool::to_string  outside_all_lets);
																if_debugging_unparse_declaration     ("\nunify_and_generalize_types/TOP: declaration unparse     =    [unify-and-generalize-types-g.pkg]", declaration);
																if_debugging_prettyprint_declaration ("\nunify_and_generalize_types/TOP: declaration prettyprint =    [unify-and-generalize-types-g.pkg]", (declaration,100));


		    # This is the core routine responsible for marking
		    # a pattern variable as typeagnostic.
		    #
		    # Our first argument below is the most important;
		    # it is from a ds::VARIABLE_IN_PATTERN.
		    # The critical part of it is the 'var_type' ref:
		    # we will overwrite it with a generalized
		    # version of itself -- a
		    #     tdt::TYPESCHEME_TYPOID { ... }
		    # record wrapping a type scheme -- and then return
		    # the list of generalized type variables as our result.
		    #
		    # We get called frome exactly one place,
		    # in generalize_pattern'() in generalize_pattern(). 
		    #
		    fun generalize_type
			    (
			      vac::PLAIN_VARIABLE { var_type, path, ... }:   vac::Variable,
			      user_typevar_refs:				List( tdt::Typevar_Ref ),			# *NAMED_VALUE.typevars -- X, Y, Z ... from a function clause pattern or such.

			      syntax_treewalk_lexical_context:                 Syntax_Treewalk_Lexical_Context,
			      generalize:                                      Bool,						# Result of tyj::is_value()
			      source_code_region:                              ds::Source_Code_Region,
			      callstack:                                       List(String)					# Debug support.
			    )
			    : List( tdt::Typevar_Ref )										# These will actually always be tdt::META_TYPEVAR or tdt::USER_TYPEVAR.
			    =>
			    {
																if *debugging
																    print_callstack "\n=============  generalize_type/TOP =============" callstack;
																    say (  "vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv  [unify-and-generalize-types-g.pkg]\n\n");
																    say ("generalize_type: " + symbol_path::to_string path);
																    say "\nuser_typevar_refs: ";
																    apply  unparse_typevar_ref  user_typevar_refs
																    where
/* */																	fun unparse_typevar_ref  typevar_ref
																	    =
																	    if_debugging_unparse_typevar_ref ("", typevar_ref);
																    end;
																    printf "\ngeneralize is %s\n" (generalize ?? "TRUE" :: "FALSE");
																    printf "lexical context: fn_nesting d= %d  outside_all_lets b= %s\n" syntax_treewalk_lexical_context.fn_nesting (syntax_treewalk_lexical_context.outside_all_lets ?? "TRUE" :: "FALSE");
																fi;

				failure =   REF FALSE;


				# Function to create dummy-type generators
				# used to resolve ungeneralizable free
				# type variables in typechecking::generalize_type:
				#
				make_dummy =  {   syntax_treewalk_lexical_context.outside_all_lets  
						      ??
						      make_dummytype_generator()
						      ::
						      make_dummy';		# Shouldn't be called.
					      }
					      where
						  #
						  fun make_dummy' ()
						      =
						      mtt::void_typoid;

																	      # stamp				is from   src/lib/compiler/front/typer-stuff/basics/stamp.pkg
						  fun make_dummytype_generator ()   :   Void -> tdt::Typoid
						      =
						      {   count = REF 0;
							  #
							  fun next ()
							      =
							      {   count := *count + 1;
								  *count;
							      };
							  #
							  fun next_type ()
							      =
							      {   name = "X" + int::to_string (next ());

								  tdt::TYPCON_TYPOID (
								      #	
								      tdt::SUM_TYPE
									{
									  stamp       =>  sta::make_static_stamp  name,
									  namepath    =>  ip::INVERSE_PATH [sy::make_type_symbol name],
									  arity	      =>  0,
									  is_eqtype =>  REF tdt::e::NO,
									  kind        =>  tdt::ABSTRACT  ctt::bool_type,
									  stub        =>  NULL
								        },

								      []
								  );
							      };

							  next_type;
						      };
					      end;


				# Track number of type variables bound.
				# This will wind up being the typescheme
				# arity:
				#
				typescheme_arg_slots_allocated
				    =
				    REF 0;

				#
				fun allocate_typescheme_arg_slot ()
				    =
				    {   slot =  *typescheme_arg_slots_allocated;
					#
					typescheme_arg_slots_allocated
					    :=
					    slot+1;

					slot;
				    };


				# Check a type variable for membership
				# in our 'user_typevar_refs' parameter:
				#
				fun is_local_function_typevar_ref  ref_typevar
				    =
				    is_member  user_typevar_refs
				    where
					fun is_member (user_typevar_ref ! rest)
						=>
						tyj::typevar_refs_are_equal (ref_typevar, user_typevar_ref)
						or
						is_member rest;

					   is_member [] =>   FALSE;
					end;
				    end;


				# Track which of the below type variables
				# need to be of equality type.  This list
				# will always be the same length as the
				# next.  Possibly they should be combined:
				#
				typescheme_eqflags
				    =
				    REF ([]: tdt::Typescheme_Eqflags);


				# In this association list we track the
				# tdt::TYPESCHEME_ARG slots of variables
				# we've already generalized, to avoid assigning
				# two slots to one variable.
				# 
				# The keys in this list are USER_TYPEVAR
				# and META_TYPEVAR type variables;  the
				# corresponding values are the
				# tdt::TYPESCHEME_ARG types (slot numbers)
				# we create for them.
				#
				# ASSERTION: There are no duplicate typevars
				#            in domain of generalized_typevars.
				#
				generalized_typevar_ref_types
				    =
				    REF ([]:   List( ( tdt::Typevar_Ref,		# This will be REF( tdt::META_TYPEVAR | tdt::USER_TYPEVAR )
						       tdt::Typoid			# This will actually always be a tdt::TYPESCHEME_ARG. 
					)          ) );

				# Add an entry to above list:
				#	
				fun note_generalized_typevar_ref_type
				    (
				      typevar_ref:	tdt::Typevar_Ref,		# This will reference a tdt::META_TYPEVAR or tdt::USER_TYPEVAR
				      sometype:		tdt::Typoid			# This will actually always be a tdt::TYPESCHEME_ARG. 
				    )
				    =
				    {
																if *debugging
																if_debugging_unparse_typevar_ref  ("\nnote_generalized_typevar_ref_type adding typevar_ref [unify-and-generalize-types-g.pkg]: ", typevar_ref);
																if_debugging_unparse_typoid       (" with type (unparse)", sometype);
																if_debugging_prprint_typoid       (" with type (prprint)", sometype);
																fi;
					generalized_typevar_ref_types
					    :=
					    (typevar_ref, sometype)
					    !
					    *generalized_typevar_ref_types;
				    };

				# Search above list.
				# Return key's value if found,
				# otherwise raise NOT_THERE.
				#
				fun find_generalized_typevar_ref_type  typevar_ref
				    =
				    search  *generalized_typevar_ref_types
				    where
					fun search ((typevar_ref', typescheme_arg) ! rest)
						=>
						tyj::typevar_refs_are_equal (typevar_ref, typevar_ref')
						    ?? 
						    typescheme_arg
						    ::
						    search rest;

					    search [] =>   raise exception NOT_THERE;
					end;
				    end;


				# Make a type typeagnostic.
				# This mainly means replacing both of
				#     META_TYPEVAR
				#     USER_TYPEVAR
				# by
				#     tdt::TYPESCHEME_ARG 
				# wherever possible, e.g., where permitted
				# by the "value restriction" as implemented
				# by tyj::is_value() and passed to us
				# via the 'generalize' parameter.
				#	
				# We construct and return a Type result
				# to replace our Type argument, but along
				# the way we also side-effect various
				# typevars etc in the expression, so
				# this code is far from pure:
				#
				fun generalize_type'
				    (typoid: tdt::Typoid)
				    :        tdt::Typoid
				    =     
				    case typoid
					#
					tdt::TYPEVAR_REF (typevar_ref as { id, ref_typevar as REF (tdt::META_TYPEVAR { fn_nesting, eq }) })
					    =>
					    {
																if *debugging
																    printf "generalize_type'/META fn_nesting d=%d eq b=%s generalize b=%s\n"
																	   fn_nesting (eq ?? "TRUE" :: "FALSE")  (generalize ?? "TRUE" :: "FALSE");
																fi;
						# This is the focal case
						# for this function. The
						# remaining cases are mostly
						# corner cases and recursive
						# descent to reach this case:
						#
						result
						    =
						    if (fn_nesting  >  syntax_treewalk_lexical_context.fn_nesting)		# If type variable is not constrained by contexts outside this 'let'.
							#
							if generalize								# If 'value restriction' permits generalization of this type variable.
							    #	
																if_debugging_say ("\ngeneralize_type'/META_TYPEVAR:  converting META to TYPESCHEME_ARG  [unify-and-generalize-types-g.pkg]\n");

							    find_generalized_typevar_ref_type  typevar_ref			# If we've already assigned this META variable a TYPESCHEME_ARG slot, return that as our replacement for it.
							    except
								NOT_THERE
								    =
								    {   new_typescheme_slot_arg					# Assign a new type scheme slot for this META type variable,
									    =
									    tdt::TYPESCHEME_ARG( allocate_typescheme_arg_slot() );

																if_debugging_say ("\ngeneralize_type'/META_TYPEVAR: converting META to TYPESCHEME_ARG by allocating new TYPESCHEME_ARG  [unify-and-generalize-types-g.pkg]\n");
									#
									typescheme_eqflags					# Remember whether this TYPESCHEME_ARG typevar must resolve to an equality type.
									    :=
									    eq ! *typescheme_eqflags;

									note_generalized_typevar_ref_type			# Note new typescheme slot for future reference.
									    (typevar_ref, new_typescheme_slot_arg); 

									new_typescheme_slot_arg;				# Return new typescheme slot as our replacement for the META type variable.
								    };
							else
							    if syntax_treewalk_lexical_context.outside_all_lets
								#
								typevar = make_dummy ();
								failure     :=  TRUE;
								ref_typevar :=  tdt::RESOLVED_TYPEVAR  typevar;			if_debugging_say ("\ngeneralize_type'/META_TYPEVAR: generalize FALSE so converting META to RESOLVED_TYPEVAR dummy  [unify-and-generalize-types-g.pkg]\n");
								typevar;
							    else
								if *tc::value_restriction_local_warn
								    #
								    error_function  source_code_region  err::WARNING
									  ( "type variable not generalized in local decl (value restriction): "
									    +
									    (uty::typevar_ref_printname  typevar_ref)
									  )
									  err::null_error_body;
								fi;

								# Reset fn_nesting to prevent later incorrect generalization
								# inside a fun/fn expression.  See typechecking test.pkg
								#	
								ref_typevar
								    :=
								    tdt::META_TYPEVAR { eq, fn_nesting => syntax_treewalk_lexical_context.fn_nesting };

																if_debugging_say ("\ngeneralize_type'/META_TYPEVAR:  generalize FALSE, resetting fn_nesting to prevent incorrect generalization  [unify-and-generalize-types-g.pkg]\n");

								typoid;	# Return our (modified) input argument as our result.
							    fi;
							fi;

						    elif (fn_nesting == 0  and  syntax_treewalk_lexical_context.outside_all_lets)

							# ASSERT: failed generalization at fn_nesting 0.
							# see bug 1066.
																if_debugging_say ("\ngeneralize_type'/META_TYPEVAR: generalize FALSE, fn_nesting==0, changing to RESOLVED_TYPEVAR dummy  [unify-and-generalize-types-g.pkg]\n");

							find_generalized_typevar_ref_type  typevar_ref
							except
							    NOT_THERE
							       =
							       {   new = make_dummy ();
								   failure := TRUE;
								   ref_typevar := tdt::RESOLVED_TYPEVAR new;
								   new;
							       };
						    else
							typoid;
						    fi;

					       result;
					   };

					tdt::TYPEVAR_REF (typevar_ref  as  { id,  ref_typevar => REF (tdt::INCOMPLETE_RECORD_TYPEVAR { fn_nesting, eq, known_fields => [ (lab, _) ] } ) })
					     =>
					     if ( (       fn_nesting  >  syntax_treewalk_lexical_context.fn_nesting
						  and   (generalize  or  syntax_treewalk_lexical_context.outside_all_lets)
						  )
						  or
						  ( fn_nesting == 0
						    and
						    syntax_treewalk_lexical_context.outside_all_lets  
						  )
					     )
						 error_function source_code_region err::ERROR
						     (string::cat
							 [ "unresolved flex record\n\
							   \   (Don't know what fields it has beyond .",
							   sy::name lab,
							   ")"
							 ]
						     )
						     err::null_error_body;

																if_debugging_say ("\ngeneralize_type': converting INCOMPLETE_RECORD_TYPEVAR to WILDCARD  [unify-and-generalize-types-g.pkg]\n");
						 tdt::WILDCARD_TYPOID;

					     else
																if_debugging_say ("\ngeneralize_type': leaving INCOMPLETE_RECORD_TYPEVAR as-is  [unify-and-generalize-types-g.pkg]\n");
						 typoid;
					     fi;

					tdt::TYPEVAR_REF (typevar_ref as { id, ref_typevar => REF (tdt::INCOMPLETE_RECORD_TYPEVAR { fn_nesting, eq, known_fields } ) } )
					     =>
					     if ( (     fn_nesting > syntax_treewalk_lexical_context.fn_nesting  
						  and (generalize or syntax_treewalk_lexical_context.outside_all_lets)
						  )
						or ( fn_nesting == 0
						     and
						     syntax_treewalk_lexical_context.outside_all_lets
						   )
						)

						error_function source_code_region err::ERROR 
						   "unresolved flex record (need to know the \
						   \names of ALL the fields\n in this context)"
						   (fn pp
						       =
						       {   uty::reset_unparse_type();
							   pp.newline();
							   pp.lit "type: ";
							   unparse_typoid pp typoid;
						       }
						   );

						tdt::WILDCARD_TYPOID;

					    else
						typoid;
					    fi;


					tdt::TYPEVAR_REF { id, ref_typevar => REF (tdt::RESOLVED_TYPEVAR typoid) }
					    =>
					    {
																if_debugging_unparse_typoid ("\ngeneralize_type'/RESOLVED_TYPEVAR: generalizing resolved type variable of type (unparse):  [unify-and-generalize-types-g.pkg]\n", typoid);
																if_debugging_prprint_typoid ("\ngeneralize_type'/RESOLVED_TYPEVAR: generalizing resolved type variable of type (prprint):  [unify-and-generalize-types-g.pkg]\n", typoid);
						# Drop from the type the now-useless prefix
						#     tdt::TYPEVAR_REF (REF (tdt::RESOLVED_TYPEVAR
						# Process and return the remainder of the type:
						#	
						generalize_type' typoid;
					    };

					tdt::TYPEVAR_REF (typevar_ref as { id, ref_typevar as REF (tdt::USER_TYPEVAR { name, fn_nesting, eq } ) } )
					    =>
					    {   										if *debugging  printf "generalize_type'/USER_TYPEVAR [unify-and-generalize-types-g.pkg]: %s fn_nesting==%d eq==%s\n" (sy::name name) fn_nesting (eq ?? "TRUE" :: "FALSE");  fi;
						# We're looking at a type variable X or Y in an
						# expression like  fun foo (x: X) = ... and
						# wondering if it is ok to generalize.

						# If it isn't on the list of type variables
						# used in the current function's pattern
						# clause(s) (parameter expressions), we have
						# no business messing with it:
						#	
						if (not (is_local_function_typevar_ref  typevar_ref))
																if_debugging_say "is not local";
						    # This USER_TYPEVAR does not belong
						    # to us, so we cannot generalize it:
						    #
						    typoid;

						else
						    # This USER_TYPEVAR -does- belong
						    # to us, so we can (maybe) generalize it:
																if_debugging_say " is local";

						    # If this type variable is mentioned in an
						    # enclosing lexical context (fun/fn), it
						    # encodes type constraints that would be
						    # lost if we generalized it, allowing
						    # incorrect code to typecheck:
						    # 
						    if ( fn_nesting > syntax_treewalk_lexical_context.fn_nesting		# External scope references do not forbid generalization.
							 and
							 generalize								# "value restrIction" does not forbid generalization.
						       )

							# We're GO to generalize this type variable.

																if_debugging_say " is generalizable, replacing USER_TYPEVAR by TYPESCHEME_ARG  [unify-and-generalize-types-g.pkg]";

							find_generalized_typevar_ref_type  typevar_ref				# If we've already generalized it, use assigned typescheme slot.
							except
							    NOT_THERE
								=
								{   # Need to assign a fresh typescheme slot,
								    # then note and return it:

								    new_typescheme_slot_arg
									=
									tdt::TYPESCHEME_ARG (allocate_typescheme_arg_slot());

								    typescheme_eqflags						# Remember whether this new type variable resolve to an equality type.
									:=
									eq ! *typescheme_eqflags;

								    note_generalized_typevar_ref_type
									( typevar_ref,
									  new_typescheme_slot_arg
									);

								    new_typescheme_slot_arg;
								};
						    else
							printf  "generalize_type'/USER_TYPEVAR: %s fn_nesting==%d syntax_treewalk_lexical_context.fn_nesting==%d\n"
								(sy::name name) fn_nesting   syntax_treewalk_lexical_context.fn_nesting;

							error_function source_code_region err::ERROR
							    (  "Explicit type variable cannot be generalized at its declaration point: "
							       +
							       (uty::typevar_ref_printname  typevar_ref)
							    )
							    err::null_error_body;

							ref_typevar :=  tdt::RESOLVED_TYPEVAR  tdt::WILDCARD_TYPOID;

							tdt::WILDCARD_TYPOID;
						    fi;

						fi;
					    };

					( tdt::TYPEVAR_REF { id, ref_typevar as REF (tdt::LITERAL_TYPEVAR     _) }
					| tdt::TYPEVAR_REF { id, ref_typevar as REF (tdt::OVERLOADED_TYPEVAR  _) }
					)
					    =>
					    typoid;

					tdt::TYPCON_TYPOID (typ, args)
					    =>
					    {											if_debugging_unparse_typoid ("\ngeneralize_type'/TYPCON_TYPE: generalizing constructor type (unparse):   [unify-and-generalize-types-g.pkg]\n", typoid);
																if_debugging_prprint_typoid ("\ngeneralize_type'/TYPCON_TYPE: generalizing constructor type (prprint):   [unify-and-generalize-types-g.pkg]\n", typoid);
						#
						tdt::TYPCON_TYPOID (typ, map generalize_type' args);
					    };

					tdt::WILDCARD_TYPOID
					    =>
					    tdt::WILDCARD_TYPOID;

					_ => bug "generalize_type -- bad arg";
				    esac;											# fun generalize_type'


																if_debugging_unparse_typoid ("\ngeneralize_type:  var_type as given to generalize_type' (unparse):      [unify-and-generalize-types-g.pkg]", *var_type);
																if_debugging_prprint_typoid ("\ngeneralize_type:  var_type as given to generalize_type' (prprint):      [unify-and-generalize-types-g.pkg]", *var_type);

				typescheme_body =  generalize_type'  *var_type;

																if_debugging_unparse_typoid ("\ngeneralize_type: var_type as converted by generalize_type' (unparse):    [unify-and-generalize-types-g.pkg]", typescheme_body);
																if_debugging_prprint_typoid ("\ngeneralize_type: var_type as converted by generalize_type' (prprint):    [unify-and-generalize-types-g.pkg]", typescheme_body);

				generalized_typevar_refs
				    =
				    map #1 (reverse *generalized_typevar_ref_types);

				# Turn user bound typevars into ordinary META typevars 
				#
																if_debugging_say ("\ngeneralize_type: running hack to eliminate user_bound variables.   [unify-and-generalize-types-g.pkg]\n");
				apply  eliminate_user_bound_typevars
				       generalized_typevar_refs
				where
				    #  A hack to eliminate all user bound type variables --zsh 
				    #  ZHONG?: is this still necessary? [dbm] XXX BUGGO FIXME 
				    #
				    fun eliminate_user_bound_typevars
					  {
					    id,
					    ref_typevar as REF (tdt::USER_TYPEVAR { fn_nesting, eq, ... } )
					  }
					    => 
					    {   										if *debugging   printf "generalize_type: eliminate_user_bound_typevars: converting USER_TYPEVAR id%d to META_TYPEVAR.   [unify-and-generalize-types-g.pkg]\n" id;   fi;
						ref_typevar :=  tdt::META_TYPEVAR { fn_nesting, eq };
					    };


					eliminate_user_bound_typevars _ 
					    =>
					    ();
				    end;
				end;

				if *failure
				    #												if_debugging_say ("\ngeneralize_type: type vars left ungeneralized because of value restriction.   [unify-and-generalize-types-g.pkg]\n");
				    if *tc::value_restriction_top_warn
					#
					error_function  source_code_region  err::WARNING
					    "type vars not generalized because of\n\
					     \   value restriction are macro expanded to dummy types (X1, X2, ...)"
					    err::null_error_body;
				    fi;
				fi;

																if_debugging_say "\ngeneralize_type:  returning   [unify-and-generalize-types-g.pkg]";

				# Set the type variable we're generalizing
				# to the type scheme we've constructed:
				#
				var_type := tdt::TYPESCHEME_TYPOID
					      {
						typescheme
						    =>
						    tdt::TYPESCHEME   { arity => *typescheme_arg_slots_allocated,
									body  =>  typescheme_body
								      },

						typescheme_eqflags
						    =>
						    reverse  *typescheme_eqflags
					      };

																if_debugging_unparse_typoid ("\ngeneralize_type: final value for *var_type (unparse):    [unify-and-generalize-types-g.pkg]", *var_type);
																if_debugging_prprint_typoid ("\ngeneralize_type: final value for *var_type (prprint):    [unify-and-generalize-types-g.pkg]", *var_type);

																if *debugging
																    printf "\ngeneralize_type returning %d type variables:           [unify-and-generalize-types-g.pkg] \n" (list::length generalized_typevar_refs);
																    apply  unparse_typevar_ref  generalized_typevar_refs
																    where
/* */																	fun unparse_typevar_ref  typevar_ref
																	    =
																	    if_debugging_unparse_typevar_ref ("", typevar_ref);
																    end;
																    say ("\n^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^\n");
																    print_callstack "=============  generalize_type/BOTTOM ==========" callstack;
																    say (  "\n");
																fi; 

				generalized_typevar_refs;									# Return generalized typevars.
			    };

			generalize_type _ => bug "generalize_type - bad arg";
		    end;													# fun generalize_type


		    # Make 'pattern' as typeagnostistic ("polymorphic")
		    # as possible by changing META_TYPEVAR and USER_TYPEVAR
		    # to tdt::TYPESCHEME_ARG 
		    # wherever possible, e.g., where permitted
		    # by the "value restriction" as implemented
		    # by tyj::is_value() and passed to us
		    # via the 'generalize' parameter.
		    # 
		    # The 'pattern' argument is updated by
		    # side effects;  we return the list of
		    # generalized type variables.
		    # 
		    # We have one call, from vb_type() in
		    # do_declaration/VALUE_DECLARATIONS:
		    #
		    fun generalize_pattern
			(
			  given_pattern:                    ds::Case_Pattern,							# Left-hand-side of a "fun foo ... = ..." or "my ... = ..." statement or such.
			  userbound:                        List( tdt::Typevar_Ref ),						# List of type variables from 'pattern'.

			  syntax_treewalk_lexical_context:  Syntax_Treewalk_Lexical_Context, 
			  generalize:                       Bool,
			  source_code_region:               ds::Source_Code_Region,
			  callstack:                        List(String)							# Debug support.
			)
			: List( tdt::Typevar_Ref )										# These will actually always be tdt::META_TYPEVAR or tdt::USER_TYPEVAR
			=
			{
																if *debugging print_callstack "\n============= generalize_pattern/TOP ============= " callstack; fi;
																if_debugging_say   "vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv   [unify-and-generalize-types-g.pkg]\n";
																if *debugging
																    printf "lexical context: lex.fn_nesting d= %d  outside_all_lets b= %s\n"
																	    syntax_treewalk_lexical_context.fn_nesting  (syntax_treewalk_lexical_context.outside_all_lets ?? "TRUE" :: "FALSE");
																fi;
																if_debugging_unparse_pattern ("\ndo_declaration/VALUE_DECLARATIONS/typecheck_named_value: pattern before generalization ==    [unify-and-generalize-types-g.pkg]\n", (given_pattern,100));
			    generalized_typevars
				=
				REF( []:  List( tdt::Typevar_Ref ) );


			    generalize_pattern'  given_pattern
			    where
				fun generalize_pattern' (ds::VARIABLE_IN_PATTERN variable)
					=> 
					{   # This is the core case for this function;
					    # the others are just recursive descent
					    # to get here:
					    # 
					    added_bound_typevar_refs
						=
						generalize_type			# This is the only call to generalize_type().
						    ( variable,
						      userbound,
						      syntax_treewalk_lexical_context,
						      generalize,
						      source_code_region,
						      "generalize_pattern" ! callstack
						    );

																if *debugging
/* */																    fun unparse_typevar_ref  typevar_ref
																	=
																	if_debugging_unparse_typevar_ref ("", typevar_ref);

																    say ("\ngeneralize_pattern' [unify-and-generalize-types-g.pkg]: added_bound_typevar_refs: ");
																    apply  unparse_typevar_ref   added_bound_typevar_refs;

																    say ("\ngeneralize_pattern' [unify-and-generalize-types-g.pkg]: *generalized_typevars: ");
																    apply  unparse_typevar_ref   *generalized_typevars;
																fi;
					    case (added_bound_typevar_refs, *generalized_typevars) 
						#
						(_ ! _, _ ! _) => bug "generalize_pattern' 1234";				# ???
						_ => ();
					    esac;

					    generalized_typevars
						:=
						added_bound_typevar_refs
						@
					       *generalized_typevars;
																if *debugging
																    say ("\ngeneralize_pattern' [unify-and-generalize-types-g.pkg]: resulting type variables list: ");
																    apply  unparse_typevar_ref  *generalized_typevars
																    where
/* */																	fun unparse_typevar_ref  typevar_ref
																	    =
																	    if_debugging_unparse_typevar_ref ("", typevar_ref);
																    end;
																    say ("\n");
																fi;
					};

				   generalize_pattern' (ds::RECORD_PATTERN { fields, ... } )          =>   apply (generalize_pattern' o #2) fields;
				   generalize_pattern' (ds::APPLY_PATTERN(_, _, arg)       )          =>   generalize_pattern' arg;
				   generalize_pattern' (ds::TYPE_CONSTRAINT_PATTERN (pattern, _)  )   =>   generalize_pattern' pattern;
				   generalize_pattern' (ds::AS_PATTERN (var_pattern, pattern))        =>   {   generalize_pattern' var_pattern;
													       generalize_pattern' pattern;
													   };
				   generalize_pattern' _ => ();
				end;
			    end;


																if_debugging_unparse_pattern  ("do_declaration/VALUE_DECLARATIONS/typecheck_named_value: pattern after generalization ==     [unify-and-generalize-types-g.pkg]\n", (given_pattern,100));
																if_debugging_say "\n^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^";
																if *debugging print_callstack "\n============= generalize_pattern/BOTTOM ==========    [unify-and-generalize-types-g.pkg]" callstack; fi;
																if_debugging_say   "\n";
			    *generalized_typevars;
			};													# fun generalize_pattern


		    # Compute type of f(x)
		    # given types for f and x:
		    #
		    fun apply_type
			  ( operator_type:	tdt::Typoid,
			    operand_type:	tdt::Typoid
			  ):			tdt::Typoid
			=
			{   result_type
				=
				tyj::make_meta_typevar_and_type
				  ( tdt::infinity,
				    ["apply_type  from  unify-and-generalize-types-g.pkg"]
				  );

																if_debugging_say "\napply_type calling unify_types   [unify-and-generalize-types-g.pkg]\n";
			    uyt::unify_types
			      (
				"operator_type",   "operand_type --> result_type",
				 operator_type,    (operand_type --> result_type),
				["apply_type"]
			      );
																if_debugging_say "\napply_type back from unify_types   [unify-and-generalize-types-g.pkg]\n";

			    result_type;
			};


		    # Use unification to compute the most
		    # general non-typeagnostic type for
		    # a pattern.  This includes checking
		    # that all values in a vector pattern
		    # are of compatible type etc.
		    #
		    # Generalizing to typeagnostic type is
		    # done later if permitted by
		    # tyj::is_value():
		    #
		    fun compute_pattern_type
			( given_pattern:       ds::Case_Pattern,								# Compute a type for this pattern.
			  fn_nesting:          Int,										# Lexical nesting of fun/fn constructs at this point in deep syntax dagwalk.
			  source_code_region:  ds::Source_Code_Region,
			  callstack:           List(String)									# Debug support.
			)
			:
			( ds::Case_Pattern,											# Possibly (probably) updated version of given_pattern deep syntax tree.
			  tdt::Typoid												# Computed type of given_pattern.
			)
			=
			{
																if *debugging print_callstack "\ncompute_pattern_type/TOP [unify-and-generalize-types-g.pkg]" callstack; fi;
																if_debugging_unparse_pattern ("\ncompute_pattern_type/TOP [unify-and-generalize-types-g.pkg] given_pattern == ", (given_pattern,100));
			    result
				=
				case given_pattern
				    #
				    ds::WILDCARD_PATTERN
					=>
					( given_pattern,
					  tyj::make_meta_typevar_and_type  (fn_nesting, ["compute_pattern_type/WILDCARD_PATTERN  from  unify-and-generalize-types-g.pkg"])
					);

				    ds::VARIABLE_IN_PATTERN (vac::PLAIN_VARIABLE { var_type as REF tdt::UNDEFINED_TYPOID, ... } )
					=> 
					{   var_type :=  tyj::make_meta_typevar_and_type
							   ( fn_nesting,
							     ["compute_pattern_type/VARIABLE_IN_PATTERN  from  unify-and-generalize-types-g.pkg"]
							   );

					    (given_pattern, *var_type);
					};

				    # Multiple occurrence due to or-pattern 
				    #
				    ds::VARIABLE_IN_PATTERN (vac::PLAIN_VARIABLE { var_type, ... } )	=>  (given_pattern, *var_type); 

				    ds::FLOAT_CONSTANT_IN_PATTERN _					=>  (given_pattern, mtt::float64_typoid);
				    ds::STRING_CONSTANT_IN_PATTERN _					=>  (given_pattern, mtt::string_typoid);
				    ds::CHAR_CONSTANT_IN_PATTERN _					=>  (given_pattern, mtt::char_typoid);

				    ds::INT_CONSTANT_IN_PATTERN (_, type)				=>  {   note_overloaded_literal type;   (given_pattern, type); };
				    ds::UNT_CONSTANT_IN_PATTERN (_, type)				=>  {   note_overloaded_literal type;   (given_pattern, type); };

				    ds::RECORD_PATTERN { fields, is_incomplete, type_ref }
					=>
					# The record fields are already sorted by label,
					# typically by make_record_pattern() in
					#
					#     src/lib/compiler/front/typer/main/typer-junk.pkg
					#
					{   my (fields, field_types)
						=
						tyj::map_unzip  do_field  fields
						where
						    fun do_field (label, pattern)
							= 
							{   my (field_pattern, field_type)
							        =
							        compute_pattern_type
								  ( pattern,
								    fn_nesting,
								    source_code_region,
								    "compute_pattern_type/RECORD_PATTERN" ! callstack
								  );

							    ( (label, field_pattern),
							      (label, field_type)
							    );
							};
						end; 

					    new_record_pattern
						=
						ds::RECORD_PATTERN { fields, is_incomplete, type_ref };

					    if (not is_incomplete)
						#
						( new_record_pattern,
						  mtt::record_typoid  field_types
						);
					    else
						# We need to recover the rest of the fields
						# in this record before we can compute a
						# full type for it.  In   src/lib/compiler/front/typer-stuff/types/type-declaration-types.api
						# we define a special INCOMPLETE_RECORD_TYPEVAR
						# just to handle this situation:
						#
						record_typoid
						    =
						    tdt::TYPEVAR_REF
						      (
							tdt::make_typevar_ref
							  (
							    tyj::make_incomplete_record_typevar
							      (field_types,  fn_nesting),

							    ["compute_pattern_type/RECORD_PATTERN  from  unify-and-generalize-types-g.pkg"]
						      )   );

						type_ref := record_typoid;

						(new_record_pattern, record_typoid);
					    fi;
					};

				    ds::VECTOR_PATTERN (patterns, _)
					=> 
					{   
					    stipulate
						fun do_pattern  pattern
						    =
						    compute_pattern_type
						      ( pattern,
							fn_nesting,
							source_code_region,
							"compute_pattern_type/VECTOR_PATTERN" ! callstack
						      );
					    herein
						(tyj::map_unzip  do_pattern  patterns)
						    ->
						    (patterns, pattern_types);
					    end;

																if_debugging_say "\ncompute_pattern_type/VECTOR_PATTERN folding calls to unify_types  [unify-and-generalize-types-g.pkg]\n";
					    # Force all vector elements
					    # to have the same type:
					    #
					    vector_element_type
						=
						fold_backward
						      (fn (a, b) =  { uyt::unify_types ("vector a", "vector b", a, b, "compute_pattern_type/VECTOR_PATTERN(2)" ! callstack); b;})
						      (tyj::make_meta_typevar_and_type  (fn_nesting, "compute_pattern_type/VECTOR_PATTERN(3)" ! callstack))
						      pattern_types;
																if_debugging_say "\ncompute_pattern_type/VECTOR_PATTERN done folding calls to unify_types  [unify-and-generalize-types-g.pkg]\n";

					    ( ds::VECTOR_PATTERN (patterns, vector_element_type),
					      tdt::TYPCON_TYPOID (mtt::vector_type, [ vector_element_type ] )
					    );
					}
					except
					    uyt::UNIFY_TYPES mode
						=
						{  error_function
						       source_code_region
						       err::ERROR 
						       (message("vector pattern type failure", mode))
						       err::null_error_body;

						   (given_pattern, tdt::WILDCARD_TYPOID);
						};


				    ds::OR_PATTERN (pattern1, pattern2)
					=> 
					  {
																if *debugging print_callstack "\ncompute_pattern_type/ds::OR_PATTERN/TOP  [unify-and-generalize-types-g.pkg]" callstack; fi;

					      my (pattern1, typoid1) = compute_pattern_type (pattern1, fn_nesting, source_code_region, "compute_pattern_type/ds::OR_PATTERN"    ! callstack);
					      my (pattern2, typoid2) = compute_pattern_type (pattern2, fn_nesting, source_code_region, "compute_pattern_type/ds::OR_PATTERN(2)" ! callstack);

																if_debugging_say "\ncompute_pattern_type/ds::OR_PATTERN calling unify_types_and_handle_errors  [unify-and-generalize-types-g.pkg]\n";
					      # Force both sides of the '|'
					      # pattern to have the same type:
					      #
					      unify_types_and_handle_errors
						  {
						    typoid1,  name1 => "expected",
						    typoid2,  name2 => "found",

						    message => "or-patterns don't agree",
						    source_code_region,

						    unparse_phrase =>  unparse_pattern,
						    phrase_name    => "pattern",
						    phrase         =>  given_pattern,

						    callstack      => "compute_pattern_type/ds::OR_PATTERN(3)" ! callstack
						  };
																if_debugging_say "\ncompute_pattern_type/ds::OR_PATTERN done calling unify_types_and_handle_errors  [unify-and-generalize-types-g.pkg]\n";

					      ( ds::OR_PATTERN (pattern1, pattern2),
						typoid1
					      );
					  };

				    ds::CONSTRUCTOR_PATTERN (valcon as tdt::VALCON { typoid, ... }, _)
					=> 
					{   (tyj::instantiate_if_typescheme  typoid)
						->
						(type, fresh_meta_typevars);
						  

					      # The following is to set the correct fn_nesting information
					      # on the type variables in type. (ZHONG)
					      #
																if_debugging_say "\ncompute_pattern_type/ds::CONSTRUCTOR_PATTERN calling unify_types  [unify-and-generalize-types-g.pkg]\n";
					      uyt::unify_types
						(
						  "type", "temp_type",
						   type,   temp_type,
						  ["compute_pattern_type/ds::CONSTRUCTOR_PATTERN  from  unify-and-generalize-types-g.pkg"] 
						)
					      where
						  temp_type = tyj::make_meta_typevar_and_type
								( fn_nesting,
								  ["compute_pattern_type/ds::CONSTRUCTOR_PATTERN  from  unify-and-generalize-types-g.pkg"]
								);
					      end;
																if_debugging_say "\ncompute_pattern_type/ds::CONSTRUCTOR_PATTERN done calling unify_types  [unify-and-generalize-types-g.pkg]\n";

					      ( ds::CONSTRUCTOR_PATTERN (valcon, fresh_meta_typevars),
						type
					      );
					 };

				    ds::APPLY_PATTERN (valcon as tdt::VALCON { typoid, form, ... }, _, arg)
					=>
					{   (compute_pattern_type ( arg,
								    fn_nesting,
								    source_code_region,
								    "compute_pattern_type/ds::APPLY_PATTERN" ! callstack
								  ))
						->
						(arg_pattern, arg_type);

					    my  (constructor, type)
						=
						case form
						    #
						    varhome::REFCELL_REP =>  (ref_new_valcon valcon,  mtt::ref_pattern_typoid);
						    _                    =>  (valcon,                 typoid    );
						esac;

					    (tyj::instantiate_if_typescheme type)
						->
						(type, fresh_meta_typevars);

					    result_pattern
					       =
					       ds::APPLY_PATTERN (constructor, fresh_meta_typevars, arg_pattern);

																if_debugging_say "\ncompute_pattern_type/ds::APPLY_PATTERN calling apply_type  [unify-and-generalize-types-g.pkg]\n";
					    ( result_pattern,
					      apply_type (type, arg_type)
					    )
					    except
						uyt::UNIFY_TYPES  mode
						    =
						    {   error_function source_code_region err::ERROR
							    (message("constructor and argument don't agree in pattern", mode))
							    (fn pp
								=
								{   uty::reset_unparse_type();			pp.newline();
								    pp.lit "constructor: ";
								    unparse_typoid  pp  type;			pp.newline();
								    pp.lit "argument:    ";
								    unparse_typoid pp arg_type;			pp.newline();
								    pp.lit "in pattern:";
								    pp::break pp { blanks=>1, indent_on_wrap=>2 };
								    unparse_pattern pp (given_pattern, *print_depth);
								}
							    );

							( given_pattern,
							  tdt::WILDCARD_TYPOID
							);
						    };
					};

				    ds::TYPE_CONSTRAINT_PATTERN (pattern, type)
					=> 
					{
																if_debugging_say "\ncompute_pattern_type/ds::TYPE_CONSTRAINT_PATTERN calling compute_pattern_type  [unify-and-generalize-types-g.pkg]\n";
					    my (npat, pat_type)
						=
						compute_pattern_type
						  ( pattern,
						    fn_nesting,
						    source_code_region,
						    "compute_pattern_type/ds::TYPE_CONSTRAINT_PATTERN" ! callstack
						  );
																if_debugging_say "\ncompute_pattern_type/ds::TYPE_CONSTRAINT_PATTERN done calling compute_pattern_type  [unify-and-generalize-types-g.pkg]\n";

																if_debugging_say "\ncompute_pattern_type/ds::TYPE_CONSTRAINT_PATTERN calling unify_types_and_handle_errors  [unify-and-generalize-types-g.pkg]\n";
					    if (unify_types_and_handle_errors
						     {
						       typoid1 => pat_type,  name1 => "pattern",
						       typoid2 => type,      name2 => "constraint",

						       message=>"pattern and constraint don't agree",
						       source_code_region,

						       unparse_phrase => unparse_pattern,
						       phrase_name    => "pattern",
						       phrase         => given_pattern,

						       callstack      => "compute_pattern_type/ds::TYPE_CONSTRAINT_PATTERN(2)" ! callstack
						     }
					       ) 

															      if_debugging_say "\ncompute_pattern_type/ds::TYPE_CONSTRAINT_PATTERN done calling unify_types_and_handle_errors (succeeded)  [unify-and-generalize-types-g.pkg]\n";
						( ds::TYPE_CONSTRAINT_PATTERN (npat, type),
						  type
						);
					    else
															       if_debugging_say "\ncompute_pattern_type/ds::TYPE_CONSTRAINT_PATTERN done calling unify_types_and_handle_errors (failed)  [unify-and-generalize-types-g.pkg]\n";
						( given_pattern,
						  tdt::WILDCARD_TYPOID
						);
					    fi;
					};

				    ds::AS_PATTERN (var_pattern as ds::VARIABLE_IN_PATTERN (vac::PLAIN_VARIABLE { var_type, ... } ), main_pattern)
					=>
					{
																if_debugging_say "\ncompute_pattern_type/AS_PATTERN calling compute_pattern_type  [unify-and-generalize-types-g.pkg]\n";
					    my  (main_pattern, main_pattern_type)
						=
						compute_pattern_type
						  ( main_pattern,
						    fn_nesting,
						    source_code_region,
						    "compute_pattern_type/AS_PATTERN" ! callstack
						  );

																if_debugging_say "\ncompute_pattern_type/AS_PATTERN done calling compute_pattern_type  [unify-and-generalize-types-g.pkg]\n";
					    var_type :=  main_pattern_type;

					    ( ds::AS_PATTERN (var_pattern, main_pattern),
					      main_pattern_type
					    );
					};

				    ds::AS_PATTERN (constraint_pattern as ds::TYPE_CONSTRAINT_PATTERN (ds::VARIABLE_IN_PATTERN (vac::PLAIN_VARIABLE { var_type, ... } ), constraining_type), main_pattern)
					=>
					{
																if_debugging_say "\ncompute_pattern_type/AS_PATTERN II calling compute_pattern_type  [unify-and-generalize-types-g.pkg]\n";
					    my  (main_pattern, main_pattern_type)
						=
						compute_pattern_type
						  ( main_pattern,
						    fn_nesting,
						    source_code_region,
						    "compute_pattern_type/AS_PATTERN(2)" ! callstack
						  );
																if_debugging_say "\ncompute_pattern_type/AS_PATTERN II done calling compute_pattern_type  [unify-and-generalize-types-g.pkg]\n";

																if_debugging_say "\ncompute_pattern_type/AS_PATTERN II calling unify_types_and_handle_errors  [unify-and-generalize-types-g.pkg]\n";
					    if (unify_types_and_handle_errors
						  {
						    typoid1        => main_pattern_type,    name1 => "pattern",
						    typoid2        => constraining_type,    name2 => "constraint",

						    message	   => "pattern and constraint don't agree",
						    source_code_region,

						    unparse_phrase =>  unparse_pattern,
						    phrase_name    => "pattern",
						    phrase         =>  given_pattern,

						    callstack      => "compute_pattern_type/AS_PATTERN(3)" ! callstack
						  }
					       )

																if_debugging_say "\ncompute_pattern_type/AS_PATTERN II done calling unify_types_and_handle_errors (succeeded)  [unify-and-generalize-types-g.pkg]\n";
						var_type :=  constraining_type;

						( ds::AS_PATTERN (constraint_pattern, main_pattern),
						  constraining_type
						);
					    else
																if_debugging_say "\ncompute_pattern_type/AS_PATTERN II done calling unify_types_and_handle_errors (failed)  [unify-and-generalize-types-g.pkg]\n";
						( given_pattern,
						  tdt::WILDCARD_TYPOID
						);
					    fi;
					};

				    p => bug "compute_pattern_type -- unexpected pattern";
				esac;												# fun compute_pattern_type

																if *debugging print_callstack "\ncompute_pattern_type/BOTTOM [unify-and-generalize-types-g.pkg] " callstack; fi;

			    result;
			};

		    # Use unification to compute the most
		    # general non-typeagnostic type for
		    # an expression.  This includes checking
		    # that all values in a vector are of
		    # compatible type etc.
		    #
		    # Generalizing to typeagnostic type
		    # is done later if permitted by
		    # is tyj::is_value():
		    #
		    fun compute_expression_type
			(
			  given_expression:                 ds::Deep_Expression,
			  syntax_treewalk_lexical_context:  Syntax_Treewalk_Lexical_Context,
			  source_code_region:               ds::Source_Code_Region,
			  callstack:                        List(String)							# Debug support.
			)
			:
			(ds::Deep_Expression, tdt::Typoid)
			=
			{
																if *debugging print_callstack        "\ncompute_expression_type/TOP [unify-and-generalize-types-g.pkg]" callstack; fi;
																if_debugging_unparse_expression     ("\ncompute_expression_type/TOP [unify-and-generalize-types-g.pkg]: expression unparse ==", (given_expression,100));
																if_debugging_prettyprint_expression ("\ncompute_expression_type/TOP [unify-and-generalize-types-g.pkg]: expression prettyprint ==", (given_expression,100));
			    fun bool_unify_err { type, name, message }
				=
				{
																if_debugging_say "\ncompute_expression_type: bool_unify_err: calling unify_and-handle_errors  [unify-and-generalize-types-g.pkg]\n";
																result =
				    unify_types_and_handle_errors
				      {
					typoid1    => type,          name1 => name,
					typoid2    => mtt::bool_typoid,   name2 => "",

					message,
					source_code_region,

					unparse_phrase =>  unparse_expression,
					phrase_name    => "expression",
					phrase         =>  given_expression,

					callstack      => "compute_expression_type()/bool_unify_error()" ! callstack
				      };
																if_debugging_say "\ncompute_expression_type: bool_unify_err: done calling unify_and-handle_errors  [unify-and-generalize-types-g.pkg]\n";
																result;
				};	

			    # Typing of boolean short-circuit
			    # operators 'and' and 'or':
			    #
			    fun short_circuit_and_or (con, what, e1, e2)
				=
				{												if_debugging_say "\ncompute_expression_type/short_circuit_and_or calling compute_expression_type.   [unify-and-generalize-types-g.pkg]";

				    my (e1', t1) =   compute_expression_type (e1, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/short_circuit_and_or"    ! callstack);
				    my (e2', t2) =   compute_expression_type (e2, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/short_circuit_and_or(2)" ! callstack);

																if_debugging_say "\ncompute_expression_type/short_circuit_and_or done calling compute_expression_type.  [unify-and-generalize-types-g.pkg]";
				    m =   string::cat ["operand of ", what, " is not of type bool"];

				    if (    bool_unify_err {   type => t1,   name => "operand",   message => m   }
				       and  bool_unify_err {   type => t2,   name => "operand",   message => m   }
				       )

					( con (e1', e2'),
					  mtt::bool_typoid
					);
				    else
					( given_expression,
					  tdt::WILDCARD_TYPOID
					);
				    fi;
				};

																if *debugging print_callstack "\ncompute_expression_type/TOP.z [unify-and-generalize-types-g.pkg] " callstack; fi;
			    case given_expression
				#
				ds::VARIABLE_IN_EXPRESSION { var             =>  var_ref  as  REF (vac::PLAIN_VARIABLE { var_type, inlining_data, ... } ),
					                     typescheme_args =>  _
				                           }
				    =>
				    { 	
																 if_debugging_say "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/TOP.   [unify-and-generalize-types-g.pkg]";
																 if_debugging_unparse_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE   unparse [unify-and-generalize-types-g.pkg]: *var_type == ", *var_type);
																 if_debugging_prprint_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE   prprint [unify-and-generalize-types-g.pkg]: *var_type == ", *var_type);
					case (inlining_data_to_my_type  inlining_data)						# For builtins like string::get, inlining_data was set up from   all_primops   in   src/lib/compiler/front/semantic/symbolmapstack/base-types-and-ops-symbolmapstack.pkg
					    #
					    THE st =>   {   (tyj::instantiate_if_typescheme  st       ) ->  (sty, fresh_meta_typevars);
							    (tyj::instantiate_if_typescheme  *var_type) ->  (nty, _);

/* */																fun prettyprint_typoid typoid =   if_debugging_prprint_typoid ("", typoid);
																len =  list::length  fresh_meta_typevars;
																if_debugging_say "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/THE st.   [unify-and-generalize-types-g.pkg]";

																if_debugging_unparse_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/THE st   [unify-and-generalize-types-g.pkg]: unparse_typoid st  == ", st);
																if_debugging_prprint_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/THE st   [unify-and-generalize-types-g.pkg]: prprint_typoid st  == ", st);

																if_debugging_unparse_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/THE st   [unify-and-generalize-types-g.pkg]: unparse_typoid sty == ", sty);
																if_debugging_prprint_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/THE st   [unify-and-generalize-types-g.pkg]: prprint_typoid sty == ", sty);

																if_debugging_unparse_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/THE st   [unify-and-generalize-types-g.pkg]: unparse_typoid nty == ", nty);
																if_debugging_prprint_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE/THE st   [unify-and-generalize-types-g.pkg]: prprint_typoid nty == ", nty);

																if_debugging_say (sprintf "\nprprinting %d fresh_meta_typevars:  compute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE [unify-and-generalize-types-g.pkg]"  len);
																apply prettyprint_typoid  fresh_meta_typevars;
																if_debugging_say (sprintf "\nprprinted %d fresh_meta_typevars.   compute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE [unify-and-generalize-types-g.pkg]"  len);

																if_debugging_say "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE II calling unify_types.  [unify-and-generalize-types-g.pkg]";

																

							    uyt::unify_types
							      ( "sty", "nty", sty, nty,
								["compute_expression_type/ds::VARIABLE_IN_EXPRESSION"]
							      )
							    except
								_ = ();	  #  ??? XXX BUGGO FIXME 
																len =  list::length  fresh_meta_typevars;
																if_debugging_say (sprintf "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE II done calling unify_types%s. [unify-and-generalize-types-g.pkg]"
																		  (len > 0 ?? " -- NOW SETTING VARIABLE_IN_EXPRESSION.typescheme_args" :: ""));
																if_debugging_say (sprintf "\nprprinting %d fresh_meta_typevars: compute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE.z [unify-and-generalize-types-g.pkg]" len);
																apply prettyprint_typoid  fresh_meta_typevars;
																if_debugging_say (sprintf "\nprprinted %d fresh_meta_typevars.  compute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE.z [unify-and-generalize-types-g.pkg]" len);

							    ( ds::VARIABLE_IN_EXPRESSION
								{ var             =>  var_ref,
								  typescheme_args =>  REF fresh_meta_typevars
								},
							      sty
							    );
							};

					    NULL =>     {
																if_debugging_say "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE I: calling tyj::instantiate_if_typescheme.  [unify-and-generalize-types-g.pkg]";
							    (tyj::instantiate_if_typescheme  *var_type)
								->
								(fresh_type, fresh_meta_typevars);
																if_debugging_say "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE I: done calling tyj::instantiate_if_typescheme.  [unify-and-generalize-types-g.pkg]";
																if_debugging_unparse_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE I: type unparse ==    [unify-and-generalize-types-g.pkg]", fresh_type);
																if_debugging_prprint_typoid ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE I: type prprint ==    [unify-and-generalize-types-g.pkg]", fresh_type);
																if_debugging_unparse_expression ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE I: result expression (unparse)==    [unify-and-generalize-types-g.pkg]",
																	(ds::VARIABLE_IN_EXPRESSION { var => var_ref, typescheme_args => REF fresh_meta_typevars },100));
																if_debugging_prettyprint_expression ("\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::PLAIN_VARIABLE I: result expression (prprint) ==    [unify-and-generalize-types-g.pkg]",
																	(ds::VARIABLE_IN_EXPRESSION { var => var_ref, typescheme_args => REF fresh_meta_typevars },100));

							    ( ds::VARIABLE_IN_EXPRESSION
								{ var             =>  var_ref,
								  typescheme_args =>  REF fresh_meta_typevars
								},
							      fresh_type
							    );
							};
					esac;
				     };

				ds::VARIABLE_IN_EXPRESSION { var =>  var_ref  as  REF (vac::OVERLOADED_VARIABLE _),  typescheme_args }
				    =>
				    {
																 if_debugging_say (sprintf "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION/vac::OVERLOADED_VARIABLE. |typescheme_args|=%d  [unify-and-generalize-types-g.pkg] "
																		           (list::length *typescheme_args));
					( given_expression,
					  note_overloaded_variable (var_ref, typescheme_args, error_function source_code_region)
					);
				    };

				ds::VARIABLE_IN_EXPRESSION { var => REF _, ... }
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/ds::VARIABLE_IN_EXPRESSION III.   [unify-and-generalize-types-g.pkg]";
					( given_expression,
					  tdt::WILDCARD_TYPOID
					);
				    };

				ds::VALCON_IN_EXPRESSION { valcon as tdt::VALCON { typoid, ... },  ... }
				    => 
				    {
																 if_debugging_say "\ncompute_expression_type/VALCON_IN_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					(tyj::instantiate_if_typescheme  typoid)
					    ->
					    (type, fresh_meta_typevars);


					( ds::VALCON_IN_EXPRESSION  { valcon,  typescheme_args => fresh_meta_typevars },
					  type
					);
				    };

				ds::INT_CONSTANT_IN_EXPRESSION (_, type) =>  { note_overloaded_literal type;  (given_expression, type);};
				ds::UNT_CONSTANT_IN_EXPRESSION (_, type) =>  { note_overloaded_literal type;  (given_expression, type);};

				ds::FLOAT_CONSTANT_IN_EXPRESSION  _ =>  (given_expression, mtt::float64_typoid);
				ds::STRING_CONSTANT_IN_EXPRESSION _ =>  (given_expression, mtt::string_typoid);
				ds::CHAR_CONSTANT_IN_EXPRESSION   _ =>  (given_expression, mtt::char_typoid);

				ds::RECORD_IN_EXPRESSION fields
				    =>
				    {												if_debugging_say "\ncompute_expression_type/RECORD_IN_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					my (fields, field_types)
					    =
					    tyj::map_unzip  do_field  fields							# Apply do_field to each field, return resulting value pairs in two lists.
					    where
						fun do_field
						    ( label as ds::NUMBERED_LABEL _,
						      expression'
						    )
						    = 
						    {   my  (expression, expression_type)
							    =
							    compute_expression_type
							      ( expression',
								syntax_treewalk_lexical_context,
								source_code_region,
								"compute_expression_type/RECORD_IN_EXPRESSION" ! callstack
							      );

							( (label, expression),
							  (label, expression_type)
							);
						    };
					    end;

					record_typoid
					    =
					    map  extract_field_name_and_type  (tyj::sort_fields field_types)
					    where
						fun extract_field_name_and_type  (ds::NUMBERED_LABEL { name, ... }, field_type)
						    =
						    (name, field_type);
					    end;

					( ds::RECORD_IN_EXPRESSION fields,
					  mtt::record_typoid   record_typoid
					);
				    };

				ds::RECORD_SELECTOR_EXPRESSION (label, record_expression)
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/RECORD_SELECTOR_EXPRESSION   [unify-and-generalize-types-g.pkg]";
																 if_debugging_say "calling compute_expression_type:  compute_expression_type/RECORD_SELECTOR_EXPRESSION   [unify-and-generalize-types-g.pkg]";
					my  (record_expression, record_expression_type)
					    =
					    compute_expression_type
					      ( record_expression,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/RECORD_SELECTOR_EXPRESSION" ! callstack
					      );
																 if_debugging_say "\ncompute_expression_type() call done:  compute_expression_type/RECORD_SELECTOR_EXPRESSION   [unify-and-generalize-types-g.pkg]";
					result_type
					    =
					    tyj::make_meta_typevar_and_type
					      ( tdt::infinity,
						["result_type in compute_expression_type/RECORD_SELECTOR_EXPRESSION  from  unify-and-generalize-types-g.pkg"]
					      );

					# Remember that we need to infer the
					# the rest of the fields in the record:
					#
					incomplete_record_type
					    =
					    tdt::TYPEVAR_REF
					      (
						tdt::make_typevar_ref
						  (
						    tyj::make_incomplete_record_typevar
						      (label_types, tdt::infinity),

						    ["incomplete_record_type in compute_expression_type/RECORD_SELECTOR_EXPRESSION  from  unify-and-generalize-types-g.pkg"]
					      )   )
					    where
						label_types =   [ (typer_junk::symbol_naming_label  label,  result_type) ];
					    end;


					{											if_debugging_say "\ncompute_expression_type/RECORD_SELECTOR_EXPRESSION: calling unify_types.   [unify-and-generalize-types-g.pkg]";
					    uyt::unify_types
					      ( "incomplete_record_type", "record_expression_type",
						 incomplete_record_type,   record_expression_type,
						["compute_expression_type/RECORD_SELECTOR_EXPRESSION"]
					      );
																 if_debugging_say "\ncompute_expression_type/RECORD_SELECTOR_EXPRESSION: done calling unify_types.   [unify-and-generalize-types-g.pkg]";
					    (ds::RECORD_SELECTOR_EXPRESSION (label, record_expression), result_type);
					}
					except
					    uyt::UNIFY_TYPES (mode)
					       =
					       {   error_function
						       source_code_region
						       err::ERROR
						       (   message("selecting a non-existing field from a record", mode))
						       (fn pp
							   =
							   {  uty::reset_unparse_type ();
							      pp.newline();
							      pp.lit "the field name: ";

							      case label
								  ds::NUMBERED_LABEL { name, ... } =>  uj::unparse_symbol pp name;
							      esac;

							      pp.newline();
							      pp.lit "the record type:    ";
							      unparse_typoid  pp  record_expression_type;			pp.newline();
							      pp.lit "in expression:"; 
							      pp::break pp { blanks=>1, indent_on_wrap=>2 };
							      unparse_expression pp (given_expression, *print_depth);
							   }
						       );

						   ( given_expression,
						     tdt::WILDCARD_TYPOID
						   );
					     };
				    };

				ds::VECTOR_IN_EXPRESSION (expressions, _)
				    =>
				    {												 if_debugging_say "\ncompute_expression_type/VECTOR_IN_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					my  (expressions, expression_types)
					    =
					    tyj::map_unzip
						(fn e =   compute_expression_type ( e,
										    syntax_treewalk_lexical_context,
										    source_code_region,
										    "compute_expression_type/VECTOR_IN_EXPRESSION" ! callstack
										  )
						)
						expressions;
																 if_debugging_say "\ncompute_expression_type/VECTOR_IN_EXPRESSION: folding unify_types calls.   [unify-and-generalize-types-g.pkg]";
					# Check that all expressions in vector have
					# compatible types, and combine them all to
					# yield the final vector element type:
					#
					vector_element_type
					    =
					    fold_backward
						(fn (a, b) =    {   uyt::unify_types  ( "vector a",
											"vector b",
											a, b,
											["compute_expression_type/VECTOR_IN_EXPRESSION  from  unify-and-generalize-types-g.pkg"]
										      );
								    b;
								}
						)
						(tyj::make_meta_typevar_and_type  (tdt::infinity, ["compute_expression_type/VECTOR_IN_EXPRESSION  from  unify-and-generalize-types-g.pkg"]))
						expression_types;
																 if_debugging_say "\ncompute_expression_type/VECTOR_IN_EXPRESSION: done folding unify_types calls.   [unify-and-generalize-types-g.pkg]";

					( ds::VECTOR_IN_EXPRESSION (expressions, vector_element_type),
					  tdt::TYPCON_TYPOID (mtt::vector_type, [vector_element_type])
					);
				    }
				    except
					uyt::UNIFY_TYPES mode
					    =
					    {   error_function source_code_region err::ERROR
						  (message("vector expression type failure", mode))
						  err::null_error_body;

						(given_expression, tdt::WILDCARD_TYPOID);
					    };


				ds::SEQUENTIAL_EXPRESSIONS expressions
				    => 
				    {	(scan expressions) ->   (expressions, sequence_type);
					#
					( ds::SEQUENTIAL_EXPRESSIONS expressions,
					  sequence_type
					);
				    }
				    where											 if_debugging_say "\ncompute_expression_type/SEQUENTIAL_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					fun scan NIL
						=>
						(NIL, mtt::void_typoid);

					    scan [expression]
						=> 
						{   my  (expression, expression_type)
							=
							compute_expression_type
							  ( expression,
							    syntax_treewalk_lexical_context,
							    source_code_region,
							    "compute_expression_type/SEQUENTIAL_EXPRESSIONS" ! callstack
							  );

						    ([expression], expression_type);
						};

					    scan (expression ! rest)
						=> 
						{   # The type of a sequence of expressions is
						    # the type of the final expression.  We
						    # do type-checking on all of them, but we
						    # only keep the result of last one:

						    my  (expression, _)								# Discard type of non-final expression.
							=
							compute_expression_type
							  ( expression,
							    syntax_treewalk_lexical_context,
							    source_code_region,
							    "compute_expression_type/SEQUENTIAL_EXPRESSIONS(2)" ! callstack
							  );

						    (scan rest) ->   (expressions, expression_type);

						    (expression ! expressions, expression_type);				# Type of final expression is type of sequence.
						};
					end;
				    end;
	
				ds::APPLY_EXPRESSION (operator, operand) 
				    =>
				    {
																 if_debugging_say                     "\ncompute_expression_type/APPLY_EXPRESSION.1 [unify-and-generalize-types-g.pkg]";
																 if_debugging_unparse_expression     ("\ncompute_expression_type/APPLY_EXPRESSION.1 [unify-and-generalize-types-g.pkg]: operator unparse == qqq", (operator,100));
																 if_debugging_prettyprint_expression ("\ncompute_expression_type/APPLY_EXPRESSION.1 [unify-and-generalize-types-g.pkg]: operator prprint == qqq", (operator,100));
																 if_debugging_say                     "\ncompute_expression_type/APPLY_EXPRESSION.1 operator pprint done.  [unify-and-generalize-types-g.pkg]";
																 if_debugging_unparse_expression     ("\ncompute_expression_type/APPLY_EXPRESSION.1 [unify-and-generalize-types-g.pkg]: operand  unparse == ", (operand, 100));
																 if_debugging_prettyprint_expression ("\ncompute_expression_type/APPLY_EXPRESSION.1 [unify-and-generalize-types-g.pkg]: operand  prprint == ", (operand, 100));
																 if_debugging_say "\ncompute_expression_type/APPLY_EXPRESSION.1: calling compute_expression_type on operator. [unify-and-generalize-types-g.pkg]";
					my  (operator, operator_type)
					    =
					    compute_expression_type
						(operator, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/APPLY_EXPRESSION" ! callstack);
																 if_debugging_say                     "\ncompute_expression_type/APPLY_EXPRESSION.2: done calling compute_expression_type on operator.   [unify-and-generalize-types-g.pkg]";
																 if_debugging_unparse_expression     ("\ncompute_expression_type/APPLY_EXPRESSION.2 [unify-and-generalize-types-g.pkg]: operator unparse == ", (operator,100));
																 if_debugging_prettyprint_expression ("\ncompute_expression_type/APPLY_EXPRESSION.2 [unify-and-generalize-types-g.pkg]: operator prprint == ", (operator,100));
																 if_debugging_say                     "\ncompute_expression_type/APPLY_EXPRESSION.2 operator pprint done.  [unify-and-generalize-types-g.pkg]";
																 if_debugging_unparse_typoid         ("\ncompute_expression_type/APPLY_EXPRESSION.2 [unify-and-generalize-types-g.pkg]: operator_type unparse == ", operator_type);
																 if_debugging_prprint_typoid         ("\ncompute_expression_type/APPLY_EXPRESSION.2 [unify-and-generalize-types-g.pkg]: operator_type prprint == ", operator_type);

																 if_debugging_say "\ncompute_expression_type/APPLY_EXPRESSION [unify-and-generalize-types-g.pkg]: calling compute_expression_type on operand.";
					my  (operand, operand_type)
					    =
					    compute_expression_type
					      ( operand,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/APPLY_EXPRESSION(2)" ! callstack
					      );
																 if_debugging_say                     "\ncompute_expression_type/APPLY_EXPRESSION.3 [unify-and-generalize-types-g.pkg]: done calling compute_expression_type on operand.";
																 if_debugging_unparse_expression     ("\ncompute_expression_type/APPLY_EXPRESSION.3 [unify-and-generalize-types-g.pkg]: operand unparse == ", (operand,100));
																 if_debugging_prettyprint_expression ("\ncompute_expression_type/APPLY_EXPRESSION.3 [unify-and-generalize-types-g.pkg]: operand prprint == ", (operand,100));
																 if_debugging_unparse_typoid         ("\ncompute_expression_type/APPLY_EXPRESSION.3 [unify-and-generalize-types-g.pkg]: operand_type unparse == ", operand_type);
																 if_debugging_prprint_typoid         ("\ncompute_expression_type/APPLY_EXPRESSION.3 [unify-and-generalize-types-g.pkg]: operand_type prprint == ", operand_type);
					expression
					    =
					    ds::APPLY_EXPRESSION (operator, operand);
																 if_debugging_unparse_expression ("\ncompute_expression_type/APPLY_EXPRESSION [unify-and-generalize-types-g.pkg]: expression == ", (expression,100));

					{
																 if_debugging_say "\ncompute_expression_type/APPLY_EXPRESSION: calling apply_type.   [unify-and-generalize-types-g.pkg]";
					    expression_type =  apply_type (operator_type, operand_type);
																 if_debugging_say "\ncompute_expression_type/APPLY_EXPRESSION: done calling apply_type I.   [unify-and-generalize-types-g.pkg]";
																 if_debugging_prprint_typoid         ("\ncompute_expression_type/APPLY_EXPRESSION.4 [unify-and-generalize-types-g.pkg]: operator_type   prprint == ", operator_type);
																 if_debugging_prprint_typoid         ("\ncompute_expression_type/APPLY_EXPRESSION.4 [unify-and-generalize-types-g.pkg]: operand_type    prprint == ", operand_type);
																 if_debugging_prprint_typoid         ("\ncompute_expression_type/APPLY_EXPRESSION.4 [unify-and-generalize-types-g.pkg]: expression_type prprint == ", expression_type);

					    ( expression,
					      expression_type
					    );
					}
					except
					    uyt::UNIFY_TYPES  mode
					    =
					    {
																 if_debugging_say "\ncompute_expression_type/APPLY_EXPRESSION: done calling apply_type II.   [unify-and-generalize-types-g.pkg]";
						operator_type   = tyj::drop_resolved_typevars  operator_type;

						reduced_operator_type =   tyj::head_reduce_typoid  operator_type;

						uty::reset_unparse_type ();

						if (mtt::is_arrow_type (reduced_operator_type))
						   #
						   error_function source_code_region err::ERROR
						       (message("Operator and operand do not agree", mode))
						       (fn pp
							   =
							   {   pp.newline();
							       pp.lit "operator domain: ";
							       unparse_typoid pp  (mtt::domain reduced_operator_type);
							       pp.newline();

							       pp.lit "operand:         ";
							       unparse_typoid pp  operand_type;
							       pp.newline();

							       pp.lit "in expression:";
							       pp::break pp { blanks=>1, indent_on_wrap=>2 };
							       unparse_expression pp (given_expression, *print_depth);
							   }
						       );

						    (given_expression, tdt::WILDCARD_TYPOID);

						else
						    error_function source_code_region err::ERROR
							(message("operator is not a function", mode))
							(fn pp
							    =
							    { pp.newline();
							      pp.lit "operator: ";
							      unparse_typoid pp (operator_type);			pp.newline();
							      pp.lit "in expression:";
							      pp::break pp { blanks=>1, indent_on_wrap=>2 };
							      unparse_expression pp (given_expression,*print_depth);
							    }
							);

						    (given_expression, tdt::WILDCARD_TYPOID);
						fi;
					    };
				    };

				ds::TYPE_CONSTRAINT_EXPRESSION (expression, constraining_type)
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/ds::TYPE_CONSTRAINT_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					my  (expression, expression_type)
					    =
					    compute_expression_type
					      ( expression,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/ds::TYPE_CONSTRAINT_EXPRESSION" ! callstack
					      );

																 if_debugging_say "\ncompute_expression_type/TYPE_CONSTRAINT_EXPRESSION: calling unify_types_and_handle_errors.   [unify-and-generalize-types-g.pkg]";
					if (unify_types_and_handle_errors
						{
						  typoid1 => expression_type,    name1 => "expression",
						  typoid2 => constraining_type,  name2 => "constraint",

						  message => "expression doesn't match constraint",
						  source_code_region,

						  unparse_phrase =>  unparse_expression,
						  phrase_name    => "expression",
						  phrase         =>  given_expression,

						  callstack      => "compute_expression_type/ds::TYPE_CONSTRAINT_EXPRESSION(2)" ! callstack
						}
					   )

																 if_debugging_say "\ncompute_expression_type/TYPE_CONSTRAINT_EXPRESSION: done calling unify_types_and_handle_errors (succeeded).   [unify-and-generalize-types-g.pkg]";
					    (ds::TYPE_CONSTRAINT_EXPRESSION (expression, constraining_type), constraining_type);
					else
																 if_debugging_say "\ncompute_expression_type/TYPE_CONSTRAINT_EXPRESSION: done calling unify_types_and_handle_errors (failed).   [unify-and-generalize-types-g.pkg]";
					    (given_expression, tdt::WILDCARD_TYPOID);
					fi;
				    };

				ds::EXCEPT_EXPRESSION (expression, (rules, _))
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/EXCEPT_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					my (expression, expression_type)
					    =
					    compute_expression_type
					      ( expression,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/EXCEPT_EXPRESSION" ! callstack
					      );

					my (rules, rule_pattern_type, exception_handler_type)
					    =
					    compute_match_type
					      ( rules,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/EXCEPT_EXPRESSION(2)" ! callstack
					      );

					expression =  ds::EXCEPT_EXPRESSION (expression, (rules, rule_pattern_type));

																 if_debugging_say "\ncompute_expression_type/EXCEPT_EXPRESSION: above call to unify_types   [unify-and-generalize-types-g.pkg]";
					{   uyt::unify_types ( "exception_handler_type",    "exception_typoid --> expression_type",            
							       exception_handler_type,  mtt::exception_typoid --> expression_type,
							      ["compute_expression_type/EXCEPT_EXPRESSION"]
							    );

					    (expression, expression_type);
					}
					except uyt::UNIFY_TYPES  mode
					    =
					    {
															      if_debugging_say "\ncompute_expression_type/EXCEPT_EXPRESSION: above second call to unify_types   [unify-and-generalize-types-g.pkg]";
						if (unify_types_and_handle_errors
							{
							  typoid1 => mtt::domain (tyj::drop_resolved_typevars  exception_handler_type),   name1 => "handler domain",
							  typoid2 => mtt::exception_typoid,                                               name2 => "",

							  message => "handler domain is not exception",
							  source_code_region,

							  unparse_phrase =>  unparse_expression,
							  phrase_name    => "expression",
							  phrase         =>  given_expression,

							  callstack      => "compute_expression_type/EXCEPT_EXPRESSION(3)" ! callstack
						        }
						   )

															      if_debugging_say "\ncompute_expression_type/EXCEPT_EXPRESSION: above third call to unify_types   [unify-and-generalize-types-g.pkg]";
						    unify_types_and_handle_errors
						      {
							typoid1 => expression_type,                                                  name1 => "body",
							typoid2 => mtt::range (tyj::drop_resolved_typevars exception_handler_type),  name2 => "handler range",

							message => "expression and handler don't agree",
							source_code_region,

							unparse_phrase =>  unparse_expression,
							phrase_name    => "expression",
							phrase         =>  given_expression,

							callstack      => "compute_expression_type/EXCEPT_EXPRESSION(4)" ! callstack
						      };
						else
						    FALSE;
						fi;

						( given_expression,
						  tdt::WILDCARD_TYPOID
						);
					    };
				    };

				ds::RAISE_EXPRESSION (expression, _)
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/RAISE_EXPRESSION:  TOP: calling compute_expression_type.   [unify-and-generalize-types-g.pkg]";
					my (expression, expression_type)
					    =
					    compute_expression_type
					      ( expression,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/RAISE_EXPRESSION" ! callstack
					      );

																 if_debugging_say "\ncompute_expression_type/RAISE_EXPRESSION:  BBB: back from compute_expression_type.   [unify-and-generalize-types-g.pkg]";

																 if_debugging_say "\ncompute_expression_type/RAISE_EXPRESSION:  CCC: calling unify_types_and_handle_errors.   [unify-and-generalize-types-g.pkg]";
					# Verify that 'expression' has an exception type:
					#
					unify_types_and_handle_errors
					    {
					      typoid1 => expression_type,     name1 => "raised",
					      typoid2 => mtt::exception_typoid,  name2 => "",

					      message => "argument of raise is not an exception",
					      source_code_region,

					      unparse_phrase =>  unparse_expression,
					      phrase_name    => "expression",
					      phrase         =>  given_expression,

					      callstack      => "compute_expression_type/RAISE_EXPRESSION(2)" ! callstack
					    };

					# Now we discard all available type information
					# and return a totally unrestricted type
					# because the environment is free to consider
					# a 'raise' as returning whatever type it likes
					# -- since in fact 'raise' does not return at all:
					#
					fantasy_return_type
					    =
					    tyj::make_meta_typevar_and_type  (tdt::infinity, ["compute_expression_type/RAISE_EXPRESSION   from   unify-and-generalize-types-g.pkg"]);


																 if_debugging_say "\ncompute_expression_type/RAISE_EXPRESSION   [unify-and-generalize-types-g.pkg]:  BOT";
					(ds::RAISE_EXPRESSION (expression, fantasy_return_type), fantasy_return_type);
				    };

				ds::LET_EXPRESSION (declaration, expression)
				    => 
				    {
					# The type of a 'let' construct
					# depends entirely upon its terminal
					# 'expression':

																 if_debugging_say "\ncompute_expression_type/LET_EXPRESSION:  TOP: calling do_declaration on LET_EXPRESSION.d.    [unify-and-generalize-types-g.pkg]";
					declaration =   do_declaration
							  ( declaration,
							    enter_let_scope (syntax_treewalk_lexical_context),
							    source_code_region,
							    "compute_expression_type/LET_EXPRESSION" ! callstack
							  );
																 if_debugging_say "\ncompute_expression_type/LET_EXPRESSION:  done calling do_declaration on LET_EXPRESSION.d.    [unify-and-generalize-types-g.pkg]";
																 if_debugging_say "\ncompute_expression_type/LET_EXPRESSION:  BBB: calling compute_expression_type on LET_EXPRESSION.e.    [unify-and-generalize-types-g.pkg]";
					my (expression, expression_type)
					    =
					    compute_expression_type
					      ( expression,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/LET_EXPRESSION(2)" ! callstack
					      );
																 if_debugging_say "\ncompute_expression_type/LET_EXPRESSION:  done calling compute_expression_type on LET_EXPRESSION.e.    [unify-and-generalize-types-g.pkg]";
																 if_debugging_say "\ncompute_expression_type/LET_EXPRESSION:  BOT.    [unify-and-generalize-types-g.pkg]";

					( ds::LET_EXPRESSION (declaration, expression),
					  expression_type
					);
				    };

				ds::CASE_EXPRESSION (expression, rules, is_match)
				    =>
				    {												 if_debugging_say "\ncompute_expression_type/CASE_EXPRESSION:  TOP.    [unify-and-generalize-types-g.pkg]";

					my (expression, expression_type) =  compute_expression_type (expression, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/CASE_EXPRESSION(2) " ! callstack);
					my (rules', _, rule_type)        =  compute_match_type      (rules,      syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/CASE_EXPRESSION(3) " ! callstack);

					expression = ds::CASE_EXPRESSION (expression, rules', is_match);

																 if_debugging_say "\ncompute_expression_type/CASE_EXPRESSION:  above call to apply_type.   [unify-and-generalize-types-g.pkg]";
					( expression,
					  apply_type (rule_type, expression_type)
					)
					except
					    uyt::UNIFY_TYPES  mode
						=
						{   if is_match
																  if_debugging_say "\ncompute_expression_type/CASE_EXPRESSION:  above call to unify_types_and_handle_errors.   [unify-and-generalize-types-g.pkg]";
							unify_types_and_handle_errors
							  {
							    typoid1 => mtt::domain rule_type,    name1 => "rule domain",
							    typoid2 => expression_type,   name2 => "object",

							    message=>"case object and rules don't agree",
							    source_code_region,

							    unparse_phrase =>  unparse_expression,
							    phrase_name    => "expression",
							    phrase         =>  given_expression,

							    callstack      => "compute_expression_type/CASE_EXPRESSION(4)" ! callstack
							  };
						    else 
							declaration =   case rules 
									    #	
									    (ds::CASE_RULE (pattern, _)) ! _
										=> 
										ds::NAMED_VALUE
										  {
										    pattern,
										    expression		=>  given_expression,
										    raw_typevars	=>  REF [],
										    generalized_typevars	=>  []
										  };

									    _ => bug "unexpected rule list 456";
									esac;

																  if_debugging_say "\ncompute_expression_type/CASE_EXPRESSION:  above second call to unify_types_and_handle_errors.   [unify-and-generalize-types-g.pkg]";
							unify_types_and_handle_errors
							  {
							    typoid1 => mtt::domain rule_type,   name1 => "pattern",
							    typoid2 => expression_type,        name2 => "expression",

							    message => "pattern and expression in my declaration don't agree",
							    source_code_region,

							    unparse_phrase =>  unparse_named_value,
							    phrase_name    => "declaration",
							    phrase         =>  declaration,

							    callstack      => "compute_expression_type/CASE_EXPRESSION(5)" ! callstack
							  };
						    fi;

																  if_debugging_say "\ncompute_expression_type/CASE_EXPRESSION:  BOT.   [unify-and-generalize-types-g.pkg]";
						    ( given_expression,
						      tdt::WILDCARD_TYPOID
						    );
						};
				    };
					#######################################################
					# This causes 'case' to behave differently from 'let'
					#	-- bound variables do not have generic types.
					#######################################################

				ds::IF_EXPRESSION { test_case, then_case, else_case }
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/IF_EXPRESSION:  TOP.";

					my (test_case', tty) = compute_expression_type (test_case, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/ds::IF_EXPRESSION(1) " ! callstack);
					my (then_case', tct) = compute_expression_type (then_case, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/ds::IF_EXPRESSION(2) " ! callstack);
					my (else_case', ect) = compute_expression_type (else_case, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/ds::IF_EXPRESSION(3) " ! callstack);

					if
					(  bool_unify_err
					       { type  => tty,
						 name    => "test expression",
						 message => "test expression in if is not of type bool"
					       }
					and
					   unify_types_and_handle_errors
					       {
						 typoid1 => tct,   name1 => "then branch",
						 typoid2 => ect,   name2 => "else branch",

						 message  => "types of if branches do not agree",
						 source_code_region,

						 unparse_phrase =>  unparse_expression,
						 phrase_name    => "expression",
						 phrase         =>  given_expression,

						 callstack          => "compute_expression_type/ds::IF_EXPRESSION(4)" ! callstack
					       }
					)

																 if_debugging_say "\ncompute_expression_type/IF_EXPRESSION:  success.   [unify-and-generalize-types-g.pkg]";
					    ( ds::IF_EXPRESSION { test_case => test_case',
								  then_case => then_case',
								  else_case => else_case'
								},
					      tct
					    );
					else
																 if_debugging_say "\ncompute_expression_type/IF_EXPRESSION:  failure.   [unify-and-generalize-types-g.pkg]";
					    ( given_expression,
					      tdt::WILDCARD_TYPOID
					    );
					fi;
				    };

				ds::AND_EXPRESSION (expression1, expression2)
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/ds::AND_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					short_circuit_and_or (ds::AND_EXPRESSION, "and", expression1, expression2);
				    };

				ds::OR_EXPRESSION (expression1, expression2)
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/OR_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					short_circuit_and_or (ds::OR_EXPRESSION, "or", expression1, expression2);
				    };

				ds::WHILE_EXPRESSION { test, expression }
				    =>
				    {
																 if_debugging_say "\ncompute_expression_type/ds::WHILE_EXPRESSION.   [unify-and-generalize-types-g.pkg]";

					my (test', testtype) = compute_expression_type (test,       syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/ds::WHILE_EXPRESSION(1) " ! callstack);
					my (expression', _)  = compute_expression_type (expression, syntax_treewalk_lexical_context, source_code_region, "compute_expression_type/ds::WHILE_EXPRESSION(2) " ! callstack);

					if (bool_unify_err
						{ type  => testtype,
						  name    => "test expression",
						  message => "test expression in while is not of type bool"
						}
					   )

					    (ds::WHILE_EXPRESSION { test => test', expression => expression' }, mtt::void_typoid);
					else
					    (expression, tdt::WILDCARD_TYPOID);
					fi;
				    };

				ds::FN_EXPRESSION (rules, _)
				    => 
				    {
																 if_debugging_say "\ncompute_expression_type/ds::FN_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					my (rules, rule_pattern_type, rule_type)
					    =
					    compute_match_type
					      ( rules,
						syntax_treewalk_lexical_context,
						source_code_region,
						"compute_expression_type/ds::FN_EXPRESSION" ! callstack
					      );

					(   ds::FN_EXPRESSION (rules, rule_pattern_type),
					    rule_type
					);
				    };

				ds::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, source_code_region)
				    => 
				    {
																 if_debugging_say "\ncompute_expression_type/ds::SOURCE_CODE_REGION_FOR_EXPRESSION.   [unify-and-generalize-types-g.pkg]";
					my (expression, expression_type)
					    =
					    compute_expression_type
					      ( expression,
						syntax_treewalk_lexical_context,
						source_code_region,
						callstack
					      );

					( ds::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, source_code_region),
					  expression_type
					);
				    };

				_ => bug "exptype -- bad expression";
			    esac;
			}													# fun compute_expression_type

		    also
		    fun compute_rule_type (ds::CASE_RULE (pattern, expression), syntax_treewalk_lexical_context, source_code_region, callstack)
			=  
			{
																if *debugging print_callstack "\ncompute_rule_type/TOP [unify-and-generalize-types-g.pkg]" callstack; fi;
																if *debugging printf "compute_rule_type/TOP [unify-and-generalize-types-g.pkg]: incrementing lex.fn_nesting from %d to %d\n"
																		     (syntax_treewalk_lexical_context.fn_nesting) (syntax_treewalk_lexical_context.fn_nesting + 1); fi;
			    syntax_treewalk_lexical_context     =  enter_fn_scope  syntax_treewalk_lexical_context;

			    my   (pattern, pattern_type)        =  compute_pattern_type    (pattern,    syntax_treewalk_lexical_context.fn_nesting,  source_code_region, "compute_rule_type(1)" ! callstack); if_debugging_say "\ncompute_rule_type calling compute_expression_type.   [unify-and-generalize-types-g.pkg]";
			    my   (expression, expression_type)  =  compute_expression_type (expression, syntax_treewalk_lexical_context,             source_code_region, "compute_rule_type(2)" ! callstack);

																if_debugging_say "\ncompute_rule_type done calling compute_expression_type.   [unify-and-generalize-types-g.pkg]";

																if *debugging print_callstack "\ncompute_rule_type/BOTTOM [unify-and-generalize-types-g.pkg]" callstack; fi;
																if *debugging printf "compute_rule_type/BOTTOM [unify-and-generalize-types-g.pkg]: returning from lex.fn_nesting %d to %d\n"
																			(syntax_treewalk_lexical_context.fn_nesting) (syntax_treewalk_lexical_context.fn_nesting - 1); fi;
			    ( ds::CASE_RULE (pattern, expression),
			      pattern_type,
			      pattern_type --> expression_type
			    );
			}

		    also
		    fun compute_match_type (rules, syntax_treewalk_lexical_context, source_code_region, callstack)
			=
			case rules
			    #
			    [] => bug "empty rule list in typecheck::compute_match_type";

			    [rule]
				=> 
				{   my (rule0, argt, rule_type)
					=
					compute_rule_type
					  ( rule,
					    syntax_treewalk_lexical_context,
					    source_code_region,
					    "compute_match_type(1)" ! callstack
					  );

				    ( [rule0],
				      argt,
				      rule_type
				    );
				};

			    rule ! rest
				=>
				{   my (rule0, argt, rule_type)
					=
					compute_rule_type
					  ( rule,
					    syntax_treewalk_lexical_context,
					    source_code_region,
					    "compute_match_type(2)" ! callstack
					  );
				    #
				    fun unify_with_rule0 rule
					=
					{   my (rule', argt', rule_type')
						=
						compute_rule_type
						  ( rule,
						    syntax_treewalk_lexical_context,
						    source_code_region,
						    "compute_match_type(3)" ! callstack
						  );

																if_debugging_say "\ncompute_match_type: unify_with_rule0: calling unify_types_and_handle_errors.   [unify-and-generalize-types-g.pkg]";
					    unify_types_and_handle_errors
					      {
						typoid1 => rule_type,  name1 => "earlier rule (s)",
						typoid2 => rule_type', name2 => "this rule",

						message => "types of rules don't agree",
						source_code_region,

						unparse_phrase =>  unparse_rule,
						phrase_name    => "rule",
						phrase         =>  rule,

						callstack      => "compute_match_type(4)" ! callstack
					      };

					    rule';
					};

				    ( rule0 ! (map  unify_with_rule0  rest),
				      argt,
				      rule_type
				    );
				};
			esac

		    also
		    fun do_declaration
			( given_declaration:			ds::Declaration,
			  syntax_treewalk_lexical_context:	Syntax_Treewalk_Lexical_Context,
			  source_code_region:			ds::Source_Code_Region,
			  callstack:				List(String)				# Debug support.
			)
			:
			ds::Declaration
			=
			{
																if *debugging   print_callstack "\ndo_declaration/TOP   [unify-and-generalize-types-g.pkg]" callstack; fi;
																if_debugging_unparse_declaration     ("\ndo_declaration/TOP given_declaration unparse     is:    [unify-and-generalize-types-g.pkg]\n", given_declaration);
																if_debugging_prettyprint_declaration ("\ndo_declaration/TOP given_declaration prettyprint is:    [unify-and-generalize-types-g.pkg]\n", (given_declaration,100));

			    case given_declaration
				#
				ds::VALUE_DECLARATIONS named_values
				    =>
				    {
																if_debugging_say "\ndo_declaration/VALUE_DECLARATIONS   [unify-and-generalize-types-g.pkg]\n";
					declaration
					    =
					    ds::VALUE_DECLARATIONS (map  do_named_value  named_values)
					    where
						fun  do_named_value
						     (  named_value
							as
							ds::NAMED_VALUE
							  {
							    pattern	     =>  given_pattern,
							    expression	     =>  given_expression,
							    raw_typevars,
							    generalized_typevars =>  generalized_typevars'			# Ignored.  Always [] at this point, I think.
							  }
						     )
						    =
						    {
																if_debugging_say "\ndo_declaration/VALUE_DECLARATIONS/do_named_value   [unify-and-generalize-types-g.pkg]\n";
																#
																if_debugging_unparse_pattern        ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  pattern unparse == \n",    (given_pattern,   100));
																if_debugging_prettyprint_pattern    ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  pattern prprint == \n",    (given_pattern,   100));
																#
																if_debugging_unparse_expression     ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  expression unparse == \n", (given_expression,100));
																if_debugging_prettyprint_expression ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  expression prprint == \n", (given_expression,100));

							my (pattern,    pattern_type   ) = compute_pattern_type    (given_pattern,        tdt::infinity,                   source_code_region, "do_declaration/VALUE_DECLARATIONS/do_named_value(1) " ! callstack);
							my (expression, expression_type) = compute_expression_type (given_expression,     syntax_treewalk_lexical_context, source_code_region, "do_declaration/VALUE_DECLARATIONS/do_named_value(2) " ! callstack);

																if_debugging_say "\ndo_declaration/VALUE_DECLARATIONS/do_named_value done calling compute_expression_type.   [unify-and-generalize-types-g.pkg]";
																#
																if_debugging_unparse_pattern        ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  pattern unparse == \n", (pattern,100));
																if_debugging_prettyprint_pattern    ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  pattern prettyprint == \n", (pattern,100));
																#
																if_debugging_unparse_expression     ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  expression unparse == \n", (expression,100));
																if_debugging_prettyprint_expression ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  expression prettyprint == \n", (expression,100));
																#
																if_debugging_unparse_typoid     ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  pattern type unparse == \n", pattern_type);
																if_debugging_prprint_typoid     ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  pattern type prprint == \n", pattern_type);
																if_debugging_unparse_typoid     ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  expression type unparse == \n", expression_type);
																if_debugging_prprint_typoid     ("\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  expression type prprint == \n", expression_type);

							generalize =  is_value  given_expression;				#  or is_variable_typoid expression_type
																#
																# This is the sole call to   tyj::is_value(),
																# the fn implementing the "value restriction" test:

																if *debugging
																    say "\n=========================== do_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  is_value() reports that:\n";
																    say (sprintf "Following expression %s a value:\n" (generalize ?? "IS" :: "is NOT"));
																    if_debugging_unparse_expression ("", (given_expression,100));
																fi;	

																if_debugging_say "\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  calling unify_types_and_handle_errors on pattern + expression\n";
							unify_types_and_handle_errors
							    {
							      name1 => "pattern",	typoid1 => pattern_type,
							      name2 => "expression",	typoid2 => expression_type,

							      message => "Pattern and expression in 'my' declaration do not agree",

							      source_code_region,

							      unparse_phrase =>  unparse_named_value,
							      phrase_name    => "declaration",
							      phrase         =>  named_value,

							      callstack      =>  "do_declaration/VALUE_DECLARATIONS/do_named_value(3)" ! callstack
							    };
																if_debugging_say "\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  done calling unify_types_and_handle_errors on pattern + expression\n";
																if_debugging_say "\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]:  calling generalize_pattern\n";
							generalized_typevars
							    =
							    generalize_pattern							# This is the only call to generalize_pattern().
							      (
								given_pattern,							# <------------ SHOULD THIS BE pattern INSTEAD? (Empirically, seems to make no difference.)
							       *raw_typevars,							# Type variables explicitly specified by user:  X, Y, ... 
								syntax_treewalk_lexical_context,
								generalize,
								source_code_region,
								"do_declaration/VALUE_DECLARATIONS/do_named_value(4)" ! callstack
							      );

																if *debugging
																    printf "\ndo_declaration/VALUE_DECLARATIONS/do_named_value [unify-and-generalize-types-g.pkg]: Creating NAMED_VALUE node with %d (was %d) generalized_typevars: \n"
																    (list::length generalized_typevars) (list::length generalized_typevars');
																    printf "\nNAMED_VALUE.generalized_typevars [unify-and-generalize-types-g.pkg]: (%d)\n" (list::length generalized_typevars);
																    apply  unparse_typevar_ref  generalized_typevars
																	where
/* */																	    fun unparse_typevar_ref  typevar_ref
																		=
																		if_debugging_unparse_typevar_ref ("", typevar_ref);
																	end;
																    printf "\n";
																    if_debugging_unparse_pattern        ("\n[unify-and-generalize-types-g.pkg]  NAMED_VALUE.pattern == \n", (pattern,100));
																    if_debugging_unparse_expression     ("\n[unify-and-generalize-types-g.pkg]  NAMED_VALUE.expression == \n", (expression,100));
																    if_debugging_prettyprint_pattern    ("\n[unify-and-generalize-types-g.pkg]  NAMED_VALUE.pattern    prettyprint == \n", (pattern,   100));
																    if_debugging_prettyprint_expression ("\n[unify-and-generalize-types-g.pkg]  NAMED_VALUE.expression prettyprint == \n", (expression,100));
																fi;
							ds::NAMED_VALUE
							  {
							    pattern,
							    expression,
							    raw_typevars,
							    generalized_typevars
							  };
						    };										# fun do_named_value
					    end;										# where
																if *debugging       print_callstack   "\ndo_declaration/VALUE_DECLARATIONS/BOTTOM [unify-and-generalize-types-g.pkg]" callstack; fi;
																if_debugging_unparse_declaration     ("\ndo_declaration/VALUE_DECLARATIONS/BOTTOM [unify-and-generalize-types-g.pkg]:  final result unparse     is:\n", declaration);
																if_debugging_prettyprint_declaration ("\ndo_declaration/VALUE_DECLARATIONS/BOTTOM [unify-and-generalize-types-g.pkg]:  final result prettyprint is:\n", (declaration,100));

					declaration;
				    };

			        ds::RECURSIVE_VALUE_DECLARATIONS  named_recursive_values_records				# "rvbs" == "recursive value bindings".
				    =>
				    {
																if *debugging print_callstack "do_declaration/RECURSIVE_VALUE_DECLARATIONS/TOP  [unify-and-generalize-types-g.pkg]" callstack; fi;
																if *debugging printf "do_declaration/RECURSIVE_VALUE_DECLARATIONS/TOP:  incrementing lex.fn_nesting from %d to %d  [unify-and-generalize-types-g.pkg]\n"
																		     (syntax_treewalk_lexical_context.fn_nesting) (syntax_treewalk_lexical_context.fn_nesting + 1);
																fi;
					previous_syntax_treewalk_lexical_context
					    =
					    syntax_treewalk_lexical_context;

					syntax_treewalk_lexical_context
					    =
					    enter_fn_scope   syntax_treewalk_lexical_context;

																if_debugging_say "\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS: TOP   [unify-and-generalize-types-g.pkg]";
					# A general RECURSIVE_VALUE_DECLARATIONS statement
					# represents mutually recursive functions each of
					# which may have multiple rules:
					#
					#    fun foo ... => ...;
					#        foo ... => ...;
					#    end
					#    also
					#    fun bar ... => ...;
					#        bar ... => ...;
					#    end;
					#
					# Here each function (foo, bar ...) will be
					# represented by one NAMED_RECURSIVE_VALUE
					# record, where NAMED_RECURSIVE_VALUE/expression
					# will be a ds::FN_EXPRESSION (rules, _) with 'rules'
					# having one CASE_PATTERN (pattern, expression) clause
					# per pattern => expression rule.  Schematically:
					#
					#     RECURSIVE_VALUE_DECLARATIONS [
					#         NAMED_RECURSIVE_VALUE.expression		# Represents all of 'foo'.
					#             =>
					#             ds::FN_EXPRESSION.rules
					#                 =>
					#                 [ CASE_PATTERN( pattern, expression )	# One  foo ... => ... ;   rule.
					#                   ...
					#                 ]
					#         ...
					#     ]
					#
					# We use a two-pass algorithm here in which the first
					# pass typecheckes the left-hand sides of all the rules
					# and the second pass typechecks the right-hand sides.


					# First pass: rule patterns and types.
					# As we go, we accumulate the second-pass
					# worklist in "expression_thunks':
					#	
					rvbs_expression_thunk_pairs
					    =
					    map  do_one_function  named_recursive_values_records
					    where
						# Process one NAMED_RECURSIVE_VALUE record,
						# which is to say all the rules for one function.
						#	
						# We derive a first-pass approximation to the
						# function's type by unifying together domain
						# types computed from the rule left hand sides
						# (the patterns) together with any explicitly
						# declared type constraints on the rule's range
						# (result) type.
						#
						# We save the result in
						#     NAMED_RECURSIVE_VALUE /
						#     ORDINARY_VARIBLE /
						#     var_type.
						#
						# During this first pass we do not compute any range
						# range types from the actual rule right hand sides
						# (the function bodies) -- we leave that for the
						# second pass.
						#
						fun do_one_function
						      ( named_recursive_values
							    as
							    ds::NAMED_RECURSIVE_VALUE
							      {
								variable =>  variable as  vac::PLAIN_VARIABLE { var_type, ... },
								expression,
								raw_typevars,
								generalized_typevars,
								null_or_type
							      }
						      )
							=> 
							{
							    result =  ( ds::NAMED_RECURSIVE_VALUE
									  {
									    variable,
									    expression,
									    raw_typevars,
									    generalized_typevars =>  *bound_typevar_refs_accumulator,
									    null_or_type
									  },
									expression_thunk
								      );

																if (*debugging and ((list::length *bound_typevar_refs_accumulator) > 0))
																if *debugging print_callstack "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function/TOP [unify-and-generalize-types-g.pkg]" callstack; fi;
																printf "\nCreating NAMED_RECURSIVE_VALUE with %d-entry generalized_typevars list do_one_function  in do_declaration/RECURSIVE_VALUE_DECLARATIONS  in  unify-and-generalize-types-g.pkg\n"
																	(list::length *bound_typevar_refs_accumulator);
																printf "\n[unify-and-generalize-types-g.pkg]  NAMED_RECURSIVE_VALUES.generalized_typevars: (%d) (was %d)\n" (list::length *bound_typevar_refs_accumulator) (list::length generalized_typevars);
																apply  unparse_typevar_ref  *bound_typevar_refs_accumulator
																where
																fun unparse_typevar_ref  typevar_ref
																=
																if_debugging_unparse_typevar_ref ("", typevar_ref);
																end;
																printf "\n";
																if_debugging_unparse_expression ("\n[unify-and-generalize-types-g.pkg]  NAMED_RECURSIVE_VALUES.expression == \n", (expression,100));
																if_debugging_prettyprint_expression ("\n[unify-and-generalize-types-g.pkg]  NAMED_RECURSVIE_VALUES.expression prettyprint == \n", (expression,100));
																fi;
							     result;
							}
							where

							    # We compute the function type by starting with
							    # an initially completely unconstrained arrow
							    # (function) type and then unifying it with all
							    # available type information.  The eventual result
							    # is a type which is as general as possible while
							    # still being consistent with all known type
							    # constraints/information.
							    #
							    # We use 'function_type_so_far' as our type result
							    # accumulator:
							    #
							    domain_type =   tyj::make_meta_typevar_and_type  (syntax_treewalk_lexical_context.fn_nesting, ["domain_type in do_one_function  in do_declaration/RECURSIVE_VALUE_DECLARATIONS  from  unify-and-generalize-types-g.pkg"]);
							    range_type  =   tyj::make_meta_typevar_and_type  (syntax_treewalk_lexical_context.fn_nesting, ["range_type  in do_one_function  in do_declaration/RECURSIVE_VALUE_DECLARATIONS  from  unify-and-generalize-types-g.pkg"]);
							    #
							    function_type_so_far =   domain_type --> range_type;


							    # If the user explicitly declared a type for the
							    # function, fold that type information into our
							    # 'function_type_so_far' accumulator:
							    #
							    case null_or_type 
								#
								NULL => TRUE;

								THE declared_function_type
								    =>
								    {
																if_debugging_say "\ndo_one_function: calling unify_types_and_handle_errors in do_declaration/RECURSIVE_VALUE_DECLARATIONS  from  unify-and-generalize-types-g.pkg\n";
									unify_types_and_handle_errors
									    {
									      typoid1 => function_type_so_far,    name1 => "",
									      typoid2 => declared_function_type,  name2 => "constraint",

									      message => "type constraint of my rec declaration\
										     \ is not a function type",
									      source_code_region,

									      unparse_phrase =>  unparse_recursively_named_value,
									      phrase_name    => "declaration",
									      phrase         =>  named_recursive_values,

									      callstack      => "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function(1)" ! callstack
									    };
								    };
							    esac;

							    # As we generalize rule patterns we
							    # generate additional bound type variables,
							    # which we will accumulate here:
							    #
							    bound_typevar_refs_accumulator
								=
								REF generalized_typevars;		# Always [] at this point.

							    my (expression, expression_thunk)
								=
								f (expression, source_code_region, function_type_so_far)
								where
								    fun f (ds::FN_EXPRESSION (rules, fn_type), source_code_region, function_type_so_far)
									    =>
									    {
										stipulate

										    pattern_type_expression_triples
											=
											map  approximate_rule_type  rules
											where
											    fun approximate_rule_type (ds::CASE_RULE (pattern, expression))
												=
												{   my (pattern, pattern_type)
													=
													compute_pattern_type
													  (
													    pattern,
													   *generalize_mutually_recursive_functions		# See Note[1].
														??  tdt::infinity
														::  syntax_treewalk_lexical_context.fn_nesting,
													    source_code_region,
													    "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function(2)" ! callstack
													  );

												    case expression
													#
													ds::TYPE_CONSTRAINT_EXPRESSION (expression, range_type_constraint)
													    =>
													    ( pattern,
													      pattern_type --> range_type_constraint,
													      (expression, source_code_region)
													    );

													expression
													    =>
													    ( pattern,
													      pattern_type --> range_type,
													      (expression, source_code_region)
													    );
												    esac;
												};
											end; 

										herein

										    rule_patterns    =  map  #1  pattern_type_expression_triples;
										    rule_types       =  map  #2  pattern_type_expression_triples;
										    rule_expressions =  map  #3  pattern_type_expression_triples;

										end;

										apply   unify_rule_type_with_function_type_so_far  rule_types
										where
										    fun unify_rule_type_with_function_type_so_far  rule_type
											=
											{
														if_debugging_say "\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS [unify-and-generalize-types-g.pkg]: do_one_function: unify_rule_type_with_function_type_so_far:  calling unify_types_and_handle_errors\n";
											    unify_types_and_handle_errors
											      {
												typoid1 => rule_type,              name1 => "this clause",
												typoid2 => function_type_so_far,   name2 => "previous clauses",

												message => "parameter or result constraints\
													   \ of clauses don't agree",

												source_code_region,

												unparse_phrase => unparse_recursively_named_value,
												phrase_name    => "declaration",
												phrase         => named_recursive_values,

												callstack      => "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function(3)" ! callstack
											      };

											    ();
											};
										end;

										var_type := function_type_so_far;

										# Added 2009-04-25 CrT: See Note[1] at bottom of tile.
										#		
# src/lib/compiler/front/typer/types/unify-and-generalize-types-g.pkg: generalize_type'/USER_TYPEVAR: X fn_nesting 1 syntax_treewalk_lexical_context.fn_nesting  1
# src/lib/thread-kit/src/core-thread-kit/event.pkg:151.9-172.3 Error: Explicit type variable cannot be generalized at its declaration point: X
# src/lib/compiler/front/typer/types/unify-and-generalize-types-g.pkg: generalize_type'/USER_TYPEVAR: X fn_nesting 1 syntax_treewalk_lexical_context.fn_nesting  1
# src/lib/thread-kit/src/core-thread-kit/event.pkg:357.9-374.3 Error: Explicit type variable cannot be generalized at its declaration point: X
										if	*generalize_mutually_recursive_functions
										    apply  generalize_rule_pattern  rule_patterns
										    where
											fun generalize_rule_pattern  pattern
											    =
											    {
																if *debugging
																printf "\n========== do_one_function CALLING generalize_pattern ==========  in do_declaration/RECURSIVE_VALUE_DECLARATIONS because *generalize_mutually_recursive_functions is TRUE.\n";
																printf   "vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv\n";
																fi;
												added_typevars
												    =
												    generalize_pattern
												      (
													pattern,
												       *raw_typevars,				# Type variables explicitly specified by user:  X, Y, ... 
													previous_syntax_treewalk_lexical_context,
													TRUE,						# "generalize"
													source_code_region,
												       "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function(4)" ! callstack
												      );

																if *debugging
																    printf "\n^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^\n";
																    printf   "========== do_one_function CALLED  generalize_pattern ==========  in do_declaration/RECURSIVE_VALUE_DECLARATIONS   [unify-and-generalize-types-g.pkg]\n";
																    say ("\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS: do_one_function: added_typevars:   [unify-and-generalize-types-g.pkg]\n");
																    apply  unparse_typevar_ref  added_typevars
																    where
/* */																	fun unparse_typevar_ref  typevar_ref
																	    =
																	    if_debugging_unparse_typevar_ref ("", typevar_ref);
																    end;
																fi;
												bound_typevar_refs_accumulator
												    :=
												    added_typevars
												    @
												  *bound_typevar_refs_accumulator;
											    };
										    end;
																if *debugging
																say ("\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS [unify-and-generalize-types-g.pkg]: do_one_function: FINAL bound_typevar_refs_accumulator: \n");
																apply  unparse_typevar_ref  *bound_typevar_refs_accumulator
																where
/* */																fun unparse_typevar_ref  typevar_ref
																=
																if_debugging_unparse_typevar_ref ("", typevar_ref);
																end;
																fi;
										fi;

										expression_thunk
										    =
										    fn ()
											=
											ds::FN_EXPRESSION
											    ( paired_lists::map
												  ds::CASE_RULE
												  ( rule_patterns,
												    map  unify_expression_with_range_type  rule_expressions
												  ),

											      mtt::domain (tyj::drop_resolved_typevars (function_type_so_far))
											    )
											where
											    fun unify_expression_with_range_type (expression, source_code_region)
												=
												{
																if_debugging_say "\ncalling compute_expression_type [unify-and-generalize-types-g.pkg]: unify_expression_with_range_type() in \
																\f() in do_one_function() in RECURSIVE_VALUE_DECLARATIONS in do_declaration() \
																\in unify-and-generalize-types-g.pkg";
												    my (expression, expression_type)
													=
													compute_expression_type
													  (
													    expression,
													    syntax_treewalk_lexical_context,
													    source_code_region,
													    "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function(5)" ! callstack
													  );
																if_debugging_say "\nback from compute_expression_type [unify-and-generalize-types-g.pkg]:  unify_expression_with_range_type() in f() in do_one_function() in \
																		    \RECURSIVE_VALUE_DECLARATIONS in do_declaration() in unify-and-generalize-types-g.pkg";

																if_debugging_say "\ncalling unify_types_and_handle_errors:   unify_expression_with_range_type() in f() in \
																		    \do_one_function() in RECURSIVE_VALUE_DECLARATIONS in do_declaration() in unify-and-generalize-types-g.pkg";
												    unify_types_and_handle_errors {

													typoid1 => expression_type,   name1 => "expression",
													typoid2 => range_type,        name2 => "result type",

													message=>"right-hand-side of clause\
														 \ doesn't agree with function result type",

													source_code_region,

													unparse_phrase =>  unparse_recursively_named_value,
													phrase_name    => "declaration",
													phrase         =>  named_recursive_values,

													callstack      => "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function(6)" ! callstack
												      };
																if_debugging_say "\nback from unify_types_and_handle_errors:   unify_expression_with_range_type() in \
																			\f() in do_one_function() in RECURSIVE_VALUE_DECLARATIONS in do_declaration() in unify-and-generalize-types-g.pkg";

												    expression;
												};

											end; 

									       ( ds::FN_EXPRESSION (rules, fn_type),
										 expression_thunk
									       );
									    };

									f (ds::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, source_code_region), _, function_type_so_far)
									    => 
									    {   (f (expression, source_code_region, function_type_so_far))
										    ->
										    (expression, subthunk);

										expression_thunk
										    =
										    fn () =  ds::SOURCE_CODE_REGION_FOR_EXPRESSION (subthunk(), source_code_region);

										( ds::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, source_code_region),
										  expression_thunk
										);
									    };

									f (ds::TYPE_CONSTRAINT_EXPRESSION (expression, constraining_type), source_code_region, function_type_so_far)
									    =>
									    {
																if_debugging_say "\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS [unify-and-generalize-types-g.pkg]: do_one_function: f:  calling unify_types_and_handle_errors\n";
										unify_types_and_handle_errors
										    {
										      typoid1 => constraining_type,      name1 => "this constraint",
										      typoid2 => function_type_so_far,   name2 => "outer constraints",

										       message=>"type constraints on my rec\
												 \ declaraction disagree",
										       source_code_region,

										       unparse_phrase =>  unparse_recursively_named_value,
										       phrase_name    => "declaration",
										       phrase         =>  named_recursive_values,

										       callstack      => "do_declaration/RECURSIVE_VALUE_DECLARATIONS/do_one_function/f/ds::TYPE_CONSTRAINT_EXPRESSION" ! callstack
										    };
																if_debugging_say "\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS [unify-and-generalize-types-g.pkg]: do_one_function: f:  done calling unify_types_and_handle_errors\n";

										(f (expression, source_code_region, function_type_so_far))
										    ->
										    (expression, subthunk);

										expression_thunk
										    =
										    fn()  = ds::TYPE_CONSTRAINT_EXPRESSION (subthunk(), constraining_type);

										( ds::TYPE_CONSTRAINT_EXPRESSION (expression, constraining_type),
										  expression_thunk
										);
									    };

								       f _ => bug "typecheck.823";
								   end;							# fun f
							       end;							# where
						       end;								# where

						    do_one_function _ => bug "do_one_function";
						end;									# fun do_one_function
					    end;									# where (rvbs_expression_thunk_pairs)

															# paired_lists		is from   src/lib/std/src/paired-lists.pkg
					named_recursive_values_records	=  map  #1  rvbs_expression_thunk_pairs;
					expression_thunks		=  map  #2  rvbs_expression_thunk_pairs;

					# Second pass: type-check and update
					# the right-hand-side expressions
					# (function bodies):
					#
					named_recursive_values_records							# "rvb" == "recursive value binding"
					    =
					    paired_lists::map
						do_rvb_expression
						(named_recursive_values_records, expression_thunks)
					    where
						fun do_rvb_expression
						    (  ds::NAMED_RECURSIVE_VALUE { variable, null_or_type, raw_typevars, generalized_typevars, expression => _ },
						       expression_thunk
						    )
						    =
						    ds::NAMED_RECURSIVE_VALUE
						      {
							expression  =>  expression_thunk(),
							variable,
							raw_typevars,
							generalized_typevars,
							null_or_type
						      };
					    end;

					# 2009-05-14 CrT:	
					# The SML/NJ code comments here:
					#	
					#    "No need to generalize here, because every RECURSIVE_VALUE_DECLARATIONS is
					#     wrapped in a VALUE_DECLARATIONS, and the generalization occurs at the
					#     outer level.  Previously had:
					#	
					#         named_recursive_values_records
					#             =
					#             map  generalize_type
					#             named_recursive_values_records
					#	
					# The above code does not actually typecheck, but something similar
					# seems needed if mutually recursive functions are to be
					# typeagnostic ("polymorphic"), which I need to make OOP work decently,
					# so I've added a
					#	
					#     if *generalize_mutually_recursive_functions
					#	       apply  generalize_rule_pattern  rule_patterns
					#	
					# call above in:
					#	
					#     do_declaration()/RECURSIVE_VALUE_DECLARATIONS/do_one_function()
					#	
					# 2011-06-07 CrT: Robert Harper informs me that finding the most general
					#  polymorphic type for mutually recursive functions is a mathematically
					#  undecidable problem. (He says that Haskell requires explicit user-supplied
					#  types in such situations, and that SML should follow suit.)  This is probably
					#  why SML/NJ doesn't attempt this. *grin*  The Definition seems notably reticent on this front....
																if_debugging_say "\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS: calling typer_junk::convert_deep_syntax_named_recursive_values_list_to_deep_syntax_value_declarations_or_recursive_value_declarations [unify-and-generalize-types-g.pkg]\n";
					declaration
					    =
					    typer_junk::convert_deep_syntax_named_recursive_values_list_to_deep_syntax_value_declarations_or_recursive_value_declarations
						named_recursive_values_records;
																if *debugging       print_callstack "\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS/BOTTOM [unify-and-generalize-types-g.pkg] " callstack; fi;
																if *debugging       printf "\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS/BOTTOM [unify-and-generalize-types-g.pkg]:  returning from lex.fn_nesting %d to %d\n"
                                                                                                                                                            syntax_treewalk_lexical_context.fn_nesting (syntax_treewalk_lexical_context.fn_nesting - 1);
                                                                                                                                fi;
																if_debugging_unparse_declaration     ("\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS/BOTTOM [unify-and-generalize-types-g.pkg]:  final result unparse     is:\n", declaration);
																if_debugging_prettyprint_declaration ("\ndo_declaration/RECURSIVE_VALUE_DECLARATIONS/BOTTOM [unify-and-generalize-types-g.pkg]:  final result prettyprint is:\n", (declaration,100));

					declaration;
				    };				# RECURSIVE_VALUE_DECLARATIONS

				ds::EXCEPTION_DECLARATIONS  ebs
				    =>
				    {
																if_debugging_say "\ndo_declaration/EXCEPTION_DECLARATIONS/TOP   [unify-and-generalize-types-g.pkg]\n";
					if_debugging_say "\ndo_declaration: EXCEPTION_DECLARATIONS";

					if (syntax_treewalk_lexical_context.fn_nesting  < 1)
					    #
					    apply eb_type ebs
					    where
						fun check (tdt::TYPEVAR_REF { id, ref_typevar as REF (tdt::USER_TYPEVAR _) } )
							=> 
							error_function source_code_region err::ERROR
							    "type variable in top level exception type"
							    err::null_error_body;

						    check (tdt::TYPCON_TYPOID(_, args))
							=>
							apply check args;

						    check _ => ();
						end;
						#
						fun eb_type (ds::NAMED_EXCEPTION { exception_typoid => THE type, ... } ) =>   check type;
						    eb_type _ => ();
						end;
					    end;
					fi;

																if_debugging_say "\ndo_declaration/EXCEPTION_DECLARATIONS/BOT   [unify-and-generalize-types-g.pkg]\n";
					given_declaration;
				    };

				ds::LOCAL_DECLARATIONS (dec_in, dec_out)
				    =>
				    {
																if_debugging_say "\ndo_declaration/LOCAL_DECLARATIONS/TOP   [unify-and-generalize-types-g.pkg]\n";

					dec_in'  = do_declaration (dec_in,  enter_let_scope syntax_treewalk_lexical_context, source_code_region, "\ndo_declaration/LOCAL_DECLARATIONS(1) " ! callstack);
					dec_out' = do_declaration (dec_out,                 syntax_treewalk_lexical_context, source_code_region, "\ndo_declaration/LOCAL_DECLARATIONS(2) " ! callstack);

																if_debugging_say "\ndo_declaration: LOCAL_DECLARATION   [unify-and-generalize-types-g.pkg] ";
																if_debugging_say "\ndo_declaration/LOCAL_DECLARATIONS/BOT   [unify-and-generalize-types-g.pkg]\n";
					ds::LOCAL_DECLARATIONS (dec_in', dec_out');
				    };

				ds::SEQUENTIAL_DECLARATIONS (decls)
				    => 
				    {
																if_debugging_say "\ndo_declaration/SEQUENTIAL_DECLARATIONS/TOP   [unify-and-generalize-types-g.pkg]\n";

					result =    ds::SEQUENTIAL_DECLARATIONS
							(map (fn decl = do_declaration  ( decl,
											  syntax_treewalk_lexical_context,
											  source_code_region,
											  "do_declaration/SEQUENTIAL_DECLARATIONS" ! callstack
											)
							     )
							     decls
							);

																if_debugging_say "\ndo_declaration/SEQUENTIAL_DECLARATIONS/BOT   [unify-and-generalize-types-g.pkg]\n";
					result;
				    };

				ds::ABSTRACT_TYPE_DECLARATION { abstract_types, with_types, body }
				    => 
				    {
																if_debugging_say "\ndo_declaration/ABSTRACT_TYPE_DECLARATION/TOP   [unify-and-generalize-types-g.pkg]\n";
					fun make_abstract (tdt::SUM_TYPE { is_eqtype, ... } )
						=>
						is_eqtype :=  tdt::e::EQ_ABSTRACT;

					    make_abstract _
						=>
						bug "make_abstract";
					end;
					#
					fun redefine_eq (ds::SUMTYPE_DECLARATIONS { sumtypes, ... } )
						=>
						{   apply  set_data  sumtypes;
						    #
						    eq_types::define_eq_props  (sumtypes, NIL, typerstore::empty);
						}
						where
						    fun set_data (tdt::SUM_TYPE { is_eqtype, ... } ) =>   is_eqtype :=  tdt::e::DATA;
							set_data _                                      =>   ();
						    end;
						end;	

					    redefine_eq (ds::SEQUENTIAL_DECLARATIONS decs)
						=>
						apply redefine_eq decs;

					    redefine_eq (ds::LOCAL_DECLARATIONS (din, dout))                        #  "You're a better man than I am, Gunga Din!" 
						=>
						{   redefine_eq din;
						    redefine_eq dout;
						};

					    redefine_eq (ds::SOURCE_CODE_REGION_FOR_DECLARATION (declaration, _))
						=>
						redefine_eq declaration;

					    redefine_eq _
						=>
						();
					end;

					body' =   do_declaration
						      (body, syntax_treewalk_lexical_context, source_code_region, "do_declaration/ABSTRACT_TYPE_DECLARATION " ! callstack);

					if_debugging_say "do_declaration: ABSTRACT_TYPE_DECLARATION";

					apply  make_abstract  abstract_types;

					redefine_eq body';

																if_debugging_say "\ndo_declaration/ABSTRACT_TYPE_DECLARATION/BOT   [unify-and-generalize-types-g.pkg]\n";
					ds::ABSTRACT_TYPE_DECLARATION {
					    abstract_types,
					    with_types,
					    body => body'
					};
				    };

				ds::SOURCE_CODE_REGION_FOR_DECLARATION (declaration, source_code_region)
				    =>
				    {
																if_debugging_say "\ndo_declaration/ds::SOURCE_CODE_REGION_FOR_DECLARATION   [unify-and-generalize-types-g.pkg]\n";
					ds::SOURCE_CODE_REGION_FOR_DECLARATION (
					    do_declaration (declaration, syntax_treewalk_lexical_context, source_code_region, callstack),
					    source_code_region
					);
				    }; 


				# The next several declarations will never be seen ordinarily;
				# they are for re-typechecking after the instrumentation phase
				# of debugger or profiler. 
				#
				ds::PACKAGE_DECLARATIONS  named_packages
				    =>
				    ds::PACKAGE_DECLARATIONS          (map (named_package_type  (syntax_treewalk_lexical_context, source_code_region, "do_declaration/PACKAGE_DECLARATIONS " ! callstack)) named_packages);

				ds::GENERIC_DECLARATIONS  named_generics
				    =>
				    ds::GENERIC_DECLARATIONS          (map (generic_naming_type (syntax_treewalk_lexical_context, source_code_region, "do_declaration/ds::GENERIC_DECLARATIONS " ! callstack)) named_generics);

				_ => given_declaration;
			    esac;
			}

		    also
		    fun generic_naming_type
			( syntax_treewalk_lexical_context, 
			  source_code_region,
			  callstack
			)
			(ds::NAMED_GENERIC { definition,
					     name_symbol,
					     a_generic
					   }
			)
			=
			ds::NAMED_GENERIC { definition  => generic_expression_type  definition,
					    name_symbol,
					    a_generic
					  }
			where
			    fun generic_expression_type ( ds::GENERIC_DEFINITION { parameter, parameter_types, definition } )
				    =>
				    ds::GENERIC_DEFINITION { parameter,
							     parameter_types,
							     definition
								 =>
								 package_expression_type
								     (syntax_treewalk_lexical_context, source_code_region, "generic_naming_type/generic_expression_type/ds::GENERIC_DEFINITION " ! callstack)
								     definition
							 };

				generic_expression_type (ds::GENERIC_LET (declaration, expression))
				    =>
				    ds::GENERIC_LET
				      ( do_declaration
					  ( declaration,
					    enter_let_scope  syntax_treewalk_lexical_context,
					    source_code_region,
					    "generic_naming_type/generic_expression_type/ds::GENERIC_LET" ! callstack
				      ),
				      generic_expression_type expression
				    );

				generic_expression_type (ds::SOURCE_CODE_REGION_FOR_GENERIC (generic_expression, source_code_region))
				    =>
				    ds::SOURCE_CODE_REGION_FOR_GENERIC (generic_expression_type generic_expression, source_code_region);

				generic_expression_type  other
				    =>
				    other;
			    end;
			end

		    also
		    fun package_expression_type
			    (syntax_treewalk_lexical_context, source_code_region, callstack)
			    (se as (ds::COMPUTED_PACKAGE { a_generic, generic_argument, parameter_types } ))
			    =>
			    se;

			package_expression_type (syntax_treewalk_lexical_context, source_code_region, callstack) (ds::PACKAGE_LET { declaration, expression })
			    =>
			    ds::PACKAGE_LET { declaration =>  do_declaration (declaration, enter_let_scope syntax_treewalk_lexical_context, source_code_region, "package_expression_type/ds::PACKAGE_LET "    ! callstack),
					      expression  =>  package_expression_type                     (syntax_treewalk_lexical_context, source_code_region, "package_expression_type/ds::PACKAGE_LET(2) " ! callstack) expression
					    };


			package_expression_type (syntax_treewalk_lexical_context, _, callstack) (ds::SOURCE_CODE_REGION_FOR_PACKAGE (expression, source_code_region))
			    => 
			    ds::SOURCE_CODE_REGION_FOR_PACKAGE (package_expression_type (syntax_treewalk_lexical_context, source_code_region, callstack) expression, source_code_region);

			package_expression_type _ other
			    =>
			    other;
		    end 

		    also
		    fun named_package_type
			    (syntax_treewalk_lexical_context, source_code_region, callstack)
			    (ds::NAMED_PACKAGE { a_package, definition, name_symbol } )
			=
			ds::NAMED_PACKAGE { a_package,
					    definition => package_expression_type (syntax_treewalk_lexical_context, source_code_region, "named_package_type/ds::NAMED_PACKAGE" ! callstack) definition,
					    name_symbol
					  };


		    #	
#		    fun drop_resolved_typevars_in_named_value (ds::NAMED_VALUE { pattern => ds::VARIABLE_IN_PATTERN (vac::PLAIN_VARIABLE { path, varhome, inlining_data, var_type } ), ... })
#			    =>
#			    case var_type
#				#
#				REF              (tdt::TYPESCHEME_TYPOID { typescheme_eqflags, typescheme => tdt::TYPESCHEME { arity, body                                     } } )
#			    =>  {
#																if *debugging   printf "drop_resolved_typevars_in_named_value: handled case.\n";   fi;
#				    var_type :=  (tdt::TYPESCHEME_TYPOID { typescheme_eqflags, typescheme => tdt::TYPESCHEME { arity, body => tyj::drop_resolved_typevars body } } );
#				};
#
#				_ =>    {
#																if *debugging   printf "drop_resolved_typevars_in_named_value: UNHANDLED VARIABLE_IN_PATTERN/PLAIN_VARIABLE case\n";   fi;
#					};
#			    esac;
#
#			drop_resolved_typevars_in_named_value _ =>   {
#																if *debugging   printf "drop_resolved_typevars_in_named_value: UNHANDLED case\n";   fi;
#						};
#		    end
#
#		    also	
#		    fun drop_resolved_typevars_in_declaration  (declaration: ds::Declaration)
#			=
#			case declaration
#			    #
#			    ds::SOURCE_CODE_REGION_FOR_DECLARATION (declaration, _)
#				=>
#				drop_resolved_typevars_in_declaration  declaration;
#
#			    ds::SEQUENTIAL_DECLARATIONS  (declarations: List(ds::Declaration))
#				=>
#				apply drop_resolved_typevars_in_declaration declarations;
#
#			    ds::VALUE_DECLARATIONS  named_values
#				=>
#				apply  drop_resolved_typevars_in_named_value  named_values;
#
#			    _  =>	{
#																if *debugging   printf "drop_resolved_typevars_in_declaration: ignoring case:\n";
#																case declaration
#																	ds::VALUE_DECLARATIONS     			_ =>  printf "declaration == ds::VALUE_DECLARATIONS _\n";
#																	ds::EXCEPTION_DECLARATIONS 			_ =>  printf "declaration == ds::EXCEPTION_DECLARATIONS _\n";
#																	ds::RECURSIVE_VALUE_DECLARATIONS 		_ =>  printf "declaration == ds::RECURSIVE_VALUE_DECLARATIONS _\n";
#																	ds::TYPE_DECLARATIONS 			_ =>  printf "declaration == ds::TYPE_DECLARATIONS _\n";
#																	ds::PACKAGE_DECLARATIONS 			_ =>  printf "declaration == ds::PACKAGE_DECLARATIONS _\n";
#																	ds::GENERIC_DECLARATIONS 			_ =>  printf "declaration == ds::GENERIC_DECLARATIONS _\n";
#																	ds::API_DECLARATIONS 			_ =>  printf "declaration == ds::API_DECLARATIONS _\n";
#																	ds::GENERIC_API_DECLARATIONS 		_ =>  printf "declaration == ds::GENERIC_API_DECLARATIONS _\n";
#																	ds::INCLUDE_DECLARATIONS 			_ =>  printf "declaration == ds::INCLUDE_DECLARATIONS _\n";
#																	ds::LOCAL_DECLARATIONS 			_ =>  printf "declaration == ds::LOCAL_DECLARATIONS _\n";
#																	ds::SEQUENTIAL_DECLARATIONS 		_ =>  printf "declaration == ds::SEQUENTIAL_DECLARATIONS _\n";
#																	ds::OVERLOADED_VARIABLE_DECLARATION		_ =>  printf "declaration == ds::OVERLOADED_VARIABLE_DECLARATION _\n";
#																	ds::SUMTYPE_DECLARATIONS			_ =>  printf "declaration == ds::SUMTYPE_DECLARATIONS _\n";
#																	ds::ABSTRACT_TYPE_DECLARATION		_ =>  printf "declaration == ds::ABSTRACT_TYPE_DECLARATION _\n";
#																	ds::FIXITY_DECLARATION			_ =>  printf "declaration == ds::FIXITY_DECLARATION _\n";
#																	ds::SOURCE_CODE_REGION_FOR_DECLARATION	_ =>  printf "declaration == ds::SOURCE_CODE_REGION_FOR_DECLARATION _\n";
#																esac;
#																fi;
#				    };
#			esac;




		end;														# function unify_and_generalize_types 

	    unify_and_generalize_types
		=
		cos::do_compiler_phase  (cos::make_compiler_phase "Compiler 035  typecheck")  unify_and_generalize_types;

	end;	# stipulate
    };		# generic package unify_and_generalize_types_g 
end;

################################################################
# Note[1]:                                        2009-04-25 CrT
#
# While implementing OOP support and attempting
# to make 'new' and method functions mutually
# recursive, I discovered that
#
#     package test: api { f: X -> Void; } {
#
#          fun f (x: X) = ();
#          fun g () = f 0;
#      };
#
# typechecked ok but
#
#     package test: api { f: X -> Void; } {
#
#          fun f (x: X) = ()
#          also
#          fun g () = f 0;
#      };
#
# did not.  This was a bit of a showstopper,
# since forbidding class methods from creating
# new class instances is a major restriction.
#
# After some digging I discovered that the SML/NJ
# version of do_declaration/RECURSIVE_VALUE_DECLARATIONS
# in this file apparently once generalized its patterns
# but stopped doing so, judging by the comment
#
#				     # No need to generalize here, because every RECURSIVE_VALUE_DECLARATIONS is
#				     # wrapped in a VALUE_DECLARATIONS, and the generalization occurs at the
#				     # outer level.  Previously: named_recursive_values_records = map generalize_type named_recursive_values_records
#
# This omission appeared to be the root cause
# of my above typechecking issue.
#
# Poking around farther, I found in
#     src/lib/compiler/back/top/translate/translate-deep-syntax-to-lambdacode.pkg
#         fun make_named_recursive_values
#             fun g
# the code and comment
#				    ee = make_expression (expression, d); #  Was make_pattern_expression (expression, d, tvs) 
#					                                  #  We no longer track type namings at NAMED_RECURSIVE_VALUE anymore ! 
#
# and in fact the NAMED_RECURSIVE_VALUE.generalized_typevars
# field seemed at to be entirely unused by the compiler.




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################



## Copyright 1996 by Bell Laboratories 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
