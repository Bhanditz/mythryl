## prettyprint-deep-syntax.pkg
#
# Nomenclature:
#     In these libraries we distinguish "unparsing" from "prettyprinting":
#
#       o The purpose of "unparsing" is to regenerate something close
#         to the language surface syntax, for example to issue syntax
#         error diagnostic messages to user.
#
#       o The purpose of "prettyprinting" is to accurately display
#         the actual internal datastructure in question, typically
#         for purposes of compiler debugging.
#
#     Both are useful, so we implement both
#     for both raw and deep syntax trees.

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

# 2009-05-13 CrT: Created  from unparse-deep-syntax.pkg.
#                 This is a really quick and dirty hack at present.

stipulate
    package ds  =  deep_syntax;				# deep_syntax			is from   src/lib/compiler/front/typer-stuff/deep-syntax/deep-syntax.pkg
    package pp  =  standard_prettyprint_mill;		# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package sci =  sourcecode_info;			# sourcecode_info		is from   src/lib/compiler/front/basics/source/sourcecode-info.pkg
    package syx =  symbolmapstack;			# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
herein

    api Prettyprint_Deep_Syntax {
	#
	prettyprint_pattern
	    :
	    syx::Symbolmapstack
	    -> pp::Prettyprint_Mill 
	    -> (ds::Case_Pattern,  Int)
	    -> Void;

	prettyprint_expression
	    :
	    (syx::Symbolmapstack,  Null_Or( sci::Sourcecode_Info ))
	    -> pp::Prettyprint_Mill
	    -> (ds::Deep_Expression,  Int)
	    -> Void;

	prettyprint_declaration
	    :
	    (syx::Symbolmapstack,  Null_Or( sci::Sourcecode_Info ))
	    -> pp::Prettyprint_Mill
	    -> (ds::Declaration,  Int)
	    -> Void;

	prettyprint_rule
	    :
	    (syx::Symbolmapstack,  Null_Or( sci::Sourcecode_Info ))
	    -> pp::Prettyprint_Mill
	    -> (ds::Case_Rule,  Int)
	    -> Void;

	prettyprint_named_value
	    :
	    (syx::Symbolmapstack,  Null_Or( sci::Sourcecode_Info ))
	    -> pp::Prettyprint_Mill
	    -> (ds::Named_Value,  Int)
	    -> Void;

	prettyprint_recursively_named_value
	    :
	    (syx::Symbolmapstack,  Null_Or( sci::Sourcecode_Info ))
	    -> pp::Prettyprint_Mill
	    -> (ds::Named_Recursive_Value,  Int)
	    -> Void;


	prettyprint_package_expression
	    :
	    (syx::Symbolmapstack,  Null_Or( sci::Sourcecode_Info ))
	    -> pp::Prettyprint_Mill
	    -> (ds::Package_Expression,  Int)
	    -> Void;

	lineprint:  Ref(  Bool );

	debugging:  Ref(  Bool );

    }; #  Api Prettyprint_Deep_Syntax 
end;


stipulate
    package ds  =  deep_syntax;				# deep_syntax			is from   src/lib/compiler/front/typer-stuff/deep-syntax/deep-syntax.pkg
    package err =  error_message;			# error_message			is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    package ip  =  inverse_path;			# inverse_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package mld =  module_level_declarations;		# module_level_declarations	is from   src/lib/compiler/front/typer-stuff/modules/module-level-declarations.pkg
    package pp  =  standard_prettyprint_mill;		# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package sci =  sourcecode_info;			# sourcecode_info		is from   src/lib/compiler/front/basics/source/sourcecode-info.pkg
    package sy  =  symbol;				# symbol			is from   src/lib/compiler/front/basics/map/symbol.pkg
    package syp =  symbol_path;				# symbol_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package syx =  symbolmapstack;			# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
    package tdt =  type_declaration_types;		# type_declaration_types	is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
    package vac =  variables_and_constructors;		# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
    package tpl =  tuples;				# tuples			is from   src/lib/compiler/front/typer-stuff/types/tuples.pkg
    package fxt =  fixity;				# fixity			is from   src/lib/compiler/front/basics/map/fixity.pkg
    package uj  =  unparse_junk;			# unparse_junk			is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
    package ppt =  prettyprint_type;			# prettyprint_type		is from   src/lib/compiler/front/typer/print/prettyprint-type.pkg
    package uv  =  unparse_value;			# unparse_value			is from   src/lib/compiler/front/typer/print/unparse-value.pkg
    package ppv =  prettyprint_value;			# prettyprint_value		is from   src/lib/compiler/front/typer/print/prettyprint-value.pkg

herein

    package   prettyprint_deep_syntax
    : (weak)  Prettyprint_Deep_Syntax			# Prettyprint_Deep_Syntax	is from   src/lib/compiler/front/typer/print/prettyprint-deep-syntax.pkg
    {
	#  Debugging 
	say = control_print::say;
	debugging = REF FALSE;

#	unparse_typevar_ref = unparse_type::unparse_typevar_ref         syx::empty;



	fun bug msg
            =
            error_message::impossible("unparse_deep_syntax: " + msg);

#	internals = typer_control::internals;
internals = log::internals;


	lineprint = REF FALSE;

	fun if_debugging_say (msg: String)
            =
	    if   *debugging   say msg;   say "\n";   fi;

	fun if_debugging_unparse_typevar_ref  (msg, typevar_ref)
	    = 
	    if *debugging
		#
                unparse_typevar_ref = unparse_type::unparse_typevar_ref         syx::empty;
		#
		typer_debugging::with_internals
		    (fn () =  typer_debugging::debug_print debugging (msg, unparse_typevar_ref, typevar_ref));
	    fi;


	fun by f x y
            =
            f y x;

	null_fix = fxt::INFIX (0, 0);
	inf_fix  = fxt::INFIX (1000000, 100000);

	fun stronger_l (fxt::INFIX(_, m), fxt::INFIX (n, _)) => m >= n;
	    stronger_l _ => FALSE;			#  should not matter 
        end;

	fun stronger_r (fxt::INFIX(_, m), fxt::INFIX (n, _)) => n > m;
	    stronger_r _ => TRUE;			#  should not matter 
         end; 

	fun prpos ( stream:  pp::Prettyprint_Mill,
		   source:  sci::Sourcecode_Info,
                   charpos: Int
                 )
            =
	    if *lineprint

	         my (file: String, line: Int, pos: Int)
                     =
                     sci::filepos source charpos;
	      
                 pp::text stream (int::to_string line);
		 pp::text stream ".";
		 pp::text stream (int::to_string pos);

	    else
                 pp::text stream (int::to_string charpos);
            fi;


	fun checkpat (n, NIL)
                =>
                TRUE;

	    checkpat (n, (symbol, _) ! fields)
		=> 
		sy::eq (symbol, tpl::number_to_label n) and checkpat (n+1, fields);
        end;

	fun checkexp (n, NIL)
                =>
                TRUE;
	    checkexp (n, (ds::NUMBERED_LABEL { name=>symbol, ... }, _) ! fields)
		=> 
		sy::eq (symbol, tpl::number_to_label n) and checkexp (n+1, fields);
        end;

	fun is_tuplepat (ds::RECORD_PATTERN { fields => [_],                  ... } ) =>  FALSE;
	    is_tuplepat (ds::RECORD_PATTERN { is_incomplete => FALSE, fields, ... } ) =>  checkpat (1, fields);
	    is_tuplepat _ => FALSE;
        end;

	fun is_tupleexp (ds::RECORD_IN_EXPRESSION [_]) => FALSE;
 	    is_tupleexp (ds::RECORD_IN_EXPRESSION fields) => checkexp (1, fields);
	    is_tupleexp (ds::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _)) => is_tupleexp a;
	    is_tupleexp _ => FALSE;
        end;

	fun get_fix (symbolmapstack, symbol)
            =
	    find_in_symbolmapstack::find_fixity_by_symbol
                (
                  symbolmapstack,
                  sy::make_fixity_symbol (sy::name symbol)
                );

	fun strip_source_code_region_data (ds::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _)) => strip_source_code_region_data a;
	    strip_source_code_region_data x => x;
        end;

	fun prettyprint_pattern symbolmapstack stream
            =
	    {   ppsay = pp::text stream;

		fun prettyprint_pattern' (_,          0)
                        =>
                        ppsay "<pattern>";

		    prettyprint_pattern' (ds::VARIABLE_IN_PATTERN v,   _)
                        =>
                        {   ppsay "ds::VARIABLE_IN_PATTERN ";
			    if *internals	ppv::prettyprint_variable stream (symbolmapstack, v);	# More verbose version of next line.
			    else		ppv::prettyprint_var stream v;
			    fi;
                            ppsay " ";
                        };

		    prettyprint_pattern' (ds::WILDCARD_PATTERN,    _)
                        =>
                        ppsay "WILDCARD_PATTERN ";

		    prettyprint_pattern' (ds::INT_CONSTANT_IN_PATTERN (i, t), _)
                        =>
                        {   ppsay "ds::INT_CONSTANT_IN_PATTERN ";  
                            ppsay (multiword_int::to_string i);
                            ppsay " ";
                        };

	/*	     (begin_block stream uj::ALIGN 2;
		      ppsay "("; ppsay (multiword_int::to_string i);
		      ppsay " :"; pp::break stream { spaces=1, indent_on_wrap=1 };
		      unparse_type symbolmapstack stream t; ppsay ")";
		      end_block stream)
         */

		    prettyprint_pattern' (ds::UNT_CONSTANT_IN_PATTERN (w, t), _)
                        =>
                        {   ppsay "ds::UNT_CONSTANT_IN_PATTERN ";  
                            ppsay (multiword_int::to_string w);
                            ppsay " ";
                        };


	/*	     (uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
		      ppsay "("; ppsay (multiword_int::to_string w);
		      ppsay " :"; pp::break stream { spaces=1, indent_on_wrap=1 };
		      unparse_type symbolmapstack stream t; ppsay ")";
		      pp::end_box stream)
         */

		    prettyprint_pattern' (ds::FLOAT_CONSTANT_IN_PATTERN  r, _)
                        =>
                        {   ppsay "ds::FLOAT_CONSTANT_IN_PATTERN ";  
                            ppsay r;
                            ppsay " ";
                        };

		    prettyprint_pattern' (ds::STRING_CONSTANT_IN_PATTERN s, _)
                        =>
                        {   ppsay "ds::STRING_CONSTANT_IN_PATTERN ";  
                            uj::unparse_mlstring  stream s;
                            ppsay " ";
                        };

		    prettyprint_pattern' (ds::CHAR_CONSTANT_IN_PATTERN   s, _)
                        =>
                        {   ppsay "ds::STRING_CONSTANT_IN_PATTERN ";  
                            uj::unparse_mlstring' stream s;
                            ppsay " ";
                        };

		    prettyprint_pattern' (ds::AS_PATTERN (v, p), d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
                            ppsay "ds::AS_PATTERN ";  
			    prettyprint_pattern'(v, d);
                            ppsay " as ";
                            prettyprint_pattern'(p, d - 1);
			    pp::end_box stream;
			};
			    #  Handle 0 length case specially to avoid {, ... }: 

		    prettyprint_pattern' (ds::RECORD_PATTERN { fields => [], is_incomplete, ... }, _)
			=>
			{   ppsay "ds::RECORD_PATTERN ";  
			    if is_incomplete      ppsay "{... }";
			    else                  ppsay "()";
			    fi;
			};

		    prettyprint_pattern' (r as ds::RECORD_PATTERN { fields, is_incomplete, ... }, d)
			=>
			{   ppsay "ds::RECORD_PATTERN ";  

			    if   (is_tuplepat r)
				#
				uj::unparse_closed_sequence stream
				  { front     => (by pp::text "("),
				    separator => (fn stream = { pp::text stream ", ";
								pp::break stream { spaces=>0, indent_on_wrap=>0 };
							      }
					         ),
				    back       =>  (by pp::text ")"),
				    print_one  =>  (fn _ = fn (symbol, pattern) = prettyprint_pattern'(pattern, d - 1) ),
				    breakstyle =>  uj::ALIGN
				  }
				  fields;
			    else
				uj::unparse_closed_sequence stream
				  { front     =>  (by pp::text "{ "),
				    separator =>  (fn stream =  { pp::text stream ", ";
								  pp::break stream { spaces=>0, indent_on_wrap=>0 };
								}
						  ),
				    back      =>  (fn stream =  if is_incomplete  pp::text stream ", ... }";
							else pp::text stream "}";
							fi
					  ),
				    print_one =>  (fn stream =  fn (symbol, pattern) =
					  { uj::unparse_symbol stream symbol; pp::text stream "=";
					    prettyprint_pattern'(pattern, d - 1);
					  }
					),
				    breakstyle =>  uj::ALIGN
				  }
				  fields;
			    fi;
			};

		    prettyprint_pattern' (ds::VECTOR_PATTERN (NIL, _), d)
                        =>
			{   ppsay "ds::VECTOR_PATTERN ";  
                            ppsay "#[]";
                        };

		    prettyprint_pattern' (ds::VECTOR_PATTERN (pats, _), d)
			=> 
			{   ppsay "ds::VECTOR_PATTERN ";  
			    #
			    fun print_one _ pattern
				=
				prettyprint_pattern'(pattern, d - 1);

			    uj::unparse_closed_sequence stream
			      { front      => (by pp::text "#["),
				separator  => (fn stream => { pp::text stream ", ";
						  pp::break stream { spaces=>0, indent_on_wrap=>0 } ;}; end ),
				back       => (by pp::text "]"),
				print_one,
				breakstyle => uj::ALIGN
			      }
			      pats;
			};

		    prettyprint_pattern' (pattern as (ds::OR_PATTERN _), d)
			=>
			{   ppsay "ds::OR_PATTERN ";  
			    #
			    fun make_list (ds::OR_PATTERN (hd, tl)) => hd ! make_list tl;
			        make_list p => [p];
                            end;

			    fun print_one _ pattern
                                =
                                prettyprint_pattern' (pattern, d - 1);

			    uj::unparse_closed_sequence stream
                              {
				front      => (by pp::text "("),
				separator  => fn stream = { pp::break stream { spaces=>1, indent_on_wrap=>0 };
						    pp::text stream "| ";},
				back       => (by pp::text ")"),
				print_one,
				breakstyle => uj::ALIGN

			      } (make_list pattern);
			};

		    prettyprint_pattern' (ds::CONSTRUCTOR_PATTERN (e, _), _)
                        =>
			{   ppsay "ds::CONSTRUCTOR_PATTERN ";  
                            ppv::prettyprint_valcon stream e;
			};

		    prettyprint_pattern' (p as ds::APPLY_PATTERN _, d)
			=>
			{   ppsay "ds::APPLY_PATTERN ";  
			    prettyprint_valcon_pattern (symbolmapstack, stream) (p, null_fix, null_fix, d);
			};

		    prettyprint_pattern' (ds::TYPE_CONSTRAINT_PATTERN (pattern, typoid), depth)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::TYPE_CONSTRAINT_PATTERN ";  
			    prettyprint_pattern' (pattern, depth - 1); ppsay " :";
			    pp::break stream { spaces=>1, indent_on_wrap=>2 };
			    ppt::prettyprint_typoid  symbolmapstack  stream  typoid;
			    pp::end_box stream;
			};

		    prettyprint_pattern' _ => bug "prettyprint_pattern'";
                end;
	    
                prettyprint_pattern';
	    }

	also
	fun prettyprint_valcon_pattern (symbolmapstack, stream)
            = 
	    {   ppsay = pp::text stream;
		#
		fun lpcond atom = if atom  ppsay "("; fi;
		fun rpcond atom = if atom  ppsay ")"; fi;

		fun prettyprint_valcon_pattern'(_, _, _, 0) => ppsay "<pattern>";
		    #
		    prettyprint_valcon_pattern' (ds::CONSTRUCTOR_PATTERN (tdt::VALCON { name, ... }, _), l: fxt::Fixity, r: fxt::Fixity, _)
			=>
			{   ppsay "ds::CONSTRUCTOR_PATTERN (tdt::VALCON { ";
			    uj::unparse_symbol  stream  name;
			    ppsay " } ) ";
                        };

		    prettyprint_valcon_pattern'(ds::TYPE_CONSTRAINT_PATTERN (pattern, typoid), l, r, depth)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::TYPE_CONSTRAINT_PATTERN (";
                            prettyprint_pattern symbolmapstack stream (pattern, depth - 1);
                            ppsay " :";
			    pp::break stream { spaces=>1, indent_on_wrap=>2 };
			    ppt::prettyprint_typoid  symbolmapstack  stream  typoid;
                            ppsay ")";
			    pp::end_box stream;
                        };

		    prettyprint_valcon_pattern'(ds::AS_PATTERN (v, p), l, r, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::AS_PATTERN (";
                            prettyprint_pattern symbolmapstack stream (v, d);
                            pp::break stream { spaces=>1, indent_on_wrap=>2 };
			    ppsay " as ";
                            prettyprint_pattern symbolmapstack stream (p, d - 1);
                            ppsay ")";
			    pp::end_box stream;
			};

		    prettyprint_valcon_pattern' (ds::APPLY_PATTERN (tdt::VALCON { name, ... }, _, p), l, r, d)
			=>
			{   name' = sy::name name; 
				#  should really have original path, like for VARIABLE_IN_EXPRESSION  XXX BUGGO FIXME

			    this_fix =  get_fix (symbolmapstack, name);
			    eff_fix  =  case this_fix    fxt::NONFIX => inf_fix;  x => x; esac;
			    atom     =  stronger_r (eff_fix, r) or stronger_l (l, eff_fix);

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });

			    ppsay "ds::APPLY_PATTERN (tdt::VALCON { ";

			    lpcond atom;

			    case (this_fix, p)
				#			      
                                 (fxt::INFIX _, ds::RECORD_PATTERN { fields => [(_, pl), (_, pr)], ... } )
				     =>
				     {   my (left, right)
                                             =
					     if atom      (null_fix, null_fix);
					     else         (       l,        r);
                                             fi;
				         prettyprint_valcon_pattern' (pl, left, this_fix, d - 1);
					 pp::break stream { spaces=>1, indent_on_wrap=>0 };
					 ppsay name';
					 pp::break stream { spaces=>1, indent_on_wrap=>0 };
					 prettyprint_valcon_pattern' (pr, this_fix, right, d - 1);
				     };
				 _ =>
				     {   ppsay name';
                                         pp::break stream { spaces=>1, indent_on_wrap=>0 };
				         prettyprint_valcon_pattern'(p, inf_fix, inf_fix, d - 1);
                                     };
                            esac;

			    rpcond atom;

			    ppsay " } ) ";

			    pp::end_box  stream;
			};

		    prettyprint_valcon_pattern' (p, _, _, d)
                        =>
                        prettyprint_pattern symbolmapstack stream (p, d);
                end;

	    
                prettyprint_valcon_pattern';
	    };

	fun trim [x] => [];
	    trim (a ! b) => a ! trim b;
	    trim [] => [];
        end;

	fun prettyprint_expression  (context as (symbolmapstack, source_opt))  stream
            =
	    {   ppsay = pp::text stream;

#	        (en_pp stream) ->   { begin_align_box, begin_wrap_box, pp::end_box, pps, pp::break, ... };

		fun lparen () = ppsay "(";
		fun rparen () = ppsay ")";

		fun lpcond atom = if atom  ppsay "(";  fi;
		fun rpcond atom = if atom  ppsay ")";  fi;

		fun prettyprint_expression' (_, _, 0) => ppsay "<expression>";					# 2nd arg is 'atom', 3rd is prettyprint 'depth'. We stop prettyprint recursion when 'depth' drops to 0.

		    prettyprint_expression' (ds::VALCON_IN_EXPRESSION (con, _),       _, _)
                        =>
                        {   ppsay "ds::VALCON_IN_EXPRESSION ";
                            ppv::prettyprint_valcon stream con;
                        };

		    prettyprint_expression' (ds::VARIABLE_IN_EXPRESSION (REF var, typoids),   _, _)
                        =>
                        {   ppsay "ds::VARIABLE_IN_EXPRESSION ( ";
			    if *internals	    ppv::prettyprint_variable stream (symbolmapstack, var);	# More verbose version of next line.
			    else		    ppv::prettyprint_var      stream                  var ;
			    fi;
#                            ppsay ", _) ";
                            ppsay ", ";
                            ppsay (sprintf "%d typoids: [ " (list::length typoids));

			    apply pp_typoid typoids
			    where
				fun pp_typoid typoid
				    =
				    {   ppt::prettyprint_typoid  symbolmapstack  stream  typoid;
					ppsay ", ";
				    };
			    end;

                            ppsay "] ) ";
                        };

		    prettyprint_expression' (   ds::INT_CONSTANT_IN_EXPRESSION (i, t), _, _)
                        =>
                        {   ppsay "ds::INT_CONSTANT_IN_EXPRESSION ";
                            ppsay (multiword_int::to_string i);
                            ppsay " ";
                        };

		    prettyprint_expression' (   ds::UNT_CONSTANT_IN_EXPRESSION (u, t),    _, _)
                        =>
                        {   ppsay "ds::UNT_CONSTANT_IN_EXPRESSION ";
                            ppsay (multiword_int::to_string u);
                            ppsay " ";
                        };

		    prettyprint_expression' ( ds::FLOAT_CONSTANT_IN_EXPRESSION r,         _, _)
                        =>
                        {   ppsay "ds::FLOAT_CONSTANT_IN_EXPRESSION ";
                            ppsay r;
                            ppsay " ";
                        };

		    prettyprint_expression' (ds::STRING_CONSTANT_IN_EXPRESSION s,       _, _)
                        =>
                        {   ppsay "ds::STRING_CONSTANT_IN_EXPRESSION ";
                            uj::unparse_mlstring  stream s;
                            ppsay " ";
                        };

		    prettyprint_expression' (  ds::CHAR_CONSTANT_IN_EXPRESSION s,    _, _)
                        =>
                        {   ppsay "ds::CHAR_CONSTANT_IN_EXPRESSION ";
                            uj::unparse_mlstring'  stream s;
                            ppsay " ";
                        };

		    prettyprint_expression' (r as ds::RECORD_IN_EXPRESSION fields, _, d)
			=>
			{   ppsay "ds::RECORD_IN_EXPRESSION ";

			    if (is_tupleexp r)
				#
				uj::unparse_closed_sequence stream
				  { front      =>  (by pp::text "("),
				    separator  =>  (fn stream => { pp::text stream ", ";
						      pp::break stream { spaces=>0, indent_on_wrap=>0 } ;}; end ),
				    back       =>  (by pp::text ")"),
				    print_one  =>  (fn _ => fn (_, expression) => prettyprint_expression'(expression, FALSE, d - 1); end; end ),
				    breakstyle =>  uj::ALIGN
                                  }
				  fields;

			    else
				uj::unparse_closed_sequence stream
				  { front     =>  (by pp::text "{ "),
				    separator =>  (fn stream => { pp::text stream ", ";
						      pp::break stream { spaces=>0, indent_on_wrap=>0 } ;}; end ),
				   back       =>  (by pp::text "}"),
				   print_one  =>  (fn stream => fn (ds::NUMBERED_LABEL { name, ... }, expression) =>
				       { uj::unparse_symbol stream name; ppsay "=";
					prettyprint_expression'(expression, FALSE, d);}; end;  end ),
				   breakstyle =>  uj::ALIGN
                                  }
				  fields;
			    fi;
			};

		    prettyprint_expression' (ds::RECORD_SELECTOR_EXPRESSION (ds::NUMBERED_LABEL { name, ... }, expression), atom, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
                            ppsay "ds::RECORD_SELECTOR_EXPRESSION (ds::NUMBERED_LABEL { ";
			    ppsay "#"; uj::unparse_symbol stream name;
                            ppsay ", ... }, ";
			    lpcond atom;
			    prettyprint_expression'(expression, TRUE, d - 1); ppsay ">";
			    rpcond atom;
                            ppsay " ) ";
			    pp::end_box stream;
			};

		    prettyprint_expression'(ds::VECTOR_IN_EXPRESSION (NIL, _), _, d)
                        =>
                        ppsay "ds::VECTOR_IN_EXPRESSION #[]";

		    prettyprint_expression'(ds::VECTOR_IN_EXPRESSION (exps, _), _, d)
			=>
			{   fun print_one _ expression
                                =
                                prettyprint_expression'(expression, FALSE, d - 1);

                            ppsay "ds::VECTOR_IN_EXPRESSION ";

			    uj::unparse_closed_sequence stream
			      { front      =>  (by pp::text "#["),
				separator  =>  (fn stream => { pp::text stream ", ";
						  pp::break stream { spaces=>1, indent_on_wrap=>0 } ;}; end ),
				back       =>  (by pp::text "]"),
				print_one,
				breakstyle =>  uj::ALIGN
			      }
			      exps;
			};

		    prettyprint_expression'(ds::ABSTRACTION_PACKING_EXPRESSION (e, t, tcs), atom, d)
			=> 
			{
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "<ds::ABSTRACTION_PACKING_EXPRESSION: ";
			    prettyprint_expression'(e, FALSE, d);
			    ppsay "; ";
			    pp::break stream { spaces=>1, indent_on_wrap=>2 };
			    ppt::prettyprint_typoid  symbolmapstack  stream  t;
			    ppsay ">";
			    pp::end_box stream;
			};

		    prettyprint_expression'(ds::SEQUENTIAL_EXPRESSIONS expressions, _, d)
			=>
			{   ppsay "ds::SEQUENTIAL_EXPRESSIONS ";
			    #
			    uj::unparse_closed_sequence stream
			      #	
			      { front      =>  (by pp::text "("),
				separator  =>  (fn stream = { pp::text stream ";";
							      pp::break stream { spaces=>1, indent_on_wrap=>0 } ;
							    }
					       ),
				back       =>  (by pp::text ")"),
				print_one  =>  (fn _ = fn expression = prettyprint_expression'(expression, FALSE, d - 1)),
				breakstyle =>  uj::ALIGN
			      }
			      #	
			      expressions;
			};

		    prettyprint_expression'(e as ds::APPLY_EXPRESSION _, atom, d)
			=>
			{   infix0 = fxt::INFIX (0, 0);
			    #
			    ppsay "ds::APPLY_EXPRESSION ";
			    lpcond atom;
			    prettyprint_app_expression (e, null_fix, null_fix, d);
			    rpcond atom;
			};

		    prettyprint_expression'(ds::TYPE_CONSTRAINT_EXPRESSION (e, t), atom, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::TYPE_CONSTRAINT_EXPRESSION ";
			    lpcond atom;
			    prettyprint_expression'(e, FALSE, d); ppsay ":";
			    pp::break stream { spaces=>1, indent_on_wrap=>2 };
			    ppt::prettyprint_typoid  symbolmapstack  stream  t;
			    rpcond atom;
			    pp::end_box stream;
			};

		    prettyprint_expression'(ds::EXCEPT_EXPRESSION (expression, (rules, _)), atom, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::EXCEPT_EXPRESSION ";
			    lpcond atom;
			    prettyprint_expression'(expression, atom, d - 1); pp::newline stream; ppsay "except ";
			    uj::newline_indent stream 2;
			    uj::ppvlist stream ("  ", "| ",
			       (fn stream => fn r => prettyprint_rule context stream (r, d - 1); end; end ), rules);
			    rpcond atom;
			    pp::end_box stream;
			};

		    prettyprint_expression'(ds::RAISE_EXPRESSION (expression, _), atom, d)
			=> 
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::RAISE_EXPRESSION ";
			    lpcond atom;
			    ppsay "raise exception "; prettyprint_expression'(expression, TRUE, d - 1);
			    rpcond atom;
			    pp::end_box stream;
			};

		    prettyprint_expression'(ds::LET_EXPRESSION (declaration, expression), _, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::LET_EXPRESSION ";
			    ppsay "stipulate ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    prettyprint_declaration context stream (declaration, d - 1); 
			    pp::end_box stream;
			    pp::break stream { spaces=>1, indent_on_wrap=>0 };
			    ppsay "herein ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			     prettyprint_expression'(expression, FALSE, d - 1);
			    pp::end_box stream;
			    pp::break stream { spaces=>1, indent_on_wrap=>0 };
			    ppsay "end;";
			    pp::end_box stream;
			};

		    prettyprint_expression'(ds::CASE_EXPRESSION (expression, rules, _), _, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::CASE_EXPRESSION ";
			    ppsay "case ("; prettyprint_expression'(expression, TRUE, d - 1); uj::newline_indent stream 2;
			    uj::ppvlist stream (") ", ";",
			      (fn stream =  fn r =  prettyprint_rule context stream (r, d - 1)), 
			       trim rules);
			    rparen();
			    ppsay "esac";
			    pp::end_box stream;
			};

		    prettyprint_expression' (ds::IF_EXPRESSION { test_case, then_case, else_case }, atom, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::IF_EXPRESSION ";
			    lpcond atom;
			    ppsay "if ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			     prettyprint_expression' (test_case, FALSE, d - 1);
			    pp::end_box stream;
			    pp::break stream { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "then ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			     prettyprint_expression' (then_case, FALSE, d - 1);
			    pp::end_box stream;
			    pp::break stream { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "else ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			     prettyprint_expression' (else_case, FALSE, d - 1);
			    pp::end_box stream;
			    rpcond atom;
			    pp::end_box stream;
			};

		    prettyprint_expression' (ds::AND_EXPRESSION (e1, e2), atom, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::AND_EXPRESSION ";
			    lpcond atom;
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    prettyprint_expression' (e1, TRUE, d - 1);
			    pp::end_box stream;
			    pp::break stream { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "and ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    prettyprint_expression' (e2, TRUE, d - 1);
			    pp::end_box stream;
			    rpcond atom;
			    pp::end_box stream;
			};

		    prettyprint_expression' (ds::OR_EXPRESSION (e1, e2), atom, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::OR_EXPRESSION ";
			    lpcond atom;
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    prettyprint_expression' (e1, TRUE, d - 1);
			    pp::end_box stream;
			    pp::break stream { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "or ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    prettyprint_expression' (e2, TRUE, d - 1);
			    pp::end_box stream;
			    rpcond atom;
			    pp::end_box stream;
			};

		    prettyprint_expression' (ds::WHILE_EXPRESSION { test, expression }, atom, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::WHILE_EXPRESSION ";
			    ppsay "while ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			     prettyprint_expression'(test, FALSE, d - 1);
			    pp::end_box stream;
			    pp::break stream { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "do ";
			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			      prettyprint_expression'(expression, FALSE, d - 1);
			    pp::end_box stream;
			    pp::end_box stream;
			};

		    prettyprint_expression'(ds::FN_EXPRESSION (rules, _), _, d)
			=>
			{   pp::begin_align_4box stream;
			    ppsay "ds::FN_EXPRESSION ";
			    uj::ppvlist stream ("(fn ", "  | ",
					    (fn stream => fn r =>
					       prettyprint_rule context stream (r, d - 1); end; end ),
					    trim rules);
			    rparen();
			    pp::end_box stream;
			};

		    prettyprint_expression' (ds::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), atom, d)
			=>
			case source_opt
			    #
			    NULL
				=>
				prettyprint_expression'(expression, atom, d);

			    THE source
				=>
				{
				     ppsay "<ds::SOURCE_CODE_REGION_FOR_EXPRESSION(";
				     prpos (stream, source, s);
				     ppsay ", ";
				     prpos (stream, source, e);
				     ppsay "): ";
				     prettyprint_expression'(expression, FALSE, d);
				     ppsay ">";
				};
			esac;
                end 

		also
		fun prettyprint_app_expression (_, _, _, 0)
                        =>
                        pp::text stream "<expression>";

		    prettyprint_app_expression arg
			=>
			{   ppsay = pp::text stream;

			    fun fixitypp (symbol, operand, left_fix, right_fix, d)
				=
				{   name =  syp::to_string  (syp::SYMBOL_PATH symbol);

				    this_fix
                                        =
                                        case symbol
                                          
					     [symbol] =>  get_fix (symbolmapstack, symbol);
					     _        =>  fxt::NONFIX;
                                        esac;

				    fun pr_non expression
					=
					{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
					    ppsay name; pp::break stream { spaces=>1, indent_on_wrap=>0 };
					    prettyprint_expression'(expression, TRUE, d - 1);
					    pp::end_box stream;
                                        };

				    case this_fix
				        #
				        fxt::INFIX _
					     =>
					     case (strip_source_code_region_data operand)
						 #
						 ds::RECORD_IN_EXPRESSION [(_, pl), (_, pr)]
						     =>
						     {   atom =  stronger_l (left_fix, this_fix)
							      or stronger_r (this_fix, right_fix);

							 my (left, right)
							     =
							     atom   ??   (null_fix, null_fix )
								    ::   (left_fix, right_fix);

							 uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
							 ppsay "ds::RECORD_IN_EXPRESSION ";
							 lpcond atom;
							 prettyprint_app_expression (pl, left, this_fix, d - 1);
							 pp::break stream { spaces=>1, indent_on_wrap=>0 };
							 ppsay name;
							 pp::break stream { spaces=>1, indent_on_wrap=>0 };
							 prettyprint_app_expression (pr, this_fix, right, d - 1);
							 rpcond atom;
							 pp::end_box stream;
						     };

						 e' => pr_non e';
					     esac;


					 fxt::NONFIX => pr_non operand;
                                    esac;
				};

			    fun apply_print (_, _, _, 0)
                                    =>
                                    ppsay "#";

			        apply_print (ds::APPLY_EXPRESSION (operator, operand), l, r, d)
				    =>
				    case (strip_source_code_region_data operator)
				        #
					ds::VALCON_IN_EXPRESSION (tdt::VALCON { name, ... }, _)
					    =>
					    fixitypp ([name], operand, l, r, d);

					ds::VARIABLE_IN_EXPRESSION (v, _)
					    =>
					    {   path =  case *v
							    vac::ORDINARY_VARIABLE { path=>syp::SYMBOL_PATH path', ... } => path';
							    vac::OVERLOADED_IDENTIFIER { name, ... } => [name];
							    errorvar => [sy::make_value_symbol "<errorvar>"];
							esac;

						fixitypp (path, operand, l, r, d);
					    };

					operator
					    =>
					    {   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
						prettyprint_expression'(operator, TRUE, d - 1);   pp::break stream { spaces=>1, indent_on_wrap=>2 };
						prettyprint_expression'(operand,  TRUE, d - 1);
						pp::end_box stream;
					    };
				    esac;

			        apply_print (ds::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), l, r, d)
				    =>
				    case source_opt
				        #
					NULL =>   apply_print (expression, l, r, d);
					#
					THE source
					    =>
					    if *internals
						#
						ppsay "<MARK(";
						prpos (stream, source, s); ppsay ", ";
						prpos (stream, source, e); ppsay "): ";
						prettyprint_expression'(expression, FALSE, d); ppsay ">";
					    else
					        apply_print (expression, l, r, d);
					    fi;
                                    esac;


			        apply_print (e, _, _, d)
                                    =>
                                    prettyprint_expression'(e, TRUE, d);
                            end;

			    apply_print arg;
			};
                end;
	    
                (fn (expression, depth)
                    =
                    prettyprint_expression' (expression, FALSE, depth));
	    }

	also
	fun prettyprint_rule (context as (symbolmapstack, source_opt)) stream (ds::CASE_RULE (pattern, expression), d)
            =
	    if   (d > 0)
	        
                 uj::open_style_box  uj::ALIGN  stream  (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
		 pp::text  stream  "CASE_RULE ";
		 prettyprint_pattern  symbolmapstack  stream  (pattern, d - 1);
		 pp::text  stream  " =>";
                 pp::break stream { spaces=>1,  indent_on_wrap=>2 };
		 prettyprint_expression  context  stream  (expression, d - 1);
		 pp::end_box  stream;
	    else
                 pp::text stream "<rule>";
            fi

	also
	fun prettyprint_named_value  (context as (symbolmapstack, source_opt))  stream  (ds::NAMED_VALUE { pattern, expression, generalized_typevars, raw_typevars }, d)
            =
	    if (d > 0)
		#	        
		uj::open_style_box  uj::ALIGN  stream  (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
		pp::text  stream  "ds::NAMED_VALUE { ";

		pp::break stream { spaces=>1,  indent_on_wrap=>2 };
		pp::text  stream  " pattern    =>  ";
		prettyprint_pattern  symbolmapstack  stream  (pattern, d - 1);

		pp::text  stream  ",";

		pp::break stream { spaces=>1,  indent_on_wrap=>2 };
		pp::text  stream  " expression =>  ";
		prettyprint_expression  context  stream  (expression, d - 1);

		pp::text  stream  ",";

		{   fun prettyprint_typevar  typevar_ref
			=
			ppt::prettyprint_typevar_ref
			    symbolmapstack
			    stream
			    typevar_ref;

		    pp::break   stream { spaces=>1,  indent_on_wrap=>2 };
		    pp::text  stream (sprintf " raw_typevars => %d-entry list: "  (length  *raw_typevars));
		    apply prettyprint_typevar *raw_typevars;

		    pp::text  stream  ",";

		    pp::break   stream { spaces=>1,  indent_on_wrap=>2 };
		    pp::text  stream (sprintf " generalized_typevars => %d-entry list: "  (length  generalized_typevars));
		    apply prettyprint_typevar generalized_typevars;
		};

		pp::newline stream;
		pp::text  stream  "}ds::NAMED_VALUE ";

		pp::end_box  stream;
	    else
		pp::text stream "<naming>";
            fi

	also
	fun prettyprint_recursively_named_value context stream (ds::NAMED_RECURSIVE_VALUE { variable=>var, expression, ... }, d)
            = 
	    if (d > 0)
	        #  
		uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
		ppv::prettyprint_var stream var;

		pp::text stream " =";

		pp::break stream { spaces=>1, indent_on_wrap=>2 };
		prettyprint_expression context stream (expression, d - 1);
		pp::end_box stream;
	    else
                pp::text stream "<rec naming>";
            fi


        # NB: The original 1992 deep syntax unparser still exists, in
        #
        #     src/lib/compiler/src/print/unparse-interactive-deep-syntax-declaration.pkg
        #
        # It gets called only by
        #
	#     src/lib/compiler/toplevel/interact/read-eval-print-loop-g.pkg
	#  
	# which uses it to display the results of interactive expression evaluation. 
	#  
        # The more recent version here gets used for everything else.
        # It gets called from:
	#  
        #     src/lib/compiler/front/typer/main/type-core-language.pkg
        #     src/lib/compiler/toplevel/main/translate-raw-syntax-to-execode-g.pkg
        #     src/lib/compiler/toplevel/main/print-hooks.pkg
        #
	also
	fun prettyprint_declaration (context as (symbolmapstack, source_opt)) stream
            =
	    {   ppsay = pp::text stream;

		fun prettyprint_declaration' (_, 0)
                        =>
                        ppsay "<declaration>";

		    prettyprint_declaration' (ds::VALUE_DECLARATIONS vbs, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::VALUE_DECLARATIONS [";

			    #
			    uj::ppvlist stream (" ", ", ",
				(fn stream =  fn named_value =  prettyprint_named_value context stream (named_value, d - 1)), vbs);

			    ppsay "] ds::VALUE_DECLARATIONS ";
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::RECURSIVE_VALUE_DECLARATIONS rvbs, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::RECURSIVE_VALUE_DECLARATIONS [";

			    uj::ppvlist stream (" ", ", ",
				(fn stream =  fn named_recursive_values =  prettyprint_recursively_named_value context stream (named_recursive_values, d - 1)), rvbs);
			    ppsay "] ds::RECURSIVE_VALUE_DECLARATIONS";
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::TYPE_DECLARATIONS types, d)
			=>
			{   fun f stream (tdt::NAMED_TYPE { namepath, typescheme=>tdt::TYPESCHEME { arity, body }, ... } )
				    =>
				    {   case arity
					    #
					    0 => ();
					    1 => ppsay "'a ";
					    n => {   uj::unparse_tuple stream pp::text (ppt::type_formals n); 
						     ppsay " ";
						 };
					esac;

					uj::unparse_symbol  stream  (ip::last  namepath);

					ppsay " = ";

					ppt::prettyprint_typoid symbolmapstack stream  body;
				    };

				f _ _
				    =>
				    bug "prettyprint_declaration' (TYPE_DECLARATIONS)";
			    end;

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });

			    uj::ppvlist stream (
                                "",			# was "type "
                                " also ",
                                f,
                                types
                            );
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::SUMTYPE_DECLARATIONS { sumtypes, with_types }, d)
			=>
			{   fun prettyprint_data stream (tdt::SUM_TYPE { namepath, arity, kind, ... } )
				    =>
				    case kind
					#
					tdt::SUMTYPE (_)
					    =>
					    {   case arity
						    #
						    0 => ();
						    1 => (ppsay "'a ");
						    n => { uj::unparse_tuple stream pp::text (ppt::type_formals n); 
							   ppsay " ";
							 };
						 esac;

						 uj::unparse_symbol stream (ip::last  namepath);		ppsay " = ...";
					    /* 

						 uj::unparse_sequence
						     stream
						      { separator => (fn stream => (pp::text stream " |";
								     pp::break stream { spaces=1, indent_on_wrap=0 } )),

							print_one  => (fn stream =
									fn (tdt::VALCON { name, ... } ) =>  
									      uj::unparse_symbol stream  name),

							breakstyle => uj::ALIGN
						      }
						      dcons;
					     */
					    };

					_   =>
					    bug "prettyprint_declaration'(SUMTYPE_DECLARATIONS) 1.1";
				   esac;

			       prettyprint_data _ _
				   =>
				   bug "prettyprint_declaration'(SUMTYPE_DECLARATIONS) 1.2";
			    end;

			    fun prettyprint_with  stream  (tdt::NAMED_TYPE { namepath, typescheme=>tdt::TYPESCHEME { arity, body }, ... } )
				    =>
				    {   case arity   
					    0 =>    ();
					    1 =>    (ppsay "'a ");
					    n =>    {    uj::unparse_tuple stream pp::text (ppt::type_formals n);    ppsay " ";  };
					esac;

					uj::unparse_symbol stream (ip::last  namepath);

					ppsay " = ";

					ppt::prettyprint_typoid  symbolmapstack  stream  body;
				    };

				prettyprint_with _ _
				    =>
				    bug "prettyprint_declaration'(SUMTYPE_DECLARATIONS) 2";
			    end;

			    #  Could call PPDec::prettyprint_declaration here: 

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    uj::ppvlist stream (
                                "",			# Was "enum "
                                "also ",
                                prettyprint_data,
                                sumtypes
                            );
			    pp::newline stream;
			    uj::ppvlist stream ("withtype ", "also ", prettyprint_with, with_types);
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::ABSTRACT_TYPE_DECLARATION _, d)
			=>
			ppsay "abstype";

		    prettyprint_declaration' (ds::EXCEPTION_DECLARATIONS ebs, d)
			=>
			{   fun f stream (   ds::NAMED_EXCEPTION {
						 exception_constructor =>  tdt::VALCON { name, ... },
						 exception_typoid      =>  etype,
						 ...
					     }
					 )
				    =>
				    {   uj::unparse_symbol  stream  name;

					case etype
                                            #
					    NULL => ();

					    THE type'
						=>
						{
#                                                    ppsay " of ";
						    ppt::prettyprint_typoid  symbolmapstack  stream  type';
						};
					esac;
				    };

				f stream (ds::DUPLICATE_NAMED_EXCEPTION { exception_constructor  =>  tdt::VALCON { name, ... },
								          equal_to               =>  tdt::VALCON { name=>name', ... }
								        }
					 )
				    =>
				    {   uj::unparse_symbol stream name;
					ppsay "=";
					uj::unparse_symbol stream name';
				    };
			    end;

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    uj::ppvlist stream ("exception ", "also ", f, ebs);
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::PACKAGE_DECLARATIONS sbs, d)
			=>
			{   fun f stream (ds::NAMED_PACKAGE { name_symbol=>name, a_package=>mld::A_PACKAGE { varhome, ... }, definition=>def } )
				    =>
				    {   uj::unparse_symbol stream name;
					ppv::prettyprint_varhome stream varhome;
					ppsay " = ";
					pp::break stream { spaces=>1, indent_on_wrap=>2 };
					prettyprint_package_expression context stream (def, d - 1);
				    };

				f _ _
				    =>
				    bug "prettyprint_declaration: PACKAGE_DECLARATION: NAMED_PACKAGE";
			    end;

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    uj::ppvlist stream ("package ", "also ", f, sbs);
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::GENERIC_DECLARATIONS fbs, d)
			=>
			{   fun f stream (ds::NAMED_GENERIC { name_symbol=>fname, a_generic => mld::GENERIC { varhome, ... }, definition=>def } )
				    =>
				    {   uj::unparse_symbol stream fname;
					ppv::prettyprint_varhome stream varhome;
					ppsay " = "; 
					pp::break stream { spaces=>1, indent_on_wrap=> 2 };
					prettyprint_generic_expression context stream (def, d - 1);
				    };

				f _ _
				    =>
				    bug "prettyprint_declaration': GENERIC_DECLARATION";
			    end;

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    uj::ppvlist stream ("generic package ", "also ", f, fbs);
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::API_DECLARATIONS sigvars, d)
			=>
			{   fun f stream (mld::API { name, ... } )
				    =>
				    {   ppsay "api "; 

					case name
                                          
					     THE s =>  uj::unparse_symbol stream s;
					     NULL  =>  ppsay "ANONYMOUS";
					esac;
				    };

			        f _ _
				    =>
				    bug "prettyprint_declaration': API_DECLARATIONS";
                            end;

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });

			    uj::unparse_sequence
				stream
				{ separator  =>  pp::newline,
				  print_one  =>  f,
				  breakstyle =>  uj::ALIGN
				}
				sigvars;

			    pp::end_box stream;
			};

		    prettyprint_declaration'(ds::GENERIC_API_DECLARATIONS sigvars, d)
			=>
			{   fun f stream (mld::GENERIC_API { kind, ... } )
				=>
				{   ppsay "funsig "; 

				    case kind   
					THE s => uj::unparse_symbol stream s;
					NULL => ppsay "ANONYMOUS";
				    esac;
				};

			       f _ _
				=>
				bug "prettyprint_declaration': GENERIC_API_DECLARATIONS"; end;

			    uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });

			    uj::unparse_sequence
				stream
				{ separator  =>  pp::newline,
				  print_one  =>  f,
				  breakstyle =>  uj::ALIGN
				}
				sigvars;

			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::LOCAL_DECLARATIONS (inner, outer), d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "with";
                            uj::newline_indent stream 2;
			    prettyprint_declaration'(inner, d - 1);
                            pp::newline stream;
			    ppsay "do";
                            pp::newline stream;
			    prettyprint_declaration'(outer, d - 1);
                            pp::newline stream;
			    ppsay "end;";
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::SEQUENTIAL_DECLARATIONS decs, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "ds::SEQUENTIAL_DECLARATIONS [";

			    uj::unparse_sequence
				stream
				{ separator  =>  pp::newline,
				  print_one  =>  (fn stream = fn declaration = prettyprint_declaration'(declaration, d)),
				  breakstyle =>  uj::ALIGN
				}
				decs;

			    ppsay "]ds::SEQUENTIAL_DECLARATIONS";
			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::FIXITY_DECLARATION { fixity, ops }, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    #
			    case fixity
				#                              
				fxt::NONFIX => ppsay "nonfix ";

				fxt::INFIX (i, _)
				    => 
				    {   if (i % 2 == 0)   ppsay "infix ";
					else              ppsay "infixr ";
                                        fi;

					if (i / 2 > 0)    ppsay (int::to_string (i / 2));
							  ppsay " ";
                                        fi;
				    };
			    esac;

			    uj::unparse_sequence
			       stream
			       { separator  => (fn stream =  pp::break stream { spaces=>1, indent_on_wrap=>0 }),
				 print_one  =>  uj::unparse_symbol,
				 breakstyle =>  uj::ALIGN
			       }
			       ops;

			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::OVERLOADED_VARIABLE_DECLARATION overloaded_variable, d)
			=>
			{   ppsay "overloaded my ";
			    ppv::prettyprint_var  stream  overloaded_variable;
			};

		    prettyprint_declaration' (ds::INCLUDE_DECLARATIONS named_packages, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "include ";
			    uj::unparse_sequence
				stream
				{ separator  =>  (fn stream =  pp::break stream { spaces=>1, indent_on_wrap=>0 }  ),
				  print_one  =>  (fn stream =  fn (sp, _) =  ppsay (syp::to_string sp)),
				  breakstyle =>  uj::ALIGN
				}
				named_packages;

			    pp::end_box stream;
			};

		    prettyprint_declaration' (ds::SOURCE_CODE_REGION_FOR_DECLARATION (declaration, (s, e)), d)
			=> 
			case source_opt
			    #
			    NULL =>  prettyprint_declaration'(declaration, d);

			    THE source
				=>
				{
#				     2007-09-14CrT: Source region stuff commented out because it clutters the printout horribly:
#                                    ppsay "ds::SOURCE_CODE_REGION_FOR_DECLARATION(";

				    prettyprint_declaration'(declaration, d);

#                                    ppsay ", ";
#				     prpos (stream, source, s);		# "s" for "start"
#                                    ppsay ", ";
#				     prpos (stream, source, e);		# "e" for "end"
#                                    ppsay ")";
				};
			esac;
                  end;
	      
                  prettyprint_declaration';
	      }

	also
	fun prettyprint_package_expression (context as (_, source_opt))  stream
            =
	    {   ppsay =   pp::text  stream;
		#
		fun prettyprint_package_expression' (_, 0)
			=>
			ppsay "<package_expression>";

		    prettyprint_package_expression' (ds::PACKAGE_BY_NAME (mld::A_PACKAGE { varhome, ... } ), d)
			=>
			ppv::prettyprint_varhome stream varhome;

		    prettyprint_package_expression'
			(
			    ds::COMPUTED_PACKAGE {
				a_generic        => mld::GENERIC   { varhome => fa, ... },
				generic_argument => mld::A_PACKAGE { varhome => sa, ... },
				...
			    },
			    d
			)
			=>
			{   ppv::prettyprint_varhome stream fa;
			    ppsay"(";
			    ppv::prettyprint_varhome stream sa;
			    ppsay")";
			};

		    prettyprint_package_expression' (ds::PACKAGE_DEFINITION namings, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "pkg"; uj::newline_indent stream 2;
			    ppsay "...";
			    #  unparse_naming not yet undefined 
			    /*
			       uj::unparse_sequence stream
				 { separator  =>  pp::newline,
				   print_one  =>  (fn stream => fn b => unparse_naming context stream (b, d - 1)),
				   breakstyle =>  uj::ALIGN
				 }
			       namings;
			     */
			    ppsay "end";
			    pp::end_box stream;
			};

		    prettyprint_package_expression' (ds::PACKAGE_LET { declaration, expression }, d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "stipulate ";
			    pp::newline stream;
                            prettyprint_declaration context stream (declaration, d - 1); 
			    pp::newline stream;
			    ppsay "herein";
                            pp::newline stream;
                            prettyprint_package_expression'(expression, d - 1);
                            pp::newline stream;
			    ppsay "end;";
			    pp::end_box stream;
			};

		    prettyprint_package_expression' (ds::SOURCE_CODE_REGION_FOR_PACKAGE (body, (s, e)), d)
			=>
			case source_opt
			    #			  
			    THE source
				=>
				{
#				    2007-09-14CrT: Source region stuff commented out because it clutters the printout horribly:
#                                   ppsay "SOURCE_CODE_REGION_FOR_PACKAGE(";

				    prettyprint_package_expression'(body, d);

#				    ppsay ", ";
#				    prpos (stream, source, s);			# "s" for "start"
#				    ppsay ", ";
#				    prpos (stream, source, e);			# "e" for "end"
#				    ppsay ")";
				};

			    NULL =>   prettyprint_package_expression'(body, d);
			esac;

		    prettyprint_package_expression' _
			=>
			bug "unexpected package expression in prettyprintStrexp'";
                end;

	    
                prettyprint_package_expression';
	    }

	also
	fun prettyprint_generic_expression (context as (_, source_opt)) stream
            = 
            prettyprint_generic_expression'
            where
	        ppsay   =   pp::text stream;

		fun prettyprint_generic_expression' (_, 0)
			=>
			ppsay "<generic_expression>";

		    prettyprint_generic_expression' (ds::GENERIC_BY_NAME (mld::GENERIC { varhome, ... } ), d)
			=>
			ppv::prettyprint_varhome stream varhome;

		    prettyprint_generic_expression' (ds::GENERIC_DEFINITION { parameter=>mld::A_PACKAGE { varhome, ... }, definition=>def, ... }, d)
			=>
			{   ppsay " GENERIC("; 
			    ppv::prettyprint_varhome  stream  varhome;
			    ppsay ") => "; pp::newline stream;
			    prettyprint_package_expression context stream (def, d - 1);
			};

		    prettyprint_generic_expression' (ds::GENERIC_LET (declaration, body), d)
			=>
			{   uj::open_style_box uj::ALIGN stream (pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 });
			    ppsay "stipulate ";
                            prettyprint_declaration context stream (declaration, d - 1); 
			    pp::newline stream;
			    ppsay "herein";
			    pp::newline stream;
                            prettyprint_generic_expression'(body, d - 1);
                            pp::newline stream;
			    ppsay "end;";
			    pp::end_box stream;
			};

		    prettyprint_generic_expression' (ds::SOURCE_CODE_REGION_FOR_GENERIC (body, (s, e)), d)
			=>
			case source_opt
			    #			  
			    THE source
				=>
				{
#				    2007-09-14CrT: Source region stuff commented out because it clutters the printout horribly:
#				    ppsay "SOURCE_CODE_REGION_FOR_GENERIC(";

				    prettyprint_generic_expression'(body, d); ppsay ", ";

#				    prpos (stream, source, s); ppsay ", ";
#				    prpos (stream, source, e); ppsay ")";
				};

			    NULL =>   prettyprint_generic_expression'(body, d);
			esac;

		    prettyprint_generic_expression' _
			=>
			bug "unexpected generic package expression in prettyprint_generic_expression'";
                end;
	    end;
    };					#  package unparse_deep_syntax 
end;					#  top-level stipulate









##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Copyright 1992 by AT&T Bell Laboratories 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
