## prettyprint-raw-syntax.pkg
## Jing Cao and Lukasz Ziarek 

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

# We refer to a literal dump of the raw syntax tree as "prettyprinting".
# We refer to reconstruction of surface syntax from the raw syntax tree as "unparsing".
# Unparsing is good for end-user diagnostics; prettyprinting is good for compiler debugging.
# This is the implementation of our raw syntax prettyprinter.
# For our raw syntax unparser, see  src/lib/compiler/front/typer/print/unparse-raw-syntax.pkg

# 2008-01-08 CrT: This file is a quick clone-and-tweak
#                 conversion of unparse-raw-syntax.pkg.
#
#                 It needs a lot more work to be a full
#                 prettyprinter, starting with doing the
#                 clone-and-convert dance on the unparse_type
#                 and unparse_value packages.



stipulate
    package err =  error_message;			# error_message			is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    package fxt =  fixity;				# fixity			is from   src/lib/compiler/front/basics/map/fixity.pkg
    package mld =  module_level_declarations;		# module_level_declarations	is from   src/lib/compiler/front/typer-stuff/modules/module-level-declarations.pkg
    package mtt =  more_type_types;			# more_type_types		is from   src/lib/compiler/front/typer/types/more-type-types.pkg
    package pp  =  standard_prettyprint_mill;		# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package rs  =  raw_syntax;				# raw_syntax			is from   src/lib/compiler/front/parser/raw-syntax/raw-syntax.pkg
    package sci =  sourcecode_info;			# sourcecode_info		is from   src/lib/compiler/front/basics/source/sourcecode-info.pkg
    package sy  =  symbol;				# symbol			is from   src/lib/compiler/front/basics/map/symbol.pkg
    package tc  =  typer_control;			# typer_control			is from   src/lib/compiler/front/typer/basics/typer-control.pkg
    package tpl =  tuples;				# tuples			is from   src/lib/compiler/front/typer-stuff/types/tuples.pkg
    package uj  =  unparse_junk;			# unparse_junk			is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
#   package ut  =  unparse_type;			# unparse_type			is from   src/lib/compiler/front/typer/print/unparse-type.pkg
#   package uv  =  unparse_value;			# unparse_value			is from   src/lib/compiler/front/typer/print/unparse-value.pkg
#   package vac =  variables_and_constructors;		# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
herein


    package   prettyprint_raw_syntax
    : (weak)  Prettyprint_Raw_Syntax			# Prettyprint_Raw_Syntax	is from   src/lib/compiler/front/typer/print/prettyprint-raw-syntax.api
    {
	internals =  tc::internals;
	#
	lineprint =  REF FALSE;

	fun by f x y
            =
            f y x;

	null_fix = fxt::INFIX (0, 0);
	inf_fix  = fxt::INFIX (1000000, 100000);

	fun stronger_l (fxt::INFIX(_, m), fxt::INFIX (n, _)) => m >= n;
	    stronger_l _ => FALSE;			#  should not matter 
        end;

	fun stronger_r (fxt::INFIX(_, m), fxt::INFIX (n, _)) => n > m;
 	    stronger_r _ => TRUE; 			#  should not matter 
        end;

	fun prpos ( pp:		pp::Prettyprint_Mill,
		    source:	sci::Sourcecode_Info,
                    charpos:	Int
                  )
            =
	    if *lineprint
		#
		(sci::filepos  source  charpos)
		    ->
		    (file: String,  line: Int,  pos: Int);

		pp::lit pp (int::to_string line);
		pp::lit pp ".";
		pp::lit pp (int::to_string pos);
	    else
                pp::lit pp (int::to_string charpos);
            fi;


	fun bug msg
            =
            err::impossible("unparse_raw_syntax: " + msg);


	arrow_stamp =  mtt::arrow_stamp;


	fun strength type
            =
	    case type
		#              
		rs::TYPEVAR_TYPE(_) =>   1;
		#
		rs::TYPE_TYPE (typ, args)
		    => 
		    case typ
			#
			[typ]
			    =>
			    if (sy::eq (sy::make_type_symbol("->"), typ))   0;
			    else                                               2;
			   fi;

			_   => 2;
		    esac;


		rs::RECORD_TYPE _ => 2;

		rs::TUPLE_TYPE _ => 1;

		_ => 2;
            esac;


	fun checkpat (n, NIL)
                =>
                TRUE;

	    checkpat (n, (symbol, _) ! fields)
                =>
                sy::eq (symbol, tpl::number_to_label n)
                and
                checkpat (n+1, fields);
        end;

	fun checkexp (n, NIL)
                =>
                TRUE;

	    checkexp (n, (symbol, expression) ! fields)
		=>
		sy::eq (symbol, tpl::number_to_label n)
                and
		checkexp (n+1, fields);
        end;

	fun is_tuplepat (rs::RECORD_PATTERN { definition => [_], ...                     } ) =>  FALSE;
	    is_tuplepat (rs::RECORD_PATTERN { definition => defs, is_incomplete => FALSE } ) =>  checkpat (1, defs);
	    is_tuplepat _                                                                    =>  FALSE;
        end;

	fun is_tupleexp (rs::RECORD_IN_EXPRESSION [_])      =>   FALSE;
	    is_tupleexp (rs::RECORD_IN_EXPRESSION fields)   =>   checkexp (1, fields);
	    #
	    is_tupleexp (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _))       =>   is_tupleexp a;
	    is_tupleexp _ => FALSE;
        end;

	fun get_fix (dictionary, symbol)
            =
	    find_in_symbolmapstack::find_fixity_by_symbol  (
                dictionary,
                sy::make_fixity_symbol (sy::name symbol)
            );



	fun strip_source_code_region_data (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _))
		=>
		strip_source_code_region_data a;

	    strip_source_code_region_data x
		=>
		x;
        end;



	fun trim     [x] =>  [];
	    trim (a ! b) =>  a ! trim b;
	    trim      [] =>  [];
        end;


	fun pp_path  pp  symbols
            =
	    {   fun print_one  pp  symbol
                    =
                    uj::unparse_symbol  pp  symbol;
	    
                uj::unparse_sequence
                    pp
		    { separator =>  (fn pp =  (pp::lit pp "::")),	# Was "."
		      print_one,
		      breakstyle =>  uj::ALIGN
                    }
		    symbols;
	    };

	fun prettyprint_pattern (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =   pp::lit  pp;
		#
		pp_symbol_list =   pp_path  pp;

		fun prettyprint_pattern' (          rs::WILDCARD_PATTERN,       _)   =>     ppsay           "rs::WILDCARD_PATTERN ";
		    prettyprint_pattern' (       rs::VARIABLE_IN_PATTERN  p,    d)   =>   { ppsay        "rs::VARIABLE_IN_PATTERN ";  pp_symbol_list p; };
		    prettyprint_pattern' (   rs::INT_CONSTANT_IN_PATTERN  i,    _)   =>   { ppsay    "rs::INT_CONSTANT_IN_PATTERN ";  ppsay (multiword_int::to_string i); };
		    prettyprint_pattern' (   rs::UNT_CONSTANT_IN_PATTERN  w,    _)   =>   { ppsay    "rs::UNT_CONSTANT_IN_PATTERN ";  ppsay (multiword_int::to_string w); };
		    prettyprint_pattern' (rs::STRING_CONSTANT_IN_PATTERN  s,    _)   =>   { ppsay "rs::STRING_CONSTANT_IN_PATTERN ";  uj::unparse_mlstring  pp s; };
		    prettyprint_pattern' (  rs::CHAR_CONSTANT_IN_PATTERN  s,    _)   =>   { ppsay   "rs::CHAR_CONSTANT_IN_PATTERN ";   uj::unparse_mlstring' pp s;};

		    prettyprint_pattern' (rs::AS_PATTERN { variable_pattern, expression_pattern }, d)
			=>
			{   pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
                            ppsay "rs::AS_PATTERN ";
			    prettyprint_pattern'(variable_pattern, d);
                            ppsay " as ";
                            prettyprint_pattern'(expression_pattern, d - 1);
			    pp::shut_box pp;
			};

		    prettyprint_pattern' (rs::RECORD_PATTERN { definition => [],   is_incomplete }, _)
			=>
                        {   ppsay "rs::RECORD_PATTERN ";
			    if   is_incomplete      ppsay "{... } (==incomplete)";
			    else                    ppsay "() (==complete)";
                            fi;
                        };

		    prettyprint_pattern' (r as rs::RECORD_PATTERN { definition, is_incomplete }, d)
			=>
                        {
			    ppsay "rs::RECORD_PATTERN ";
			    #
			    if (is_tuplepat r)
				#			        
				uj::unparse_closed_sequence
				    pp
				    { front       =>  (by pp::lit "("),
				      separator   =>  (fn pp
							     =
							     {   pp::lit pp ", ";
								 pp::break pp { spaces=>0, indent_on_wrap=>0 };
							     }
							 ),
				      back       =>  (by pp::lit ")"),
				      print_one  =>  (fn _ = fn (symbol, pattern) = prettyprint_pattern' (pattern, d - 1)),
				      breakstyle =>  uj::ALIGN
				    }
				    definition;
			    else
				uj::unparse_closed_sequence
				    pp
				    { front     =>  (by pp::lit "{ "),
				      separator =>  (fn pp =  { pp::lit pp ", ";
						       pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}),
				      back      =>  (fn pp =  if is_incomplete  pp::lit pp ", ... }";
						       else pp::lit pp "}";fi),
				      print_one =>  (fn pp =  fn (symbol, pattern) =  {   uj::unparse_symbol pp symbol;
											      pp::lit pp "=";
											      prettyprint_pattern' (pattern, d - 1);
											  }
						   ),
				     breakstyle => uj::ALIGN
				    }
				    definition;
			    fi;
			};

		    prettyprint_pattern' (rs::LIST_PATTERN NIL, d)
                        =>
                        ppsay "rs::LIST_PATTERN []";

		    prettyprint_pattern' (rs::LIST_PATTERN l, d)
			=>	
			{   ppsay "rs::LIST_PATTERN ";
			    #
                            fun print_one _ pattern
                                =
                                prettyprint_pattern' (pattern, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "["),
				  separator  =>  (fn pp = { pp::lit pp ", ";
								pp::break pp { spaces=>0, indent_on_wrap=>0 };
							      }
						 ),
				  back       =>  (by pp::lit "]"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				l;
			};

		    prettyprint_pattern' (rs::TUPLE_PATTERN t, d)
			=> 
			{   ppsay "rs::TUPLE_PATTERN "; 
			    #
                            fun print_one _ pattern
                                =
                                prettyprint_pattern'(pattern, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "("),
				  separator  =>  (fn pp =   {   pp::lit pp ", ";
								   pp::break pp { spaces=>0, indent_on_wrap=>0 };
							       }
						 ),
				  back       =>  (by pp::lit ")"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				t;
			};

		    prettyprint_pattern' (rs::PRE_FIXITY_PATTERN fap, d)
			=>
			{   ppsay "rs::PRE_FIXITY_PATTERN ";
			    #
                            fun print_one _ { item, fixity, source_code_region }
                                =
                                prettyprint_pattern'(item, d - 1);		

			    uj::unparse_sequence
				pp
				{   separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				    print_one,
				    breakstyle =>  uj::ALIGN
				}
				fap;
			}; 

		    prettyprint_pattern' (rs::APPLY_PATTERN { constructor, argument }, d)
			=> 
			{   ppsay "rs::APPLY_PATTERN ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_pattern' (constructor, d);
			    ppsay " as ";
			    prettyprint_pattern'(argument, d);
			    pp::shut_box pp;
			};

		    prettyprint_pattern' (rs::TYPE_CONSTRAINT_PATTERN { pattern, type_constraint }, d)
			=> 
			{   ppsay "rs::TYPE_CONSTRAINT_PATTERN ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    prettyprint_pattern' (pattern, d - 1);
			    ppsay " :";
			    pp::break pp { spaces => 1,   indent_on_wrap => 2 };
			    prettyprint_type context pp (type_constraint, d);
			    pp::shut_box pp;
			};

		    prettyprint_pattern' (rs::VECTOR_PATTERN NIL, d)
			=>
			ppsay "rs::VECTOR_PATTERN #[]";

		    prettyprint_pattern' (rs::VECTOR_PATTERN v, d)
			=> 
			{   ppsay "rs::VECTOR_PATTERN ";
			    #
                            fun print_one _ pattern
                                =
                                prettyprint_pattern'(pattern, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front     =>  (by pp::lit "#["),
				  separator =>  (fn pp => { pp::lit pp ", ";pp::break pp { spaces=>1, indent_on_wrap=>0 } ;}; end ),
				  back      =>  (by pp::lit "]"),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				v;
			 };

		    prettyprint_pattern' (rs::SOURCE_CODE_REGION_FOR_PATTERN (pattern, (s, e)), d)
			=> 
			case source_opt
			    #
			    THE source
				=>
				{
# Commented out to reduce verbosity:
#				      ppsay "SOURCE_CODE_REGION_FOR_PATTERN [";
#				      prpos (pp, source, s); ppsay ", ";
#				      prpos (pp, source, e); ppsay "): ";

				     prettyprint_pattern'(pattern, d);

#                                      ppsay "]";
				};

			    NULL => {   ppsay "SOURCE_CODE_REGION_FOR_PATTERN [] ";
					prettyprint_pattern'(pattern, d);
				    };
			 esac;

		    prettyprint_pattern' (rs::OR_PATTERN orpat, d)
			=>
			{   ppsay "rs::OR_PATTERN ";
			    #
                            fun print_one _ pattern
                                =
                                prettyprint_pattern'(pattern, d - 1);		

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "("),
				  separator  =>  (fn pp = { pp::break pp { spaces=>1, indent_on_wrap=>0 }; pp::lit pp "| "; }),
				  back       =>  (by pp::lit ")"),
				  print_one,
				  breakstyle => uj::ALIGN
				};
			}
                        orpat;
                end;

	    
                prettyprint_pattern';
	    }


	also
	fun prettyprint_expression (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		fun lparen () = ppsay "("; 
		fun rparen () = ppsay ")";
		#
		fun lpcond atom = if atom  ppsay "(";  fi;      
		fun rpcond atom = if atom  ppsay ")";  fi;

		pp_symbol_list = pp_path pp;

		fun prettyprint_expression' (_, _, 0) => ppsay "<rs::Expression>";
		    prettyprint_expression' (rs::VARIABLE_IN_EXPRESSION   p, _, _) => {   ppsay "rs::VARIABLE_IN_EXPRESSION ";    pp_symbol_list (p); };
		    prettyprint_expression' (rs::IMPLICIT_THUNK_PARAMETER p, _, _) => {   ppsay "rs::IMPLICIT_THUNK_PARAMETER #"; pp_symbol_list (p); };
		    prettyprint_expression' (rs::FN_EXPRESSION NIL, _,          d) =>     ppsay "FN_EXPRESSION NIL";

		    prettyprint_expression' (rs::FN_EXPRESSION rules, _, d)
			=>	
			{   ppsay "rs::FN_EXPRESSION ";
			    #
                            fun print_one _ pattern
                                =
                                prettyprint_rule context pp (pattern, d - 1);

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  { pp::lit pp "|"; pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				rules;
			};

		    prettyprint_expression' (rs::PRE_FIXITY_EXPRESSION fap, _, d)
			=> 
			{   ppsay "rs::PRE_FIXITY_EXPRESSION[ ";
			    pp::newline pp;
			    ppsay "    ";

                            fun print_one _ { item, fixity, source_code_region }
                                =
                                prettyprint_expression'(item, TRUE, d);		

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				fap;

			    pp::newline pp;
                            ppsay " ]PRE_FIXITY_EXPRESSION ";
			}; 

		    prettyprint_expression' (e as rs::APPLY_EXPRESSION _, atom, d)
			=>
			{   ppsay "rs::APPLY_EXPRESSION ";
			    #
                            infix0 = fxt::INFIX (0, 0);

			    lpcond atom;
			    prettyprint_app_expression (e, null_fix, null_fix, d);
			    rpcond atom;
			};

		    prettyprint_expression' (rs::OBJECT_FIELD_EXPRESSION { object, field }, atom, d)
			=>
			{   ppsay "rs::OBJECT_FIELD_EXPRESSION ";
                            prettyprint_expression'(object, TRUE, d - 1);
			    ppsay "->";
                            uj::unparse_symbol pp field;
			};

		    prettyprint_expression' (rs::CASE_EXPRESSION { expression, rules }, _, d)
			=> 
			{   ppsay "rs::CASE_EXPRESSION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "case (";		# Was "(case ";
                            prettyprint_expression'(expression, TRUE, d - 1);
                            pp::newline pp;		# Was newline_indent pp 2;
			    uj::ppvlist pp (
                                ") ",
                                ";",		# Was "   | ",
                                (fn pp =  fn r =  prettyprint_rule context pp (r, d - 1)),
                                trim rules
                            );
			    ppsay "esac;";	# Was rparen();
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::LET_EXPRESSION { declaration, expression }, _, d)
			=>
			{   ppsay "rs::LET_EXPRESSION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "stipulate ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_declaration context pp (declaration, d - 1); 
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    ppsay "herein ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression'(expression, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    ppsay "end";
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::SEQUENCE_EXPRESSION exps, _, d)
			=>
                        {   ppsay "rs::SEQUENCE_EXPRESSION ";
			    #
			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "("),
				  separator  =>  (fn pp => { pp::lit pp ";";
						     pp::break pp { spaces=>1, indent_on_wrap=>0 } ;}; end ),
				  back       =>  (by pp::lit ")"),
				  print_one  =>  (fn _ => fn expression => prettyprint_expression'(expression, FALSE, d - 1); end; end ),
				  breakstyle =>  uj::ALIGN
				}
				exps;
                        };

		    prettyprint_expression' (   rs::INT_CONSTANT_IN_EXPRESSION   i, _, _)   =>   { ppsay    "rs::INT_CONSTANT_IN_EXPRESSION "; ppsay (multiword_int::to_string i); };
		    prettyprint_expression' (   rs::UNT_CONSTANT_IN_EXPRESSION   w, _, _)   =>   { ppsay "   rs::UNT_CONSTANT_IN_EXPRESSION "; ppsay (multiword_int::to_string w); };
		    prettyprint_expression' ( rs::FLOAT_CONSTANT_IN_EXPRESSION   r, _, _)   =>   { ppsay  "rs::FLOAT_CONSTANT_IN_EXPRESSION "; ppsay r; };
		    prettyprint_expression' (rs::STRING_CONSTANT_IN_EXPRESSION   s, _, _)   =>   { ppsay "rs::STRING_CONSTANT_IN_EXPRESSION ";    uj::unparse_mlstring  pp s; };
		    prettyprint_expression' (  rs::CHAR_CONSTANT_IN_EXPRESSION   s, _, _)   =>   { ppsay   "rs::CHAR_CONSTANT_IN_EPXRESSION "; uj::unparse_mlstring' pp s; };

		    prettyprint_expression'(r as rs::RECORD_IN_EXPRESSION fields, _, d)
			=>
                        {   ppsay "rs::RECORD EXPRESSION ";
			    #
			    if (is_tupleexp r)
				#			        
				uj::unparse_closed_sequence
				    pp
				    { front      =>  (by pp::lit "("),
				      separator  =>  (fn pp => { pp::lit pp ", ";
							  pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}; end ),
				      back       =>  (by pp::lit ")"),
				      print_one  =>  (fn _ => fn (_, expression) => prettyprint_expression'(expression, FALSE, d - 1); end; end ),
				      breakstyle =>  uj::ALIGN
				    }
				    fields;
			    else
				uj::unparse_closed_sequence
				    pp
				    { front     =>  (by pp::lit "{ "),
				      separator =>  (fn pp => { pp::lit pp ", ";
							  pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}; end ),
				      back      =>  (by pp::lit "}"),
				      print_one =>  (fn pp = fn (name, expression)
							      =
							      {   uj::unparse_symbol pp name;
								  ppsay "=";
								  prettyprint_expression'(expression, FALSE, d);
							      }
						),
				      breakstyle =>  uj::ALIGN
				    }
				    fields;
			    fi;
			};

		    prettyprint_expression' (rs::LIST_EXPRESSION p, _, d)
			=> 
			{   ppsay "rs::LIST_EXPRESSION ";
			    #
			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "["),
				  separator  =>  (fn pp => { pp::lit pp ", ";
						    pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}; end ),
				  back       =>  (by pp::lit "]"),
				  print_one  =>  (fn pp => fn expression =>
							  (prettyprint_expression'(expression, FALSE, d - 1)); end; end ),
				  breakstyle =>  uj::ALIGN
				}
				p;
			};

		    prettyprint_expression' (rs::TUPLE_EXPRESSION p, _, d)
			=>
			{   ppsay "rs::TUPLE_EXPRESSION ";
			    #
			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "("),
				  separator  =>  (fn pp => { pp::lit pp ", ";
							   pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}; end ),
				  back       =>  (by pp::lit ")"),
				  print_one  =>  (fn pp => fn expression =>
							  (prettyprint_expression'(expression, FALSE, d - 1)); end; end ),
				  breakstyle =>  uj::ALIGN
				}
				p;
			};

		    prettyprint_expression' (rs::RECORD_SELECTOR_EXPRESSION name, atom, d)
			=>
			{   ppsay "rs::RECORD_SELECTOR_EXPRESSION( ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond (atom);
                            uj::unparse_symbol pp name;
			    rpcond (atom);
			    ppsay " )RECORD_SELECTOR_EXPRESSION ";
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::TYPE_CONSTRAINT_EXPRESSION { expression, constraint }, atom, d)
			=> 
			{   ppsay "rs::TYPE_CONSTRAINT_EXPRESSION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    lpcond (atom);
			    prettyprint_expression'(expression, FALSE, d); ppsay ":";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    prettyprint_type context pp (constraint, d);
			    rpcond (atom);
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::EXCEPT_EXPRESSION { expression, rules }, atom, d)
			=>
			{   ppsay "rs::EXCEPT_EXPRESSION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    prettyprint_expression'(expression, atom, d - 1);
                            pp::newline pp;
                            ppsay "except ";
			    uj::newline_indent pp 2;
			    uj::ppvlist pp (
                                "  ",
                                "; ",			# Was "| ",
				(fn pp =  fn r =  prettyprint_rule context pp (r, d - 1)),
                                rules
                            );
			    rpcond atom;
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::RAISE_EXPRESSION expression, atom, d)
			=> 
			{   ppsay "rs::RAISE_EXPRESSION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    ppsay "raise exception ";
                            prettyprint_expression'(expression, TRUE, d - 1);
			    rpcond atom;
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::IF_EXPRESSION { test_case, then_case, else_case }, atom, d)
			=>
			{   ppsay "rs::IF_EXPRESSION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond (atom);
			    ppsay "if ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression' (test_case, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "then ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression' (then_case, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "else ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression' (else_case, FALSE, d - 1);
			    pp::shut_box pp;
			    rpcond (atom);
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::AND_EXPRESSION (e1, e2), atom, d)
			=>
			{   ppsay "rs::AND_EXPRESSION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression' (e1, TRUE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "and ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression' (e2, TRUE, d - 1);
			    pp::shut_box pp;
			    rpcond  atom;
			    pp::shut_box pp;
			 };

		    prettyprint_expression' (rs::OR_EXPRESSION (e1, e2), atom, d)
			=>
			{   ppsay "rs::OR_EXPRESSION ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond (atom);
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression' (e1, TRUE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "or ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression' (e2, TRUE, d - 1);
			    pp::shut_box pp;
			    rpcond  atom;
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::WHILE_EXPRESSION { test, expression }, atom, d)
			=>
			{   ppsay "rs::WHILE_EXPRESSION ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "while ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression'(test, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "do ";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    prettyprint_expression'(expression, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::shut_box pp;
			};

		    prettyprint_expression' (rs::VECTOR_IN_EXPRESSION NIL, _, d)
			=>
			ppsay "rs::VECTOR_IN_EXPRESSION NIL ";

		    prettyprint_expression' (rs::VECTOR_IN_EXPRESSION exps, _, d)
			=>
			{   ppsay "rs::VECTOR EXPRESSION ";
			    #	
			    fun print_one _ expression
				=
				prettyprint_expression'(expression, FALSE, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "#["),
				  separator  =>  (fn pp => { pp::lit pp ", ";
						   pp::break pp { spaces=>1, indent_on_wrap=>0 } ;}; end ),
				  back       =>  (by pp::lit "]"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				exps;
			};

		    prettyprint_expression' (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), atom, d)
			=>
			case source_opt
			    #
			    THE source
				=>
				{
# Commented out to reduce verbosity:
#				     ppsay "rs::SOURCE_CODE_REGION_FOR_EXPRESSION [ ";
#				     prpos (pp, source, s); ppsay ", ";
#				     prpos (pp, source, e); ppsay "): ";
				     prettyprint_expression'(expression, FALSE, d);
#				     ppsay " ] ";
				};

			    NULL =>
				{
# Commented out to reduce verbosity:
#				     ppsay "rs::SOURCE_CODE_REGION_FOR_EXPRESSION [ ";
				     prettyprint_expression'(expression, atom, d);
#                                    ppsay " ] "; 
				};
			esac;
                   end 

		    also
		    fun prettyprint_app_expression (_, _, _, 0)
			    =>
			    pp::lit pp "<expression>";

			prettyprint_app_expression arg
			    =>
			    {   ppsay = pp::lit pp;
				#
				fun fixitypp (name, operand, left_fix, right_fix, d)
				    =
				    {   dname = symbol_path::to_string (symbol_path::SYMBOL_PATH name);
					#
					this_fix =  case name
							[id] =>  get_fix (dictionary, id);
							   _ =>  fxt::NONFIX;
						    esac;

					fun pr_non  expression
					    =
					    {   pp::open_box (pp, pp::typ::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
						ppsay dname;
						pp::break pp { spaces=>1, indent_on_wrap=>0 };
						prettyprint_expression'(expression, TRUE, d - 1);
						pp::shut_box pp;
					    };

					case this_fix
					    #
					    fxt::INFIX _ =>  case (strip_source_code_region_data operand)
							    #						  	
							    rs::RECORD_IN_EXPRESSION [(_, pl), (_, pr)]
								=>
								{   atom =   stronger_l (left_fix, this_fix)
									 or  stronger_r (this_fix, right_fix);

								    my (left, right)
									=
									atom  ??  (null_fix, null_fix)
									      ::  (left_fix, right_fix);

								    {   pp::open_box (pp, pp::typ::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
									lpcond atom;
									prettyprint_app_expression (pl, left, this_fix, d - 1);
									pp::break pp { spaces=>1, indent_on_wrap=>0 };
									ppsay dname;
									pp::break pp { spaces=>1, indent_on_wrap=>0 };
									prettyprint_app_expression (pr, this_fix, right, d - 1);
									rpcond atom;
									pp::shut_box pp;
								    };
								};

						    e' => pr_non e';
						esac;

					   fxt::NONFIX => pr_non operand;
				       esac;
				    };

				fun apply_print (_, _, _, 0)
					=>
					ppsay "#";

				    apply_print (rs::APPLY_EXPRESSION { function=>operator, argument=>operand }, l, r, d)
					=>
					case (strip_source_code_region_data operator)
					    #
					    rs::VARIABLE_IN_EXPRESSION v
						=>
						{   path = v;
						    #
						    fixitypp (path, operand, l, r, d);
						};

					    operator
						=>
						{   pp::open_box (pp, pp::typ::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
						    prettyprint_expression'(operator, TRUE, d - 1); pp::break pp { spaces=>1, indent_on_wrap=>2 };
						    prettyprint_expression'(operand,  TRUE, d - 1);
						    pp::shut_box pp;
						};
					esac;


				    apply_print (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), l, r, d)
					=>
					case source_opt
					    #
					    THE source
						=>
						{
# Commented out to reduce verbosity:
#						    ppsay "SOURCE_CODE_REGION_FOR_EXPRESSION [ ";
#						    prpos (pp, source, s); ppsay ", ";
#						    prpos (pp, source, e); ppsay "): ";
						    prettyprint_expression'(expression, FALSE, d);
#						    ppsay " ] ";

						};

					    NULL => apply_print (expression, l, r, d);
					esac;

				   apply_print (e, _, _, d)
				    =>
				    prettyprint_expression'(e, TRUE, d); end;

				apply_print arg;
			    };
                end;
	    
                fn (expression, depth)
                    =
                    prettyprint_expression' (expression, FALSE, depth);
	    }

	also
	fun prettyprint_rule (context as (dictionary, source_opt)) pp (rs::CASE_RULE { pattern, expression }, d)
            =
	    if (d == 0) 
                pp::lit pp "<rs::CASE_RULE>";
	    else
		pp::lit pp  "rs::CASE_RULE ";
		pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
		prettyprint_pattern context pp (pattern, d - 1);
		pp::lit pp " =>"; pp::break pp { spaces=>1, indent_on_wrap=>2 };
		prettyprint_expression context pp (expression, d - 1);
		pp::shut_box pp;
            fi

	also
	fun prettyprint_package_expression (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		pp_symbol_list = pp_path pp;

		fun  prettyprint_package_expression'(_, 0)
			 =>
			 ppsay "<package_expression>";

		     prettyprint_package_expression' (rs::PACKAGE_BY_NAME p, d)
			 =>
                         {   ppsay "rs::PACKAGE_BY_NAME ";
			     pp_symbol_list (p);
                         };

		     prettyprint_package_expression' (rs::PACKAGE_DEFINITION (rs::SEQUENTIAL_DECLARATIONS NIL), d)
			 =>
			 {   ppsay "rs::PACKAGE_DEFINITION (rs::SEQUENTIAL_DECLARATIONS_NIL) ";
                             pp::nonbreakable_spaces pp 1;
                             ppsay "end";
                         };

		     prettyprint_package_expression' (rs::PACKAGE_DEFINITION de, d)
			 =>
			 {   
                             pp::newline pp;
			     ppsay "rs::PACKAGE_DEFINITION[";
			     pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
                             pp::newline pp;
                             unparse_junk::newline_indent pp 2;
			     prettyprint_declaration context pp (de, d - 1);
			     pp::shut_box pp;
                             pp::newline pp;
			     ppsay "rs::]PACKAGE_DEFINITION";
                             pp::newline pp;
			 };

		     prettyprint_package_expression' (rs::PACKAGE_CAST (stre, constraint), d)
			 =>
			 {   ppsay "rs::PACKAGE_CAST ";
                             pp::open_box (pp, pp::typ::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			     prettyprint_package_expression' (stre, d - 1);

			     case constraint
			         #
				 rs::NO_PACKAGE_CAST
				     =>
				     ppsay "rs::NO_PACKAGE_CAST ";

				 rs::WEAK_PACKAGE_CAST api_expression
				     => 
				     {   ppsay "rs::WEAK_PACKAGE_CAST :";
					 pp::break pp { spaces=>1, indent_on_wrap=>2 };
					 prettyprint_api_expression context pp (api_expression, d - 1);
				     };

				 rs::PARTIAL_PACKAGE_CAST api_expression
				     => 
				     {   ppsay "rs::PARTIAL_PACKAGE_CAST :";
					 pp::break pp { spaces=>1, indent_on_wrap=>2 };
					 prettyprint_api_expression context pp (api_expression, d - 1);
				     };

				 rs::STRONG_PACKAGE_CAST api_expression
				     => 
				     {   ppsay "rs::STRONG_PACKAGE_CAST :>";
					 pp::break pp { spaces=>1, indent_on_wrap=>2 };
					 prettyprint_api_expression context pp (api_expression, d - 1);
				     };
			     esac;

			     pp::shut_box  pp;
			 };

		     prettyprint_package_expression' (rs::CALL_OF_GENERIC (path, str_list), d)
			 => 
			 {   ppsay "rs::CALL_OF_GENERIC ";
			     #
                             fun print_one pp (strl, bool)
				 =
				 {   ppsay "("; prettyprint_package_expression context pp (strl, d); ppsay ")";};

			     pp_symbol_list (path);

			     uj::unparse_sequence
				 pp
				 { separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				   print_one,
				   breakstyle =>  uj::ALIGN
				 }
				 str_list;
			   };	

		     prettyprint_package_expression' (rs::INTERNAL_CALL_OF_GENERIC (path, str_list), d)
			 => 
			 {   ppsay "rs::INTERNAL_CALL_OF_GENERIC ";
			     #
                             fun print_one pp (strl, bool)
				 =
				   {   ppsay "(";
                                       prettyprint_package_expression context pp (strl, d);
                                       ppsay ")";
                                   };

			     pp_symbol_list (path);

			     uj::unparse_sequence
				 pp
				 { separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				   print_one,
				   breakstyle =>  uj::ALIGN
				 }
				 str_list;
			 };	

		     prettyprint_package_expression' (rs::LET_IN_PACKAGE (declaration, body), d)
			 =>
			 {
			     pp::newline pp;
			     ppsay "rs::LET_IN_PACKAGE[ ";
			     pp::newline pp;
			     pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );	
			     pp::newline pp;
			     ppsay "    ";
                             prettyprint_declaration context pp (declaration, d - 1); 
			     pp::newline pp;
			     pp::shut_box pp;
			     pp::newline pp;
			     ppsay ";LET_IN_PACKAGE ";
			     pp::newline pp;
			     pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
			     ppsay "    ";
                             prettyprint_package_expression'(body, d - 1);
			     pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
                             pp::newline pp;
			     pp::shut_box pp;
                             pp::newline pp;
			     ppsay " ]LET_IN_PACKAGE ";
			 };

		     prettyprint_package_expression' (rs::SOURCE_CODE_REGION_FOR_PACKAGE (body, (s, e)), d)
			 =>
                         {
# Commented out to reduce verbosity:
#			     ppsay "rs::SOURCE_CODE_REGION_FOR_PACKAGE (...) ";
			     prettyprint_package_expression' (body, d);
                         };
                 end;

	/*	      (case source_opt
			of THE source =>
			   (ppsay "rs::SOURCE_CODE_REGION_FOR_PACKAGE(";
			      prettyprintPackageexpression'(body, d); ppsay ", ";
			      prpos (pp, source, s); ppsay ", ";
			      prpos (pp, source, e); ppsay ")")
			 | NULL => prettyprintPackageexpression'(body, d))
	*/
	    
		prettyprint_package_expression';
	    }

	also
	fun prettyprint_generic_expression (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		pp_symbol_list = pp_path pp;

		fun prettyprint_generic_expression'(_, 0)
                        =>
                        ppsay "<generic_expression>";

		    prettyprint_generic_expression' (rs::GENERIC_BY_NAME (p, _), d)
                        =>
                        {   ppsay "rs::GENERIC_BY_NAME ";
			    #
                            pp_symbol_list (p);
                        };

		    prettyprint_generic_expression'(rs::LET_IN_GENERIC (declaration, body), d)
			=>
			{   ppsay "rs::LET_IN_GENERIC ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "stipulate ";
                            prettyprint_declaration context pp (declaration, d - 1); 
			    pp::newline pp;
			    ppsay " herein ";
                            prettyprint_generic_expression'(body, d - 1);
                            pp::newline pp;
			    ppsay "end";
			    pp::shut_box pp;
			};

		    prettyprint_generic_expression' (rs::CONSTRAINED_CALL_OF_GENERIC (path, sblist, fsigconst), d)
			=>
			{   ppsay "rs::CONSTRAINED_GENERIC ";

                            fun print_one pp (package_expression, _)
				=
				{   ppsay "(";
				    prettyprint_package_expression context pp (package_expression, d);
				    ppsay ")";
				};

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    pp_symbol_list path;

			    uj::unparse_sequence
				pp
				{ separator =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sblist;

			    pp::shut_box pp;
			};

		    prettyprint_generic_expression' (rs::SOURCE_CODE_REGION_FOR_GENERIC (body, (s, e)), d)
			=>
                        {   ppsay "rs::SOURCE_CODE_REGION_FOR_GENERIC (...) ";
			    #
			    prettyprint_generic_expression' (body, d);
                        };


		    prettyprint_generic_expression' (rs::GENERIC_DEFINITION _, d)
			=>
                        {   ppsay "rs::GENERIC DEFINITION <- NOT LEGAL HERE!! ";
                        };
                end;
	    
		prettyprint_generic_expression';
	    }

	also
	fun prettyprint_where_spec (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;

		fun prettyprint_where_spec'(_, 0)
                        =>
                        ppsay "<WhereSpec>";


		    prettyprint_where_spec' (rs::WHERE_TYPE([],[], type), d)
                        =>
                        {   ppsay "rs::WHERE TYPE ";
			    #
                            prettyprint_type context pp (type, d);
                        };

		    prettyprint_where_spec' (rs::WHERE_TYPE (slist, tvlist, type), d)
			=> 
			{   ppsay "rs::WHERE_TYPE ";
			    #
                            fun print_one _ symbol
				=
				uj::unparse_symbol pp symbol;

			    fun print_one' _ tyv
				=
				prettyprint_typevar context pp (tyv, d);

			    ppsay "typeX ";

			    uj::unparse_sequence
				pp
				{ separator =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one =>  print_one',
				  breakstyle =>  uj::ALIGN
				}
				tvlist;

			    pp::break pp { spaces=>1, indent_on_wrap=>0 };

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist;	 

			    ppsay" =";
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    prettyprint_type context pp (type, d);
			};

		    prettyprint_where_spec' (rs::WHERE_PACKAGE (slist, slist'), d)
			=>
			{   ppsay "rs::WHERE_PACKAGE ";
			    #
                            fun print_one _ symbol
				=
				uj::unparse_symbol pp symbol;

			    ppsay "packageZ ";

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist;  pp::break pp { spaces=>1, indent_on_wrap=>0 };

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist';
			};
                end;
	    
		prettyprint_where_spec';
	    }

	also
	fun prettyprint_api_expression (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		fun prettyprint_api_expression'(_, 0)
                        =>
                        ppsay "<api_expression>";

		    prettyprint_api_expression' (rs::API_BY_NAME s, d)
                        =>
                        {   ppsay "rs::API_BY_NAME ";
			    #
                            uj::unparse_symbol  pp  s;
                        }; 

		    prettyprint_api_expression' (rs::API_WITH_WHERE_SPECS (an_api, wherel), d)
			=>
			{   ppsay "rs::API_WITH_WHERE_SPECS ";
                            prettyprint_api_expression' (an_api, d);
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };

			    case an_api
			        #
				rs::API_BY_NAME s
				    =>
				    {   ppsay "rs::API_BY_NAME ";
					#
					uj::ppvlist pp (
					    "where ",
					    "also ",
					    (fn pp =  fn r =  prettyprint_where_spec context pp (r, d - 1)),
					    wherel
					);
				    };

				rs::SOURCE_CODE_REGION_FOR_API (rs::API_BY_NAME s, r)
				    =>
				    {   ppsay "rs::SOURCE_CODE_REGION_FOR_API ";
					#
					uj::ppvlist pp (
					    "where ",
					    "also ",
					    (fn pp =  fn r =  prettyprint_where_spec context pp (r, d - 1)),
					    wherel
					);
				    };
				_
				=>
				{   pp::newline pp;
				    #
				    uj::ppvlist pp (
					"where ",
					"also ",
					(fn pp =  fn r =  prettyprint_where_spec context pp (r, d - 1)),
					wherel
				    );
				};
			    esac;
			};

		    prettyprint_api_expression' (rs::API_DEFINITION [], d)
			=>	
			{   ppsay "rs::API_DEFINITION ";
                            ppsay "api";
			    pp::nonbreakable_spaces pp 1;
			    ppsay"end;";
			};

		    prettyprint_api_expression' (rs::API_DEFINITION specl, d)
			=> 
			{   ppsay "rs::API_DEFINITION ";
			    #
                            fun print_one pp speci
                                =
                                prettyprint_specification context pp (speci, d);

			    pp::newline pp; # XXX BUGGO TEST ONLY
			    ppsay "api";


			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::vertical,	100 );

			    pp::newline pp;
#			    unparse_junk::newline_indent pp 4;

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  pp::newline pp),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				specl;

			    pp::shut_box pp;

			    pp::newline pp;
			    ppsay "end;";
			};

		    prettyprint_api_expression' (rs::SOURCE_CODE_REGION_FOR_API (m, r), d)
                        =>
                        {   ppsay "rs::SOURCE_CODE_REGION_FOR_API (...) ";
                            prettyprint_api_expression context pp (m, d);
                        };
                end;
	    
		prettyprint_api_expression';
	    }

	also
	fun prettyprint_generic_api_expression (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =  pp::lit pp;
		#
		fun prettyprint_generic_api_expression'(_, 0)
                        =>
                        ppsay "<generic_api_expression>";

		    prettyprint_generic_api_expression' (rs::GENERIC_API_BY_NAME s, d)
                        =>
                        {   ppsay "rs::GENERIC_API_BY_NAME ";
			    #
                            uj::unparse_symbol pp s;
                        };

		    prettyprint_generic_api_expression' (rs::GENERIC_API_DEFINITION { parameter, result }, d)
			=>
			{   ppsay "rs::GENERIC_API_DEFINITION ";
			    #
                            fun print_one pp (THE symbol, api_expression)
				    =>
				    {   ppsay "(";
                                        uj::unparse_symbol pp symbol;
                                        ppsay ":";
					prettyprint_api_expression context pp (api_expression, d);
					ppsay ")";
				    };

			        print_one pp (NULL, api_expression)
				    =>
				    {   ppsay "("; prettyprint_api_expression context pp (api_expression, d);
                                        ppsay ")";
                                    };
                            end;

			    uj::unparse_sequence
				pp
				{ separator  => (fn pp =  (pp::newline pp)),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				parameter;

			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    ppsay "=> ";
			    prettyprint_api_expression context pp (result, d);
			};

		    prettyprint_generic_api_expression' (rs::SOURCE_CODE_REGION_FOR_GENERIC_API (m, r), d)
			=>
                        {   ppsay "rs::SOURCE_CODE_REGION_FOR_GENERIC_API (...) ";
			    #
			    prettyprint_generic_api_expression context pp (m, d);
                        };
                end;
	    
		prettyprint_generic_api_expression';
	    }

	also
	fun prettyprint_specification (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;

		fun pp_tyvar_list ([], d)
                        =>
                        ();

		    pp_tyvar_list ( [typevar], d)
			=> 
			{   prettyprint_typevar context pp (typevar, d);
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			};

		    pp_tyvar_list (tyvar_list, d)
			=> 
			{   fun print_one _ (typevar)
				=
				(prettyprint_typevar context pp (typevar, d));

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (fn pp => pp::lit pp "("; end ),
				  separator  =>  { pp::lit pp ", ";fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ;},
				  back       =>  { pp::lit pp ")";fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ;},
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				tyvar_list;
			};
                end;

		fun prettyprint_specification'(_, 0)
                        =>
                        ppsay "<Specification>";

		    prettyprint_specification' (rs::PACKAGES_IN_API sspo_list, d)
			=>
			{   ppsay "rs::PACKAGES_IN_API ";
			    #
                            fun print_one _ (symbol, api_expression, path)
				=
				case path
				    #				  
				    THE p => {   uj::unparse_symbol pp symbol;
						 ppsay " = ";
						 prettyprint_api_expression context pp (api_expression, d);
						 pp::break pp { spaces=>1, indent_on_wrap=>0 };
						 pp_path pp p;
					     };

				    NULL  => {   uj::unparse_symbol pp symbol;
						 ppsay " = ";
						 prettyprint_api_expression context pp (api_expression, d);
					     };
				esac;

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "packageY "),
				  separator  =>  (fn pp
						     =
						     {   pp::lit pp ", ";
							 pp::break pp { spaces=>1, indent_on_wrap=>0 };
						     }
						 ),
				  back       =>  (by pp::lit ""),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sspo_list;
			}; 

		    prettyprint_specification' (rs::TYPES_IN_API (stto_list, bool), d)
			=> 
			{   ppsay "rs::TYPES_IN_API ";
			    #
                            fun print_one _ (symbol, tyvar_list, tyo)
				=
				case tyo
				    #
				    THE type => {   pp_tyvar_list (tyvar_list, d);
						    uj::unparse_symbol pp symbol;
						    ppsay " =  ";
						    prettyprint_type context pp (type, d);
						};

				    NULL =>     {    pp_tyvar_list (tyvar_list, d);
						     uj::unparse_symbol pp symbol;
						};
				esac;


			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit ""),			# Was "type "
				  separator  =>  fn pp =  {   pp::lit pp "|";
								  pp::newline pp;
							      },
				  back       =>  (by pp::lit ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				stto_list;
			}; 

		    prettyprint_specification' (rs::GENERICS_IN_API sf_list, d)
			=>
			{   ppsay "rs::GENERICS_IN_API ";
			    #
                            fun pr pp (symbol, generic_api_expression)
				=
				{   uj::unparse_symbol pp symbol;
                                    ppsay " : ";
				    prettyprint_generic_api_expression context pp (generic_api_expression, d - 1);
				}; 

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("generic package ", "also ", pr, sf_list);
			    pp::shut_box pp;
			}; 

		    prettyprint_specification' (rs::VALUES_IN_API st_list, d)
			=> 
			{   ppsay "rs::VALUES_IN_API ";
			    #
                            fun pr pp (symbol, type)
				= 
				{   uj::unparse_symbol pp symbol;
                                    ppsay ":   ";
                                    prettyprint_type context pp (type, d);
                                }; 

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp (
                                "",		# Was "my ",
                                "also ",
                                pr, 
                                st_list
                            );
                            ppsay "; ";
			    pp::shut_box pp;
			}; 

		    prettyprint_specification' (rs::VALCONS_IN_API { sumtypes, with_types => [] }, d)
			=> 
			{   ppsay "rs::VALCONS_IN_API ";
			    #
                            fun pr pp (dbing)
				=
				(prettyprint_sumtype context pp (dbing, d));

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("", "also ", pr, sumtypes);
			    pp::shut_box pp;
			}; 

		    prettyprint_specification' (rs::VALCONS_IN_API { sumtypes, with_types }, d)
			=> 
			{   ppsay "rs::VALCONS_IN_API ";
			    #
                            fun prd pp (dbing) = (prettyprint_sumtype context pp (dbing, d));
			    fun prw pp (tbing) = (prettyprint_named_type context pp (tbing, d));

			    {   pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				uj::ppvlist pp ("", "also ", prd, sumtypes);
				pp::newline pp;
				uj::ppvlist pp ("", "also ", prw, with_types);
				pp::shut_box pp;
			    };
			};

		    prettyprint_specification' (rs::EXCEPTIONS_IN_API sto_list, d)
			=> 
			{   ppsay "rs::EXCEPTIONS_IN_API ";
			    #
                            fun pr pp (symbol, tyo)
				=
				case tyo
				    #
				    THE type => {   uj::unparse_symbol pp symbol;
						    ppsay " : ";
						    prettyprint_type context pp (type, d);
						};

				    NULL =>     uj::unparse_symbol pp symbol;
				esac;

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("exception ", "also ", pr, sto_list);
			    pp::shut_box pp;
			}; 

		    prettyprint_specification' (rs::PACKAGE_SHARING_IN_API paths, d)
			=> 
			{   ppsay "rs::PACKAGE_SHARING_IN_API ";
			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("sharing ", " = ", pp_path, paths);
			    pp::shut_box pp;
			};

		    prettyprint_specification' (rs::TYPE_SHARING_IN_API paths, d)
			=> 
			{   ppsay "rs::TYPE_SHARING_IN_API ";
			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("sharing ", " = ", pp_path, paths);
			    pp::shut_box pp;
			};

		    prettyprint_specification' (rs::IMPORT_IN_API api_expression, d)
			=>
                        {   ppsay "rs::IMPORT_IN_API ";
			    prettyprint_api_expression context pp (api_expression, d);
                        };

		    prettyprint_specification' (rs::SOURCE_CODE_REGION_FOR_API_ELEMENT (m, r), d)
			=>
                        {   ppsay "rs::SOURCE_CODE_REGION_FOR_API_ELEMENT ";
			    prettyprint_specification context pp (m, d);
                        };
                end;
	    
		prettyprint_specification';
	    }

	also
	fun prettyprint_declaration (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		pp_symbol_list =  pp_path  pp;

		fun prettyprint_declaration'(_, 0)
                        =>
                        ppsay "<declaration>";

		    prettyprint_declaration' (rs::VALUE_DECLARATIONS (vbs, typevars), d)
			=>
			{
			    pp::newline pp;
			    pp::newline pp;
			    ppsay "rs::VALUE_DECLARATIONS";
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
			    uj::ppvlist pp (
                                "    [ ",
                                " ;rs::VALUE_DECLARATIONS ",
                                (fn pp =  fn named_value =  prettyprint_named_value context pp (named_value, d - 1)),
                                vbs
                            );
			    pp::shut_box pp;
			    pp::newline pp;
                            ppsay "]rs::VALUE_DECLARATIONS ";
			    pp::newline pp;
			    pp::newline pp;
			};

		    prettyprint_declaration' (rs::FIELD_DECLARATIONS (fields, typevars), d)
			=>
			{   ppsay "rs::FIELD_DECLARATIONS";
			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp (
                                "[ ",
                                " ;rs::FIELD_DECLARATIONS ",
                                (fn pp =  fn named_field =  prettyprint_named_field context pp (named_field, d - 1)),
                                fields
                            );
                            ppsay " ]FIELD_DECLARATIONS ";
			    pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::RECURSIVE_VALUE_DECLARATIONS (rvbs, typevars), d)
			=> 
			{
			    pp::newline pp;
			    ppsay "rs::RECURSIVE_VALUE_DECLARATIONS";

                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist
				pp
				(   "[ ",
				    " ;rs::RECURSIVE_VALUE_DECLARATIONS ",
				    (  fn pp =
				       fn named_recursive_values =
				       prettyprint_named_recursive_values
					   context
					   pp
					   (named_recursive_values, d - 1)
				    ),
				    rvbs
				);

                            ppsay "]rs::RECURSIVE_VALUE_DECLARATIONS ";

			    pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::FUNCTION_DECLARATIONS (fbs, typevars), d)
			=>
			{
			    pp::newline pp;
			    ppsay "rs::FUNCTION_DECLARATIONS ";
			    pp::newline pp;
			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
			    pp::newline pp;
			    ppsay "    ";

			    uj::ppvlist'
				pp
				(   "[ ",
				    " ;rs::FUNCTION_DECLARATIONS ",
				    (   fn pp =
					fn str =
					fn fb =
					prettyprint_named_function
					    context
					    pp
					    str
					    (fb, d - 1)
				    ),
				    fbs
				);

			    pp::newline pp;
                            ppsay "]rs::FUNCTION_DECLARATIONS ";

			    pp::shut_box  pp;
			};

		    prettyprint_declaration' (rs::NADA_FUNCTION_DECLARATIONS (fbs, typevars), d)
			=>
			{   ppsay "rs::NADA_FUNCTION_DECLARATIONS ";
			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist'
				pp
				(   "fun ",
				    "also ",
				    (   fn pp =
					fn str =
					fn fb =
					prettyprint_named_lib7function
					    context
					    pp
					    str
					    (fb, d - 1)
				    ),
				    fbs
				);

			    pp::shut_box  pp;
			};

		    prettyprint_declaration' (rs::TYPE_DECLARATIONS types, d)
			=>
			{   ppsay "rs::TYPE_DECLARATIONS [ ";
			    #
                            fun print_one pp type
				=
				(prettyprint_named_type context pp (type, d));

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit ""),	# Was "type "
				  separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  back       =>  (by pp::lit ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				types;

                            ppsay " ]rs::TYPE_DECLARATIONS ";
			}; 	

		    prettyprint_declaration' (rs::SUMTYPE_DECLARATIONS { sumtypes, with_types => [] }, d)
			=> 
			{   ppsay "rs::SUMTYPE_DECLARATIONS[ ";
			    #
                            fun print_one _ (dbing)
				=
				(prettyprint_sumtype context pp (dbing, d));


			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit ""),
				  separator  =>  (fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ),
				  back       =>  (by pp::lit ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sumtypes;

                            ppsay " ]rs::SUMTYPE_DECLARATIONS ";
			};             

		    prettyprint_declaration' (rs::SUMTYPE_DECLARATIONS { sumtypes, with_types }, d)
			=> 
			{   ppsay "rs::SUMTYPE_DECLARATIONS[ ";
			    #
                            fun prd pp dbing = (prettyprint_sumtype context pp (dbing, d));
			    fun prw pp tbing = (prettyprint_named_type context pp (tbing, d));

			    {   pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				#
				uj::unparse_closed_sequence
				    pp
				    { front      =>  (by pp::lit ""),
				      separator  =>  (fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ),
				      back       =>  (by pp::lit ";"),
				      print_one  =>  prd,
				      breakstyle =>  uj::ALIGN
				    }
				    sumtypes;

				pp::newline pp;

				uj::unparse_closed_sequence
				    pp
				    { front      =>  (by pp::lit "withtype "),
				      separator  =>  (fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ),
				      back       =>  (by pp::lit ""),
				      print_one  =>  prw,
				      breakstyle =>  uj::ALIGN
				    }
				    with_types;

                                ppsay " ]rs::SUMTYPE_DECLARATIONS ";

				pp::shut_box pp;
			    };
			};

		    prettyprint_declaration' (rs::ABSTRACT_TYPE_DECLARATIONS { abstract_types, with_types => [], body }, d)
			=> 
			{   ppsay "rs::ABSTRACT_TYPE_DECLARATIONS ";
			    #
                            fun prd pp dbing = (prettyprint_sumtype context pp (dbing, d));
			    fun prw pp tbing = (prettyprint_named_type     context pp (tbing, d));

			    {   pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				(   uj::unparse_closed_sequence
					pp
					{ front      =>  (by pp::lit ""),
					  separator  =>  (fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ),
					  back       =>  (by pp::lit ";"),
					  print_one  =>  prd,
					  breakstyle =>  uj::ALIGN
					}
					abstract_types
				);
				pp::newline pp;
				prettyprint_declaration' (body, d);
				pp::shut_box pp;
			    };
			};

		    prettyprint_declaration' (rs::ABSTRACT_TYPE_DECLARATIONS { abstract_types, with_types, body }, d)
			=> 
			{   ppsay "rs::ABSTRACT_TYPE_DECLARATIONS ";
			    #
                            fun prd _ (dbing) = (prettyprint_sumtype context pp (dbing, d));
			    fun prw _ (tbing) = (prettyprint_named_type context pp (tbing, d));

			    {   pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				(   uj::unparse_closed_sequence
					pp
					{ front      =>  (by pp::lit ""),
					  separator  =>  (fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ),
					  back       =>  (by pp::lit ";"),
					  print_one  =>  prd,
					  breakstyle =>  uj::ALIGN
					}
					abstract_types
				);
				pp::newline pp;
				(   uj::unparse_closed_sequence
					pp
					{ front      =>  (by pp::lit "withtype "),
					  separator  =>  (fn pp => (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); end ),
					  back       =>  (by pp::lit ""),
					  print_one  =>  prw,
					  breakstyle =>  uj::ALIGN
					}
					with_types
				);
				pp::newline pp;
				prettyprint_declaration' (body, d);
				pp::shut_box pp;
			    };
			};

		    prettyprint_declaration' (rs::EXCEPTION_DECLARATIONS ebs, d)
			=>
			{   ppsay "rs::EXCEPTION_DECLARATIONS[ ";
 			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    (   (fn pp =  fn eb =  prettyprint_named_exception context pp (eb, d - 1)),   ebs   );

                            ppsay " ]rs::EXCEPTION_DECLARATIONS ";

			    pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::PACKAGE_DECLARATIONS sbs, d)
			=>
			{
			    pp::newline pp;
			    pp::newline pp;
			    ppsay "rs::PACKAGE_DECLARATIONS";
			    pp::newline pp;

                            fun print_one _ sbing
				=
				prettyprint_named_package context pp (sbing, d);

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "[ "),
				  separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  back       =>  (by pp::lit ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sbs;

			    pp::newline pp;
                            ppsay " ]rs::PACKAGE_DECLARATIONS ";
			};

		    prettyprint_declaration' (rs::GENERIC_DECLARATIONS fbs, d)
			=> 
			{   ppsay "rs::GENERIC_DECLARATIONS ";
			    #
                            fun f pp generic_naming
				=
				prettyprint_named_generic context pp (generic_naming, d);


			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("generic package ", "also ", f, fbs);
			    pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::API_DECLARATIONS sigvars, d)
			=> 
			{   ppsay "rs::API_DECLARATIONS ";
			    #
                            fun f pp (rs::NAMED_API { name_symbol=>fname, definition=>def } )
				=>
				{   uj::unparse_symbol  pp  fname;
				    pp::newline pp;
                                    ppsay "=";
				    prettyprint_api_expression context pp (def, d);
				};

				f pp (rs::SOURCE_CODE_REGION_FOR_NAMED_API (t, r))
                                    =>
                                    f pp t;
			    end;


			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("api ", "also ", f, sigvars);	# Was "api "
			    pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::GENERIC_API_DECLARATIONS sigvars, d)
			=> 
			{   ppsay "rs::GENERIC_API_DECLARATIONS ";
			    #
                            fun print_one pp sigv
				=
				prettyprint_generic_api_naming context pp (sigv, d);

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);

			    uj::unparse_sequence
				pp
				{ separator  =>  pp::newline,
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sigvars;

			    pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::LOCAL_DECLARATIONS (inner, outer), d)
			=>
			{   ppsay "rs::LOCAL_DECLARATIONS ";
			    #
                            pp.box .{
				pp::newline pp;  ppsay "with";
				pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::vertical,	100 );
				    pp::newline pp;  prettyprint_declaration'(inner, d - 1);
                                pp::shut_box pp;
				pp::newline pp;  ppsay "do ";
				pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::vertical,	100 );
				    pp::newline pp;  prettyprint_declaration'(outer, d - 1);
                                pp::shut_box pp;
				pp::newline pp;  ppsay "end;\t\t# with";
			    };
			    pp::newline pp;
			};

		    prettyprint_declaration' (rs::SEQUENTIAL_DECLARATIONS decs, d)
			=>
			{
			    pp::newline pp;
                            ppsay "rs::SEQUENTIAL_DECLARATIONS[ ";

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  { pp::newline pp; pp::lit pp ";SEQUENTIAL_DECLARATIONS";  pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}),
				  print_one  =>  (fn pp =  fn declaration =  prettyprint_declaration'(declaration, d)),
				  breakstyle =>   uj::ALIGN
				}
				decs;

			    pp::shut_box pp;
			    pp::newline pp;
                            ppsay "]rs::SEQUENTIAL DECLARATIONS ";
			};

		    prettyprint_declaration' (rs::INCLUDE_DECLARATIONS named_packages, d)
			=> 
			{   ppsay "rs::INCLUDE_DECLARATIONS ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "include ";

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  pp::break pp { spaces=>1, indent_on_wrap=>0 } ),
				  print_one  =>  (fn pp =  fn sp =  pp_symbol_list sp),
				  breakstyle =>  uj::ALIGN
				}
				named_packages;

			    pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::OVERLOADED_VARIABLE_DECLARATION (symbol, type, explist, extension), d)
		        =>
		        {   ppsay "rs::OVERLOADED_VARIABLE_DECLARATION ";
                            uj::unparse_symbol pp symbol;
                        };

		    prettyprint_declaration' (rs::FIXITY_DECLARATIONS { fixity, ops }, d)
			=>
			{   ppsay "rs::FIXITY_DECLARATIONS ";
			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);

			    case fixity
                                # 
				fxt::NONFIX => ppsay "fxt::NONFIX ";

				fxt::INFIX (i, _)
				    => 
				    {   if (i % 2 == 0)	  ppsay "fxt::INFIX ";
					else		  ppsay "fxt::INFIXR ";
					fi;

					if (i / 2 > 0)
					    #  
					    ppsay (int::to_string (i / 2));
					    ppsay " ";
					fi;
				    };
			   esac;

			   uj::unparse_sequence
			       pp
			       { separator  =>  (fn pp =  pp::break pp { spaces=>1, indent_on_wrap=>0 }),
				 print_one  =>  uj::unparse_symbol,
				 breakstyle =>  uj::ALIGN
			       }
			       ops;

			   pp::shut_box pp;
			};

		    prettyprint_declaration' (rs::SOURCE_CODE_REGION_FOR_DECLARATION (declaration, (s, e)), d)
			=>  
			case source_opt
			    #                          
			    THE source
				 =>
				 {
# Commented out to reduce verbosity:
#				     ppsay "rs::SOURCE_CODE_REGION_FOR_DECLARATION [ ";

				     prettyprint_declaration'(declaration, d);

#				     ppsay ", ";
#				     prpos (pp, source, s); ppsay ", ";
#				     prpos (pp, source, e); ppsay " ] ";
				 };

			    NULL => {
# Commented out to reduce verbosity:
#					ppsay "rs::SOURCE_CODE_REGION_FOR_DECLARATION <...> ";
					prettyprint_declaration' (declaration, d);
				    };
			esac;

		    prettyprint_declaration' (rs::PRE_COMPILE_CODE string, d)
			=>
			ppsay ("rs::PRE_COMPILE_CODE \"" + string + "\"");
                  end;
	      
                  prettyprint_declaration';
	      }

	also
	fun prettyprint_named_value (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun prettyprint_named_value' (_, 0)
                        =>
                        ppsay "<naming>";

		    prettyprint_named_value' (rs::NAMED_VALUE { pattern, expression, ... }, d)
			=> 
			{   ppsay "rs::NAMED_VALUE[ ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    pp::newline pp;
			    prettyprint_pattern context pp (pattern, d - 1);
			    pp::shut_box pp;
			    pp::newline pp;
			    pp::lit pp " = (NAMED_VALUE)";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    pp::newline pp;
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    prettyprint_expression context pp (expression, d - 1);
			    pp::shut_box pp;
			    pp::newline pp;
			    ppsay "]rs::NAMED_VALUE";
			};

		    prettyprint_named_value' (rs::SOURCE_CODE_REGION_FOR_NAMED_VALUE (named_value, source_code_region), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_NAMED_VALUE ";
			    prettyprint_named_value' (named_value, d);
                        };
                end;
	    
		prettyprint_named_value';
	    }

	also
	fun prettyprint_named_field (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun prettyprint_named_field'(_, 0)
                        =>
                        ppsay "<field>";

		    prettyprint_named_field' (rs::NAMED_FIELD { name, type, init }, d)
			=> 
			{   ppsay "rs::NAMED_FIELD[";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    pp::newline pp;
                            pp_path pp [name];
			    pp::shut_box pp;
			    pp::newline pp;
                            ppsay " : (rs::NAMED_FIELD)";
			    pp::newline pp;
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    pp::newline pp;
			    prettyprint_type context pp (type, d);
			    pp::shut_box pp;
			    pp::newline pp;
			    ppsay "]rs::NAMED_FIELD";
			    pp::newline pp;
			};

		    prettyprint_named_field' (rs::SOURCE_CODE_REGION_FOR_NAMED_FIELD (named_field, source_code_region), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_NAMED_FIELD ";
			    prettyprint_named_field' (named_field, d);
                        };
                end;
	    
		prettyprint_named_field';
	    }

	also
	fun prettyprint_named_recursive_values (context as (_, source_opt)) pp
            = 
	    {   ppsay =  pp::lit  pp;
		#
		fun prettyprint_named_recursive_values'(_, 0)=> ppsay "<rec naming>";

		    prettyprint_named_recursive_values' (rs::NAMED_RECURSIVE_VALUE { variable_symbol, expression, ... }, d)
			=>
			{   pp::open_box (pp, pp::typ::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    uj::unparse_symbol pp variable_symbol;
                            pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
                            prettyprint_expression context pp (expression, d - 1);
			    pp::shut_box pp;
			};

		    prettyprint_named_recursive_values' (rs::SOURCE_CODE_REGION_FOR_RECURSIVELY_NAMED_VALUE (named_recursive_values, source_code_region), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_RECURSIVELY_NAMED_VALUE ";
			    prettyprint_named_recursive_values' (named_recursive_values, d);
                        };
                end;
	    
		prettyprint_named_recursive_values';
	    }

	also
	fun prettyprint_named_function (context as (_, source_opt)) pp head
            = 
	    {   ppsay =  pp::lit  pp;
		#
		fun prettyprint_named_function'(_, 0)
                        =>
                        ppsay "<NAMED_FUNCTION>";

		    prettyprint_named_function'(rs::NAMED_FUNCTION { pattern_clauses, is_lazy, kind, null_or_type }, d)
			=>
                        {
                            case kind
				#
			        rs::PLAIN_FUN => ppsay "rs::NAMED_FUNCTION[ ";
			       rs::METHOD_FUN => ppsay "rs::NAMED_FUNCTION[ (method) ";
			      rs::MESSAGE_FUN => ppsay "rs::NAMED_FUNCTION[ (message) ";
                            esac;

			    pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
			    pp::newline pp;

                            case null_or_type
				#
				THE anytype =>  {   prettyprint_type context pp (anytype, d - 1);
						    pp::newline pp;
					        };
				NULL => ();
                            esac;

			    uj::ppvlist pp
                              ( head, "  | ",
			        (fn pp =  fn (cl: rs::Pattern_Clause) =  (prettyprint_pattern_clause context pp (cl, d))),
			        pattern_clauses
                              );

			    pp::shut_box pp;
			    pp::newline pp;
                            ppsay "]rs::NAMED_FUNCTION";
			    pp::newline pp;
                        };

		    prettyprint_named_function' (rs::SOURCE_CODE_REGION_FOR_NAMED_FUNCTION (t, r), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_NAMED_FUNCTION ";
			    prettyprint_named_function context pp head (t, d);
                        };
                end;
	    
		prettyprint_named_function';
	    }

	also
	fun prettyprint_pattern_clause (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun prettyprint_pattern_clause' (rs::PATTERN_CLAUSE { patterns, result_type, expression }, d)
                    =
		    {   ppsay "rs::PATTERN_CLAUSE[ ";
			pp::newline pp;
			pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
			pp::newline pp;

                        fun print_one _ {   item:		rs::Case_Pattern,
					    fixity:		Null_Or( rs::Symbol ),
					    source_code_region:	rs::Source_Code_Region
					}
                            =
			    case fixity
			        #
				THE a =>  prettyprint_pattern context pp (item, d);

				NULL => case item
					    #
					    rs::PRE_FIXITY_PATTERN p
						=>
						{   ppsay "rs::PRE_FIXITY_PATTERN ";
						    pp::lit pp "(";prettyprint_pattern context pp (item, d);
						    pp::lit pp ")";
						};

					    rs::TYPE_CONSTRAINT_PATTERN p
						=>
						{   ppsay "rs::TYPE_CONSTRAINT_PATTERN ";
						    pp::lit pp "(";prettyprint_pattern context pp (item, d);
						    pp::lit pp ")";
						};

					    rs::AS_PATTERN p
						=>
						{   ppsay "rs::AS_PATTERN ";
						    pp::lit pp"(";prettyprint_pattern context pp (item, d);
						    pp::lit pp ")";
						};

					    rs::OR_PATTERN p
						=>
						{   ppsay "rs::OR_PATTERN ";
						    pp::lit pp "(";prettyprint_pattern context pp (item, d);
						    pp::lit pp ")";
						};

					    _   =>
						prettyprint_pattern context pp (item, d);
					esac;
			    esac;


		    
			pp::shut_box pp;
			pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
			pp::newline pp;

			uj::unparse_sequence
			    pp
			    { separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
			      print_one,
			      breakstyle =>  uj::ALIGN
			    }
			    patterns;

			case result_type
			    #			  
			    THE type
				=>
				{   pp::lit pp ":";
				    prettyprint_type context pp (type, d);
				};

			    NULL => ();
			esac;

			pp::newline pp;
			pp::shut_box pp;
			pp::newline pp;
			pp::lit pp "= (PATTERN_CLAUSE) ";
			pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::vertical, 100 );
			pp::newline pp;
			pp::break pp { spaces=>1, indent_on_wrap=>0 }; 
			prettyprint_expression context pp (expression, d);
			pp::newline pp;
		        ppsay "]PATTERN_CLAUSE";
			pp::newline pp;
			pp::shut_box pp;
			pp::newline pp;
		    }; 

		prettyprint_pattern_clause';
	    }

	also
	fun prettyprint_named_lib7function (context as (_, source_opt)) pp head
            = 
	    {   ppsay =  pp::lit pp;

		fun prettyprint_named_lib7function'(_, 0)
                        =>
                        ppsay "<rs::NADA_NAMED_FUNCTION>";

		    prettyprint_named_lib7function' (rs::NADA_NAMED_FUNCTION (clauses, ops), d)
			=>
                        {   ppsay "rs::NADA_NAMED_FUNCTION ";
			    #
			    uj::ppvlist pp ( head, "  | ",
					     (fn pp = fn (cl: rs::Nada_Pattern_Clause) = (prettyprint_lib7pattern_clause context pp (cl, d))),
					     clauses
					   );
                        };

		    prettyprint_named_lib7function' (rs::SOURCE_CODE_REGION_FOR_NADA_NAMED_FUNCTION (t, r), d)
			=>
			{   ppsay "rs::SOURCE_CODE_REGION_FOR_NADA_NAMED_FUNCTION ";
			    #
			    prettyprint_named_lib7function context pp head (t, d);
                        };
                end;
	    
		prettyprint_named_lib7function';
	    }

	also
	fun prettyprint_lib7pattern_clause (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit pp;
		#
		fun prettyprint_lib7pattern_clause' (rs::NADA_PATTERN_CLAUSE { pattern, result_type, expression }, d)
                    =
		    {   ppsay "rs::NADA_PATTERN_CLAUSE ";
			#
                        fun print_one _ (item:   rs::Case_Pattern)
                            =
                            #  XXX BUGGO FIXME:  Need to be more intelligent about paren insertion: 
			    {   pp::lit pp "(";
                                prettyprint_pattern context pp (item, d);
				pp::lit pp ")";
                            };

		    
			pp::open_box (pp, pp::typ::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );

			uj::unparse_sequence
			    pp
			    { separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
			      print_one,
			      breakstyle =>  uj::ALIGN
			    }
			    [ pattern ];         #  XXX BUGGO FIXME this list is always len 1 (obviously) so the logic here can probably be simplified. 


			case result_type
			    # 
			    THE type
				=>
				{   pp::lit pp ":";
				    prettyprint_type context pp (type, d);
				};

			    NULL => ();
			esac;


			pp::lit pp " =";
			pp::break pp { spaces=>1, indent_on_wrap=>0 }; 
			prettyprint_expression context pp (expression, d);
			pp::shut_box pp;

		    }; 

	    
		prettyprint_lib7pattern_clause';
	    }

	also
	fun prettyprint_named_type (context as (_, source_opt)) pp 
            = 
	    {   ppsay = pp::lit pp;
		#
		fun pp_tyvar_list (symbol_list, d)
                    =
		    {   fun print_one _ (typevar)
                            =
                            prettyprint_typevar  context  pp  (typevar, d);
		    
                        uj::unparse_sequence
                            pp
			    { separator  =>  (fn pp =  { pp::lit pp ",";	# Was "*"
					       pp::break pp { spaces=>1, indent_on_wrap=>0 } ;}),
			      print_one,
			      breakstyle =>  uj::ALIGN
                            }
			    symbol_list;
		    };

		fun prettyprint_named_type'(_, 0)
                        =>
                        ppsay "<t::naming>";

		    prettyprint_named_type' (rs::NAMED_TYPE { name_symbol, definition, typevars }, d)
			=> 
			{   ppsay "rs::NAMED_TYPE( ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    uj::unparse_symbol pp  name_symbol;
                            ppsay " ";
			    pp_tyvar_list (typevars, d);
                            pp::lit pp " = ";
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
                            prettyprint_type context pp (definition, d);
			    pp::shut_box pp;
                            ppsay " )rs::NAMED_TYPE ";
			};

		    prettyprint_named_type' (rs::SOURCE_CODE_REGION_FOR_NAMED_TYPE (t, r), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_NAMED_TYPE ";
			    prettyprint_named_type context pp (t, d);
                        };
                end;
	    
		prettyprint_named_type';
	    }

	also
	fun prettyprint_sumtype  (context as (_, source_opt))  pp
            = 
	    {   ppsay = pp::lit pp;
		#
		fun pp_tyvar_list (symbol_list, d)
                    =
		    {   fun print_one _ typevar
                            =
                            (prettyprint_typevar context pp (typevar, d));
		    
                        uj::unparse_sequence
                            pp
			    { separator  =>  (fn pp =  {   pp::lit pp ",";	# Was "*"
					                       pp::break pp { spaces=>1, indent_on_wrap=>0 };
							   }
                                             ),
			      print_one,
			      breakstyle => uj::ALIGN
                            }
			    symbol_list;
		    };

		fun prettyprint_sumtype'(_, 0)
                        =>
                        ppsay "<rs::SUM_TYPE>";

		    prettyprint_sumtype' (rs::SUM_TYPE { name_symbol, typevars, right_hand_side, is_lazy }, d)
			=> 
			{   ppsay "rs::SUM_TYPE ";
			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );

			    uj::unparse_symbol pp  name_symbol;
			    pp::lit pp " =";

			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    prettyprint_sumtype_right_hand_side context pp (right_hand_side, d);

			    pp::shut_box pp;
			};

		    prettyprint_sumtype' (rs::SOURCE_CODE_REGION_FOR_UNION_TYPE (t, r), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_UNION_TYPE ";
			    #
			    prettyprint_sumtype context pp (t, d);
                        };
                end;
	    
		prettyprint_sumtype';
	    }

	also
	fun prettyprint_sumtype_right_hand_side (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit pp;
		#
		fun prettyprint_sumtype_right_hand_side'(_, 0)
                        =>
                        ppsay "<rs::VALCONS>";

		    prettyprint_sumtype_right_hand_side' (rs::VALCONS const, d)
			=> 
			{   ppsay "rs::VALCONS ";
			    #
                            fun print_one  pp  (symbol: rs::Symbol,  tv: Null_Or(rs::Any_Type))
				=
				case tv
				    #  
				    THE a =>    {   uj::unparse_symbol pp symbol;
						    ppsay" ";	# Was " of "
						    prettyprint_type context pp (a, d);
						};

				    NULL  =>  (uj::unparse_symbol pp symbol);
				esac;

			    uj::unparse_sequence
				pp
				{ separator   => (fn pp = {   pp::lit pp " |";
						              pp::break pp { spaces=>1, indent_on_wrap=>0 } ;
                                                          }
                                             ),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				const;
			};

		    prettyprint_sumtype_right_hand_side' (rs::REPLICAS symlist, d)
			=> 
                        {   ppsay "rs::REPLICAS ";
			    #
			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp
						     =
						     {   pp::lit pp " |";
							 pp::break pp { spaces=>1, indent_on_wrap=>0 };
						     }
						 ),
				  print_one  =>  (fn pp =  fn symbol =  uj::unparse_symbol pp symbol),
				  breakstyle =>  uj::ALIGN
				}
				symlist;
                        };
                end;
	    
                prettyprint_sumtype_right_hand_side';
	    }

	also
	fun prettyprint_named_exception (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		pp_symbol_list = pp_path pp;

		fun prettyprint_named_exception'(_, 0)
                        =>
                        ppsay "<Eb>";

		    prettyprint_named_exception' (   rs::NAMED_EXCEPTION {
                                                         exception_symbol => exn,
                                                         exception_type   => etype
                                                     },
                                                     d
                                                 )
			=> 
                        {   ppsay "rs::EXCEPTION NAMING ";
			    #
			    case etype
			        #
				THE a =>    {   pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
						uj::unparse_symbol pp exn;
						pp::lit pp " =";
						pp::break pp { spaces=>1, indent_on_wrap=>2 };
						prettyprint_type context pp (a, d - 1);
						pp::shut_box pp;
					    };

				NULL =>     {   pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
						uj::unparse_symbol pp exn; 
						pp::shut_box pp;
					    };
			    esac;
			};

		    prettyprint_named_exception' (rs::DUPLICATE_NAMED_EXCEPTION { exception_symbol=>exn, equal_to=>edef }, d)
			=> 
			# ASK MACQUEEN IF WE NEED TO PRINT EDEF XXX BUGGO FIXME 
			{   ppsay "rs::DUPLICATE_NAMED_EXCEPTION ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::unparse_symbol pp exn;
			    pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    pp_symbol_list (edef);
			    pp::shut_box pp;
			};

		    prettyprint_named_exception' (rs::SOURCE_CODE_REGION_FOR_NAMED_EXCEPTION (t, r), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_NAMED_EXCEPTION ";
			    prettyprint_named_exception context pp (t, d);
                        };
                end;
	    
		prettyprint_named_exception';
	    }

	also
	fun prettyprint_named_package (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		fun prettyprint_named_package' (_, 0)
                        =>
                        ppsay "<rs::NAMED_PACKAGE>";

		    prettyprint_named_package' (rs::NAMED_PACKAGE { name_symbol=>name, definition=>def, constraint, kind }, d)
			=> 
			{   ppsay "rs::NAMED_PACKAGE ";
			    #
			    case kind
				rs::PLAIN_PACKAGE  =>  ();
				rs::CLASS_PACKAGE  =>  ppsay " (class) ";
				rs::CLASS2_PACKAGE =>  ppsay " (class2) ";
			    esac;

                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::unparse_symbol pp name;

                            case constraint
				rs::NO_PACKAGE_CAST => ();
				rs::WEAK_PACKAGE_CAST    api_expression => { pp::lit pp " : (weak) ";    prettyprint_api_expression context pp (api_expression, d); };
				rs::STRONG_PACKAGE_CAST  api_expression => { pp::lit pp " : ";           prettyprint_api_expression context pp (api_expression, d); };
				rs::PARTIAL_PACKAGE_CAST api_expression => { pp::lit pp " : (partial) "; prettyprint_api_expression context pp (api_expression, d); };
                            esac; 

                            pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
                            prettyprint_package_expression context pp (def, d - 1);
			    pp::shut_box pp;
			};

		    prettyprint_named_package' (rs::SOURCE_CODE_REGION_FOR_NAMED_PACKAGE (t, r), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_NAMED_PACKAGE ";
			    prettyprint_named_package context pp (t, d);
                        };
                end;
	    
		prettyprint_named_package';
	    }

	also
	fun prettyprint_named_generic (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun prettyprint_named_generic' (_, 0)
                        =>
                        ppsay "<rs::NAMED_GENERIC>";

		    prettyprint_named_generic' (
                        rs::NAMED_GENERIC {
                            name_symbol =>  name,
                            definition  =>  rs::GENERIC_DEFINITION { parameters, body, constraint }
                        },
                        d
                    )
			=>
			{   ppsay "rs::NAMED_GENERIC ";
			    #
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);

			    uj::unparse_symbol  pp  name;

			    {   fun print_one pp (THE symbol, api_expression)
					=>
					{   ppsay "(";
					    uj::unparse_symbol pp symbol;
					    ppsay " : ";
					    prettyprint_api_expression context pp (api_expression, d);
					    ppsay ")";
					};

				    print_one pp (NULL, api_expression)
					=>
					{   ppsay "(";
					    prettyprint_api_expression context pp (api_expression, d);
					    ppsay ")";
					};
                                end;

				{   uj::unparse_sequence
					pp
					{ separator  => (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
					  print_one,
					  breakstyle =>  uj::ALIGN
					}
					parameters;

				    case constraint
				        #
					rs::NO_PACKAGE_CAST
					    =>
					    ppsay "rs::NO_PACKAGE_CAST ";

					rs::WEAK_PACKAGE_CAST api_expression
					    => 
					    {   ppsay "rs::WEAK_PACKAGE_CAST: ";
						pp::break pp { spaces=>1, indent_on_wrap=>2 };
						prettyprint_api_expression context pp (api_expression, d);
					    };

					rs::PARTIAL_PACKAGE_CAST api_expression
					    => 
					    {   ppsay "rs::PARTIAL_PACKAGE_CAST: ";
						pp::break pp { spaces=>1, indent_on_wrap=>2 };
						prettyprint_api_expression context pp (api_expression, d);
					    };

					rs::STRONG_PACKAGE_CAST (api_expression)
					    => 
					    {   ppsay "rs::STRONG_PACKAGE_CAST: ";
						pp::break pp { spaces=>1, indent_on_wrap=>2 };
						prettyprint_api_expression context pp (api_expression, d);
					    };
                                    esac;

				    pp::nonbreakable_spaces pp 1;

				    ppsay "=";
                                    pp::break pp { spaces=>1, indent_on_wrap=>0 };

				    prettyprint_package_expression context pp (body, d);};
			    };
			    pp::shut_box pp;
			};

		    prettyprint_named_generic' (rs::NAMED_GENERIC { name_symbol=>name, definition=>def }, d)
			=>
			{   ppsay "rs::NAMED_GENERIC ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::unparse_symbol pp name;
			    pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    prettyprint_generic_expression context pp (def, d - 1);
			    pp::shut_box pp;
			}; 

		    prettyprint_named_generic' (rs::SOURCE_CODE_REGION_FOR_NAMED_GENERIC (t, r), d)
			=>
                        {   ppsay "rs::SOURCE_CODE_REGION_FOR_NAMED_GENERIC ";
			    prettyprint_named_generic context pp (t, d);
                        };
                end;
	    
		prettyprint_named_generic';
	    }

	also
	fun prettyprint_generic_api_naming (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;

		fun prettyprint_generic_api_naming'(_, 0)
                        =>
                        ppsay "<NAMED_GENERIC_API>";

		    prettyprint_generic_api_naming' (rs::NAMED_GENERIC_API { name_symbol=>name, definition=>def }, d)
			=> 
			{   ppsay "rs::NAMED_GENERIC_API ";
                            pp::open_box (pp, pp::typ::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "funsig ";
                            uj::unparse_symbol pp name;
                            ppsay " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
                            prettyprint_generic_api_expression context pp (def, d - 1);
			    pp::shut_box pp;
			};

		    prettyprint_generic_api_naming' (rs::SOURCE_REGION_FOR_NAMED_GENERIC_API (t, r), d)
			=>
                        {   ppsay "rs::SOURCE_REGION_FOR_NAMED_GENERIC_API ";
			    #
			    prettyprint_generic_api_naming context pp (t, d);
                        };
                end;
	    
		prettyprint_generic_api_naming';
	    }

	also
	fun prettyprint_typevar (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		fun prettyprint_typevar' (_, 0)
                        =>
                        ppsay "<typevar>";

		    prettyprint_typevar' (rs::TYPEVAR s, d)
                        =>
                        {   ppsay "rs::TYPEVAR( ";
                            uj::unparse_symbol pp s;
                            ppsay ")rs::TYPEVAR;";
                        };

		    prettyprint_typevar' (rs::SOURCE_CODE_REGION_FOR_TYPEVAR (t, r), d)
                        =>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_TYPEVAR ";
                            prettyprint_typevar context pp (t, d);
                        };
                end;
	    
		prettyprint_typevar';
	    }

	also
	fun prettyprint_type (context as (dictionary, source_opt)) pp
            =			
	    {   ppsay =  pp::lit pp;

		fun prettyprint_type' (_, 0)
                        =>
                        ppsay "<type>";

		    prettyprint_type' (rs::TYPEVAR_TYPE t, d)
                        =>
                        {   ppsay "rs::TYPEVAR_TYPE( ";
                            prettyprint_typevar context pp (t, d);
                            ppsay ")rs::TYPEVAR_TYPE ";
                        };

		    prettyprint_type' (rs::TYPE_TYPE (typ, []), d)
			=>
			{   ppsay "rs::TYPE_TYPE( ";
			    pp::open_box (pp, pp::typ::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::normal, 100 );
			    pp_path pp typ;
			    pp::shut_box pp;
                            ppsay ")rs::TYPE_TYPE ";
			};

		    prettyprint_type' (rs::TYPE_TYPE (typ, args), d)
			=> 
			{   ppsay "rs::TYPE_TYPE( ";
			    #
			    pp::open_box (pp, pp::typ::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::normal, 100 );

			    case typ
				#			      
				[typ]
				    =>
				    if (sy::eq (sy::make_type_symbol("->"), typ))
					#				         
					case args
					    #
					    [dom, ran]
						=>
						{   prettyprint_type' (dom, d - 1);
						    ppsay " ->";
						    pp::break pp { spaces=>1, indent_on_wrap=>2 };
						    prettyprint_type' (ran, d - 1);
						};

					    _   =>
						err::impossible "wrong args for -> type";
					esac;

				    else
					uj::unparse_symbol  pp  typ;
					ppsay " ";
					prettyprint_type_args  (args, d);
				    fi;

				_ => {   pp_path  pp  typ;
                                         ppsay " ";
					 prettyprint_type_args  (args, d);
				     };
                            esac;

			    pp::shut_box pp;
                            ppsay ")rs::TYPE_TYPE ";
			};

		    prettyprint_type' (rs::RECORD_TYPE s, d)
			=> 
			{   ppsay "rs::RECORD_TYPE ";
			    # 
                            fun print_one  pp  (symbol: rs::Symbol,  tv: rs::Any_Type)
				= 
				{   uj::unparse_symbol pp symbol;
				    ppsay ":";
				    prettyprint_type context pp (tv, d);
				};

			    uj::unparse_closed_sequence
				pp
				{ front      => (by pp::lit "{ "),
				  separator  => (fn pp = {   pp::lit pp ", ";
								  pp::break pp { spaces=>1, indent_on_wrap=>0 } ;
							      }
						 ),
				  back       => (by pp::lit "}"),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				s;
			};

		    prettyprint_type' (rs::TUPLE_TYPE t, d)
			=> 
			{   ppsay "rs::TUPLE_TYPE ";
			    #
                            fun print_one _ (tv: rs::Any_Type)
				=
				(prettyprint_type context pp (tv, d));

			    uj::unparse_sequence 
				pp
				{ separator   => (fn pp =  {   pp::lit pp ", ";				# Was " *"
								   pp::break pp { spaces=>1, indent_on_wrap=>0 };
							       }
						 ),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				t;
			};

		    prettyprint_type' (rs::SOURCE_CODE_REGION_FOR_TYPE (t, r), d)
			=>
                        {
# Commented out to reduce verbosity:
#			    ppsay "rs::SOURCE_CODE_REGION_FOR_TYPE ";
			    prettyprint_type context pp (t, d);
                        };
                end 

		also
		fun prettyprint_type_args ([], d)
                        =>
                        ();

		    prettyprint_type_args ( [type], d)
			=> 
			{   if (strength type <= 1)
			        #
				pp::open_box (pp, pp::typ::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
				ppsay "("; 
				prettyprint_type' (type, d); 
				ppsay ")";
				pp::shut_box pp;
			    else 
				prettyprint_type' (type, d);
			    fi;

			    pp::break pp { spaces => 1,  indent_on_wrap => 0 };
			};

		    prettyprint_type_args (tys, d)
			=>
			uj::unparse_closed_sequence
			    pp 
			    { front      =>  by pp::lit "(",
			      separator  =>  fn pp =  { pp::lit pp ", ";
							    pp::break pp { spaces=>0, indent_on_wrap=>0 };
							  },
			      back       =>  by pp::lit ") ",
			      breakstyle =>  uj::ALIGN, 
			      print_one  =>  fn _ =  fn type =  prettyprint_type' (type, d)
			    }
			    tys;
                end; 
	    
                prettyprint_type';
	    };
    };						# package unparse_raw_syntax 
end;						# top-level stipulate









##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################



## Copyright 2003 by University of Chicago 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
