## unparse-type.pkg 
## Copyright 2003 by The SML/NJ Fellowship 

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

#  modified to use Lib7 Lib pp. [dbm, 7/30/03]) 

stipulate 
    package tdt =  type_declaration_types;		# type_declaration_types		is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
herein
    api Unparse_Type {

	 type_formals
	     :
	     Int
	  -> List( String );

	 typevar_ref_printname
	     :
	     tdt::Typevar_Ref
	  -> String;

	 unparse_type
	     :
	     symbolmapstack::Symbolmapstack
	  -> prettyprint::Stream 
	  -> tdt::Type
	  -> Void;

	 unparse_typescheme
	     :
	     symbolmapstack::Symbolmapstack
	  -> prettyprint::Stream 
	  -> tdt::Typescheme
	  -> Void; 

	 unparse_typoid
	     :
	     symbolmapstack::Symbolmapstack
	  -> prettyprint::Stream 
	  -> tdt::Typoid
	  -> Void;

	 unparse_typevar_ref
	     :
	     symbolmapstack::Symbolmapstack
	  -> prettyprint::Stream 
	  -> tdt::Typevar_Ref
	  -> Void;

	 unparse_sumtype_constructor_domain
	     :
	     ((Vector( tdt::Sumtype_Member ), List( tdt::Type )) )
	  -> symbolmapstack::Symbolmapstack 
	  -> prettyprint::Stream
	  -> tdt::Typoid
	  -> Void;

	 unparse_sumtype_constructor_types
	     :
	     symbolmapstack::Symbolmapstack
	  -> prettyprint::Stream 
	  -> tdt::Type
	  -> Void;

	 reset_unparse_type
	     :
	     Void -> Void;

	 unparse_formals
	     :
	     prettyprint::Stream
	  -> Int
	  -> Void;

	 debugging:  Ref( Bool );
	 unalias:    Ref( Bool );
    };
end;


stipulate 
    package ip  =  inverse_path;		# inverse_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package pp  =  prettyprint;			# prettyprint			is from   src/lib/prettyprint/big/src/prettyprint.pkg
    package sp  =  symbol_path;			# symbol_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package syx =  symbolmapstack;		# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
    package sta =  stamp;			# stamp				is from   src/lib/compiler/front/typer-stuff/basics/stamp.pkg
    package mtt =  more_type_types;		# more_type_types		is from   src/lib/compiler/front/typer/types/more-type-types.pkg
    package tdt =  type_declaration_types;	# type_declaration_types	is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
    package ts  =  type_junk;			# type_junk			is from   src/lib/compiler/front/typer-stuff/types/type-junk.pkg
    package ups =  unparse_junk; 		# unparse_junk			is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
    package tup =  tuples;			# tuples			is from   src/lib/compiler/front/typer-stuff/types/tuples.pkg
    #
herein


    package   unparse_type
    : (weak)  Unparse_Type
    {
	debugging = REF FALSE;
	unalias   = REF TRUE;
	#
	fun bug s
            =
            error_message::impossible ("unparse_type: " + s);

	pps = pp::string;
	#
	fun by f x y
	    =
	    f y x;

	internals
            =
            typer_control::internals;

	unit_path
            =
            ip::extend
                (
                  ip::empty,
                  symbol::make_type_symbol "Void"
                );


        # Map small integer 'k' to a type variable name.
        # We name the first three X Y Z,
        # then run through A B C ... W
        # and then start in on AA, AB...	XXX BUGGO FIXME AA AB etc aren't legal syntax, need A_1 or A_a or such.
	#
	fun bound_typevar_name k
	    =
	    {   a =   char::to_int 'A';

                case k
                  
                     0 => "X";
                     1 => "Y";
                     2 => "Z";
                     _ => 
			  if   (k < 26)
			      
			       string::from_char (char::from_int (k + a - 3));
			  else
			       implode [ char::from_int (int::(/) (k, 26) + a), 
					 char::from_int (int::(%) (k, 26) + a)
				       ];
			  fi;
                esac;
	    };

	#
	fun meta_tyvar_name' k
	    =
	    {
#                2009-04-23 CrT: This is the old logic.
#                It crashes the compiler ("BAD_CHAR" exception) for large k:
#                a =  char::to_int 'a';	#  use reverse order for meta vars 
#
#		if   (k < 26)
#		    
#		     string::from_char (char::from_int (a + k));
#		else 
#		     implode [ char::from_int (a + (int::(/) (k, 26))), 
#			       char::from_int (a + (int::(%) (k, 26)))
#			     ];
#                fi;
		"meta" + int::to_string k;
	    };
	#
	fun type_formals n
	    =
	    loop 0
            where
	        fun loop i
		    =
		    if   (i >= n)
		        
			 [];
		    else 
			 (bound_typevar_name i)  !  loop (i + 1);
                    fi;
	    end;
	#
	fun literal_kind_printname (lk: tdt::Literal_Kind)
	    =
	    case lk
		#	      
		tdt::INT    => "Int";       # or "INT" 
		tdt::UNT    => "Unt";       # or "UNT" 
		tdt::FLOAT  => "Float";     # or "FLOAT" 
		tdt::CHAR   => "Char";      # or "CHAR" 
		tdt::STRING => "String";    # or "STRING" 
            esac;

	stipulate  #  WARNING -- compiler global variables 
	    count = REF(-1);  								# XXX BUGGO FIXME more icky thread-hostile mutable global state :-(

	    meta_tyvars =   REF ([]:   List(tdt::Typevar_Ref));
	herein

	    fun meta_tyvar_name   (typevar_ref as { id, ref_typevar }:  tdt::Typevar_Ref)
		=
		meta_tyvar_name' (find_or_add (*meta_tyvars, 0))
                where
		    fun find_or_add ([], _)
			    =>
			    {   meta_tyvars := typevar_ref ! *meta_tyvars;
			        count := *count+1;
			        *count;
                            };

		        find_or_add ({ id, ref_typevar => ref_typevar' } ! rest, k)
			    =>
			    ref_typevar == ref_typevar'
                                ??   *count - k
			        ::   find_or_add (rest, k+1);
                    end;
		end;
	    #
	    fun reset_unparse_type ()
		=
		{   count := -1;
		    meta_tyvars := [];
		};
	end;
	#
	fun tv_head (eq, base)		# "tv" for "type variable", "eq" for "equality type".
	    =
	    base
            +
	    (eq  ??  "(==)"
	         ::   ""
            );

	#
	fun annotate (name, annotation, maybe_fn_nesting)
	    =
	    if *internals

		 cat (  name
			 ! "."
			 ! annotation
			 ! case maybe_fn_nesting

				THE fn_nesting =>  ["( fn_nesting => ", (int::to_string fn_nesting), " )"];
				NULL           =>  NIL;
			   esac
			);
	    else
		 name;
            fi;
	#
	fun typevar_ref_printname'  (typevar_ref as { id, ref_typevar })
	    =
	    sprint_typevar  *ref_typevar
            where
	        fun sprint_typevar  typevar
		    =
		    case typevar
			#		      
			tdt::RESOLVED_TYPEVAR (tdt::TYPEVAR_REF (typevar_ref as { id, ref_typevar }) )
			    =>
			    {   my (printname, null_or_type)
				    = 
				    typevar_ref_printname'  typevar_ref;

				( (sprintf "[id%d]" id) + printname,
				  null_or_type
				);
			    };

			tdt::RESOLVED_TYPEVAR  type
			    =>
			    ( (sprintf "[id%d]" id)  +  "<tdt::RESOLVED_TYPEVAR ?>",
			      THE type
			    );

			tdt::META_TYPEVAR { fn_nesting, eq }
			    =>
			    ( (sprintf "[id%d]" id)
			      +
			      tv_head (eq, annotate ( meta_tyvar_name typevar_ref,
						      "META",
						      THE fn_nesting
				      )             ),

			      NULL
			    );

			tdt::INCOMPLETE_RECORD_TYPEVAR { fn_nesting, eq, known_fields }
			    =>
			    ( (sprintf "[id%d]" id)
			      +
			      tv_head (eq, annotate ( meta_tyvar_name  typevar_ref,
						      "INCOMPLETE_RECORD",
						      THE fn_nesting
				      )             ),

			      NULL
			    );


			tdt::USER_TYPEVAR { name, fn_nesting, eq }
			    =>
			    ( (sprintf "[id%d]" id)
			      +
			      tv_head (eq, annotate (symbol::name name, "USER", THE fn_nesting)),

			      NULL
			    );

			tdt::LITERAL_TYPEVAR { kind, ... }
			    =>
			    ( (sprintf "[id%d]" id)
			      +
			      annotate (literal_kind_printname kind, "LITERAL", NULL),

			      NULL
			    );

			tdt::OVERLOADED_TYPEVAR eq
			    =>
			    ( (sprintf "[id%d]" id)
			      +
			      tv_head (eq, annotate (meta_tyvar_name typevar_ref, "OVERLOADED", NULL)),

			      NULL
			    );

			tdt::TYPEVAR_MARK _
			    =>
			    ( (sprintf "[id%d]" id)
			      +
			      "<tdt::TYPEVAR_MARK ?>",

			      NULL
			    );
                    esac;
	    end;

	#
	fun typevar_ref_printname  typevar_ref
	    =
	    {   my (printname, null_or_type)
                    =
                    typevar_ref_printname'  typevar_ref;

                printname;
            };


#	fun ppkind stream kind
#            =
#	     {   pps stream
#	           case kind
#		       tdt::BASE _ => "BASE";
#                      tdt::FORMAL     => "FORMAL";
#		       tdt::FLEXIBLE_TYPE _ => "FLEXIBLE_TYPE";
#                      tdt::ABSTRACT _ => "ABSTYC";
#		       tdt::SUMTYPE _ => "SUMTYPE";
#                      tdt::TEMP => "TEMP"
#                  esac;
#            };
#
	#
	fun ppkind stream kind
	    =
	    pps stream
	       case kind
		   tdt::BASE _                 => "PRIM";
		   tdt::FORMAL                      => "FORM";
		   tdt::FLEXIBLE_TYPE _ => "FLEX";
		   tdt::ABSTRACT _                  => "ABST";
		   tdt::SUMTYPE _                  => "DATA";
		   tdt::TEMP                        => "TEMP";
              esac;
	#
	fun effective_path (path, type, symbolmapstack) : String
	    =
	    {   fun typepath ( tdt::SUM_TYPE          { path, ... }
			     | tdt::NAMED_TYPE        { path, ... }
			     | tdt::TYPE_BY_STAMPPATH { path, ... }
			     )
			=>
			THE path;

		    typepath _
			=>
			NULL;
                end;
		#
		fun find (path, type)
		    =
		    (ups::find_path (path,
			(fn type' = ts::type_equality (type', type)),
			(fn x = find_in_symbolmapstack::find_type_via_symbol_path (symbolmapstack, x,
						(fn _ = raise exception syx::UNBOUND))))
                    );
		#
		fun search (path, type)
		    =
		    {   (find (path, type)) ->   (suffix, found);
			#
			if found
			    (suffix, TRUE);
			else
			    if (not *unalias)
				#    
				(suffix, FALSE);
			    else
				case (ts::unwrap_definition_1  type)
				    #
				    THE type' =>    case (typepath type')
							#
							THE path'
							    =>
							    {   (search (path', type'))
								    ->
								    x as (suffix', found');

								if found'      x;
								else           (suffix, FALSE);
								fi;
							    };

							NULL => (suffix, FALSE);
						    esac;

				    NULL => (suffix, FALSE);
				esac;
			    fi;
                        fi;
		    };

		(search (path, type)) ->   (suffix, found);

		name =   sp::to_string (sp::SYMBOL_PATH suffix);

		if found          name;
		else /* "?." + */ name;
		fi;				# 2008-01-02 CrT This seems more confusing than helpful, for the moment at least.
	    };

	arrow_stamp = mtt::arrow_stamp;

	fun strength  type
	    =
	    case type
		#
		tdt::TYPEVAR_REF { id, ref_typevar => REF (tdt::RESOLVED_TYPEVAR  type') }
		    =>
		    strength  type';

		tdt::TYPCON_TYPOID (type, args)
		    =>
		    case type
			#
			tdt::SUM_TYPE { stamp, kind => tdt::BASE _, ... }
			    =>
			    if (sta::same_stamp (stamp, arrow_stamp))    0;
			    else                                         2;
			    fi;

			tdt::RECORD_TYPE (_ ! _)						# Excepting type Void
			    => 
			    if (tup::is_tuple_type  type)         1;
			    else                                  2;
                            fi;

			_   => 2;
		    esac;

	        _ => 2;
            esac;
	#
	fun unparse_eq_prop stream p
	    =
	    pps stream a
	    where
	        a =    case p
			   tdt::e::NO            =>  "EQ=NO";
			   tdt::e::YES           =>  "EQ=YES";
			   tdt::e::INDETERMINATE =>  "EQ=INDETERMINATE";
			   tdt::e::CHUNK         =>  "EQ=CHUNK";
			   tdt::e::DATA          =>  "EQ=DATA";
			   tdt::e::EQ_ABSTRACT   =>  "EQ=ABSTRACT";
			   tdt::e::UNDEF         =>  "EQ=UNDEF";
                       esac;
	    end;
	#
	fun unparse_inverse_path ppstream (inverse_path::INVERSE_PATH inverse_path: inverse_path::Inverse_Path)
	    = 
	    pp::string ppstream (symbol_path::to_string (symbol_path::SYMBOL_PATH (reverse inverse_path)));
	#
	fun unparse_type' symbolmapstack stream members_op
	    =
	    unparse_type''
            where

	        my  { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, break, ... }
                    =
                    ups::en_pp stream;
		#
		fun unparse_type'' (type as tdt::SUM_TYPE { path, stamp, eqtype_info, kind, ... } )
			=>
			if *internals
			    #
			    begin_wrap_box 1;
			    unparse_inverse_path stream path;
			    pps "[";
			    pps "tdt::SUM_TYPE ";   ppkind stream kind;   pps ";"; 
			    pps (sta::to_short_string  stamp);
			    pps ";";
			    unparse_eq_prop  stream  *eqtype_info;
			    pps "]";
			    end_box ();
			else
			    pps (effective_path (path, type, symbolmapstack));
			fi;

		    unparse_type'' (type as tdt::NAMED_TYPE { path, typescheme => tdt::TYPESCHEME { body, ... }, ... } )
			=>
			if *internals
			    #
			    begin_wrap_box 1;
			    unparse_inverse_path stream path;
			    pps "["; pps "tdt::NAMED_TYPE; "; 
			    unparse_typoid  symbolmapstack  stream  body;
			    pps "]";
			    end_box();
			else
			    pps (effective_path (path, type, symbolmapstack));
                        fi;

		    unparse_type'' (tdt::RECORD_TYPE labels)
			=>
			ups::unparse_closed_sequence
			    #
			    stream
			    #
			    { front =>  by pp::string "{ ",
			      sep   =>  fn stream =  { pp::string stream ", "; 
						      pp::break stream { spaces=>0, indent_on_wrap=>0 };
						    },
			      back  =>  by pp::string  "}",
			      style =>  ups::INCONSISTENT,
			      pr    =>  ups::unparse_symbol
			    }
			    #
			    labels;

		    unparse_type'' (tdt::RECURSIVE_TYPE n)
			=>
			case members_op
			    #
			    THE (members, _)
				=> 
				{   my { type_name, valcons, ... }
					=
					vector::get (members, n);

				    ups::unparse_symbol  stream  type_name;
				};

			    NULL =>   pps (string::cat ["<RECURSIVE_TYPE ", int::to_string n, ">"]);
			esac;


		    unparse_type'' (tdt::FREE_TYPE n)
			=>
			case members_op
			    #			  
			    THE (_, free_types)
				=> 
				{   type =  (   list::nth (free_types, n)
						except _
						    =
						    bug "unexpected free_types in unparse_type''"
					    );

				     unparse_type'' type;
				};

			    NULL =>   pps (string::cat ["<FREE_TYPE ", int::to_string n, ">"]);
                        esac;

		    unparse_type'' (tdt::TYPE_BY_STAMPPATH { arity, stamppath, path } )
			=>
			if *internals
			    #
			    begin_wrap_box 1;
			    unparse_inverse_path stream path; pps "[TYPE_BY_STAMPPATH; "; 
			    pps (stamppath::stamppath_to_string stamppath);
			    pps "]";
			    end_box ();
			else
                            unparse_inverse_path stream path;
                        fi;

		    unparse_type'' tdt::ERRONEOUS_TYPE
                        =>
                        pps "[E]";
                end;

	    end


	also
	fun unparse_typoid'  symbolmapstack  stream
            (
              typoid:		tdt::Typoid,
              an_api:		tdt::Typescheme_Eqflags, 
              members_op:	Null_Or( (Vector( tdt::Sumtype_Member ), List( tdt::Type )) )
            )
            : Void
	    =
	    print_type  typoid
	    where
		(ups::en_pp stream)
		    ->
	            { begin_horizontal_else_vertical_box,
                      begin_wrap_box,
                      end_box,
                      pps,
		      break,
                      newline
                    };
		#
		fun print_type  typoid
		    =
		    case typoid
			#		      
			tdt::TYPEVAR_REF { id, ref_typevar => REF (tdt::RESOLVED_TYPEVAR  type') }
			    =>
			    print_type  type';

			tdt::TYPEVAR_REF  typevar_ref
			    =>
			    unparse_typevar_ref' typevar_ref;

			tdt::TYPESCHEME_ARG n
			    =>
			    {   eq =   list::nth (an_api, n) 
				       except
					   (SUBSCRIPT | INDEX_OUT_OF_BOUNDS)
					       =
					       FALSE;

				pps (tv_head (eq, (bound_typevar_name n)));
			    };

			tdt::TYPCON_TYPOID (type, args)
			    =>
			    {   fun otherwise ()
				    =
				    {   begin_wrap_box 2;
					#
					unparse_type'  symbolmapstack  stream  members_op  type;

					case args
					    #
					    [] => ();

					    _  => {   pps "(";
						      break { spaces=>0, indent_on_wrap=>0 };
						      unparse_type_args args; 
						      pps ")";
						  };
					esac;

					end_box();
				    };

				case type
				    #	
				    tdt::SUM_TYPE { stamp, kind, ... }
					=>
					case kind
					    #
					    tdt::BASE _ 
						=>
						if (sta::same_stamp (stamp, arrow_stamp))
						    #
						    case args

							 [domain, range]
							     =>
							     {   begin_horizontal_else_vertical_box 0;

								 if   (strength domain == 0)

								      begin_horizontal_else_vertical_box 1;
								      pps "(";
								      print_type domain;
								      pps ")";
								      end_box();
								 else
								      print_type domain;
								 fi;

								 break { spaces=>1, indent_on_wrap=>0 };
								 pps "-> ";
								 print_type range;
								 end_box();
							     };

							 _   => bug "TYPCON_TYPE: arity";
						    esac;
						else
						    begin_wrap_box 2;
						    unparse_type'  symbolmapstack  stream  members_op  type;

						    case args
							#
							[] => ();

							 _ => {   pps "(";
								  break { spaces=>0, indent_on_wrap=>0 };
								  unparse_type_args args;
								  pps ")";
							      };
						    esac;

						    end_box();
						fi;

					    _   => otherwise ();
					esac;

				    tdt::RECORD_TYPE labels
					=>
					if (tup::is_tuple_type  type)
					     unparse_tuplety args;
					else unparse_recordty (labels, args);
					fi;

				    _ => otherwise ();
				esac;
			    };

			tdt::TYPESCHEME_TYPOID { typescheme_eqflags => an_api,
						 typescheme => tdt::TYPESCHEME { arity, body }
					       }
			    => 
			    unparse_typoid' symbolmapstack stream (body, an_api, members_op);

			tdt::WILDCARD_TYPOID
			    =>
			    pps "_";

			tdt::UNDEFINED_TYPOID
			    =>
			    pps "undef";
                    esac

		also
		fun unparse_type_args []
                        =>
                        ();

		    unparse_type_args [type]
		        => 
		        {   if   (strength type <= 1)
		                
                                 begin_wrap_box 1;
			         pps "("; 
			         print_type type; 
			         pps ")";
			         end_box();
		            else
                                 print_type type;
                            fi;

		            break { spaces=>0, indent_on_wrap=>0 };
                        };

		    unparse_type_args tys
			=>
			ups::unparse_closed_sequence
			    stream 
			    { front =>  by pp::string "(",
			      sep   =>  fn stream =  { pp::string stream ", ";
						       pp::break stream { spaces=>0, indent_on_wrap=>0 };
						     },
			      back  =>  by pp::string ")",
			      style =>  ups::INCONSISTENT, 
			      pr    =>  fn _ =  fn type =  print_type type
			    }
			    tys;
                end 

		also
		fun unparse_tuplety []
                        =>
                        pps (effective_path (unit_path, tdt::RECORD_TYPE [], symbolmapstack));

		    unparse_tuplety tys
			=>
                        {   pps "(";

			    ups::unparse_sequence
			       stream
			       {   sep   => fn stream =  {   pp::string stream ",";	# Was "* "
							     pp::break stream { spaces=>1, indent_on_wrap=>0 };
							 },
				   style => ups::INCONSISTENT,
				   pr    => (fn _ =  fn type = if   (strength type <= 1)
								      begin_wrap_box 1;
								      pps "(";
								      print_type type; 
								      pps ")";
								      end_box ();
								 else
								      print_type type;
								 fi
					   )
			       }
			       tys;

			       pps ")";
		        };
                end 

		also
		fun unparse_field (lab, type)
		    =
		    {   begin_horizontal_else_vertical_box 0;
			ups::unparse_symbol stream lab; 
			pps ": ";
			print_type type;
			end_box ();
		    }

		also
		fun unparse_recordty ([],[])
			=>
			pps (effective_path (unit_path, tdt::RECORD_TYPE [], symbolmapstack));
			  #  this case should not occur 

		    unparse_recordty (lab ! labels, arg ! args)
			=>
			{   begin_wrap_box 1;
			    pps "{ ";
			    unparse_field (lab, arg);

			    paired_lists::apply 
				(fn field'
				    =
				    {   pps ", ";
					break { spaces=>1, indent_on_wrap=>0 };
					unparse_field field';
				    }
				)
				(labels, args);

			    pps "}";
			    end_box ();
			};

		    unparse_recordty _
			=>
			bug "unparse_type::prettyprintRECORDty";
                end 

		also
		fun unparse_typevar_ref' (typevar_ref as { id, ref_typevar as REF typevar }:  tdt::Typevar_Ref):  Void
		    =
		    {   printname =   typevar_ref_printname  typevar_ref;

			case typevar
			    #			  
			    tdt::INCOMPLETE_RECORD_TYPEVAR { fn_nesting, eq, known_fields }
				=>
				case known_fields
				    #
				    []  =>
					{   pps "{ ";
					    pps printname;
					    pps "}";
					};

				    field' ! fields
					=>
					{   begin_wrap_box 1;
					    pps "{ ";
					    unparse_field field';
					    apply (fn x =  {   pps ", ";
							       break { spaces=>0, indent_on_wrap=>0 };
							       unparse_field x;
							   }
						  )
						 fields;
					    pps ";";
					    break { spaces=>1, indent_on_wrap=>0 };
					    pps printname;
					    pps "}";
					    end_box ();
					};
				esac;

			    _   =>  pps printname;
                        esac;
		    };
	    end				#  where (fun unparse_type')

	also
	fun unparse_typoid
            (symbolmapstack: syx::Symbolmapstack)
            stream
            (type:  tdt::Typoid)
            :
            Void
	    = 
	    {   pp::begin_indented_wrap_box  stream  (pp::CURSOR_RELATIVE 1);
		unparse_typoid'  symbolmapstack  stream  (type, [], NULL);
		pp::end_box stream;
	    };

	#
	fun unparse_typevar_ref
                (symbolmapstack:	syx::Symbolmapstack)
                (stream:		pp::Stream )
                (typevar_ref:		tdt::Typevar_Ref)
            :
            Void
	    =
	    {   my (printname, null_or_type)
                    =
                    typevar_ref_printname'  typevar_ref;

	        my  { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, break, ... }
                    =
                    ups::en_pp  stream;

		begin_wrap_box 1;
		pps " typevar_ref: ";
		pps printname;

                case null_or_type
		    #
		    NULL     => ();

		    THE type => {   pps " == ";
				    unparse_typoid  symbolmapstack  stream  type;
				};
                esac;

		end_box ();
            };

	#
	fun unparse_sumtype_constructor_domain members (symbolmapstack: syx::Symbolmapstack) stream (type: tdt::Typoid)
	    : Void
	    = 
	    {   pp::begin_indented_wrap_box stream (pp::CURSOR_RELATIVE 1);
		unparse_typoid' symbolmapstack stream (type,[], THE members);
		pp::end_box stream;
	    };

	#
	fun unparse_type  symbolmapstack stream      type
	    =
	    unparse_type' symbolmapstack stream NULL type;

	#
	fun unparse_typescheme symbolmapstack stream (tdt::TYPESCHEME { arity, body } )
	    =
	    {   (ups::en_pp stream)
		    ->
		    { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, break, ... };

		begin_wrap_box 2;
		pps "tdt::TYPESCHEME( { arity="; 
		ups::unparse_int stream arity;   ups::unparse_comma stream;
		break { spaces=>0, indent_on_wrap=>0 };
		pps "body="; 
		unparse_typoid  symbolmapstack  stream  body; 
		pps "} )";
		end_box();
	    };
	#
	fun unparse_formals  stream
	    =
	    unparse_f
            where
	        fun unparse_f 0 =>  ();
		    unparse_f 1 =>  pps stream "(X)";			# 2008-01-03 CrT: Was " 'a"

		    unparse_f n
                        =>
			ups::unparse_tuple
			    stream
			    (fn stream =  fn s =  pps stream s)		# 2008-01-03 CrT: Was ("'" + s)
			    (type_formals n);
                end;
	    end;

	#
	fun unparse_sumtype_constructor_types symbolmapstack stream (tdt::SUM_TYPE { kind => tdt::SUMTYPE dt, ... } )
		=>
		{   dt ->   { index, free_types, family=> { members, ... }, ... };
		    #
		    (ups::en_pp stream) ->   { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, break, ... };

		    (vector::get (members, index)) ->    { valcons, ... };

		    begin_horizontal_else_vertical_box  0;

		    apply
                        (fn { name, domain, ... }
                            =
			    {   pps (symbol::name name);
                                pps ": ";

			        case domain
			            #
				    THE type => unparse_typoid'
						    symbolmapstack
						    stream
						    (type,[], THE (members, free_types));

				    NULL =>   pps "CONST";
                                esac;

			        break { spaces=>1, indent_on_wrap=>0 };
                            }
			)
			valcons;

		    end_box ();
		};

	    unparse_sumtype_constructor_types  symbolmapstack  stream  _
		=>
		bug "unparse_sumtype_constructor_types";
       end;
    };										#  package unparse_type 
end;										#  toplevel "stipulate"


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Copyright 1991 by AT&T Bell Laboratories 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
