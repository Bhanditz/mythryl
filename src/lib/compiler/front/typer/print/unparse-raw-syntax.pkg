## unparse-raw-syntax.pkg
## Jing Cao and Lukasz Ziarek 

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

# We refer to a literal dump of the raw syntax tree as "prettyprinting".
# We refer to reconstruction of surface syntax from the raw syntax tree as "unparsing".
# Unparsing is good for end-user diagnostics; prettyprinting is good for compiler debugging.
# This is the implementation of our raw syntax unparser.
# For our raw syntax prettyprinter, see  src/lib/compiler/front/typer/print/prettyprint-raw-syntax.pkg



stipulate
    package err =  error_message;				# error_message			is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    package fxt =  fixity;					# fixity			is from   src/lib/compiler/front/basics/map/fixity.pkg
    package mtt =  more_type_types;				# more_type_types		is from   src/lib/compiler/front/typer/types/more-type-types.pkg
    package pp  =  standard_prettyprinter;			# standard_prettyprinter	is from   src/lib/prettyprint/big/src/standard-prettyprinter.pkg
    package rs  =  raw_syntax;					# raw_syntax			is from   src/lib/compiler/front/parser/raw-syntax/raw-syntax.pkg
    package sci =  sourcecode_info;				# sourcecode_info		is from   src/lib/compiler/front/basics/source/sourcecode-info.pkg
    package sy  =  symbol;					# symbol			is from   src/lib/compiler/front/basics/map/symbol.pkg
    package tc  =  typer_control;				# typer_control			is from   src/lib/compiler/front/typer/basics/typer-control.pkg
    package tpl =  tuples;					# tuples			is from   src/lib/compiler/front/typer-stuff/types/tuples.pkg
    package uj  =  unparse_junk;				# unparse_junk			is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
#   package ut  =  unparse_type;				# unparse_type			is from   src/lib/compiler/front/typer/print/unparse-type.pkg
#   package uv  =  unparse_value;				# unparse_value			is from   src/lib/compiler/front/typer/print/unparse-value.pkg
#   package vac =  variables_and_constructors;			# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
herein


    package   unparse_raw_syntax
    : (weak)  Unparse_Raw_Syntax				# Unparse_Raw_Syntax		is from   src/lib/compiler/front/typer/print/unparse-raw-syntax.api
    {
#	internals =  tc::internals;
internals = log::internals;

	lineprint =  REF FALSE;

	fun by f x y
            =
            f y x;

	null_fix = fxt::INFIX (0, 0);
	inf_fix  = fxt::INFIX (1000000, 100000);

	fun stronger_l (fxt::INFIX(_, m), fxt::INFIX (n, _)) => m >= n;
	    stronger_l _ => FALSE;			#  should not matter 
        end;

	fun stronger_r (fxt::INFIX(_, m), fxt::INFIX (n, _)) => n > m;
 	    stronger_r _ => TRUE; 			#  should not matter 
        end;

	fun prpos (   buf:  pp::Prettyprinter,
		      source:  sci::Sourcecode_Info,
                      charpos: Int
                  )
            =
	    if *lineprint
		  my (file: String, line: Int, pos: Int)
                      =
                      sci::filepos  source  charpos;

		  pp::string buf (int::to_string line);
		  pp::string buf ".";
		  pp::string buf (int::to_string pos);
	    else
                 pp::string buf (int::to_string charpos);
            fi;


	fun bug msg
            =
            error_message::impossible("unparse_raw_syntax: " + msg);


	arrow_stamp =  mtt::arrow_stamp;


	fun strength  type
            =
	    case type
		#              
		rs::TYPEVAR_TYPE(_) => 1;

		rs::TYPE_TYPE (type, args)
		    => 
		    case type
			#
			[type]
			    =>
			    if (sy::eq (sy::make_type_symbol("->"), type))   0;
			    else                                             2;
			    fi;

			_   => 2;
		    esac;


		rs::RECORD_TYPE _ => 2;

		rs::TUPLE_TYPE _ => 1;

		_ => 2;
            esac;


	fun checkpat (n, NIL)
                =>
                TRUE;

	    checkpat (n, (symbol, _) ! fields)
                =>
                sy::eq (symbol, tpl::number_to_label n)
                and
                checkpat (n+1, fields);
        end;

	fun checkexp (n, NIL)
                =>
                TRUE;

	    checkexp (n, (symbol, expression) ! fields)
		=>
		sy::eq (symbol, tpl::number_to_label n)
                and
		checkexp (n+1, fields);
        end;

	fun is_tuplepat (rs::RECORD_PATTERN { definition => [_], ...                   } ) =>  FALSE;
	    is_tuplepat (rs::RECORD_PATTERN { definition => defs, is_incomplete => FALSE } ) =>  checkpat (1, defs);
	    is_tuplepat _                                                              =>  FALSE;
        end;

	fun is_tupleexp (rs::RECORD_IN_EXPRESSION [_])      =>   FALSE;
	    is_tupleexp (rs::RECORD_IN_EXPRESSION fields)   =>   checkexp (1, fields);
	    is_tupleexp (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _))       =>   is_tupleexp a;
	    is_tupleexp _ => FALSE;
        end;

	fun get_fix (dictionary, symbol)
            =
	    find_in_symbolmapstack::find_fixity_by_symbol  (
                dictionary,
                sy::make_fixity_symbol (sy::name symbol)
            );



	fun strip_source_code_region_data (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _))
		=>
		strip_source_code_region_data a;

	    strip_source_code_region_data x
		=>
		x;
        end;



	fun trim     [x] =>  [];
	    trim (a ! b) =>  a ! trim b;
	    trim      [] =>  [];
        end;


	fun pp_path  buf  symbols
            =
	    {   fun print_one buf symbol
                    =
                    uj::unparse_symbol  buf  symbol;
	    
                uj::unparse_sequence
                    buf
		    { separator  =>  (fn buf =  (pp::string buf "::")),	# Was "."
		      print_one,
		      breakstyle =>  uj::ALIGN
                    }
		    symbols;
	    };

	fun unparse_pattern (context as (dictionary, source_opt)) buf
            =
	    {   ppsay =   pp::string  buf;
		#
		pp_symbol_list =   pp_path  buf;

		fun unparse_pattern' (rs::WILDCARD_PATTERN,                 _)   =>   ppsay "_";
		    unparse_pattern' (rs::VARIABLE_IN_PATTERN         p,    d)   =>   pp_symbol_list (p);
		    unparse_pattern' (rs::INT_CONSTANT_IN_PATTERN     i,    _)   =>   ppsay (multiword_int::to_string i);
		    unparse_pattern' (rs::UNT_CONSTANT_IN_PATTERN     w,    _)   =>   ppsay (multiword_int::to_string w);
		    unparse_pattern' (rs::STRING_CONSTANT_IN_PATTERN  s,    _)   =>   uj::unparse_mlstring  buf s;
		    unparse_pattern' (rs::CHAR_CONSTANT_IN_PATTERN    s,    _)   =>   uj::unparse_mlstring' buf s;

		    unparse_pattern' (rs::AS_PATTERN { variable_pattern, expression_pattern }, d)
			=>
			{   pp::begin_align_box buf;
			    unparse_pattern'(variable_pattern, d); ppsay " as "; unparse_pattern'(expression_pattern, d - 1);
			    pp::end_box buf;
			};

		    unparse_pattern' (rs::RECORD_PATTERN { definition => [],   is_incomplete }, _)
			=>
			if   is_incomplete      ppsay "{ ... }";
			else                    ppsay "()";
                        fi;

		    unparse_pattern' (r as rs::RECORD_PATTERN { definition, is_incomplete }, d)
			=>
			if (is_tuplepat r)
			    #
			    uj::unparse_closed_sequence
				buf
				{ front        =>  (by pp::string "("),
				  separator    =>  (fn buf
						       =
						       {   pp::string buf ", ";
							   pp::break buf { spaces=>0, indent_on_wrap=>0 };
						       }
						   ),
				  back       =>  (by pp::string ")"),
				  print_one  =>  (fn _ = fn (symbol, pattern) = unparse_pattern' (pattern, d - 1)),
				  breakstyle =>  uj::ALIGN
				}
				definition;
			else
			    uj::unparse_closed_sequence
				buf
				{ front     => (by pp::string "{ "),
				  separator => (fn buf =  { pp::string buf ", ";
							       pp::break buf { spaces=>0, indent_on_wrap=>0 };
							     }
					       ),
				  back      => (fn buf =  if is_incomplete  pp::string buf ", ... }";
							     else pp::string buf "}";
							     fi
					       ),
				  print_one => (fn buf =  fn (symbol, pattern) =  {   uj::unparse_symbol buf symbol;
											 pp::string buf " => ";
											 unparse_pattern' (pattern, d - 1);
										     }
					      ),
				  breakstyle => uj::ALIGN
				}
				definition;
			fi;

		    unparse_pattern' (rs::LIST_PATTERN NIL, d) =>   ppsay "[]";

		    unparse_pattern' (rs::LIST_PATTERN l, d)
			=>	
			{   fun print_one _ pattern
                                =
                                unparse_pattern' (pattern, d - 1);

			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string "["),
				  separator  =>  (fn buf = { pp::string buf ", ";
								pp::break buf { spaces=>0, indent_on_wrap=>0 };
							      }
						 ),
				  back       =>  (by pp::string "]"),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				l;
			};

		    unparse_pattern' (rs::TUPLE_PATTERN t, d)
			=> 
			{   fun print_one _ pattern
                                =
                                unparse_pattern'(pattern, d - 1);

			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string "("),
				  separator  =>  (fn buf = {   pp::string buf ", ";
								  pp::break buf { spaces=>0, indent_on_wrap=>0 };
							      }
						 ),
				  back       =>  (by pp::string ")"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				t;
			};

		    unparse_pattern' (rs::PRE_FIXITY_PATTERN fap, d)
			=>
			{   fun print_one _ { item, fixity, source_code_region }
                                =
                                unparse_pattern'(item, d - 1);		

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				fap;
			}; 

		    unparse_pattern' (rs::APPLY_PATTERN { constructor, argument }, d)
			=> 
			{   pp::begin_align_box buf;
			    unparse_pattern' (constructor, d);
			    ppsay " as ";
			    unparse_pattern'(argument, d);
			    pp::end_box buf;
			};

		    unparse_pattern' (rs::TYPE_CONSTRAINT_PATTERN { pattern, type_constraint }, d)
			=> 
			{   pp::begin_wrap_box buf;
			    unparse_pattern' (pattern, d - 1);
			    ppsay " :";
			    pp::break buf { spaces => 1,   indent_on_wrap => 2 };
			    unparse_type context buf (type_constraint, d);
			    pp::end_box buf;
			};

		    unparse_pattern' (rs::VECTOR_PATTERN NIL, d)
			=>
			ppsay "#[]";

		    unparse_pattern' (rs::VECTOR_PATTERN v, d)
			=> 
			{   fun print_one _ pattern
                                =
                                unparse_pattern'(pattern, d - 1);

			    uj::unparse_closed_sequence
				buf
				{ front       =>  (by pp::string "#["),
				  separator   =>  (fn buf = {    pp::string buf ", ";
								    pp::break buf { spaces=>1, indent_on_wrap=>0 };
							       }
						  ),
				  back       =>  (by pp::string "]"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				v;
			 };

		    unparse_pattern' (rs::SOURCE_CODE_REGION_FOR_PATTERN (pattern, (s, e)), d)
			=> 
			case source_opt
                            #
			    THE source
				=>
				if *internals
				     ppsay "<rs::SOURCE_CODE_REGION_FOR_PATTERN(";
				     prpos (buf, source, s); ppsay ", ";
				     prpos (buf, source, e); ppsay "): ";
				     unparse_pattern'(pattern, d); ppsay ">";
				else
				     unparse_pattern'(pattern, d);
				fi;

			     NULL => unparse_pattern'(pattern, d);
			 esac;

		    unparse_pattern' (rs::OR_PATTERN orpat, d)
			=>
			{   fun print_one _ pattern
                                =
                                unparse_pattern' (pattern, d - 1);		

			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string "("),
				  separator  =>  (fn buf = { pp::break buf { spaces=>1, indent_on_wrap=>0 }; pp::string buf "| ";}),
				  back       =>  (by pp::string ")"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				};
			}
                        orpat;
                end;

	    
                unparse_pattern';
	    }


	also
	fun unparse_expression (context as (dictionary, source_opt)) buf
            =
	    {   ppsay = pp::string buf;
		#
		fun lparen () = ppsay "("; 
		fun rparen () = ppsay ")";

		fun lpcond atom = if atom  ppsay "("; fi;      
		fun rpcond atom = if atom  ppsay ")"; fi;

		pp_symbol_list = pp_path buf;

		fun unparse_expression' (_, _, 0) => ppsay "<expression>";
		    unparse_expression' (rs::VARIABLE_IN_EXPRESSION p, _, _) => pp_symbol_list  p;
		    unparse_expression' (rs::IMPLICIT_THUNK_PARAMETER p, _, _) => { ppsay "#"; pp_symbol_list  p; };
		    unparse_expression' (rs::FN_EXPRESSION NIL, _, d) => ppsay "<function>";
		    unparse_expression' (rs::FN_EXPRESSION rules, _, d)
			=>	
			{   fun print_one _ pattern
                                =
                                unparse_rule context buf (pattern, d - 1);

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf = { pp::string buf "|";pp::break buf { spaces=>0, indent_on_wrap=>0 }; }),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				rules;
			};

		    unparse_expression' (rs::PRE_FIXITY_EXPRESSION fap, _, d)
			=> 
			{   fun print_one _ { item, fixity, source_code_region }
                                =
                                unparse_expression'(item, TRUE, d);		

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				fap;
			}; 

		    unparse_expression' (e as rs::APPLY_EXPRESSION _, atom, d)
			=>
			{   lpcond atom;
			    unparse_app_expression (e, null_fix, null_fix, d);
			    rpcond atom;
			};

		    unparse_expression' (rs::OBJECT_FIELD_EXPRESSION { object, field }, _, d)
			=>
			{   unparse_expression' (object, TRUE, d - 1);
			    ppsay "->";
                            uj::unparse_symbol buf field;
			};

		    unparse_expression' (rs::CASE_EXPRESSION { expression, rules }, _, d)
			=> 
			{   pp::begin_align_box buf;
			    ppsay "case (";		# Was "(case ";
                            unparse_expression'(expression, TRUE, d - 1);
                            pp::newline buf;		# Was uj::newline_indent buf 2;
			    uj::ppvlist buf (
                                ") ",
                                ";",		# Was "   | ",
                                (fn buf =  fn r =  unparse_rule context buf (r, d - 1)),
                                trim rules
                            );
			    ppsay "esac;";	# Was rparen();
			    pp::end_box buf;
			};

		    unparse_expression' (rs::LET_EXPRESSION { declaration, expression }, _, d)
			=>
			{   pp::begin_align_box buf;
			    ppsay "stipulate ";
			    pp::begin_align_box buf;
			    unparse_declaration context buf (declaration, d - 1); 
			    pp::end_box buf;
			    pp::break buf { spaces=>1, indent_on_wrap=>0 };
			    ppsay "herein ";
			    pp::begin_align_box buf;
			    unparse_expression'(expression, FALSE, d - 1);
			    pp::end_box buf;
			    pp::break buf { spaces=>1, indent_on_wrap=>0 };
			    ppsay "end";
			    pp::end_box buf;
			};

		    unparse_expression' (rs::SEQUENCE_EXPRESSION exps, _, d)
			=>
			uj::unparse_closed_sequence
			    buf
			    { front      =>  (by pp::string "("),
			      separator  =>  (fn buf = { pp::string buf ";";
							    pp::break buf { spaces=>1, indent_on_wrap=>0 };
							  }
					     ),
			      back       =>  (by pp::string ")"),
			      print_one  =>  (fn _ = fn expression = unparse_expression'(expression, FALSE, d - 1)),
			      breakstyle =>  uj::ALIGN
			    }
			    exps;

		    unparse_expression' (   rs::INT_CONSTANT_IN_EXPRESSION   i, _, _)   =>   ppsay (multiword_int::to_string i);
		    unparse_expression' (   rs::UNT_CONSTANT_IN_EXPRESSION   w, _, _)   =>   ppsay (multiword_int::to_string w);
		    unparse_expression' ( rs::FLOAT_CONSTANT_IN_EXPRESSION   r, _, _)   =>   ppsay r;
		    unparse_expression' (rs::STRING_CONSTANT_IN_EXPRESSION   s, _, _)   =>   uj::unparse_mlstring  buf s;
		    unparse_expression' (  rs::CHAR_CONSTANT_IN_EXPRESSION   s, _, _)   =>   uj::unparse_mlstring' buf s;

		    unparse_expression'(r as rs::RECORD_IN_EXPRESSION fields, _, d)
			=>
			if (is_tupleexp r)
			    #			    
			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string "("),
				  back       =>  (by pp::string ")"),
				  print_one  =>  (fn _ = fn (_, expression) = unparse_expression'(expression, FALSE, d - 1)),
				  breakstyle =>  uj::ALIGN,
				  separator  =>  (fn buf =  { pp::string buf ", ";
								 pp::break buf { spaces=>0, indent_on_wrap=>0 };
							       }
						 )
				}
				fields;
			else
			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string "{ "),
				  separator  =>  (fn buf = { pp::string buf ", ";
								pp::break buf { spaces=>0, indent_on_wrap=>0 } ;}
						 ),
				  back       =>  (by pp::string "}"),
				  print_one  =>  (fn buf = fn (name, expression)
							=
							{   uj::unparse_symbol buf name;
							    ppsay " => ";
							    unparse_expression'(expression, FALSE, d);
							}
					         ),
				  breakstyle =>  uj::ALIGN
				}
				fields;
                        fi;

		    unparse_expression' (rs::LIST_EXPRESSION p, _, d)
			=> 
			uj::unparse_closed_sequence
			    buf
			    { front      =>  (by pp::string "["),
			      separator  =>  (fn buf = { pp::string buf ", ";
							    pp::break buf { spaces=>0, indent_on_wrap=>0 } ;}
					     ),
			      back       =>  (by pp::string "]"),
			      print_one  =>  (fn buf =
						fn expression =
							    (unparse_expression'(expression, FALSE, d - 1))
					       ),
			      breakstyle =>  uj::ALIGN
			    }
			    p;

		    unparse_expression' (rs::TUPLE_EXPRESSION p, _, d)
			=>
			uj::unparse_closed_sequence
			    buf
			    {   front      =>  (by pp::string "("),
				separator  =>  (fn buf = { pp::string buf ", ";
							      pp::break buf { spaces=>0, indent_on_wrap=>0 };
							    }
					       ),
				back       =>  (by pp::string ")"),
				print_one  =>  (fn buf =
						  fn expression =  (unparse_expression'(expression, FALSE, d - 1))
						 ),
				breakstyle => uj::ALIGN
			    }
			    p;

		    unparse_expression' (rs::RECORD_SELECTOR_EXPRESSION name, atom, d)
			=>
			{   pp::begin_align_box buf;
#			    lpcond atom;			# Seems like pure clutter so commented out  2009-08-06 CrT
			    ppsay ".";			# Was "#"
                            uj::unparse_symbol buf name;
#			    rpcond atom;
			    pp::end_box buf;
			};

		    unparse_expression' (rs::TYPE_CONSTRAINT_EXPRESSION { expression, constraint }, atom, d)
			=> 
			{   pp::begin_wrap_box buf;
			    lpcond atom;
			    unparse_expression'(expression, FALSE, d); ppsay ":";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    unparse_type context buf (constraint, d);
			    rpcond atom;
			    pp::end_box buf;
			};

		    unparse_expression'(rs::EXCEPT_EXPRESSION { expression, rules }, atom, d)
			=>
			{   pp::begin_align_box buf;
			    lpcond atom;
			    unparse_expression'(expression, atom, d - 1);
                            pp::newline buf;
                            ppsay "except ";
			    uj::newline_indent buf 2;
			    uj::ppvlist buf (
                                "  ",
                                "; ",			# Was "| ",
				(fn buf =  fn r =  unparse_rule context buf (r, d - 1)),
                                rules
                            );
			    rpcond atom;
			    pp::end_box buf;
			};

		    unparse_expression' (rs::RAISE_EXPRESSION expression, atom, d)
			=> 
			{   pp::begin_align_box buf;
			    lpcond atom;
			    ppsay "raise exception ";
                            unparse_expression'(expression, TRUE, d - 1);
			    rpcond atom;
			    pp::end_box buf;
			};

		    unparse_expression' (rs::IF_EXPRESSION { test_case, then_case, else_case }, atom, d)
			=>
			{   pp::begin_align_box buf;
			    lpcond atom;
			    ppsay "if ";
			    pp::begin_align_box buf;
			    unparse_expression' (test_case, FALSE, d - 1);
			    pp::end_box buf;
			    pp::break buf { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "then ";
			    pp::begin_align_box buf;
			    unparse_expression' (then_case, FALSE, d - 1);
			    pp::end_box buf;
			    pp::break buf { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "else ";
			    pp::begin_align_box buf;
			    unparse_expression' (else_case, FALSE, d - 1);
			    pp::end_box buf;
			    rpcond atom;
			    pp::end_box buf;
			};

		    unparse_expression' (rs::AND_EXPRESSION (e1, e2), atom, d)
			=>
			{   pp::begin_align_box buf;
			    lpcond atom;
			    pp::begin_align_box buf;
			    unparse_expression' (e1, TRUE, d - 1);
			    pp::end_box buf;
			    pp::break buf { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "and ";
			    pp::begin_align_box buf;
			    unparse_expression' (e2, TRUE, d - 1);
			    pp::end_box buf;
			    rpcond atom;
			    pp::end_box buf;
			 };

		    unparse_expression' (rs::OR_EXPRESSION (e1, e2), atom, d)
			=>
			{   pp::begin_align_box buf;
			    lpcond atom;
			    pp::begin_align_box buf;
			    unparse_expression' (e1, TRUE, d - 1);
			    pp::end_box buf;
			    pp::break buf { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "or ";
			    pp::begin_align_box buf;
			    unparse_expression' (e2, TRUE, d - 1);
			    pp::end_box buf;
			    rpcond atom;
			    pp::end_box buf;
			};

		    unparse_expression' (rs::WHILE_EXPRESSION { test, expression }, atom, d)
			=>
			{   pp::begin_align_box buf;
			    ppsay "while ";
			    pp::begin_align_box buf;
			    unparse_expression'(test, FALSE, d - 1);
			    pp::end_box buf;
			    pp::break buf { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "do ";
			    pp::begin_align_box buf;
			     unparse_expression'(expression, FALSE, d - 1);
			    pp::end_box buf;
			    pp::end_box buf;
			};

		    unparse_expression' (rs::VECTOR_IN_EXPRESSION NIL, _, d) => ppsay "#[]";

		    unparse_expression' (rs::VECTOR_IN_EXPRESSION exps, _, d)
			=>
			{   fun print_one _ expression
				=
				unparse_expression'(expression, FALSE, d - 1);

			    uj::unparse_closed_sequence
				buf
				{ front     =>  (by pp::string "#["),
				  separator =>  (fn buf = { pp::string buf ", ";
							       pp::break buf { spaces=>1, indent_on_wrap=>0 };
							     }
						),
				  back  => (by pp::string "]"),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				exps;
			};

		    unparse_expression' (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), atom, d)
			=>
			case source_opt
			    #
			    THE source
				=>
				if *internals
				     ppsay "<MARK(";
				     prpos (buf, source, s); ppsay ", ";
				     prpos (buf, source, e); ppsay "): ";
				     unparse_expression'(expression, FALSE, d); ppsay ">";
				else
				     unparse_expression'(expression, atom, d);
				fi;

			    NULL => unparse_expression'(expression, atom, d);
			esac;
		end 

		also
		fun unparse_app_expression (_, _, _, 0)
			=>
			pp::string buf "<expression>";

		    unparse_app_expression arg
			=>
			{   ppsay = pp::string buf;

			    fun fixitypp (name, operand, left_fix, right_fix, d)
				=
				{   dname = symbol_path::to_string (symbol_path::SYMBOL_PATH name);
				    #
				    this_fix = case name
						       [id] => get_fix (dictionary, id);
						      _ => fxt::NONFIX;
					       esac;

				    fun pr_non expression
					=
					{   pp::begin_indented_wrap_box buf (pp::CURSOR_RELATIVE 2);
					    ppsay dname; pp::break buf { spaces=>1, indent_on_wrap=>0 };
					    unparse_expression'(expression, TRUE, d - 1);
					    pp::end_box buf;
					};

				    case this_fix
					#
					fxt::INFIX _
					    =>
					    case (strip_source_code_region_data operand)
						#
						rs::RECORD_IN_EXPRESSION [(_, pl), (_, pr)]
						    =>
						    {   atom =  stronger_l (left_fix, this_fix)
                                                             or stronger_r (this_fix, right_fix);

							my (left, right)
							    =
							    if atom     (null_fix, null_fix);
							    else	(left_fix, right_fix);
							    fi;

							{   pp::begin_indented_wrap_box buf (pp::CURSOR_RELATIVE 2);
							    lpcond atom;
							    unparse_app_expression (pl, left, this_fix, d - 1);
							    pp::break buf { spaces=>1, indent_on_wrap=>0 };
							    ppsay dname;
							    pp::break buf { spaces=>1, indent_on_wrap=>0 };
							    unparse_app_expression (pr, this_fix, right, d - 1);
							    rpcond atom;
							    pp::end_box buf;
							};
						    };

						e' => pr_non e';
					    esac;


					fxt::NONFIX => pr_non operand;
				    esac;
				};

			    fun apply_print (_, _, _, 0)
				    =>
				    ppsay "#";

				apply_print (rs::APPLY_EXPRESSION { function=>operator, argument=>operand }, l, r, d)
				    =>
				    case (strip_source_code_region_data operator)
					#
					rs::VARIABLE_IN_EXPRESSION v
					    =>
					    { path = v;

						fixitypp (path, operand, l, r, d);
					    };

					operator
					    =>
					    {   pp::begin_indented_wrap_box buf (pp::CURSOR_RELATIVE 2);
						unparse_expression'(operator, TRUE, d - 1); pp::break buf { spaces=>1, indent_on_wrap=>2 };
						unparse_expression'(operand,  TRUE, d - 1);
						pp::end_box buf;
					    };
				     esac;


				apply_print (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), l, r, d)
				    =>
				    case source_opt
					#
					THE source
					    =>
					    if *internals
						 ppsay "<MARK(";
						 prpos (buf, source, s); ppsay ", ";
						 prpos (buf, source, e); ppsay "): ";
						 unparse_expression'(expression, FALSE, d);
						 ppsay ">";
					    else
						 apply_print (expression, l, r, d);
					    fi;

					NULL => apply_print (expression, l, r, d);
				    esac;

				apply_print (e, _, _, d)
				    =>
				    unparse_expression'(e, TRUE, d);
			    end;

			    apply_print arg;
			};
                end;
	    
                fn (expression, depth)
                    =
                    unparse_expression' (expression, FALSE, depth);
	    }

	also
	fun unparse_rule (context as (dictionary, source_opt)) buf (rs::CASE_RULE { pattern, expression }, d)
            =
	    if (d>0) 
		#                
		pp::begin_align_box buf;
		unparse_pattern context buf (pattern, d - 1);
		pp::string buf " =>";
		pp::break buf { spaces=>1, indent_on_wrap=>2 };
		unparse_expression context buf (expression, d - 1);
		pp::end_box buf;
	    else
                pp::string buf "<rule>";
            fi

	also
	fun unparse_package_cast (context as (_, source_opt)) buf package_cast d
            =
	    {   ppsay = pp::string buf;
		#
		case package_cast
		    #
		    rs::NO_PACKAGE_CAST
			=>
			();

		    rs::WEAK_PACKAGE_CAST api_expression
			=> 
			{   ppsay " : (weak)";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    unparse_api_expression context buf (api_expression, d - 1);
			};

		    rs::PARTIAL_PACKAGE_CAST api_expression
			=> 
			{   ppsay " : (partial)";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    unparse_api_expression context buf (api_expression, d - 1);
			};

		    rs::STRONG_PACKAGE_CAST api_expression
			=> 
			{   ppsay " : ";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    unparse_api_expression context buf (api_expression, d - 1);
			};
		esac;
	    }

	also
	fun unparse_package_expression (context as (_, source_opt)) buf
            =
	    {   ppsay = pp::string buf;
		pp_symbol_list = pp_path buf;

		fun unparse_package_expression'(_, 0)
			=>
			ppsay "<package_expression>";

		    unparse_package_expression' (rs::PACKAGE_BY_NAME p, d)
			=>
			pp_symbol_list (p);

		    unparse_package_expression' (rs::PACKAGE_DEFINITION (rs::SEQUENTIAL_DECLARATIONS NIL), d)
			=>
			{   ppsay "package {";
			    pp::nonbreakable_spaces buf 1;
			    ppsay "};";
			};

		    unparse_package_expression' (rs::PACKAGE_DEFINITION de, d)
			=>
			{   pp::begin_vertical_box buf;
			    ppsay "package {";
			    uj::newline_indent buf 2;
			    unparse_declaration context buf (de, d - 1);
			    ppsay "};";
			    pp::end_box buf;
			};

		    unparse_package_expression' (rs::PACKAGE_CAST (stre, constraint), d)
			=>
			{   pp::begin_wrap_box buf;
			    unparse_package_expression' (stre, d - 1);

			    unparse_package_cast context buf constraint d;

			    pp::end_box  buf;
			};

		    unparse_package_expression' (rs::CALL_OF_GENERIC (path, str_list), d)
			=> 
			{   fun print_one buf (strl, bool)
				=
				{   ppsay "("; unparse_package_expression context buf (strl, d); ppsay ")";};

			    pp_symbol_list (path);

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				str_list;
			  };	

		    unparse_package_expression' (rs::INTERNAL_CALL_OF_GENERIC (path, str_list), d)
			=> 
			{   fun print_one buf (strl, bool)
				=
				{   ppsay "(";
				    unparse_package_expression context buf (strl, d);
				    ppsay ")";
				};

			    pp_symbol_list (path);

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				str_list;
			};	

		    unparse_package_expression' (rs::LET_IN_PACKAGE (declaration, body), d)
			=>
			{   pp::begin_align_box buf;
			    ppsay "stipulate ";
			    unparse_declaration context buf (declaration, d - 1); 
			    pp::newline buf;
			    ppsay " herein ";
			    unparse_package_expression'(body, d - 1);
			    pp::newline buf;
			    ppsay "end";
			    pp::end_box buf;
			};

		    unparse_package_expression' (rs::SOURCE_CODE_REGION_FOR_PACKAGE (body, (s, e)), d)
			=>
			unparse_package_expression' (body, d);
		end;

#		      (case source_opt
#			of THE source =>
#			   (ppsay "rs::SOURCE_CODE_REGION_FOR_PACKAGE(";
#			      prettyprintPackageexpression'(body, d); ppsay ", ";
#			      prpos (buf, source, s); ppsay ", ";
#			      prpos (buf, source, e); ppsay ")")
#			 | NULL => prettyprintPackageexpression'(body, d))

	    
		unparse_package_expression';
	    }

	also
	fun unparse_generic_expression (context as (_, source_opt)) buf
            =
	    {   ppsay = pp::string buf;
		pp_symbol_list = pp_path buf;

		fun unparse_generic_expression'(_, 0)
                        =>
                        ppsay "<generic_expression>";

		    unparse_generic_expression' (rs::GENERIC_BY_NAME (p, _), d)
                        =>
                        pp_symbol_list (p);

		    unparse_generic_expression' (rs::LET_IN_GENERIC (declaration, body), d)
			=>
			{   pp::begin_align_box buf;
			    ppsay "stipulate ";
                            unparse_declaration context buf (declaration, d - 1); 
			    pp::newline buf;
			    ppsay " herein ";
                            unparse_generic_expression'(body, d - 1);
                            pp::newline buf;
			    ppsay "end";
			    pp::end_box buf;
			};

		    unparse_generic_expression' (rs::CONSTRAINED_CALL_OF_GENERIC (path, sblist, fsigconst), d)
			=>
			{   fun print_one buf (package_expression, _)
				=
				{   ppsay "(";
				    unparse_package_expression context buf (package_expression, d);
				    ppsay ")";
				};

			    pp::begin_align_box buf;
			    pp_symbol_list path;

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sblist;

			    pp::end_box buf;
			};

		    unparse_generic_expression' (rs::SOURCE_CODE_REGION_FOR_GENERIC (body, (s, e)), d)
			=>
			unparse_generic_expression' (body, d);


		    unparse_generic_expression' (rs::GENERIC_DEFINITION _, d)
			=>
			error_message::impossible "prettyprintGenericexpression: GENERIC_DEFINITION";
                end;
	    
		unparse_generic_expression';
	    }

	also
	fun unparse_where_spec (context as (dictionary, source_opt)) buf
            =
	    {   ppsay = pp::string buf;

		fun unparse_where_spec'(_, 0)
                        =>
                        ppsay "<WhereSpec>";


		    unparse_where_spec' (rs::WHERE_TYPE([],[], type), d)
                        =>
                        unparse_type context buf (type, d);


		    unparse_where_spec' (rs::WHERE_TYPE (slist, tvlist, type), d)
			=> 
			{   fun print_one _ symbol
				=
				uj::unparse_symbol buf symbol;

			    fun print_one' _ tyv
				=
				unparse_typevar context buf (tyv, d);

			    ppsay "typeX ";

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one  =>  print_one',
				  breakstyle =>  uj::ALIGN
				}
				tvlist;

			    pp::break buf { spaces=>1, indent_on_wrap=>0 };

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist;	 

			    ppsay" =";
			    pp::break buf { spaces=>1, indent_on_wrap=>0 };
			    unparse_type context buf (type, d);
			};

		    unparse_where_spec' (rs::WHERE_PACKAGE (slist, slist'), d)
			=>
			{   fun print_one _ symbol
				=
				uj::unparse_symbol buf symbol;

			    ppsay "packageZ ";

			    uj::unparse_sequence
				buf
				{ separator =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist;

			    pp::break buf { spaces=>1, indent_on_wrap=>0 };

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist';
			};
                end;
	    
		unparse_where_spec';
	    }

	also
	fun unparse_api_expression (context as (dictionary, source_opt)) buf
            =
	    {   ppsay = pp::string buf;
		#
		fun unparse_api_expression'(_, 0)
                        =>
                        ppsay "<api_expression>";

		    unparse_api_expression'(rs::API_BY_NAME s, d)
                        =>
                        uj::unparse_symbol  buf  s;

		    unparse_api_expression'(rs::API_WITH_WHERE_SPECS (an_api, wherel), d)
			=>
			{   unparse_api_expression' (an_api, d);
			    pp::break buf { spaces=>1, indent_on_wrap=>0 };

			    case an_api
				#			      
				rs::API_BY_NAME s
				    =>
				    uj::ppvlist buf (
					"where ",
					"also ",
					(fn buf =  fn r =  unparse_where_spec context buf (r, d - 1)),
					wherel
				    );

				rs::SOURCE_CODE_REGION_FOR_API (rs::API_BY_NAME s, r)
				    =>
				    uj::ppvlist buf (
					"where ",
					"also ",
					(fn buf =  fn r =  unparse_where_spec context buf (r, d - 1)),
					wherel
				    );

				_ =>
				    {   pp::newline buf;
					#
					uj::ppvlist buf (
					    "where ",
					    "also ",
					    (fn buf =  fn r =  unparse_where_spec context buf (r, d - 1)),
					    wherel
					);
				    };
			    esac;
			};

		    unparse_api_expression' (rs::API_DEFINITION [], d)
			=>	
			{   ppsay "api {";
			    pp::nonbreakable_spaces buf 1;
			    ppsay "};";
			};

		    unparse_api_expression' (rs::API_DEFINITION specl, d)
			=> 
			{   fun print_one buf speci
                                =
                                unparse_specification context buf (speci, d);

			    pp::newline buf; # XXX BUGGO TEST ONLY
			    ppsay "api {";

			    pp::begin_vertical_box buf;
			    pp::newline buf;
#			    uj::newline_indent buf 4;

			    uj::unparse_sequence
				buf
				{ separator   =>  (fn buf =  pp::newline buf),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				specl;

			    pp::end_box buf;

			    pp::newline buf;
			    ppsay "};";
			};

		    unparse_api_expression' (rs::SOURCE_CODE_REGION_FOR_API (m, r), d)
                        =>
                        unparse_api_expression context buf (m, d);
                end;
	    
		unparse_api_expression';
	    }

	also
	fun unparse_generic_api_expression (context as (dictionary, source_opt)) buf
            =
	    {   ppsay =  pp::string buf;
		#
		fun unparse_generic_api_expression'(_, 0)
                        =>
                        ppsay "<generic_api_expression>";

		    unparse_generic_api_expression' (rs::GENERIC_API_BY_NAME s, d)
                        =>
                        uj::unparse_symbol buf s;

		    unparse_generic_api_expression' (rs::GENERIC_API_DEFINITION { parameter, result }, d)
			=>
			{   fun print_one buf (THE symbol, api_expression)
				    =>
				    {   ppsay "(";
                                        uj::unparse_symbol buf symbol;
                                        ppsay ":";
					unparse_api_expression context buf (api_expression, d);
					ppsay ")";
				    };

			        print_one buf (NULL, api_expression)
				    =>
				    {   ppsay "("; unparse_api_expression context buf (api_expression, d);
                                        ppsay ")";
                                    };
                            end;

			    uj::unparse_sequence
				buf
				{ separator  => (fn buf =  (pp::newline buf)),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				parameter;

			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    ppsay "=> ";
			    unparse_api_expression context buf (result, d);
			};

		    unparse_generic_api_expression' (rs::SOURCE_CODE_REGION_FOR_GENERIC_API (m, r), d)
			=>
			unparse_generic_api_expression context buf (m, d);
                end;
	    
		unparse_generic_api_expression';
	    }

	also
	fun unparse_specification (context as (dictionary, source_opt)) buf
            =
	    {   ppsay =  pp::string  buf;
		#
		fun pp_tyvar_list ([], d)
                        =>
                        ();

		    pp_tyvar_list ( [typevar], d)
			=> 
			{   unparse_typevar context buf (typevar, d);
			    pp::break buf { spaces=>1, indent_on_wrap=>0 };
			};

		    pp_tyvar_list (tyvar_list, d)
			=> 
			{   fun print_one _ typevar
				=
				(unparse_typevar context buf (typevar, d));

			    uj::unparse_closed_sequence
				buf
				{ front      =>  (fn buf = pp::string buf "("),
				  separator  =>  { pp::string buf ", "; fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } ); },
				  back       =>  { pp::string buf ")" ; fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } ); },
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				tyvar_list;
			};
                end;

		fun unparse_specification'(_, 0)
                        =>
                        ppsay "<Specification>";

		    unparse_specification' (rs::PACKAGES_IN_API sspo_list, d)
			=>
			{   fun print_one _ (symbol, api_expression, path)
				=
				case path
				    #
				    THE p => {   uj::unparse_symbol buf symbol;
						 ppsay " = ";
						 unparse_api_expression context buf (api_expression, d);
						 pp::break buf { spaces=>1, indent_on_wrap=>0 };
						 pp_path buf p;
					     };

				    NULL  => {   uj::unparse_symbol buf symbol;
						 ppsay " = ";
						 unparse_api_expression context buf (api_expression, d);
					     };
				esac;

			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string "packageY "),
				  separator  =>  (fn buf
						     =
						     {   pp::string buf ", ";
							 pp::break buf { spaces=>1, indent_on_wrap=>0 };
						     }
						 ),
				  back       =>  (by pp::string ""),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sspo_list;
			}; 

		    unparse_specification' (rs::TYPES_IN_API (stto_list, bool), d)
			=> 
			{   fun print_one _ (symbol, tyvar_list, tyo)
				=
				case tyo
				    #
				    THE type
					=>
					{   uj::unparse_symbol buf symbol;
					    ppsay "(";
					    pp_tyvar_list (tyvar_list, d);
					    ppsay ") =  ";
					    unparse_type context buf (type, d);
					};

				    NULL
					=>
					{   uj::unparse_symbol buf symbol;
					    ppsay "(";
					    pp_tyvar_list (tyvar_list, d);
					    ppsay ")";
					};
				esac;


			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string ""),			# Was "type "
				  separator  =>  fn buf =  {   pp::string buf "|";
							     pp::newline buf;
							 },
				  back       =>  (by pp::string ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				stto_list;
			}; 

		    unparse_specification' (rs::GENERICS_IN_API sf_list, d)
			=>
			{   fun pr buf (symbol, generic_api_expression)
				=
				{   uj::unparse_symbol buf symbol;
                                    ppsay " : ";
				    unparse_generic_api_expression context buf (generic_api_expression, d - 1);
				}; 

			    pp::begin_align_box buf;
			    uj::ppvlist buf ("generic package ", "also ", pr, sf_list);
			    pp::end_box buf;
			}; 

		    unparse_specification' (rs::VALUES_IN_API st_list, d)
			=> 
			{   fun pr buf (symbol, type)
				= 
				{   uj::unparse_symbol buf symbol;
                                    ppsay ":   ";
                                    unparse_type context buf (type, d);
                                }; 

			    pp::begin_align_box buf;
			    uj::ppvlist buf (
                                "",		# Was "my ",
                                "also ",
                                pr, 
                                st_list
                            );
                            ppsay "; ";
			    pp::end_box buf;
			}; 

		    unparse_specification' (rs::VALCONS_IN_API { sumtypes, with_types => [] }, d)
			=> 
			{   fun pr buf (dbing)
				=
				(unparse_sumtype context buf (dbing, d));

			    pp::begin_align_box buf;
			    uj::ppvlist buf ("", "also ", pr, sumtypes);
			    pp::end_box buf;
			}; 

		    unparse_specification' (rs::VALCONS_IN_API { sumtypes, with_types }, d)
			=> 
			{   fun prd buf (dbing) = (unparse_sumtype context buf (dbing, d));
			    fun prw buf (tbing) = (unparse_named_type context buf (tbing, d));

			    {   pp::begin_align_box buf;
				uj::ppvlist buf ("", "also ", prd, sumtypes);
				pp::newline buf;
				uj::ppvlist buf ("", "also ", prw, with_types);
				pp::end_box buf;
			    };
			};

		    unparse_specification' (rs::EXCEPTIONS_IN_API sto_list, d)
			=> 
			{   fun pr buf (symbol, tyo)
				=
				case tyo
				    #
				    THE type => {   uj::unparse_symbol buf symbol;
						    ppsay " : ";
						    unparse_type context buf (type, d);
						};

				    NULL => uj::unparse_symbol buf symbol;
				esac;

			    pp::begin_align_box buf;
			    uj::ppvlist buf ("exception ", "also ", pr, sto_list);
			    pp::end_box buf;
			}; 

		    unparse_specification' (rs::PACKAGE_SHARING_IN_API paths, d)
			=> 
			{   pp::begin_align_box buf;
			    uj::ppvlist buf ("sharing ", " = ", pp_path, paths);
			    pp::end_box buf;
			};

		    unparse_specification' (rs::TYPE_SHARING_IN_API paths, d)
			=> 
			{   pp::begin_align_box buf;
			    uj::ppvlist buf ("sharing ", " = ", pp_path, paths);
			    pp::end_box buf;
			};

		    unparse_specification' (rs::IMPORT_IN_API api_expression, d)
			=>
			unparse_api_expression context buf (api_expression, d);

		    unparse_specification' (rs::SOURCE_CODE_REGION_FOR_API_ELEMENT (m, r), d)
			=>
			unparse_specification context buf (m, d);
                end;
	    
		unparse_specification';
	    }

	also
	fun unparse_declaration (context as (dictionary, source_opt)) buf
            =
	    {   ppsay = pp::string buf;
		#
		pp_symbol_list =  pp_path  buf;

		fun unparse_declaration' (_, 0)
                        =>
                        ppsay "<declaration>";

		    unparse_declaration' (rs::VALUE_DECLARATIONS (vbs, typevars), d)
			=>
			{   pp::begin_align_box buf;
			    uj::ppvlist buf (
                                "my ",
                                "also ",
                                (fn buf =  fn named_value =  unparse_named_value context buf (named_value, d - 1)),
                                vbs
                            );
			    pp::end_box buf;
			};

		    unparse_declaration' (rs::FIELD_DECLARATIONS (fields, typevars), d)
			=>
			# 2009-02-23 CrT: A quick first-cut solution, duplicated from VALUE_DECLARATIONS: case:
			#
			{   pp::begin_align_box buf;
			    uj::ppvlist buf (
                                "field ",
                                "also ",
                                (fn buf =  fn named_value =  unparse_named_field context buf (named_value, d - 1)),
                                fields
                            );
			    pp::end_box buf;
			};

		    unparse_declaration' (rs::RECURSIVE_VALUE_DECLARATIONS (rvbs, typevars), d)
			=> 
			{   pp::begin_align_box buf;
			    uj::ppvlist
				buf
				(   "my rec ",
				    "also ",
				    (  fn buf =
				       fn named_recursive_values =
				       unparse_named_recursive_values
					   context
					   buf
					   (named_recursive_values, d - 1)
				    ),
				    rvbs
				);

			    pp::end_box buf;
			};

		    unparse_declaration' (rs::FUNCTION_DECLARATIONS (fbs, typevars), d)
			=>
			{   pp::begin_align_box buf;
			    uj::ppvlist'
				buf
				(   "fun ",
				    "also ",
				    (   fn buf =
					fn str =
					fn fb =
					unparse_named_sml_function
					    context
					    buf
					    str
					    (fb, d - 1)
				    ),
				    fbs
				);

			    pp::end_box  buf;
			};

		    unparse_declaration' (rs::NADA_FUNCTION_DECLARATIONS (fbs, typevars), d)
			=>
			{   pp::begin_align_box buf;
			    uj::ppvlist'
				buf
				(   "fun ",
				    "also ",
				    (   fn buf =
					fn str =
					fn fb =
					unparse_named_lib7function
					    context
					    buf
					    str
					    (fb, d - 1)
				    ),
				    fbs
				);

			    pp::end_box  buf;
			};

		    unparse_declaration' (rs::TYPE_DECLARATIONS types, d)
			=>
			{   fun print_one  buf  type
				=
				(unparse_named_type context buf (type, d));

			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string ""),	# Was "type "
				  separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  back       =>  (by pp::string ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				types;
			}; 	

		    unparse_declaration' (rs::SUMTYPE_DECLARATIONS { sumtypes, with_types => [] }, d)
			=> 
			{   fun print_one _ (dbing)
				=
				(unparse_sumtype context buf (dbing, d));


			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string ""),
				  separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				  back       =>  (by pp::string ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sumtypes;
			};             

		    unparse_declaration' (rs::SUMTYPE_DECLARATIONS { sumtypes, with_types }, d)
			=> 
			{   fun prd buf dbing = (unparse_sumtype context buf (dbing, d));
			    fun prw buf tbing = (unparse_named_type context buf (tbing, d));

			    {   pp::begin_align_box buf;
				#
				uj::unparse_closed_sequence
				    buf
				    { front      =>  (by pp::string ""),
				      separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				      back       =>  (by pp::string ";"),
				      print_one  =>  prd,
				      breakstyle =>  uj::ALIGN
				    }
				    sumtypes;

				pp::newline buf;

				uj::unparse_closed_sequence
				    buf
				    { front      =>  (by pp::string "withtype "),
				      separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				      back       =>  (by pp::string ""),
				      print_one  =>  prw,
				      breakstyle =>  uj::ALIGN
				    }
				    with_types;

				pp::end_box buf;
			    };
			};

		    unparse_declaration' (rs::ABSTRACT_TYPE_DECLARATIONS { abstract_types, with_types => [], body }, d)
			=> 
			{   fun prd buf dbing = (unparse_sumtype context buf (dbing, d));
			    fun prw buf tbing = (unparse_named_type     context buf (tbing, d));

			    {   pp::begin_align_box buf;
				(   uj::unparse_closed_sequence
					buf
					{ front      =>  (by pp::string ""),
					  separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
					  back       =>  (by pp::string ";"),
					  print_one  =>  prd,
					  breakstyle =>  uj::ALIGN
					}
					abstract_types
				);
				pp::newline buf;
				unparse_declaration' (body, d);
				pp::end_box buf;
			    };
			};

		    unparse_declaration' (rs::ABSTRACT_TYPE_DECLARATIONS { abstract_types, with_types, body }, d)
			=> 
			{   fun prd _ (dbing) = (unparse_sumtype context buf (dbing, d));
			    fun prw _ (tbing) = (unparse_named_type context buf (tbing, d));

			    {   pp::begin_align_box buf;
				(   uj::unparse_closed_sequence
					buf
					{ front      =>  (by pp::string ""),
					  separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
					  back       =>  (by pp::string ";"),
					  print_one  =>  prd,
					  breakstyle =>  uj::ALIGN
					}
					abstract_types
				);
				pp::newline buf;
				(   uj::unparse_closed_sequence
					buf
					{ front      =>  (by pp::string "withtype "),
					  separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
					  back       =>  (by pp::string ""),
					  print_one  =>  prw,
					  breakstyle =>  uj::ALIGN
					}
					with_types
				);
				pp::newline buf;
				unparse_declaration' (body, d);
				pp::end_box buf;
			    };
			};

		    unparse_declaration' (rs::EXCEPTION_DECLARATIONS ebs, d)
			=>
			{   pp::begin_align_box buf;
			    (   (fn buf =  fn eb =  unparse_named_exception context buf (eb, d - 1)),   ebs   );
			    pp::end_box buf;
			};

		    unparse_declaration' (rs::PACKAGE_DECLARATIONS sbs, d)
			=>
			{   fun print_one _ sbing
				=
				(unparse_named_package context buf (sbing, d));

			    uj::unparse_closed_sequence
				buf
				{   front      =>  (by pp::string "package "),
				    separator  =>  (fn buf =  (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
				    back       =>  (by pp::string ";"),
				    print_one,
				    breakstyle =>  uj::ALIGN
				}
				sbs;
			};

		    unparse_declaration' (rs::GENERIC_DECLARATIONS fbs, d)
			=> 
			{   fun f buf generic_naming
				=
				unparse_named_generic context buf (generic_naming, d);


			    pp::begin_align_box buf;
			    uj::ppvlist buf ("generic package ", "also ", f, fbs);
			    pp::end_box buf;
			};

		    unparse_declaration' (rs::API_DECLARATIONS sigvars, d)
			=> 
			{   fun f buf (rs::NAMED_API { name_symbol=>fname, definition=>def } )
				=>
				{   uj::unparse_symbol  buf  fname;
				    pp::newline buf;
                                    ppsay "=";
				    unparse_api_expression context buf (def, d);
				};

				f buf (rs::SOURCE_CODE_REGION_FOR_NAMED_API (t, r))
                                    =>
                                    f buf t;
			    end;


			    pp::begin_align_box buf;
			    uj::ppvlist buf ("api ", "also ", f, sigvars);	# Was "api "
			    pp::end_box buf;
			};

		    unparse_declaration' (rs::GENERIC_API_DECLARATIONS sigvars, d)
			=> 
			{   fun print_one buf sigv
				=
				unparse_generic_api_naming context buf (sigv, d);

			    pp::begin_align_box buf;

			    uj::unparse_sequence
				buf
				{ separator  =>  pp::newline,
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sigvars;

			    pp::end_box buf;
			};

		    unparse_declaration' (rs::LOCAL_DECLARATIONS (inner, outer), d)
			=>
			{   pp::align_box buf .{
				pp::newline buf;  ppsay "with";
                                pp::vertical_box buf .{
				    pp::newline buf;  unparse_declaration'(inner, d - 1);
                                };
				pp::newline buf;  ppsay "do ";
                                pp::vertical_box buf .{
				    pp::newline buf;  unparse_declaration'(outer, d - 1);
	                        };
				pp::newline buf;  ppsay "end;\t\t# with";
			    };
			    pp::newline buf;
			};

		    unparse_declaration' (rs::SEQUENTIAL_DECLARATIONS decs, d)
			=>
			{   pp::begin_align_box buf;
			    #
			    uj::unparse_sequence
				buf
				{ separator  =>  pp::newline,
				  print_one  =>  (fn buf =  fn declaration =  unparse_declaration' (declaration, d)),
				  breakstyle =>  uj::ALIGN
				}
				decs;

			    pp::end_box buf;
			};

		    unparse_declaration' (rs::INCLUDE_DECLARATIONS named_packages, d)
			=> 
			{   pp::begin_align_box buf;
			    ppsay "use ";

			    uj::unparse_sequence
				buf
				{ separator  =>  (fn buf =  pp::break buf { spaces=>1, indent_on_wrap=>0 }),
				  print_one  =>  (fn buf =  fn sp =  pp_symbol_list sp),
				  breakstyle =>  uj::ALIGN
				}
				named_packages;

			    pp::end_box buf;
			};

		    unparse_declaration' (rs::OVERLOADED_VARIABLE_DECLARATION (symbol, type, explist, extension), d)
		        =>
			{   ppsay "overloaded my ";
		            uj::unparse_symbol buf symbol;
			    ppsay ( extension ?? " += ... " :: " = ... ");
			};

		    unparse_declaration' (rs::FIXITY_DECLARATIONS { fixity, ops }, d)
			=>
			{   pp::begin_align_box buf;
			    #
			    case fixity
                                #
				fxt::NONFIX => ppsay "nonfix my ";

				fxt::INFIX (i, _)
				 => 
				 {   if (i % 2 == 0)	ppsay "infix my ";
				     else		ppsay "infixr my ";
                                     fi;

				     if (i / 2 > 0)
				         #
					 ppsay (int::to_string (i / 2));
					 ppsay " ";
				     fi;
				 };
			   esac;

			   uj::unparse_sequence
			       buf
			       { separator  =>  (fn buf =  pp::break buf { spaces=>1, indent_on_wrap=>0 }),
				 print_one  =>  uj::unparse_symbol,
				 breakstyle =>  uj::ALIGN
			       }
			       ops;

			   pp::end_box buf;
			};

		    unparse_declaration' (rs::SOURCE_CODE_REGION_FOR_DECLARATION (declaration, (s, e)), d)
			=>  
#			case source_opt
#			    #
#			    THE source
#				 =>
#				 {   ppsay "rs::SOURCE_CODE_REGION_FOR_DECLARATION(";
#				     unparse_declaration'(declaration, d); ppsay ", ";
#				     prpos (buf, source, s); ppsay ", ";
#				     prpos (buf, source, e); ppsay ")";
#				 };
#
#			    NULL
#			       =>
			       unparse_declaration' (declaration, d);
#			esac;

		    unparse_declaration' (rs::PRE_COMPILE_CODE string, d)
			=>
			ppsay ("#DO " + string);

                  end;
	      
                  unparse_declaration';
	      }

	also
	fun unparse_named_value (context as (dictionary, source_opt)) buf
            =
	    {   ppsay =  pp::string  buf;
		#
		fun unparse_named_value'(_, 0)
                        =>
                        ppsay "<naming>";

		    unparse_named_value' (rs::NAMED_VALUE { pattern, expression, ... }, d)
			=> 
			{   pp::begin_align_box buf;
			    unparse_pattern context buf (pattern, d - 1);
			    pp::string buf " =";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    unparse_expression context buf (expression, d - 1);
			    pp::end_box buf;
			};

		    unparse_named_value' (rs::SOURCE_CODE_REGION_FOR_NAMED_VALUE (named_value, source_code_region), d)
			=>
			unparse_named_value' (named_value, d);
                end;
	    
		unparse_named_value';
	    }

	also
	fun unparse_named_field (context as (dictionary, source_opt)) buf
            =
	    # 2009-02-23 CrT: A quick first-cut solution
	    # duplicated from unparse_named_value:
	    #
	    {   ppsay =  pp::string  buf;
		#
		fun unparse_named_field'(_, 0)
                        =>
                        ppsay "<field>";

		    unparse_named_field' (rs::NAMED_FIELD { name => symbol, type => case_pattern, init }, d)
			=> 
			{   pp::begin_align_box buf;
			    pp_path buf [symbol];
			    pp::end_box buf;
			};

		    unparse_named_field' (rs::SOURCE_CODE_REGION_FOR_NAMED_FIELD (named_field, source_code_region), d)
			=>
			unparse_named_field' (named_field, d);
                end;
	    
		unparse_named_field';
	    }

	also
	fun unparse_named_recursive_values (context as (_, source_opt)) buf
            = 
	    {   ppsay =  pp::string  buf;
		#
		fun unparse_named_recursive_values'(_, 0)=> ppsay "<rec naming>";

		    unparse_named_recursive_values' (rs::NAMED_RECURSIVE_VALUE { variable_symbol, expression, ... }, d)
			=>
			{   pp::begin_wrap_box buf;
			    uj::unparse_symbol buf variable_symbol;
                            pp::string buf " =";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
                            unparse_expression context buf (expression, d - 1);
			    pp::end_box buf;
			};

		    unparse_named_recursive_values' (rs::SOURCE_CODE_REGION_FOR_RECURSIVELY_NAMED_VALUE (named_recursive_values, source_code_region), d)
			=>
			unparse_named_recursive_values' (named_recursive_values, d);
                end;
	    
		unparse_named_recursive_values';
	    }

	also
	fun unparse_named_sml_function (context as (_, source_opt)) buf head
            = 
	    {   ppsay =  pp::string  buf;


		fun unparse_named_sml_function'(_, 0)
                        =>
                        ppsay "<FunNaming>";

		    unparse_named_sml_function' (rs::NAMED_FUNCTION { pattern_clauses, is_lazy, kind, null_or_type }, d)
			=>
			{
			    case kind
				rs::PLAIN_FUN => ppsay "";
			       rs::METHOD_FUN => ppsay " (method) ";
			      rs::MESSAGE_FUN => ppsay " (message) ";
			    esac;

                            case null_or_type 
				THE type => {   ppsay " : ";
						unparse_type context buf (type, d - 1);
					    };
				NULL => ();
			    esac;

			    uj::ppvlist buf
                              ( head, "  ; ",
			        (fn buf =   fn (cl: rs::Pattern_Clause) =   (unparse_pattern_clause context buf (cl, d))),
			        pattern_clauses
                              );
			};

		    unparse_named_sml_function' (rs::SOURCE_CODE_REGION_FOR_NAMED_FUNCTION (t, r), d)
			=>
			unparse_named_sml_function context buf head (t, d);
                end;
	    
		unparse_named_sml_function';
	    }

	also
	fun unparse_pattern_clause (context as (_, source_opt)) buf
            =
	    {   ppsay =  pp::string  buf;

		fun unparse_pattern_clause' (rs::PATTERN_CLAUSE { patterns, result_type, expression }, d)
                    =
		    {   fun print_one _ {   item:		    rs::Case_Pattern,
					    fixity:		    Null_Or( rs::Symbol ),
					    source_code_region:    rs::Source_Code_Region
					}
                            =
			    case fixity
			        #
			         THE a =>   unparse_pattern context buf (item, d);

				 NULL =>    case item
						#
						rs::PRE_FIXITY_PATTERN p
						    =>
						    {   pp::string buf "(";unparse_pattern context buf (item, d);
							pp::string buf ")";
						    };

						rs::TYPE_CONSTRAINT_PATTERN p
						    =>
						    {   pp::string buf "(";unparse_pattern context buf (item, d);
							pp::string buf ")";
						    };

						rs::AS_PATTERN p
						    =>
						    {   pp::string buf"(";unparse_pattern context buf (item, d);
							pp::string buf ")";
						    };

						rs::OR_PATTERN p
						    =>
						    {   pp::string buf "(";unparse_pattern context buf (item, d);
							pp::string buf ")";
						    };

						_   =>
						    unparse_pattern context buf (item, d);
					    esac;
			    esac;


		    
			pp::begin_wrap_box buf;

			uj::unparse_sequence
			    buf
			    { separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
			      print_one,
			      breakstyle =>  uj::ALIGN
			    }
			    patterns;

			case result_type
			    #			  
			    THE type => {   pp::string buf ":";
					    unparse_type context buf (type, d);
					};

			    NULL => ();
			esac;

			pp::string buf " =";
			pp::break buf { spaces=>1, indent_on_wrap=>0 }; 
			unparse_expression context buf (expression, d);
			pp::end_box buf;

		    }; 

	    
		unparse_pattern_clause';
	    }

	also
	fun unparse_named_lib7function (context as (_, source_opt)) buf head
            = 
	    {   ppsay =  pp::string buf;
		#
		fun unparse_named_lib7function'(_, 0)
                        =>
                        ppsay "<FunNaming>";

		    unparse_named_lib7function' (rs::NADA_NAMED_FUNCTION (clauses, ops), d)
			=>
			uj::ppvlist buf (head, "  | ",
			   (fn buf =
                            fn (cl: rs::Nada_Pattern_Clause) = (unparse_lib7pattern_clause context buf (cl, d))
                           ),
			   clauses);

		    unparse_named_lib7function' (rs::SOURCE_CODE_REGION_FOR_NADA_NAMED_FUNCTION (t, r), d)
			=>
			unparse_named_lib7function context buf head (t, d);
                end;
	    
		unparse_named_lib7function';
	    }

	also
	fun unparse_lib7pattern_clause (context as (_, source_opt)) buf
            =
	    {   ppsay =  pp::string buf;
		#
		fun unparse_lib7pattern_clause' (rs::NADA_PATTERN_CLAUSE { pattern, result_type, expression }, d)
                    =
		    {   fun print_one _ (item:   rs::Case_Pattern)
                            =
                            #  XXX BUGGO FIXME:  Need to be more intelligent about paren insertion: 
			    {   pp::string buf "(";
                                unparse_pattern context buf (item, d);
				pp::string buf ")";
                            };

		    
			pp::begin_wrap_box buf;

			uj::unparse_sequence
			    buf
			    { separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
			      print_one,
			      breakstyle =>  uj::ALIGN
			    }
			    [ pattern ];         #  XXX BUGGO FIXME this list is always len 1 (obviously) so the logic here can probably be simplified. 


			case result_type
			    #
			    THE type => {   pp::string buf ":";
					    unparse_type context buf (type, d);
					};

			    NULL => ();
			esac;


			pp::string buf " =";
			pp::break buf { spaces=>1, indent_on_wrap=>0 }; 
			unparse_expression context buf (expression, d);
			pp::end_box buf;

		    }; 

	    
		unparse_lib7pattern_clause';
	    }

	also
	fun unparse_named_type (context as (_, source_opt)) buf 
            = 
	    {   ppsay = pp::string buf;

		fun pp_tyvar_list (symbol_list, d)
                    =
		    {   fun print_one _ (typevar)
                            =
                            unparse_typevar  context  buf  (typevar, d);
		    
                        uj::unparse_sequence
                            buf
			    { separator  =>  (fn buf =  { pp::string buf ",";	# Was "*"
					       pp::break buf { spaces=>1, indent_on_wrap=>0 } ;}),
			      print_one,
			      breakstyle =>  uj::ALIGN
                            }
			    symbol_list;
		    };

		fun unparse_named_type'(_, 0)
                        =>
                        ppsay "<t::naming>";

		    unparse_named_type' (rs::NAMED_TYPE { name_symbol, definition, typevars }, d)
			=> 
			{   pp::begin_wrap_box buf;
			    uj::unparse_symbol buf name_symbol;
                            if (list::length typevars > 0)
				pp::string buf "(";
				pp_tyvar_list (typevars, d);
				pp::string buf ")";
			    fi;
			    pp::string buf " =";

			    pp::break buf { spaces=>1, indent_on_wrap=>0 };
                            unparse_type context buf (definition, d);
			    pp::end_box buf;
			};

		    unparse_named_type' (rs::SOURCE_CODE_REGION_FOR_NAMED_TYPE (t, r), d)
			=>
			unparse_named_type context buf (t, d);
                end;
	    
		unparse_named_type';
	    }

	also
	fun unparse_sumtype (context as (_, source_opt)) buf
            = 
	    {   ppsay = pp::string buf;

# Commented out because apparently unused  -- 2009-08-08 CrT
#		fun pp_tyvar_list (symbol_list, d)
#                    =
#		    {   fun print_one _ (typevar)
#                            =
#                            (unparse_typevar context buf (typevar, d));
#		    
#                        uj::unparse_sequence
#                            buf
#			    { separator   => (fn buf =  { pp::string buf ",";	# Was "*"
#					     pp::break buf { spaces=>1, indent_on_wrap=>0 } ;}),
#			      print_one,
#			      breakstyle => uj::ALIGN
#                            }
#			    symbol_list;
#		    };

		fun unparse_sumtype'(_, 0)
                        =>
                        ppsay "<d::naming>";

		    unparse_sumtype' (rs::SUM_TYPE { name_symbol, typevars, right_hand_side, is_lazy }, d)
			=> 
			{   pp::begin_wrap_box buf;

			    uj::unparse_symbol buf name_symbol;
			    pp::string buf " =";

			    pp::break buf { spaces=>1, indent_on_wrap=>0 };
			    unparse_sumtype_right_hand_side context buf (right_hand_side, d);

			    pp::end_box buf;
			};

		    unparse_sumtype' (rs::SOURCE_CODE_REGION_FOR_UNION_TYPE (t, r), d)
			=>
			unparse_sumtype context buf (t, d);
                end;
	    
		unparse_sumtype';
	    }

	also
	fun unparse_sumtype_right_hand_side (context as (_, source_opt)) buf
            =
	    {   ppsay =  pp::string buf;
		#
		fun unparse_sumtype_right_hand_side'(_, 0)
                        =>
                        ppsay "<sumtype_naming_right_hand_side>";

		    unparse_sumtype_right_hand_side' (rs::VALCONS const, d)
			=> 
			{   fun print_one buf   (symbol: rs::Symbol,   tv: Null_Or(rs::Any_Type))
				=
				case tv
				    #
				    THE a =>    {    uj::unparse_symbol buf symbol;
						     ppsay " ";	# Was " of "
						     unparse_type context buf (a, d);
						};

				    NULL =>   (uj::unparse_symbol buf symbol);
				esac;

			    uj::unparse_sequence
				buf
				{ separator   => (fn buf = { pp::string buf " |";
								pp::break buf { spaces=>1, indent_on_wrap=>0 };
							      }
						 ),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				const;
			};

		    unparse_sumtype_right_hand_side' (rs::REPLICAS symlist, d)
			=> 
			uj::unparse_sequence
			    buf
			    { separator  =>  (fn buf
						 =
						 {   pp::string buf " |";
						     pp::break buf { spaces=>1, indent_on_wrap=>0 };
						 }
					     ),
			      print_one  =>  (fn buf =  fn symbol =  uj::unparse_symbol buf symbol),
			      breakstyle =>  uj::ALIGN
			    }
			    symlist;
                end;
	    
                unparse_sumtype_right_hand_side';
	    }

	also
	fun unparse_named_exception (context as (_, source_opt)) buf
            =
	    {   ppsay = pp::string buf;
		pp_symbol_list = pp_path buf;

		fun unparse_named_exception' (_, 0)
                        =>
                        ppsay "<Eb>";

		    unparse_named_exception' (   rs::NAMED_EXCEPTION {
                                                         exception_symbol => exn,
                                                         exception_type   => etype
                                                     },
                                                     d
                                                 )
			=> 
			case etype
			    #			  
			    THE a =>    {   pp::begin_align_box buf;
					    uj::unparse_symbol buf exn;
					    pp::string buf " =";
					    pp::break buf { spaces=>1, indent_on_wrap=>2 };
					    unparse_type context buf (a, d - 1);
					    pp::end_box buf;
					};

			    NULL =>     {   pp::begin_align_box buf;
					    uj::unparse_symbol buf exn; 
					    pp::end_box buf;
					};
			esac;


		    unparse_named_exception' ( rs::DUPLICATE_NAMED_EXCEPTION { exception_symbol=>exn, equal_to=>edef }, d)
			=> 
			# ASK MACQUEEN IF WE NEED TO PRINT EDEF XXX BUGGO FIXME 
			{   pp::begin_align_box buf;
			    uj::unparse_symbol buf exn;
			    pp::string buf " =";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    pp_symbol_list (edef);
			    pp::end_box buf;
			};

		    unparse_named_exception' (rs::SOURCE_CODE_REGION_FOR_NAMED_EXCEPTION (t, r), d)
			=>
			unparse_named_exception context buf (t, d);
                end;
	    
		unparse_named_exception';
	    }

	also
	fun unparse_named_package (context as (_, source_opt)) buf
            =
	    {   ppsay = pp::string buf;

		fun unparse_named_package' (_, 0)
                        =>
                        ppsay "<rs::NAMED_PACKAGE>";

		    unparse_named_package' ( rs::NAMED_PACKAGE { name_symbol=>name, definition=>def, constraint, kind }, d)
			=> 
			{   ppsay case kind
                                      rs::PLAIN_PACKAGE  => "package ";
                                      rs::CLASS_PACKAGE  => "class ";
                                      rs::CLASS2_PACKAGE => "class2 ";
                                  esac;

			    pp::begin_align_box buf;
			    uj::unparse_symbol buf name;

                            unparse_package_cast context buf constraint d;

                            pp::string buf " =";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
                            unparse_package_expression context buf (def, d - 1);
			    pp::end_box buf;
			};

		    unparse_named_package' (rs::SOURCE_CODE_REGION_FOR_NAMED_PACKAGE (t, r), d)
			=>
			unparse_named_package context buf (t, d);
                end;
	    
		unparse_named_package';
	    }

	also
	fun unparse_named_generic (context as (_, source_opt)) buf
            =
	    {   ppsay =  pp::string  buf;
		#
		fun unparse_named_generic' (_, 0)
                        =>
                        ppsay "<rs::NAMED_GENERIC>";

		    unparse_named_generic' (
                        rs::NAMED_GENERIC {
                            name_symbol => name,
                            definition => rs::GENERIC_DEFINITION { parameters, body, constraint }
                        },
                        d
                    )
			=>
			{   pp::begin_align_box buf;
			    #
			    uj::unparse_symbol buf name;

			    {   fun print_one buf (THE symbol, api_expression)
					=>
					{   ppsay "(";
					    uj::unparse_symbol buf symbol;
					    ppsay " : ";
					    unparse_api_expression context buf (api_expression, d);
					    ppsay ")";
					};

				    print_one buf (NULL, api_expression)
					=>
					{   ppsay "(";
					    unparse_api_expression context buf (api_expression, d);
					    ppsay ")";
					};
                                end;

				{   uj::unparse_sequence
					buf
					{ separator  =>  (fn buf = (pp::break buf { spaces=>1, indent_on_wrap=>0 } )),
					  print_one,
					  breakstyle =>  uj::ALIGN
					}
					parameters;



                                    unparse_package_cast  context  buf  constraint  d;


				    pp::nonbreakable_spaces buf 1;

				    ppsay "=";
                                    pp::break buf { spaces=>1, indent_on_wrap=>0 };

				    unparse_package_expression context buf (body, d);};
			    };
			    pp::end_box buf;
			};

		    unparse_named_generic' ( rs::NAMED_GENERIC { name_symbol=>name, definition=>def }, d)
			=>
			{   pp::begin_align_box buf;
			    uj::unparse_symbol buf name;
			    pp::string buf " =";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
			    unparse_generic_expression context buf (def, d - 1);
			    pp::end_box buf;
			}; 

		    unparse_named_generic' (rs::SOURCE_CODE_REGION_FOR_NAMED_GENERIC (t, r), d)
			=>
			unparse_named_generic context buf (t, d);
                end;
	    
		unparse_named_generic';
	    }

	also
	fun unparse_generic_api_naming (context as (_, source_opt)) buf
            =
	    {   ppsay =  pp::string  buf;
		#
		fun unparse_generic_api_naming'(_, 0)
                        =>
                        ppsay "<rs::NAMED_GENERIC_API>";

		    unparse_generic_api_naming' (rs::NAMED_GENERIC_API { name_symbol=>name, definition=>def }, d)
			=> 
			{   pp::begin_align_box buf;
			    ppsay "funsig ";
                            uj::unparse_symbol buf name;
                            ppsay " =";
			    pp::break buf { spaces=>1, indent_on_wrap=>2 };
                            unparse_generic_api_expression context buf (def, d - 1);
			    pp::end_box buf;
			};

		    unparse_generic_api_naming' (rs::SOURCE_REGION_FOR_NAMED_GENERIC_API (t, r), d)
			=>
			unparse_generic_api_naming context buf (t, d);
                end;
	    
		unparse_generic_api_naming';
	    }

	also
	fun unparse_typevar (context as (_, source_opt)) buf
            =
	    {   ppsay = pp::string buf;
		#
		fun unparse_typevar' (_, 0) => ppsay "<typevar>";
		    unparse_typevar' (rs::TYPEVAR s, d) => (uj::unparse_symbol buf s); 
		    unparse_typevar' (rs::SOURCE_CODE_REGION_FOR_TYPEVAR (t, r), d) => unparse_typevar context buf (t, d);
                end;
	    
		unparse_typevar';
	    }

	also
	fun unparse_type (context as (dictionary, source_opt)) buf
            =			
	    {   ppsay =  pp::string buf;
		#
		fun unparse_type' (_, 0)
                        =>
                        ppsay "<type>";

		    unparse_type' (rs::TYPEVAR_TYPE t, d)
                        =>
                        (unparse_typevar context buf (t, d));

		    unparse_type' (rs::TYPE_TYPE (type, []), d)
			=>
			{   pp::begin_indented_align_box buf (pp::CURSOR_RELATIVE 1);
			    pp_path buf type;
			    pp::end_box buf;
			};

		    unparse_type' (rs::TYPE_TYPE (type, args), d)
			=> 
			{   pp::begin_indented_align_box  buf  (pp::CURSOR_RELATIVE 1);
			    #
			    case type
				#			      
				[type] =>   if (sy::eq (sy::make_type_symbol("->"), type))
						#				         
						case args

						     [dom, ran]
							 =>
							 {   unparse_type' (dom, d - 1);
							     ppsay " ->";
							     pp::break buf { spaces=>1, indent_on_wrap=>2 };
							     unparse_type' (ran, d - 1);
							 };

						     _   =>
							 err::impossible "wrong args for -> type";
						esac;
					    else
						uj::unparse_symbol  buf  type;
						ppsay "(";
						unparse_type_args  (args, d);
						ppsay ")";
					    fi;

				_ => {   pp_path  buf  type;
					 ppsay "(";
                                         unparse_type_args  (args, d);
					 ppsay ")";
				     };
                            esac;

			    pp::end_box buf;
			};

		    unparse_type' (rs::RECORD_TYPE s, d)
			=> 
			{   fun print_one buf (symbol: rs::Symbol, tv: rs::Any_Type)
				= 
				{   uj::unparse_symbol buf symbol;
				    ppsay ": ";
				    unparse_type context buf (tv, d);
				};

			    uj::unparse_closed_sequence
				buf
				{ front      =>  (by pp::string "{ "),
				  separator  =>  (fn buf = { pp::string buf ", ";
								pp::break buf { spaces=>1, indent_on_wrap=>0 };
							      }
						 ),
				  back       =>  (by pp::string "}"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				s;
			};

		    unparse_type' (rs::TUPLE_TYPE t, d)
			=> 
			{   fun print_one _ (tv: rs::Any_Type)
				=
				(unparse_type context buf (tv, d));

			    uj::unparse_closed_sequence 
				buf
				{ front      =>  (by pp::string "("),
				  separator  =>  (fn buf =  {   pp::string buf ", ";				# Was " *"
								   pp::break buf { spaces=>0, indent_on_wrap=>0 };
							       }
						 ),
				  back       =>  (by pp::string ")"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				t;
			};

		    unparse_type' (rs::SOURCE_CODE_REGION_FOR_TYPE (t, r), d)
			=>
			unparse_type context buf (t, d);
                end 

		also
		fun unparse_type_args ([], d)
                        =>
                        ();

		    unparse_type_args ( [type], d)
			=> 
			{   if (strength type <= 1)
			        #  
				pp::begin_indented_wrap_box buf (pp::CURSOR_RELATIVE 1);
				ppsay "("; 
				unparse_type' (type, d); 
				ppsay ")";
				pp::end_box buf;
			    else 
				unparse_type' (type, d);
			    fi;

			    pp::break buf { spaces => 0,  indent_on_wrap => 0 };
			};

		    unparse_type_args (tys, d)
			=>
			uj::unparse_closed_sequence
			    buf 
			    { front      =>  by pp::string "(",
			      separator  =>  fn buf =  { pp::string buf ", ";
							 pp::break buf { spaces=>0, indent_on_wrap=>0 };
						       },
			      back       =>  by pp::string ") ",
			      breakstyle =>  uj::ALIGN, 
			      print_one  =>  fn _ =  fn type =  unparse_type' (type, d)
			    }
			    tys;
                end; 
	    
                unparse_type';
	    };
    };					# package unparse_raw_syntax 
end;					# top-level local 








##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################



## Copyright 2003 by University of Chicago 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
