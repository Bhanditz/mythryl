## unparse-raw-syntax.pkg
## Jing Cao and Lukasz Ziarek 

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

# We refer to a literal dump of the raw syntax tree as "prettyprinting".
# We refer to reconstruction of surface syntax from the raw syntax tree as "unparsing".
# Unparsing is good for end-user diagnostics; prettyprinting is good for compiler debugging.
# This is the implementation of our raw syntax unparser.
# For our raw syntax prettyprinter, see  src/lib/compiler/front/typer/print/prettyprint-raw-syntax.pkg



stipulate
    package err =  error_message;				# error_message			is from   src/lib/compiler/front/basics/errormsg/error-message.pkg
    package fxt =  fixity;					# fixity			is from   src/lib/compiler/front/basics/map/fixity.pkg
    package mtt =  more_type_types;				# more_type_types		is from   src/lib/compiler/front/typer/types/more-type-types.pkg
    package pp  =  standard_prettyprint_mill;			# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package rs  =  raw_syntax;					# raw_syntax			is from   src/lib/compiler/front/parser/raw-syntax/raw-syntax.pkg
    package sci =  sourcecode_info;				# sourcecode_info		is from   src/lib/compiler/front/basics/source/sourcecode-info.pkg
    package sy  =  symbol;					# symbol			is from   src/lib/compiler/front/basics/map/symbol.pkg
    package tc  =  typer_control;				# typer_control			is from   src/lib/compiler/front/typer/basics/typer-control.pkg
    package tpl =  tuples;					# tuples			is from   src/lib/compiler/front/typer-stuff/types/tuples.pkg
    package uj  =  unparse_junk;				# unparse_junk			is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
#   package ut  =  unparse_type;				# unparse_type			is from   src/lib/compiler/front/typer/print/unparse-type.pkg
#   package uv  =  unparse_value;				# unparse_value			is from   src/lib/compiler/front/typer/print/unparse-value.pkg
#   package vac =  variables_and_constructors;			# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
herein


    package   unparse_raw_syntax
    : (weak)  Unparse_Raw_Syntax				# Unparse_Raw_Syntax		is from   src/lib/compiler/front/typer/print/unparse-raw-syntax.api
    {
#	internals =  tc::internals;
internals = log::internals;

	lineprint =  REF FALSE;

	fun by f x y
            =
            f y x;

	null_fix = fxt::INFIX (0, 0);
	inf_fix  = fxt::INFIX (1000000, 100000);

	fun stronger_l (fxt::INFIX(_, m), fxt::INFIX (n, _)) => m >= n;
	    stronger_l _ => FALSE;			#  should not matter 
        end;

	fun stronger_r (fxt::INFIX(_, m), fxt::INFIX (n, _)) => n > m;
 	    stronger_r _ => TRUE; 			#  should not matter 
        end;

	fun prpos (   pp:  pp::Prettyprint_Mill,
		      source:  sci::Sourcecode_Info,
                      charpos: Int
                  )
            =
	    if *lineprint
		  my (file: String, line: Int, pos: Int)
                      =
                      sci::filepos  source  charpos;

		  pp::lit pp (int::to_string line);
		  pp::lit pp ".";
		  pp::lit pp (int::to_string pos);
	    else
                 pp::lit pp (int::to_string charpos);
            fi;


	fun bug msg
            =
            error_message::impossible("unparse_raw_syntax: " + msg);


	arrow_stamp =  mtt::arrow_stamp;


	fun strength  type
            =
	    case type
		#              
		rs::TYPEVAR_TYPE(_) => 1;

		rs::TYPE_TYPE (type, args)
		    => 
		    case type
			#
			[type]
			    =>
			    if (sy::eq (sy::make_type_symbol("->"), type))   0;
			    else                                             2;
			    fi;

			_   => 2;
		    esac;


		rs::RECORD_TYPE _ => 2;

		rs::TUPLE_TYPE _ => 1;

		_ => 2;
            esac;


	fun checkpat (n, NIL)
                =>
                TRUE;

	    checkpat (n, (symbol, _) ! fields)
                =>
                sy::eq (symbol, tpl::number_to_label n)
                and
                checkpat (n+1, fields);
        end;

	fun checkexp (n, NIL)
                =>
                TRUE;

	    checkexp (n, (symbol, expression) ! fields)
		=>
		sy::eq (symbol, tpl::number_to_label n)
                and
		checkexp (n+1, fields);
        end;

	fun is_tuplepat (rs::RECORD_PATTERN { definition => [_], ...                   } ) =>  FALSE;
	    is_tuplepat (rs::RECORD_PATTERN { definition => defs, is_incomplete => FALSE } ) =>  checkpat (1, defs);
	    is_tuplepat _                                                              =>  FALSE;
        end;

	fun is_tupleexp (rs::RECORD_IN_EXPRESSION [_])      =>   FALSE;
	    is_tupleexp (rs::RECORD_IN_EXPRESSION fields)   =>   checkexp (1, fields);
	    is_tupleexp (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _))       =>   is_tupleexp a;
	    is_tupleexp _ => FALSE;
        end;

	fun get_fix (dictionary, symbol)
            =
	    find_in_symbolmapstack::find_fixity_by_symbol  (
                dictionary,
                sy::make_fixity_symbol (sy::name symbol)
            );



	fun strip_source_code_region_data (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (a, _))
		=>
		strip_source_code_region_data a;

	    strip_source_code_region_data x
		=>
		x;
        end;



	fun trim     [x] =>  [];
	    trim (a ! b) =>  a ! trim b;
	    trim      [] =>  [];
        end;


	fun pp_path  pp  symbols
            =
	    {   fun print_one pp symbol
                    =
                    uj::unparse_symbol  pp  symbol;
	    
                uj::unparse_sequence
                    pp
		    { separator  =>  (fn pp =  (pp::lit pp "::")),	# Was "."
		      print_one,
		      breakstyle =>  uj::ALIGN
                    }
		    symbols;
	    };

	fun unparse_pattern (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =   pp::lit  pp;
		#
		pp_symbol_list =   pp_path  pp;

		fun unparse_pattern' (rs::WILDCARD_PATTERN,                 _)   =>   ppsay "_";
		    unparse_pattern' (rs::VARIABLE_IN_PATTERN         p,    d)   =>   pp_symbol_list (p);
		    unparse_pattern' (rs::INT_CONSTANT_IN_PATTERN     i,    _)   =>   ppsay (multiword_int::to_string i);
		    unparse_pattern' (rs::UNT_CONSTANT_IN_PATTERN     w,    _)   =>   ppsay (multiword_int::to_string w);
		    unparse_pattern' (rs::STRING_CONSTANT_IN_PATTERN  s,    _)   =>   uj::unparse_mlstring  pp s;
		    unparse_pattern' (rs::CHAR_CONSTANT_IN_PATTERN    s,    _)   =>   uj::unparse_mlstring' pp s;

		    unparse_pattern' (rs::AS_PATTERN { variable_pattern, expression_pattern }, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_pattern'(variable_pattern, d); ppsay " as "; unparse_pattern'(expression_pattern, d - 1);
			    pp::shut_box pp;
			};

		    unparse_pattern' (rs::RECORD_PATTERN { definition => [],   is_incomplete }, _)
			=>
			if   is_incomplete      ppsay "{ ... }";
			else                    ppsay "()";
                        fi;

		    unparse_pattern' (r as rs::RECORD_PATTERN { definition, is_incomplete }, d)
			=>
			if (is_tuplepat r)
			    #
			    uj::unparse_closed_sequence
				pp
				{ front        =>  (by pp::lit "("),
				  separator    =>  (fn pp
						       =
						       {   pp::lit pp ", ";
							   pp::break pp { spaces=>0, indent_on_wrap=>0 };
						       }
						   ),
				  back       =>  (by pp::lit ")"),
				  print_one  =>  (fn _ = fn (symbol, pattern) = unparse_pattern' (pattern, d - 1)),
				  breakstyle =>  uj::ALIGN
				}
				definition;
			else
			    uj::unparse_closed_sequence
				pp
				{ front     => (by pp::lit "{ "),
				  separator => (fn pp =  { pp::lit pp ", ";
							       pp::break pp { spaces=>0, indent_on_wrap=>0 };
							     }
					       ),
				  back      => (fn pp =  if is_incomplete  pp::lit pp ", ... }";
							     else pp::lit pp "}";
							     fi
					       ),
				  print_one => (fn pp =  fn (symbol, pattern) =  {   uj::unparse_symbol pp symbol;
											 pp::lit pp " => ";
											 unparse_pattern' (pattern, d - 1);
										     }
					      ),
				  breakstyle => uj::ALIGN
				}
				definition;
			fi;

		    unparse_pattern' (rs::LIST_PATTERN NIL, d) =>   ppsay "[]";

		    unparse_pattern' (rs::LIST_PATTERN l, d)
			=>	
			{   fun print_one _ pattern
                                =
                                unparse_pattern' (pattern, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "["),
				  separator  =>  (fn pp = { pp::lit pp ", ";
								pp::break pp { spaces=>0, indent_on_wrap=>0 };
							      }
						 ),
				  back       =>  (by pp::lit "]"),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				l;
			};

		    unparse_pattern' (rs::TUPLE_PATTERN t, d)
			=> 
			{   fun print_one _ pattern
                                =
                                unparse_pattern'(pattern, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "("),
				  separator  =>  (fn pp = {   pp::lit pp ", ";
								  pp::break pp { spaces=>0, indent_on_wrap=>0 };
							      }
						 ),
				  back       =>  (by pp::lit ")"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				t;
			};

		    unparse_pattern' (rs::PRE_FIXITY_PATTERN fap, d)
			=>
			{   fun print_one _ { item, fixity, source_code_region }
                                =
                                unparse_pattern'(item, d - 1);		

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				fap;
			}; 

		    unparse_pattern' (rs::APPLY_PATTERN { constructor, argument }, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_pattern' (constructor, d);
			    ppsay " as ";
			    unparse_pattern'(argument, d);
			    pp::shut_box pp;
			};

		    unparse_pattern' (rs::TYPE_CONSTRAINT_PATTERN { pattern, type_constraint }, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    unparse_pattern' (pattern, d - 1);
			    ppsay " :";
			    pp::break pp { spaces => 1,   indent_on_wrap => 2 };
			    unparse_type context pp (type_constraint, d);
			    pp::shut_box pp;
			};

		    unparse_pattern' (rs::VECTOR_PATTERN NIL, d)
			=>
			ppsay "#[]";

		    unparse_pattern' (rs::VECTOR_PATTERN v, d)
			=> 
			{   fun print_one _ pattern
                                =
                                unparse_pattern'(pattern, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front       =>  (by pp::lit "#["),
				  separator   =>  (fn pp = {    pp::lit pp ", ";
								    pp::break pp { spaces=>1, indent_on_wrap=>0 };
							       }
						  ),
				  back       =>  (by pp::lit "]"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				v;
			 };

		    unparse_pattern' (rs::SOURCE_CODE_REGION_FOR_PATTERN (pattern, (s, e)), d)
			=> 
			case source_opt
                            #
			    THE source
				=>
				if *internals
				     ppsay "<rs::SOURCE_CODE_REGION_FOR_PATTERN(";
				     prpos (pp, source, s); ppsay ", ";
				     prpos (pp, source, e); ppsay "): ";
				     unparse_pattern'(pattern, d); ppsay ">";
				else
				     unparse_pattern'(pattern, d);
				fi;

			     NULL => unparse_pattern'(pattern, d);
			 esac;

		    unparse_pattern' (rs::OR_PATTERN orpat, d)
			=>
			{   fun print_one _ pattern
                                =
                                unparse_pattern' (pattern, d - 1);		

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "("),
				  separator  =>  (fn pp = { pp::break pp { spaces=>1, indent_on_wrap=>0 }; pp::lit pp "| ";}),
				  back       =>  (by pp::lit ")"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				};
			}
                        orpat;
                end;

	    
                unparse_pattern';
	    }


	also
	fun unparse_expression (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		fun lparen () = ppsay "("; 
		fun rparen () = ppsay ")";

		fun lpcond atom = if atom  ppsay "("; fi;      
		fun rpcond atom = if atom  ppsay ")"; fi;

		pp_symbol_list = pp_path pp;

		fun unparse_expression' (_, _, 0) => ppsay "<expression>";
		    unparse_expression' (rs::VARIABLE_IN_EXPRESSION p, _, _) => pp_symbol_list  p;
		    unparse_expression' (rs::IMPLICIT_THUNK_PARAMETER p, _, _) => { ppsay "#"; pp_symbol_list  p; };
		    unparse_expression' (rs::FN_EXPRESSION NIL, _, d) => ppsay "<function>";
		    unparse_expression' (rs::FN_EXPRESSION rules, _, d)
			=>	
			{   fun print_one _ pattern
                                =
                                unparse_rule context pp (pattern, d - 1);

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp = { pp::lit pp "|";pp::break pp { spaces=>0, indent_on_wrap=>0 }; }),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				rules;
			};

		    unparse_expression' (rs::PRE_FIXITY_EXPRESSION fap, _, d)
			=> 
			{   fun print_one _ { item, fixity, source_code_region }
                                =
                                unparse_expression'(item, TRUE, d);		

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				fap;
			}; 

		    unparse_expression' (e as rs::APPLY_EXPRESSION _, atom, d)
			=>
			{   lpcond atom;
			    unparse_app_expression (e, null_fix, null_fix, d);
			    rpcond atom;
			};

		    unparse_expression' (rs::OBJECT_FIELD_EXPRESSION { object, field }, _, d)
			=>
			{   unparse_expression' (object, TRUE, d - 1);
			    ppsay "->";
                            uj::unparse_symbol pp field;
			};

		    unparse_expression' (rs::CASE_EXPRESSION { expression, rules }, _, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "case (";		# Was "(case ";
                            unparse_expression'(expression, TRUE, d - 1);
                            pp::newline pp;		# Was uj::newline_indent pp 2;
			    uj::ppvlist pp (
                                ") ",
                                ";",		# Was "   | ",
                                (fn pp =  fn r =  unparse_rule context pp (r, d - 1)),
                                trim rules
                            );
			    ppsay "esac;";	# Was rparen();
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::LET_EXPRESSION { declaration, expression }, _, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "stipulate ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_declaration context pp (declaration, d - 1); 
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    ppsay "herein ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression'(expression, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    ppsay "end";
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::SEQUENCE_EXPRESSION exps, _, d)
			=>
			uj::unparse_closed_sequence
			    pp
			    { front      =>  (by pp::lit "("),
			      separator  =>  (fn pp = { pp::lit pp ";";
							    pp::break pp { spaces=>1, indent_on_wrap=>0 };
							  }
					     ),
			      back       =>  (by pp::lit ")"),
			      print_one  =>  (fn _ = fn expression = unparse_expression'(expression, FALSE, d - 1)),
			      breakstyle =>  uj::ALIGN
			    }
			    exps;

		    unparse_expression' (   rs::INT_CONSTANT_IN_EXPRESSION   i, _, _)   =>   ppsay (multiword_int::to_string i);
		    unparse_expression' (   rs::UNT_CONSTANT_IN_EXPRESSION   w, _, _)   =>   ppsay (multiword_int::to_string w);
		    unparse_expression' ( rs::FLOAT_CONSTANT_IN_EXPRESSION   r, _, _)   =>   ppsay r;
		    unparse_expression' (rs::STRING_CONSTANT_IN_EXPRESSION   s, _, _)   =>   uj::unparse_mlstring  pp s;
		    unparse_expression' (  rs::CHAR_CONSTANT_IN_EXPRESSION   s, _, _)   =>   uj::unparse_mlstring' pp s;

		    unparse_expression'(r as rs::RECORD_IN_EXPRESSION fields, _, d)
			=>
			if (is_tupleexp r)
			    #			    
			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "("),
				  back       =>  (by pp::lit ")"),
				  print_one  =>  (fn _ = fn (_, expression) = unparse_expression'(expression, FALSE, d - 1)),
				  breakstyle =>  uj::ALIGN,
				  separator  =>  (fn pp =  { pp::lit pp ", ";
								 pp::break pp { spaces=>0, indent_on_wrap=>0 };
							       }
						 )
				}
				fields;
			else
			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "{ "),
				  separator  =>  (fn pp = { pp::lit pp ", ";
								pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}
						 ),
				  back       =>  (by pp::lit "}"),
				  print_one  =>  (fn pp = fn (name, expression)
							=
							{   uj::unparse_symbol pp name;
							    ppsay " => ";
							    unparse_expression'(expression, FALSE, d);
							}
					         ),
				  breakstyle =>  uj::ALIGN
				}
				fields;
                        fi;

		    unparse_expression' (rs::LIST_EXPRESSION p, _, d)
			=> 
			uj::unparse_closed_sequence
			    pp
			    { front      =>  (by pp::lit "["),
			      separator  =>  (fn pp = { pp::lit pp ", ";
							    pp::break pp { spaces=>0, indent_on_wrap=>0 } ;}
					     ),
			      back       =>  (by pp::lit "]"),
			      print_one  =>  (fn pp =
						fn expression =
							    (unparse_expression'(expression, FALSE, d - 1))
					       ),
			      breakstyle =>  uj::ALIGN
			    }
			    p;

		    unparse_expression' (rs::TUPLE_EXPRESSION p, _, d)
			=>
			uj::unparse_closed_sequence
			    pp
			    {   front      =>  (by pp::lit "("),
				separator  =>  (fn pp = { pp::lit pp ", ";
							      pp::break pp { spaces=>0, indent_on_wrap=>0 };
							    }
					       ),
				back       =>  (by pp::lit ")"),
				print_one  =>  (fn pp =
						  fn expression =  (unparse_expression'(expression, FALSE, d - 1))
						 ),
				breakstyle => uj::ALIGN
			    }
			    p;

		    unparse_expression' (rs::RECORD_SELECTOR_EXPRESSION name, atom, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
#			    lpcond atom;			# Seems like pure clutter so commented out  2009-08-06 CrT
			    ppsay ".";			# Was "#"
                            uj::unparse_symbol pp name;
#			    rpcond atom;
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::TYPE_CONSTRAINT_EXPRESSION { expression, constraint }, atom, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    lpcond atom;
			    unparse_expression'(expression, FALSE, d); ppsay ":";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    unparse_type context pp (constraint, d);
			    rpcond atom;
			    pp::shut_box pp;
			};

		    unparse_expression'(rs::EXCEPT_EXPRESSION { expression, rules }, atom, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    unparse_expression'(expression, atom, d - 1);
                            pp::newline pp;
                            ppsay "except ";
			    uj::newline_indent pp 2;
			    uj::ppvlist pp (
                                "  ",
                                "; ",			# Was "| ",
				(fn pp =  fn r =  unparse_rule context pp (r, d - 1)),
                                rules
                            );
			    rpcond atom;
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::RAISE_EXPRESSION expression, atom, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    ppsay "raise exception ";
                            unparse_expression'(expression, TRUE, d - 1);
			    rpcond atom;
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::IF_EXPRESSION { test_case, then_case, else_case }, atom, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    ppsay "if ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression' (test_case, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "then ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression' (then_case, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "else ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression' (else_case, FALSE, d - 1);
			    pp::shut_box pp;
			    rpcond atom;
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::AND_EXPRESSION (e1, e2), atom, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression' (e1, TRUE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "and ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression' (e2, TRUE, d - 1);
			    pp::shut_box pp;
			    rpcond atom;
			    pp::shut_box pp;
			 };

		    unparse_expression' (rs::OR_EXPRESSION (e1, e2), atom, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    lpcond atom;
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression' (e1, TRUE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "or ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression' (e2, TRUE, d - 1);
			    pp::shut_box pp;
			    rpcond atom;
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::WHILE_EXPRESSION { test, expression }, atom, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "while ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_expression'(test, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::break pp { spaces=>1, indent_on_wrap=> 0 };
			    ppsay "do ";
			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			     unparse_expression'(expression, FALSE, d - 1);
			    pp::shut_box pp;
			    pp::shut_box pp;
			};

		    unparse_expression' (rs::VECTOR_IN_EXPRESSION NIL, _, d) => ppsay "#[]";

		    unparse_expression' (rs::VECTOR_IN_EXPRESSION exps, _, d)
			=>
			{   fun print_one _ expression
				=
				unparse_expression'(expression, FALSE, d - 1);

			    uj::unparse_closed_sequence
				pp
				{ front     =>  (by pp::lit "#["),
				  separator =>  (fn pp = { pp::lit pp ", ";
							       pp::break pp { spaces=>1, indent_on_wrap=>0 };
							     }
						),
				  back  => (by pp::lit "]"),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				exps;
			};

		    unparse_expression' (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), atom, d)
			=>
			case source_opt
			    #
			    THE source
				=>
				if *internals
				     ppsay "<MARK(";
				     prpos (pp, source, s); ppsay ", ";
				     prpos (pp, source, e); ppsay "): ";
				     unparse_expression'(expression, FALSE, d); ppsay ">";
				else
				     unparse_expression'(expression, atom, d);
				fi;

			    NULL => unparse_expression'(expression, atom, d);
			esac;
		end 

		also
		fun unparse_app_expression (_, _, _, 0)
			=>
			pp::lit pp "<expression>";

		    unparse_app_expression arg
			=>
			{   ppsay = pp::lit pp;

			    fun fixitypp (name, operand, left_fix, right_fix, d)
				=
				{   dname = symbol_path::to_string (symbol_path::SYMBOL_PATH name);
				    #
				    this_fix = case name
						       [id] => get_fix (dictionary, id);
						      _ => fxt::NONFIX;
					       esac;

				    fun pr_non expression
					=
					{   pp::open_box (pp, pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
					    ppsay dname; pp::break pp { spaces=>1, indent_on_wrap=>0 };
					    unparse_expression'(expression, TRUE, d - 1);
					    pp::shut_box pp;
					};

				    case this_fix
					#
					fxt::INFIX _
					    =>
					    case (strip_source_code_region_data operand)
						#
						rs::RECORD_IN_EXPRESSION [(_, pl), (_, pr)]
						    =>
						    {   atom =  stronger_l (left_fix, this_fix)
                                                             or stronger_r (this_fix, right_fix);

							my (left, right)
							    =
							    if atom     (null_fix, null_fix);
							    else	(left_fix, right_fix);
							    fi;

							{   pp::open_box (pp, pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
							    lpcond atom;
							    unparse_app_expression (pl, left, this_fix, d - 1);
							    pp::break pp { spaces=>1, indent_on_wrap=>0 };
							    ppsay dname;
							    pp::break pp { spaces=>1, indent_on_wrap=>0 };
							    unparse_app_expression (pr, this_fix, right, d - 1);
							    rpcond atom;
							    pp::shut_box pp;
							};
						    };

						e' => pr_non e';
					    esac;


					fxt::NONFIX => pr_non operand;
				    esac;
				};

			    fun apply_print (_, _, _, 0)
				    =>
				    ppsay "#";

				apply_print (rs::APPLY_EXPRESSION { function=>operator, argument=>operand }, l, r, d)
				    =>
				    case (strip_source_code_region_data operator)
					#
					rs::VARIABLE_IN_EXPRESSION v
					    =>
					    { path = v;

						fixitypp (path, operand, l, r, d);
					    };

					operator
					    =>
					    {   pp::open_box (pp, pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
						unparse_expression'(operator, TRUE, d - 1); pp::break pp { spaces=>1, indent_on_wrap=>2 };
						unparse_expression'(operand,  TRUE, d - 1);
						pp::shut_box pp;
					    };
				     esac;


				apply_print (rs::SOURCE_CODE_REGION_FOR_EXPRESSION (expression, (s, e)), l, r, d)
				    =>
				    case source_opt
					#
					THE source
					    =>
					    if *internals
						 ppsay "<MARK(";
						 prpos (pp, source, s); ppsay ", ";
						 prpos (pp, source, e); ppsay "): ";
						 unparse_expression'(expression, FALSE, d);
						 ppsay ">";
					    else
						 apply_print (expression, l, r, d);
					    fi;

					NULL => apply_print (expression, l, r, d);
				    esac;

				apply_print (e, _, _, d)
				    =>
				    unparse_expression'(e, TRUE, d);
			    end;

			    apply_print arg;
			};
                end;
	    
                fn (expression, depth)
                    =
                    unparse_expression' (expression, FALSE, depth);
	    }

	also
	fun unparse_rule (context as (dictionary, source_opt)) pp (rs::CASE_RULE { pattern, expression }, d)
            =
	    if (d>0) 
		#                
		pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
		unparse_pattern context pp (pattern, d - 1);
		pp::lit pp " =>";
		pp::break pp { spaces=>1, indent_on_wrap=>2 };
		unparse_expression context pp (expression, d - 1);
		pp::shut_box pp;
	    else
                pp::lit pp "<rule>";
            fi

	also
	fun unparse_package_cast (context as (_, source_opt)) pp package_cast d
            =
	    {   ppsay = pp::lit pp;
		#
		case package_cast
		    #
		    rs::NO_PACKAGE_CAST
			=>
			();

		    rs::WEAK_PACKAGE_CAST api_expression
			=> 
			{   ppsay " : (weak)";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    unparse_api_expression context pp (api_expression, d - 1);
			};

		    rs::PARTIAL_PACKAGE_CAST api_expression
			=> 
			{   ppsay " : (partial)";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    unparse_api_expression context pp (api_expression, d - 1);
			};

		    rs::STRONG_PACKAGE_CAST api_expression
			=> 
			{   ppsay " : ";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    unparse_api_expression context pp (api_expression, d - 1);
			};
		esac;
	    }

	also
	fun unparse_package_expression (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		pp_symbol_list = pp_path pp;

		fun unparse_package_expression'(_, 0)
			=>
			ppsay "<package_expression>";

		    unparse_package_expression' (rs::PACKAGE_BY_NAME p, d)
			=>
			pp_symbol_list (p);

		    unparse_package_expression' (rs::PACKAGE_DEFINITION (rs::SEQUENTIAL_DECLARATIONS NIL), d)
			=>
			{   ppsay "package {";
			    pp::nonbreakable_spaces pp 1;
			    ppsay "};";
			};

		    unparse_package_expression' (rs::PACKAGE_DEFINITION de, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::vertical,	100 );
			    ppsay "package {";
			    uj::newline_indent pp 2;
			    unparse_declaration context pp (de, d - 1);
			    ppsay "};";
			    pp::shut_box pp;
			};

		    unparse_package_expression' (rs::PACKAGE_CAST (stre, constraint), d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    unparse_package_expression' (stre, d - 1);

			    unparse_package_cast context pp constraint d;

			    pp::shut_box  pp;
			};

		    unparse_package_expression' (rs::CALL_OF_GENERIC (path, str_list), d)
			=> 
			{   fun print_one pp (strl, bool)
				=
				{   ppsay "("; unparse_package_expression context pp (strl, d); ppsay ")";};

			    pp_symbol_list (path);

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				str_list;
			  };	

		    unparse_package_expression' (rs::INTERNAL_CALL_OF_GENERIC (path, str_list), d)
			=> 
			{   fun print_one pp (strl, bool)
				=
				{   ppsay "(";
				    unparse_package_expression context pp (strl, d);
				    ppsay ")";
				};

			    pp_symbol_list (path);

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				str_list;
			};	

		    unparse_package_expression' (rs::LET_IN_PACKAGE (declaration, body), d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "stipulate ";
			    unparse_declaration context pp (declaration, d - 1); 
			    pp::newline pp;
			    ppsay " herein ";
			    unparse_package_expression'(body, d - 1);
			    pp::newline pp;
			    ppsay "end";
			    pp::shut_box pp;
			};

		    unparse_package_expression' (rs::SOURCE_CODE_REGION_FOR_PACKAGE (body, (s, e)), d)
			=>
			unparse_package_expression' (body, d);
		end;

#		      (case source_opt
#			of THE source =>
#			   (ppsay "rs::SOURCE_CODE_REGION_FOR_PACKAGE(";
#			      prettyprintPackageexpression'(body, d); ppsay ", ";
#			      prpos (pp, source, s); ppsay ", ";
#			      prpos (pp, source, e); ppsay ")")
#			 | NULL => prettyprintPackageexpression'(body, d))

	    
		unparse_package_expression';
	    }

	also
	fun unparse_generic_expression (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		pp_symbol_list = pp_path pp;

		fun unparse_generic_expression'(_, 0)
                        =>
                        ppsay "<generic_expression>";

		    unparse_generic_expression' (rs::GENERIC_BY_NAME (p, _), d)
                        =>
                        pp_symbol_list (p);

		    unparse_generic_expression' (rs::LET_IN_GENERIC (declaration, body), d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "stipulate ";
                            unparse_declaration context pp (declaration, d - 1); 
			    pp::newline pp;
			    ppsay " herein ";
                            unparse_generic_expression'(body, d - 1);
                            pp::newline pp;
			    ppsay "end";
			    pp::shut_box pp;
			};

		    unparse_generic_expression' (rs::CONSTRAINED_CALL_OF_GENERIC (path, sblist, fsigconst), d)
			=>
			{   fun print_one pp (package_expression, _)
				=
				{   ppsay "(";
				    unparse_package_expression context pp (package_expression, d);
				    ppsay ")";
				};

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    pp_symbol_list path;

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sblist;

			    pp::shut_box pp;
			};

		    unparse_generic_expression' (rs::SOURCE_CODE_REGION_FOR_GENERIC (body, (s, e)), d)
			=>
			unparse_generic_expression' (body, d);


		    unparse_generic_expression' (rs::GENERIC_DEFINITION _, d)
			=>
			error_message::impossible "prettyprintGenericexpression: GENERIC_DEFINITION";
                end;
	    
		unparse_generic_expression';
	    }

	also
	fun unparse_where_spec (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;

		fun unparse_where_spec'(_, 0)
                        =>
                        ppsay "<WhereSpec>";


		    unparse_where_spec' (rs::WHERE_TYPE([],[], type), d)
                        =>
                        unparse_type context pp (type, d);


		    unparse_where_spec' (rs::WHERE_TYPE (slist, tvlist, type), d)
			=> 
			{   fun print_one _ symbol
				=
				uj::unparse_symbol pp symbol;

			    fun print_one' _ tyv
				=
				unparse_typevar context pp (tyv, d);

			    ppsay "typeX ";

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one  =>  print_one',
				  breakstyle =>  uj::ALIGN
				}
				tvlist;

			    pp::break pp { spaces=>1, indent_on_wrap=>0 };

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist;	 

			    ppsay" =";
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    unparse_type context pp (type, d);
			};

		    unparse_where_spec' (rs::WHERE_PACKAGE (slist, slist'), d)
			=>
			{   fun print_one _ symbol
				=
				uj::unparse_symbol pp symbol;

			    ppsay "packageZ ";

			    uj::unparse_sequence
				pp
				{ separator =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist;

			    pp::break pp { spaces=>1, indent_on_wrap=>0 };

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				slist';
			};
                end;
	    
		unparse_where_spec';
	    }

	also
	fun unparse_api_expression (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		fun unparse_api_expression'(_, 0)
                        =>
                        ppsay "<api_expression>";

		    unparse_api_expression'(rs::API_BY_NAME s, d)
                        =>
                        uj::unparse_symbol  pp  s;

		    unparse_api_expression'(rs::API_WITH_WHERE_SPECS (an_api, wherel), d)
			=>
			{   unparse_api_expression' (an_api, d);
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };

			    case an_api
				#			      
				rs::API_BY_NAME s
				    =>
				    uj::ppvlist pp (
					"where ",
					"also ",
					(fn pp =  fn r =  unparse_where_spec context pp (r, d - 1)),
					wherel
				    );

				rs::SOURCE_CODE_REGION_FOR_API (rs::API_BY_NAME s, r)
				    =>
				    uj::ppvlist pp (
					"where ",
					"also ",
					(fn pp =  fn r =  unparse_where_spec context pp (r, d - 1)),
					wherel
				    );

				_ =>
				    {   pp::newline pp;
					#
					uj::ppvlist pp (
					    "where ",
					    "also ",
					    (fn pp =  fn r =  unparse_where_spec context pp (r, d - 1)),
					    wherel
					);
				    };
			    esac;
			};

		    unparse_api_expression' (rs::API_DEFINITION [], d)
			=>	
			{   ppsay "api {";
			    pp::nonbreakable_spaces pp 1;
			    ppsay "};";
			};

		    unparse_api_expression' (rs::API_DEFINITION specl, d)
			=> 
			{   fun print_one pp speci
                                =
                                unparse_specification context pp (speci, d);

			    pp::newline pp; # XXX BUGGO TEST ONLY
			    ppsay "api {";

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::vertical,	100 );
			    pp::newline pp;
#			    uj::newline_indent pp 4;

			    uj::unparse_sequence
				pp
				{ separator   =>  (fn pp =  pp::newline pp),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				specl;

			    pp::shut_box pp;

			    pp::newline pp;
			    ppsay "};";
			};

		    unparse_api_expression' (rs::SOURCE_CODE_REGION_FOR_API (m, r), d)
                        =>
                        unparse_api_expression context pp (m, d);
                end;
	    
		unparse_api_expression';
	    }

	also
	fun unparse_generic_api_expression (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =  pp::lit pp;
		#
		fun unparse_generic_api_expression'(_, 0)
                        =>
                        ppsay "<generic_api_expression>";

		    unparse_generic_api_expression' (rs::GENERIC_API_BY_NAME s, d)
                        =>
                        uj::unparse_symbol pp s;

		    unparse_generic_api_expression' (rs::GENERIC_API_DEFINITION { parameter, result }, d)
			=>
			{   fun print_one pp (THE symbol, api_expression)
				    =>
				    {   ppsay "(";
                                        uj::unparse_symbol pp symbol;
                                        ppsay ":";
					unparse_api_expression context pp (api_expression, d);
					ppsay ")";
				    };

			        print_one pp (NULL, api_expression)
				    =>
				    {   ppsay "("; unparse_api_expression context pp (api_expression, d);
                                        ppsay ")";
                                    };
                            end;

			    uj::unparse_sequence
				pp
				{ separator  => (fn pp =  (pp::newline pp)),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				parameter;

			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    ppsay "=> ";
			    unparse_api_expression context pp (result, d);
			};

		    unparse_generic_api_expression' (rs::SOURCE_CODE_REGION_FOR_GENERIC_API (m, r), d)
			=>
			unparse_generic_api_expression context pp (m, d);
                end;
	    
		unparse_generic_api_expression';
	    }

	also
	fun unparse_specification (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun pp_tyvar_list ([], d)
                        =>
                        ();

		    pp_tyvar_list ( [typevar], d)
			=> 
			{   unparse_typevar context pp (typevar, d);
			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			};

		    pp_tyvar_list (tyvar_list, d)
			=> 
			{   fun print_one _ typevar
				=
				(unparse_typevar context pp (typevar, d));

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (fn pp = pp::lit pp "("),
				  separator  =>  { pp::lit pp ", "; fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); },
				  back       =>  { pp::lit pp ")" ; fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } ); },
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				tyvar_list;
			};
                end;

		fun unparse_specification'(_, 0)
                        =>
                        ppsay "<Specification>";

		    unparse_specification' (rs::PACKAGES_IN_API sspo_list, d)
			=>
			{   fun print_one _ (symbol, api_expression, path)
				=
				case path
				    #
				    THE p => {   uj::unparse_symbol pp symbol;
						 ppsay " = ";
						 unparse_api_expression context pp (api_expression, d);
						 pp::break pp { spaces=>1, indent_on_wrap=>0 };
						 pp_path pp p;
					     };

				    NULL  => {   uj::unparse_symbol pp symbol;
						 ppsay " = ";
						 unparse_api_expression context pp (api_expression, d);
					     };
				esac;

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "packageY "),
				  separator  =>  (fn pp
						     =
						     {   pp::lit pp ", ";
							 pp::break pp { spaces=>1, indent_on_wrap=>0 };
						     }
						 ),
				  back       =>  (by pp::lit ""),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sspo_list;
			}; 

		    unparse_specification' (rs::TYPES_IN_API (stto_list, bool), d)
			=> 
			{   fun print_one _ (symbol, tyvar_list, tyo)
				=
				case tyo
				    #
				    THE type
					=>
					{   uj::unparse_symbol pp symbol;
					    ppsay "(";
					    pp_tyvar_list (tyvar_list, d);
					    ppsay ") =  ";
					    unparse_type context pp (type, d);
					};

				    NULL
					=>
					{   uj::unparse_symbol pp symbol;
					    ppsay "(";
					    pp_tyvar_list (tyvar_list, d);
					    ppsay ")";
					};
				esac;


			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit ""),			# Was "type "
				  separator  =>  fn pp =  {   pp::lit pp "|";
							     pp::newline pp;
							 },
				  back       =>  (by pp::lit ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				stto_list;
			}; 

		    unparse_specification' (rs::GENERICS_IN_API sf_list, d)
			=>
			{   fun pr pp (symbol, generic_api_expression)
				=
				{   uj::unparse_symbol pp symbol;
                                    ppsay " : ";
				    unparse_generic_api_expression context pp (generic_api_expression, d - 1);
				}; 

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("generic package ", "also ", pr, sf_list);
			    pp::shut_box pp;
			}; 

		    unparse_specification' (rs::VALUES_IN_API st_list, d)
			=> 
			{   fun pr pp (symbol, type)
				= 
				{   uj::unparse_symbol pp symbol;
                                    ppsay ":   ";
                                    unparse_type context pp (type, d);
                                }; 

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp (
                                "",		# Was "my ",
                                "also ",
                                pr, 
                                st_list
                            );
                            ppsay "; ";
			    pp::shut_box pp;
			}; 

		    unparse_specification' (rs::VALCONS_IN_API { sumtypes, with_types => [] }, d)
			=> 
			{   fun pr pp (dbing)
				=
				(unparse_sumtype context pp (dbing, d));

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("", "also ", pr, sumtypes);
			    pp::shut_box pp;
			}; 

		    unparse_specification' (rs::VALCONS_IN_API { sumtypes, with_types }, d)
			=> 
			{   fun prd pp (dbing) = (unparse_sumtype context pp (dbing, d));
			    fun prw pp (tbing) = (unparse_named_type context pp (tbing, d));

			    {   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				uj::ppvlist pp ("", "also ", prd, sumtypes);
				pp::newline pp;
				uj::ppvlist pp ("", "also ", prw, with_types);
				pp::shut_box pp;
			    };
			};

		    unparse_specification' (rs::EXCEPTIONS_IN_API sto_list, d)
			=> 
			{   fun pr pp (symbol, tyo)
				=
				case tyo
				    #
				    THE type => {   uj::unparse_symbol pp symbol;
						    ppsay " : ";
						    unparse_type context pp (type, d);
						};

				    NULL => uj::unparse_symbol pp symbol;
				esac;

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("exception ", "also ", pr, sto_list);
			    pp::shut_box pp;
			}; 

		    unparse_specification' (rs::PACKAGE_SHARING_IN_API paths, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("sharing ", " = ", pp_path, paths);
			    pp::shut_box pp;
			};

		    unparse_specification' (rs::TYPE_SHARING_IN_API paths, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("sharing ", " = ", pp_path, paths);
			    pp::shut_box pp;
			};

		    unparse_specification' (rs::IMPORT_IN_API api_expression, d)
			=>
			unparse_api_expression context pp (api_expression, d);

		    unparse_specification' (rs::SOURCE_CODE_REGION_FOR_API_ELEMENT (m, r), d)
			=>
			unparse_specification context pp (m, d);
                end;
	    
		unparse_specification';
	    }

	also
	fun unparse_declaration (context as (dictionary, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		pp_symbol_list =  pp_path  pp;

		fun unparse_declaration' (_, 0)
                        =>
                        ppsay "<declaration>";

		    unparse_declaration' (rs::VALUE_DECLARATIONS (vbs, typevars), d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp (
                                "my ",
                                "also ",
                                (fn pp =  fn named_value =  unparse_named_value context pp (named_value, d - 1)),
                                vbs
                            );
			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::FIELD_DECLARATIONS (fields, typevars), d)
			=>
			# 2009-02-23 CrT: A quick first-cut solution, duplicated from VALUE_DECLARATIONS: case:
			#
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp (
                                "field ",
                                "also ",
                                (fn pp =  fn named_value =  unparse_named_field context pp (named_value, d - 1)),
                                fields
                            );
			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::RECURSIVE_VALUE_DECLARATIONS (rvbs, typevars), d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist
				pp
				(   "my rec ",
				    "also ",
				    (  fn pp =
				       fn named_recursive_values =
				       unparse_named_recursive_values
					   context
					   pp
					   (named_recursive_values, d - 1)
				    ),
				    rvbs
				);

			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::FUNCTION_DECLARATIONS (fbs, typevars), d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist'
				pp
				(   "fun ",
				    "also ",
				    (   fn pp =
					fn str =
					fn fb =
					unparse_named_sml_function
					    context
					    pp
					    str
					    (fb, d - 1)
				    ),
				    fbs
				);

			    pp::shut_box  pp;
			};

		    unparse_declaration' (rs::NADA_FUNCTION_DECLARATIONS (fbs, typevars), d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist'
				pp
				(   "fun ",
				    "also ",
				    (   fn pp =
					fn str =
					fn fb =
					unparse_named_lib7function
					    context
					    pp
					    str
					    (fb, d - 1)
				    ),
				    fbs
				);

			    pp::shut_box  pp;
			};

		    unparse_declaration' (rs::TYPE_DECLARATIONS types, d)
			=>
			{   fun print_one  pp  type
				=
				(unparse_named_type context pp (type, d));

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit ""),	# Was "type "
				  separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  back       =>  (by pp::lit ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				types;
			}; 	

		    unparse_declaration' (rs::SUMTYPE_DECLARATIONS { sumtypes, with_types => [] }, d)
			=> 
			{   fun print_one _ (dbing)
				=
				(unparse_sumtype context pp (dbing, d));


			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit ""),
				  separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				  back       =>  (by pp::lit ";"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sumtypes;
			};             

		    unparse_declaration' (rs::SUMTYPE_DECLARATIONS { sumtypes, with_types }, d)
			=> 
			{   fun prd pp dbing = (unparse_sumtype context pp (dbing, d));
			    fun prw pp tbing = (unparse_named_type context pp (tbing, d));

			    {   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				#
				uj::unparse_closed_sequence
				    pp
				    { front      =>  (by pp::lit ""),
				      separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				      back       =>  (by pp::lit ";"),
				      print_one  =>  prd,
				      breakstyle =>  uj::ALIGN
				    }
				    sumtypes;

				pp::newline pp;

				uj::unparse_closed_sequence
				    pp
				    { front      =>  (by pp::lit "withtype "),
				      separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				      back       =>  (by pp::lit ""),
				      print_one  =>  prw,
				      breakstyle =>  uj::ALIGN
				    }
				    with_types;

				pp::shut_box pp;
			    };
			};

		    unparse_declaration' (rs::ABSTRACT_TYPE_DECLARATIONS { abstract_types, with_types => [], body }, d)
			=> 
			{   fun prd pp dbing = (unparse_sumtype context pp (dbing, d));
			    fun prw pp tbing = (unparse_named_type     context pp (tbing, d));

			    {   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				(   uj::unparse_closed_sequence
					pp
					{ front      =>  (by pp::lit ""),
					  separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
					  back       =>  (by pp::lit ";"),
					  print_one  =>  prd,
					  breakstyle =>  uj::ALIGN
					}
					abstract_types
				);
				pp::newline pp;
				unparse_declaration' (body, d);
				pp::shut_box pp;
			    };
			};

		    unparse_declaration' (rs::ABSTRACT_TYPE_DECLARATIONS { abstract_types, with_types, body }, d)
			=> 
			{   fun prd _ (dbing) = (unparse_sumtype context pp (dbing, d));
			    fun prw _ (tbing) = (unparse_named_type context pp (tbing, d));

			    {   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
				(   uj::unparse_closed_sequence
					pp
					{ front      =>  (by pp::lit ""),
					  separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
					  back       =>  (by pp::lit ";"),
					  print_one  =>  prd,
					  breakstyle =>  uj::ALIGN
					}
					abstract_types
				);
				pp::newline pp;
				(   uj::unparse_closed_sequence
					pp
					{ front      =>  (by pp::lit "withtype "),
					  separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
					  back       =>  (by pp::lit ""),
					  print_one  =>  prw,
					  breakstyle =>  uj::ALIGN
					}
					with_types
				);
				pp::newline pp;
				unparse_declaration' (body, d);
				pp::shut_box pp;
			    };
			};

		    unparse_declaration' (rs::EXCEPTION_DECLARATIONS ebs, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    (   (fn pp =  fn eb =  unparse_named_exception context pp (eb, d - 1)),   ebs   );
			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::PACKAGE_DECLARATIONS sbs, d)
			=>
			{   fun print_one _ sbing
				=
				(unparse_named_package context pp (sbing, d));

			    uj::unparse_closed_sequence
				pp
				{   front      =>  (by pp::lit "package "),
				    separator  =>  (fn pp =  (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
				    back       =>  (by pp::lit ";"),
				    print_one,
				    breakstyle =>  uj::ALIGN
				}
				sbs;
			};

		    unparse_declaration' (rs::GENERIC_DECLARATIONS fbs, d)
			=> 
			{   fun f pp generic_naming
				=
				unparse_named_generic context pp (generic_naming, d);


			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("generic package ", "also ", f, fbs);
			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::API_DECLARATIONS sigvars, d)
			=> 
			{   fun f pp (rs::NAMED_API { name_symbol=>fname, definition=>def } )
				=>
				{   uj::unparse_symbol  pp  fname;
				    pp::newline pp;
                                    ppsay "=";
				    unparse_api_expression context pp (def, d);
				};

				f pp (rs::SOURCE_CODE_REGION_FOR_NAMED_API (t, r))
                                    =>
                                    f pp t;
			    end;


			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::ppvlist pp ("api ", "also ", f, sigvars);	# Was "api "
			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::GENERIC_API_DECLARATIONS sigvars, d)
			=> 
			{   fun print_one pp sigv
				=
				unparse_generic_api_naming context pp (sigv, d);

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);

			    uj::unparse_sequence
				pp
				{ separator  =>  pp::newline,
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				sigvars;

			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::LOCAL_DECLARATIONS (inner, outer), d)
			=>
			{   pp.box .{
				pp::newline pp;  ppsay "with";
                                pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::vertical,	100 );
				    pp::newline pp;  unparse_declaration'(inner, d - 1);
                                pp::shut_box pp;

				pp::newline pp;  ppsay "do ";
				pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::vertical,	100 );
				    pp::newline pp;  unparse_declaration'(outer, d - 1);
                                pp::shut_box pp;
				pp::newline pp;  ppsay "end;\t\t# with";
			    };
			    pp::newline pp;
			};

		    unparse_declaration' (rs::SEQUENTIAL_DECLARATIONS decs, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    #
			    uj::unparse_sequence
				pp
				{ separator  =>  pp::newline,
				  print_one  =>  (fn pp =  fn declaration =  unparse_declaration' (declaration, d)),
				  breakstyle =>  uj::ALIGN
				}
				decs;

			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::INCLUDE_DECLARATIONS named_packages, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "use ";

			    uj::unparse_sequence
				pp
				{ separator  =>  (fn pp =  pp::break pp { spaces=>1, indent_on_wrap=>0 }),
				  print_one  =>  (fn pp =  fn sp =  pp_symbol_list sp),
				  breakstyle =>  uj::ALIGN
				}
				named_packages;

			    pp::shut_box pp;
			};

		    unparse_declaration' (rs::OVERLOADED_VARIABLE_DECLARATION (symbol, type, explist, extension), d)
		        =>
			{   ppsay "overloaded my ";
		            uj::unparse_symbol pp symbol;
			    ppsay ( extension ?? " += ... " :: " = ... ");
			};

		    unparse_declaration' (rs::FIXITY_DECLARATIONS { fixity, ops }, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    #
			    case fixity
                                #
				fxt::NONFIX => ppsay "nonfix my ";

				fxt::INFIX (i, _)
				 => 
				 {   if (i % 2 == 0)	ppsay "infix my ";
				     else		ppsay "infixr my ";
                                     fi;

				     if (i / 2 > 0)
				         #
					 ppsay (int::to_string (i / 2));
					 ppsay " ";
				     fi;
				 };
			   esac;

			   uj::unparse_sequence
			       pp
			       { separator  =>  (fn pp =  pp::break pp { spaces=>1, indent_on_wrap=>0 }),
				 print_one  =>  uj::unparse_symbol,
				 breakstyle =>  uj::ALIGN
			       }
			       ops;

			   pp::shut_box pp;
			};

		    unparse_declaration' (rs::SOURCE_CODE_REGION_FOR_DECLARATION (declaration, (s, e)), d)
			=>  
#			case source_opt
#			    #
#			    THE source
#				 =>
#				 {   ppsay "rs::SOURCE_CODE_REGION_FOR_DECLARATION(";
#				     unparse_declaration'(declaration, d); ppsay ", ";
#				     prpos (pp, source, s); ppsay ", ";
#				     prpos (pp, source, e); ppsay ")";
#				 };
#
#			    NULL
#			       =>
			       unparse_declaration' (declaration, d);
#			esac;

		    unparse_declaration' (rs::PRE_COMPILE_CODE string, d)
			=>
			ppsay ("#DO " + string);

                  end;
	      
                  unparse_declaration';
	      }

	also
	fun unparse_named_value (context as (dictionary, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun unparse_named_value'(_, 0)
                        =>
                        ppsay "<naming>";

		    unparse_named_value' (rs::NAMED_VALUE { pattern, expression, ... }, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    unparse_pattern context pp (pattern, d - 1);
			    pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    unparse_expression context pp (expression, d - 1);
			    pp::shut_box pp;
			};

		    unparse_named_value' (rs::SOURCE_CODE_REGION_FOR_NAMED_VALUE (named_value, source_code_region), d)
			=>
			unparse_named_value' (named_value, d);
                end;
	    
		unparse_named_value';
	    }

	also
	fun unparse_named_field (context as (dictionary, source_opt)) pp
            =
	    # 2009-02-23 CrT: A quick first-cut solution
	    # duplicated from unparse_named_value:
	    #
	    {   ppsay =  pp::lit  pp;
		#
		fun unparse_named_field'(_, 0)
                        =>
                        ppsay "<field>";

		    unparse_named_field' (rs::NAMED_FIELD { name => symbol, type => case_pattern, init }, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    pp_path pp [symbol];
			    pp::shut_box pp;
			};

		    unparse_named_field' (rs::SOURCE_CODE_REGION_FOR_NAMED_FIELD (named_field, source_code_region), d)
			=>
			unparse_named_field' (named_field, d);
                end;
	    
		unparse_named_field';
	    }

	also
	fun unparse_named_recursive_values (context as (_, source_opt)) pp
            = 
	    {   ppsay =  pp::lit  pp;
		#
		fun unparse_named_recursive_values'(_, 0)=> ppsay "<rec naming>";

		    unparse_named_recursive_values' (rs::NAMED_RECURSIVE_VALUE { variable_symbol, expression, ... }, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    uj::unparse_symbol pp variable_symbol;
                            pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
                            unparse_expression context pp (expression, d - 1);
			    pp::shut_box pp;
			};

		    unparse_named_recursive_values' (rs::SOURCE_CODE_REGION_FOR_RECURSIVELY_NAMED_VALUE (named_recursive_values, source_code_region), d)
			=>
			unparse_named_recursive_values' (named_recursive_values, d);
                end;
	    
		unparse_named_recursive_values';
	    }

	also
	fun unparse_named_sml_function (context as (_, source_opt)) pp head
            = 
	    {   ppsay =  pp::lit  pp;


		fun unparse_named_sml_function'(_, 0)
                        =>
                        ppsay "<FunNaming>";

		    unparse_named_sml_function' (rs::NAMED_FUNCTION { pattern_clauses, is_lazy, kind, null_or_type }, d)
			=>
			{
			    case kind
				rs::PLAIN_FUN => ppsay "";
			       rs::METHOD_FUN => ppsay " (method) ";
			      rs::MESSAGE_FUN => ppsay " (message) ";
			    esac;

                            case null_or_type 
				THE type => {   ppsay " : ";
						unparse_type context pp (type, d - 1);
					    };
				NULL => ();
			    esac;

			    uj::ppvlist pp
                              ( head, "  ; ",
			        (fn pp =   fn (cl: rs::Pattern_Clause) =   (unparse_pattern_clause context pp (cl, d))),
			        pattern_clauses
                              );
			};

		    unparse_named_sml_function' (rs::SOURCE_CODE_REGION_FOR_NAMED_FUNCTION (t, r), d)
			=>
			unparse_named_sml_function context pp head (t, d);
                end;
	    
		unparse_named_sml_function';
	    }

	also
	fun unparse_pattern_clause (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;

		fun unparse_pattern_clause' (rs::PATTERN_CLAUSE { patterns, result_type, expression }, d)
                    =
		    {   fun print_one _ {   item:		    rs::Case_Pattern,
					    fixity:		    Null_Or( rs::Symbol ),
					    source_code_region:    rs::Source_Code_Region
					}
                            =
			    case fixity
			        #
			         THE a =>   unparse_pattern context pp (item, d);

				 NULL =>    case item
						#
						rs::PRE_FIXITY_PATTERN p
						    =>
						    {   pp::lit pp "(";unparse_pattern context pp (item, d);
							pp::lit pp ")";
						    };

						rs::TYPE_CONSTRAINT_PATTERN p
						    =>
						    {   pp::lit pp "(";unparse_pattern context pp (item, d);
							pp::lit pp ")";
						    };

						rs::AS_PATTERN p
						    =>
						    {   pp::lit pp"(";unparse_pattern context pp (item, d);
							pp::lit pp ")";
						    };

						rs::OR_PATTERN p
						    =>
						    {   pp::lit pp "(";unparse_pattern context pp (item, d);
							pp::lit pp ")";
						    };

						_   =>
						    unparse_pattern context pp (item, d);
					    esac;
			    esac;


		    
			pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );

			uj::unparse_sequence
			    pp
			    { separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
			      print_one,
			      breakstyle =>  uj::ALIGN
			    }
			    patterns;

			case result_type
			    #			  
			    THE type => {   pp::lit pp ":";
					    unparse_type context pp (type, d);
					};

			    NULL => ();
			esac;

			pp::lit pp " =";
			pp::break pp { spaces=>1, indent_on_wrap=>0 }; 
			unparse_expression context pp (expression, d);
			pp::shut_box pp;

		    }; 

	    
		unparse_pattern_clause';
	    }

	also
	fun unparse_named_lib7function (context as (_, source_opt)) pp head
            = 
	    {   ppsay =  pp::lit pp;
		#
		fun unparse_named_lib7function'(_, 0)
                        =>
                        ppsay "<FunNaming>";

		    unparse_named_lib7function' (rs::NADA_NAMED_FUNCTION (clauses, ops), d)
			=>
			uj::ppvlist pp (head, "  | ",
			   (fn pp =
                            fn (cl: rs::Nada_Pattern_Clause) = (unparse_lib7pattern_clause context pp (cl, d))
                           ),
			   clauses);

		    unparse_named_lib7function' (rs::SOURCE_CODE_REGION_FOR_NADA_NAMED_FUNCTION (t, r), d)
			=>
			unparse_named_lib7function context pp head (t, d);
                end;
	    
		unparse_named_lib7function';
	    }

	also
	fun unparse_lib7pattern_clause (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit pp;
		#
		fun unparse_lib7pattern_clause' (rs::NADA_PATTERN_CLAUSE { pattern, result_type, expression }, d)
                    =
		    {   fun print_one _ (item:   rs::Case_Pattern)
                            =
                            #  XXX BUGGO FIXME:  Need to be more intelligent about paren insertion: 
			    {   pp::lit pp "(";
                                unparse_pattern context pp (item, d);
				pp::lit pp ")";
                            };

		    
			pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );

			uj::unparse_sequence
			    pp
			    { separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
			      print_one,
			      breakstyle =>  uj::ALIGN
			    }
			    [ pattern ];         #  XXX BUGGO FIXME this list is always len 1 (obviously) so the logic here can probably be simplified. 


			case result_type
			    #
			    THE type => {   pp::lit pp ":";
					    unparse_type context pp (type, d);
					};

			    NULL => ();
			esac;


			pp::lit pp " =";
			pp::break pp { spaces=>1, indent_on_wrap=>0 }; 
			unparse_expression context pp (expression, d);
			pp::shut_box pp;

		    }; 

	    
		unparse_lib7pattern_clause';
	    }

	also
	fun unparse_named_type (context as (_, source_opt)) pp 
            = 
	    {   ppsay = pp::lit pp;

		fun pp_tyvar_list (symbol_list, d)
                    =
		    {   fun print_one _ (typevar)
                            =
                            unparse_typevar  context  pp  (typevar, d);
		    
                        uj::unparse_sequence
                            pp
			    { separator  =>  (fn pp =  { pp::lit pp ",";	# Was "*"
					       pp::break pp { spaces=>1, indent_on_wrap=>0 } ;}),
			      print_one,
			      breakstyle =>  uj::ALIGN
                            }
			    symbol_list;
		    };

		fun unparse_named_type'(_, 0)
                        =>
                        ppsay "<t::naming>";

		    unparse_named_type' (rs::NAMED_TYPE { name_symbol, definition, typevars }, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );
			    uj::unparse_symbol pp name_symbol;
                            if (list::length typevars > 0)
				pp::lit pp "(";
				pp_tyvar_list (typevars, d);
				pp::lit pp ")";
			    fi;
			    pp::lit pp " =";

			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
                            unparse_type context pp (definition, d);
			    pp::shut_box pp;
			};

		    unparse_named_type' (rs::SOURCE_CODE_REGION_FOR_NAMED_TYPE (t, r), d)
			=>
			unparse_named_type context pp (t, d);
                end;
	    
		unparse_named_type';
	    }

	also
	fun unparse_sumtype (context as (_, source_opt)) pp
            = 
	    {   ppsay = pp::lit pp;

# Commented out because apparently unused  -- 2009-08-08 CrT
#		fun pp_tyvar_list (symbol_list, d)
#                    =
#		    {   fun print_one _ (typevar)
#                            =
#                            (unparse_typevar context pp (typevar, d));
#		    
#                        uj::unparse_sequence
#                            pp
#			    { separator   => (fn pp =  { pp::lit pp ",";	# Was "*"
#					     pp::break pp { spaces=>1, indent_on_wrap=>0 } ;}),
#			      print_one,
#			      breakstyle => uj::ALIGN
#                            }
#			    symbol_list;
#		    };

		fun unparse_sumtype'(_, 0)
                        =>
                        ppsay "<d::naming>";

		    unparse_sumtype' (rs::SUM_TYPE { name_symbol, typevars, right_hand_side, is_lazy }, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE  { spaces => 1, tab_to => 0, tabstops_are_every => 4 },  pp::ragged_right, 100 );

			    uj::unparse_symbol pp name_symbol;
			    pp::lit pp " =";

			    pp::break pp { spaces=>1, indent_on_wrap=>0 };
			    unparse_sumtype_right_hand_side context pp (right_hand_side, d);

			    pp::shut_box pp;
			};

		    unparse_sumtype' (rs::SOURCE_CODE_REGION_FOR_UNION_TYPE (t, r), d)
			=>
			unparse_sumtype context pp (t, d);
                end;
	    
		unparse_sumtype';
	    }

	also
	fun unparse_sumtype_right_hand_side (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit pp;
		#
		fun unparse_sumtype_right_hand_side'(_, 0)
                        =>
                        ppsay "<sumtype_naming_right_hand_side>";

		    unparse_sumtype_right_hand_side' (rs::VALCONS const, d)
			=> 
			{   fun print_one pp   (symbol: rs::Symbol,   tv: Null_Or(rs::Any_Type))
				=
				case tv
				    #
				    THE a =>    {    uj::unparse_symbol pp symbol;
						     ppsay " ";	# Was " of "
						     unparse_type context pp (a, d);
						};

				    NULL =>   (uj::unparse_symbol pp symbol);
				esac;

			    uj::unparse_sequence
				pp
				{ separator   => (fn pp = { pp::lit pp " |";
								pp::break pp { spaces=>1, indent_on_wrap=>0 };
							      }
						 ),
				  print_one,
				  breakstyle => uj::ALIGN
				}
				const;
			};

		    unparse_sumtype_right_hand_side' (rs::REPLICAS symlist, d)
			=> 
			uj::unparse_sequence
			    pp
			    { separator  =>  (fn pp
						 =
						 {   pp::lit pp " |";
						     pp::break pp { spaces=>1, indent_on_wrap=>0 };
						 }
					     ),
			      print_one  =>  (fn pp =  fn symbol =  uj::unparse_symbol pp symbol),
			      breakstyle =>  uj::ALIGN
			    }
			    symlist;
                end;
	    
                unparse_sumtype_right_hand_side';
	    }

	also
	fun unparse_named_exception (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		pp_symbol_list = pp_path pp;

		fun unparse_named_exception' (_, 0)
                        =>
                        ppsay "<Eb>";

		    unparse_named_exception' (   rs::NAMED_EXCEPTION {
                                                         exception_symbol => exn,
                                                         exception_type   => etype
                                                     },
                                                     d
                                                 )
			=> 
			case etype
			    #			  
			    THE a =>    {   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
					    uj::unparse_symbol pp exn;
					    pp::lit pp " =";
					    pp::break pp { spaces=>1, indent_on_wrap=>2 };
					    unparse_type context pp (a, d - 1);
					    pp::shut_box pp;
					};

			    NULL =>     {   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
					    uj::unparse_symbol pp exn; 
					    pp::shut_box pp;
					};
			esac;


		    unparse_named_exception' ( rs::DUPLICATE_NAMED_EXCEPTION { exception_symbol=>exn, equal_to=>edef }, d)
			=> 
			# ASK MACQUEEN IF WE NEED TO PRINT EDEF XXX BUGGO FIXME 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::unparse_symbol pp exn;
			    pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    pp_symbol_list (edef);
			    pp::shut_box pp;
			};

		    unparse_named_exception' (rs::SOURCE_CODE_REGION_FOR_NAMED_EXCEPTION (t, r), d)
			=>
			unparse_named_exception context pp (t, d);
                end;
	    
		unparse_named_exception';
	    }

	also
	fun unparse_named_package (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;

		fun unparse_named_package' (_, 0)
                        =>
                        ppsay "<rs::NAMED_PACKAGE>";

		    unparse_named_package' ( rs::NAMED_PACKAGE { name_symbol=>name, definition=>def, constraint, kind }, d)
			=> 
			{   ppsay case kind
                                      rs::PLAIN_PACKAGE  => "package ";
                                      rs::CLASS_PACKAGE  => "class ";
                                      rs::CLASS2_PACKAGE => "class2 ";
                                  esac;

			    pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::unparse_symbol pp name;

                            unparse_package_cast context pp constraint d;

                            pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
                            unparse_package_expression context pp (def, d - 1);
			    pp::shut_box pp;
			};

		    unparse_named_package' (rs::SOURCE_CODE_REGION_FOR_NAMED_PACKAGE (t, r), d)
			=>
			unparse_named_package context pp (t, d);
                end;
	    
		unparse_named_package';
	    }

	also
	fun unparse_named_generic (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun unparse_named_generic' (_, 0)
                        =>
                        ppsay "<rs::NAMED_GENERIC>";

		    unparse_named_generic' (
                        rs::NAMED_GENERIC {
                            name_symbol => name,
                            definition => rs::GENERIC_DEFINITION { parameters, body, constraint }
                        },
                        d
                    )
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    #
			    uj::unparse_symbol pp name;

			    {   fun print_one pp (THE symbol, api_expression)
					=>
					{   ppsay "(";
					    uj::unparse_symbol pp symbol;
					    ppsay " : ";
					    unparse_api_expression context pp (api_expression, d);
					    ppsay ")";
					};

				    print_one pp (NULL, api_expression)
					=>
					{   ppsay "(";
					    unparse_api_expression context pp (api_expression, d);
					    ppsay ")";
					};
                                end;

				{   uj::unparse_sequence
					pp
					{ separator  =>  (fn pp = (pp::break pp { spaces=>1, indent_on_wrap=>0 } )),
					  print_one,
					  breakstyle =>  uj::ALIGN
					}
					parameters;



                                    unparse_package_cast  context  pp  constraint  d;


				    pp::nonbreakable_spaces pp 1;

				    ppsay "=";
                                    pp::break pp { spaces=>1, indent_on_wrap=>0 };

				    unparse_package_expression context pp (body, d);};
			    };
			    pp::shut_box pp;
			};

		    unparse_named_generic' ( rs::NAMED_GENERIC { name_symbol=>name, definition=>def }, d)
			=>
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    uj::unparse_symbol pp name;
			    pp::lit pp " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
			    unparse_generic_expression context pp (def, d - 1);
			    pp::shut_box pp;
			}; 

		    unparse_named_generic' (rs::SOURCE_CODE_REGION_FOR_NAMED_GENERIC (t, r), d)
			=>
			unparse_named_generic context pp (t, d);
                end;
	    
		unparse_named_generic';
	    }

	also
	fun unparse_generic_api_naming (context as (_, source_opt)) pp
            =
	    {   ppsay =  pp::lit  pp;
		#
		fun unparse_generic_api_naming'(_, 0)
                        =>
                        ppsay "<rs::NAMED_GENERIC_API>";

		    unparse_generic_api_naming' (rs::NAMED_GENERIC_API { name_symbol=>name, definition=>def }, d)
			=> 
			{   pp::open_box (pp, pp::pmt::BOX_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 },	pp::normal,	100	);
			    ppsay "funsig ";
                            uj::unparse_symbol pp name;
                            ppsay " =";
			    pp::break pp { spaces=>1, indent_on_wrap=>2 };
                            unparse_generic_api_expression context pp (def, d - 1);
			    pp::shut_box pp;
			};

		    unparse_generic_api_naming' (rs::SOURCE_REGION_FOR_NAMED_GENERIC_API (t, r), d)
			=>
			unparse_generic_api_naming context pp (t, d);
                end;
	    
		unparse_generic_api_naming';
	    }

	also
	fun unparse_typevar (context as (_, source_opt)) pp
            =
	    {   ppsay = pp::lit pp;
		#
		fun unparse_typevar' (_, 0) => ppsay "<typevar>";
		    unparse_typevar' (rs::TYPEVAR s, d) => (uj::unparse_symbol pp s); 
		    unparse_typevar' (rs::SOURCE_CODE_REGION_FOR_TYPEVAR (t, r), d) => unparse_typevar context pp (t, d);
                end;
	    
		unparse_typevar';
	    }

	also
	fun unparse_type (context as (dictionary, source_opt)) pp
            =			
	    {   ppsay =  pp::lit pp;
		#
		fun unparse_type' (_, 0)
                        =>
                        ppsay "<type>";

		    unparse_type' (rs::TYPEVAR_TYPE t, d)
                        =>
                        (unparse_typevar context pp (t, d));

		    unparse_type' (rs::TYPE_TYPE (type, []), d)
			=>
			{   pp::open_box (pp, pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::normal, 100 );
			    pp_path pp type;
			    pp::shut_box pp;
			};

		    unparse_type' (rs::TYPE_TYPE (type, args), d)
			=> 
			{   pp::open_box (pp, pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::normal, 100 );
			    #
			    case type
				#			      
				[type] =>   if (sy::eq (sy::make_type_symbol("->"), type))
						#				         
						case args

						     [dom, ran]
							 =>
							 {   unparse_type' (dom, d - 1);
							     ppsay " ->";
							     pp::break pp { spaces=>1, indent_on_wrap=>2 };
							     unparse_type' (ran, d - 1);
							 };

						     _   =>
							 err::impossible "wrong args for -> type";
						esac;
					    else
						uj::unparse_symbol  pp  type;
						ppsay "(";
						unparse_type_args  (args, d);
						ppsay ")";
					    fi;

				_ => {   pp_path  pp  type;
					 ppsay "(";
                                         unparse_type_args  (args, d);
					 ppsay ")";
				     };
                            esac;

			    pp::shut_box pp;
			};

		    unparse_type' (rs::RECORD_TYPE s, d)
			=> 
			{   fun print_one pp (symbol: rs::Symbol, tv: rs::Any_Type)
				= 
				{   uj::unparse_symbol pp symbol;
				    ppsay ": ";
				    unparse_type context pp (tv, d);
				};

			    uj::unparse_closed_sequence
				pp
				{ front      =>  (by pp::lit "{ "),
				  separator  =>  (fn pp = { pp::lit pp ", ";
								pp::break pp { spaces=>1, indent_on_wrap=>0 };
							      }
						 ),
				  back       =>  (by pp::lit "}"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				s;
			};

		    unparse_type' (rs::TUPLE_TYPE t, d)
			=> 
			{   fun print_one _ (tv: rs::Any_Type)
				=
				(unparse_type context pp (tv, d));

			    uj::unparse_closed_sequence 
				pp
				{ front      =>  (by pp::lit "("),
				  separator  =>  (fn pp =  {   pp::lit pp ", ";				# Was " *"
								   pp::break pp { spaces=>0, indent_on_wrap=>0 };
							       }
						 ),
				  back       =>  (by pp::lit ")"),
				  print_one,
				  breakstyle =>  uj::ALIGN
				}
				t;
			};

		    unparse_type' (rs::SOURCE_CODE_REGION_FOR_TYPE (t, r), d)
			=>
			unparse_type context pp (t, d);
                end 

		also
		fun unparse_type_args ([], d)
                        =>
                        ();

		    unparse_type_args ( [type], d)
			=> 
			{   if (strength type <= 1)
			        #  
				pp::open_box (pp, pp::pmt::CURSOR_RELATIVE { spaces => 1, tab_to => 0, tabstops_are_every => 4 }, pp::ragged_right, 100 );
				ppsay "("; 
				unparse_type' (type, d); 
				ppsay ")";
				pp::shut_box pp;
			    else 
				unparse_type' (type, d);
			    fi;

			    pp::break pp { spaces => 0,  indent_on_wrap => 0 };
			};

		    unparse_type_args (tys, d)
			=>
			uj::unparse_closed_sequence
			    pp 
			    { front      =>  by pp::lit "(",
			      separator  =>  fn pp =  { pp::lit pp ", ";
							 pp::break pp { spaces=>0, indent_on_wrap=>0 };
						       },
			      back       =>  by pp::lit ") ",
			      breakstyle =>  uj::ALIGN, 
			      print_one  =>  fn _ =  fn type =  unparse_type' (type, d)
			    }
			    tys;
                end; 
	    
                unparse_type';
	    };
    };					# package unparse_raw_syntax 
end;					# top-level local 








##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################



## Copyright 2003 by University of Chicago 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
