## prettyprint-value.pkg 
#
# This is a very quick-and-dirty partial conversion of unparse-value.pkg into prettyprint-value.pkg.
#
# The intended distinction between unparsing and prettyprinting is:
#
#  o  unparsing strives to produce something as close as possible
#     to the original input -- Mythryl syntax code -- whereas 
#
#  o  prettyprinting strives to produce a clear display of the
#     datastructures in question -- mainly the parsetree.
#
# Unparsing is useful for showing what is being processed in compact
# and human-readable fashion;  prettyprinting is useful for showing
# all the gory details of the intermediate representation so as to
# facilitate debugging detailed processing of it.   -- 2013-09-24 CrT

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

#  Modified to use Lib7 Lib pp. [dbm, 7/30/03]) 

stipulate
    package id  =  inlining_data;		# inlining_data			is from   src/lib/compiler/front/typer-stuff/basics/inlining-data.pkg
    package pp  =  standard_prettyprint_mill;	# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package syx =  symbolmapstack;		# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
    package tdt =  type_declaration_types;	# type_declaration_types	is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
    package vac =  variables_and_constructors;	# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
    package vh  =  varhome;			# varhome			is from   src/lib/compiler/front/typer-stuff/basics/varhome.pkg
herein

    api Prettyprint_Value {
	#
	prettyprint_constructor_representation:  pp::Prettyprint_Mill
					       -> vh::Valcon_Form
					       -> Void;

	prettyprint_varhome:     pp::Prettyprint_Mill ->  vh::Varhome  -> Void;
	prettyprint_valcon:      pp::Prettyprint_Mill ->  tdt::Valcon   -> Void;
	prettyprint_var:         pp::Prettyprint_Mill -> vac::Variable -> Void;

	prettyprint_variable
	    :
	    pp::Prettyprint_Mill
	    -> (syx::Symbolmapstack, vac::Variable)
	    -> Void
            ;

	prettyprint_debug_valcon
	    #
	    :  pp::Prettyprint_Mill
	    -> syx::Symbolmapstack
	    -> tdt::Valcon
	    -> Void
            ;

	prettyprint_constructor
	    :
	    pp::Prettyprint_Mill
	    -> syx::Symbolmapstack
	    ->  tdt::Valcon
	    ->      Void;

	prettyprint_debug_var
	    :
	    (id::Inlining_Data -> String)
	    -> pp::Prettyprint_Mill 
	    -> syx::Symbolmapstack
	    -> vac::Variable
	    -> Void;

    };						# Api Prettyprint_Value 
end;


stipulate
    package fis =  find_in_symbolmapstack;	# find_in_symbolmapstack	is from   src/lib/compiler/front/typer-stuff/symbolmapstack/find-in-symbolmapstack.pkg
    package ip  =  inverse_path;		# inverse_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package mtt =  more_type_types;		# more_type_types		is from   src/lib/compiler/front/typer/types/more-type-types.pkg
    package pp  =  standard_prettyprint_mill;	# standard_prettyprint_mill	is from   src/lib/prettyprint/big/src/standard-prettyprint-mill.pkg
    package ppt =  prettyprint_type;		# prettyprint_type		is from   src/lib/compiler/front/typer/print/prettyprint-type.pkg
    package syp =  symbol_path;			# symbol_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package syx =  symbolmapstack;		# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
    package tc  =  typer_control;		# typer_control			is from   src/lib/compiler/front/typer/basics/typer-control.pkg
    package tdt =  type_declaration_types;	# type_declaration_types	is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
    package tys =  type_junk;			# type_junk			is from   src/lib/compiler/front/typer-stuff/types/type-junk.pkg
    package uj  =  unparse_junk;		# unparse_junk			is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
    package ut  =  unparse_type;		# unparse_type			is from   src/lib/compiler/front/typer/print/unparse-type.pkg
    package vac =  variables_and_constructors;	# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
    package vh  =  varhome;			# varhome			is from   src/lib/compiler/front/typer-stuff/basics/varhome.pkg

    Pp = pp::Pp;

#   package id  =  inlining_data;		# inlining_data			is from   src/lib/compiler/front/typer-stuff/basics/inlining-data.pkg

    prettyprint_typoid      =  ppt::prettyprint_typoid;
    prettyprint_type        =  ppt::prettyprint_type;
    prettyprint_typescheme  =  ppt::prettyprint_typescheme;
herein 

    package   prettyprint_value
    : (weak)  Prettyprint_Value
    {
#	internals =   tc::internals;
internals =   log::internals;

	fun by f x y
            =
            f y x;

#	pps = pp::lit;


	fun prettyprint_varhome  (pp:Pp)  a
            =
            pp.lit     (   " ["
                       +   (vh::print_varhome a)
                       +   "]"
                       );

	fun prettyprint_inlining_data inlining_info_to_string  (pp:Pp)  a
            =
            pp.lit (" [" + (inlining_info_to_string a) + "]");

	fun prettyprint_constructor_representation  (pp:Pp)  representation
            =
            pp.lit (vh::print_representation representation);

	fun prettyprint_csig  (pp:Pp) csig
            =
            pp.lit (vh::print_constructor_api csig);

	fun prettyprint_valcon pp
            =
            prettyprint_d
	    where
	        fun prettyprint_d ( tdt::VALCON { name, form => vh::EXCEPTION acc, ... } )
			=>
			{   uj::unparse_symbol  pp  name;
			    #
			    if *internals     prettyprint_varhome  pp  acc;     fi;
			};

		    prettyprint_d (tdt::VALCON { name, ... } )
			=>
			uj::unparse_symbol  pp  name;
                end;
	    end;

	fun prettyprint_debug_valcon pp symbolmapstack (tdt::VALCON { name, form, is_constant, typoid, signature, is_lazy } )
            =
	    {   unparse_symbol =  uj::unparse_symbol  pp;
		#
                pp.box .{
		    #
		    pp.lit "VALCON";
		    pp.cut();
		    pp.lit "{ name = ";		unparse_symbol name;					uj::unparse_comma_newline pp;
		    pp.lit "is_constant = ";	pp.lit (bool::to_string is_constant);			uj::unparse_comma_newline pp;
		    pp.lit "typoid = ";		prettyprint_typoid  symbolmapstack  pp  typoid;		uj::unparse_comma_newline pp;
		    pp.lit "is_lazy = ";	pp.lit (bool::to_string is_lazy);			uj::unparse_comma_newline pp;

		    pp.lit "pick_valcon_form =";
		    prettyprint_constructor_representation
			pp
			form;
											 uj::unparse_comma_newline pp;
		    pp.lit "signature = [";
		    prettyprint_csig pp signature;
		    pp.lit "] }";
		};
	    };

	fun prettyprint_constructor pp symbolmapstack (tdt::VALCON { name, form, is_constant, typoid, signature, is_lazy } )
            =
	    {   unparse_symbol =  uj::unparse_symbol  pp;
		#
                pp.box .{
		    unparse_symbol name;
		    pp.txt " : ";
		    prettyprint_typoid  symbolmapstack  pp  typoid;
		};
	    };

	fun prettyprint_sumtype
              (
		symbolmapstack: syx::Symbolmapstack,
		tdt::VALCON { name, typoid, ... }
              )
              pp
            =
	    pp.wrap' 0 -1 .{
		uj::unparse_symbol pp name;   pp.txt " : ";
		prettyprint_typoid  symbolmapstack  pp  typoid;
	    };

# Is this ever used?
	fun prettyprint_constructor_naming pp
            =
            prettyprint_constructor
            where
		fun prettyprint_constructor (tdt::VALCON { name, typoid, form=>vh::EXCEPTION _, ... }, symbolmapstack)
			=>
			{   pp.box' 0 -1 .{
				#
				pp.lit "exception ";
				uj::unparse_symbol  pp  name; 

				if (mtt::is_arrow_type  typoid)
				    #
				    pp.txt " "; 
				    prettyprint_typoid  symbolmapstack  pp  (mtt::domain  typoid);
				fi;

				pp.endlit ";"; 
			    };
			};

		    prettyprint_constructor (con, symbolmapstack)
			=> 
			{   exception HIDDEN;
			    #
			    visible_valcon_type
				=
				{   type =   tys::sumtype_to_type   con;

				    (   type_junk::type_equality (
					    fis::find_type_via_symbol_path
					    (    symbolmapstack,
						 syp::SYMBOL_PATH
						 [ ip::last (type_junk::namepath_of_type type) ],
						 fn _ = raise exception HIDDEN
					    ),
					    type
					)
					except
                                            HIDDEN = FALSE
				    );
				};

			    if (*internals
                                or
				not visible_valcon_type
			    )
				pp.box' 0 -1 .{
				    pp.lit "con ";
				    prettyprint_sumtype (symbolmapstack, con) pp;
				    pp.endlit ";";
				};
                            fi;
			};
                end;
	    end;

	fun prettyprint_var pp (vac::ORDINARY_VARIABLE { varhome, path, ... } )
		=>
		{   pp.lit (syp::to_string path);
		    #
		    if *internals
			 prettyprint_varhome pp varhome;
		    fi;
		};

	    prettyprint_var pp (vac::OVERLOADED_IDENTIFIER { name, ... } )
		=>
		uj::unparse_symbol pp (name);

	    prettyprint_var pp (errorvar)
		=>
		pp.lit "<errorvar>";
        end;

	fun prettyprint_debug_var inlining_info_to_string pp symbolmapstack
            = 
	    {   prettyprint_varhome 	    = prettyprint_varhome pp;
		prettyprint_inlining_data   = prettyprint_inlining_data inlining_info_to_string pp;

		fun prettyprintdebugvar (vac::ORDINARY_VARIABLE { varhome, path, var_type, inlining_data } )
			=> 
			{   pp.box' 0 -1 .{
				pp.lit "vac::ORDINARY_VARIABLE";
				pp.box .{
				    pp.lit "( { varhome => ";		prettyprint_varhome varhome;			uj::unparse_comma_newline pp;
				    pp.lit "inlining_data => "; 	prettyprint_inlining_data inlining_data;	uj::unparse_comma_newline pp;
				    pp.lit "path=";			pp.lit (syp::to_string path);			uj::unparse_comma_newline pp;
				    pp.lit "var_type => REF ";		prettyprint_typoid  symbolmapstack  pp  *var_type; 
				    pp.lit "} )";
				};
			    };
			};

		    prettyprintdebugvar (vac::OVERLOADED_IDENTIFIER { name, alternatives, typescheme } )
			=> 
			{   pp.box' 0 -1 .{
				#
				pp.lit "vac::OVERLOADED_IDENTIFIER";
				pp.box .{
				    pp.lit "( { name="; uj::unparse_symbol pp (name);		uj::unparse_comma_newline pp;
				    pp.lit "alternative=["; 
				    (uj::ppvseq pp 0 ", "
				      (fn pp = fn { indicator, variant }
					  =
					  {   pp.lit "{ indicator=";		prettyprint_typoid  symbolmapstack  pp   indicator; 
					      uj::unparse_comma_newline pp;
					      pp.lit " variant =";		prettyprint_debug_var inlining_info_to_string pp symbolmapstack variant;
					      pp.lit "}";
					  }
				      )
				      *alternatives);
				    pp.lit "]";
				    uj::unparse_comma_newline pp;
				    pp.lit "typescheme=";
				    prettyprint_typescheme  symbolmapstack  pp  typescheme;
				    pp.lit "} )";
				};
			    };
			};

		    prettyprintdebugvar (errorvar) =>  pp.lit "<ERRORvar>";
                end;
	    
                prettyprintdebugvar;
	    };

	fun prettyprint_variable pp
            =
            prettyprint_variable'
            where
		#
		fun prettyprint_variable'
                        (
                          symbolmapstack: syx::Symbolmapstack,
			  vac::ORDINARY_VARIABLE { path, varhome, var_type, inlining_data }
			)
			=> 
			{   pp.box' 0 -1 .{
				#
				pp.lit "vac::ORDINARY_VARIABLE { path => ";
				pp.lit (syp::to_string path);

				if *internals
				    pp.lit ", varhome => ";
				    prettyprint_varhome pp  varhome;
				fi;

				pp.lit ", inlining_data ";	# prettyprint_inlining_data inlining_data;        uj::unparse_comma_newline pp;

				pp.lit ", var_type => ";
				prettyprint_typoid  symbolmapstack  pp  *var_type;

				pp.lit "}";
			    };
			};

		    prettyprint_variable'
                        (
                          symbolmapstack,
                          vac::OVERLOADED_IDENTIFIER { name, alternatives=>REF alternatives, typescheme=>tdt::TYPESCHEME { body, ... } }
                        )
			=>
			{   pp.box' 0 -1 .{
				#
				pp.lit "vac::OVERLOADED_IDENTIFIER { name => ";
				uj::unparse_symbol pp name;

				pp.lit ", typescheme => tdt::TYPESCHEME { body => ";
				prettyprint_typoid  symbolmapstack  pp  body; 

				pp.lit ", ... }, alternatives => REF ";
				uj::unparse_sequence
				    pp
				      { separator  =>  fn pp = pp.txt " ",
					print_one  =>  fn pp = fn { variant, ... } = prettyprint_variable' (symbolmapstack, variant),
					breakstyle =>  uj::ALIGN
				      }
				    alternatives;
				pp.lit " }";
			    };
		        };

		    prettyprint_variable' (_, errorvar)
                        =>
                        pp.lit "<ERRORvar>;";
                end;
	    end;
    };								# package prettyprint_value 
end;								# stipulate











##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Copyright 1996 by AT&T Bell Laboratories 
## Copyright 2003 by The SML/NJ Fellowship 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
