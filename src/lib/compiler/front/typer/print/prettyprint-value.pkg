## prettyprint-value.pkg 
#
# This is a very quick-and-dirty partial conversion of unparse-value.pkg into prettyprint-value.pkg.
#
# The intended distinction between unparsing and prettyprinting is:
#
#  o  unparsing strives to produce something as close as possible
#     to the original input -- Mythryl syntax code -- whereas 
#
#  o  prettyprinting strives to produce a clear display of the
#     datastructures in question -- mainly the parsetree.
#
# Unparsing is useful for showing what is being processed in compact
# and human-readable fashion;  prettyprinting is useful for showing
# all the gory details of the intermediate representation so as to
# facilitate debugging detailed processing of it.   -- 2013-09-24 CrT

# Compiled by:
#     src/lib/compiler/front/typer/typer.sublib

#  Modified to use Lib7 Lib pp. [dbm, 7/30/03]) 

stipulate
    package id  =  inlining_data;		# inlining_data			is from   src/lib/compiler/front/typer-stuff/basics/inlining-data.pkg
    package pp  =  plain_prettyprint_buffer;	# plain_prettyprint_buffer	is from   src/lib/prettyprint/big/src/plain-prettyprint-buffer.pkg
    package syx =  symbolmapstack;		# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
    package tdt =  type_declaration_types;	# type_declaration_types	is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
    package vac =  variables_and_constructors;	# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
    package vh  =  varhome;			# varhome			is from   src/lib/compiler/front/typer-stuff/basics/varhome.pkg
herein

    api Prettyprint_Value {
	#
	prettyprint_constructor_representation:  pp::Prettyprint_Buffer
					       -> vh::Valcon_Form
					       -> Void;

	prettyprint_varhome:     pp::Prettyprint_Buffer ->  vh::Varhome  -> Void;
	prettyprint_valcon:      pp::Prettyprint_Buffer ->  tdt::Valcon   -> Void;
	prettyprint_var:         pp::Prettyprint_Buffer -> vac::Variable -> Void;

	prettyprint_variable
	    :
	    pp::Prettyprint_Buffer
	    -> (syx::Symbolmapstack, vac::Variable)
	    -> Void
            ;

	prettyprint_debug_valcon
	    #
	    :  pp::Prettyprint_Buffer
	    -> syx::Symbolmapstack
	    -> tdt::Valcon
	    -> Void
            ;

	prettyprint_constructor
	    :
	    pp::Prettyprint_Buffer
	    -> syx::Symbolmapstack
	    ->  tdt::Valcon
	    ->      Void;

	prettyprint_debug_var
	    :
	    (id::Inlining_Data -> String)
	    -> pp::Prettyprint_Buffer 
	    -> syx::Symbolmapstack
	    -> vac::Variable
	    -> Void;

    };						# Api Prettyprint_Value 
end;


stipulate
    package fis =  find_in_symbolmapstack;	# find_in_symbolmapstack	is from   src/lib/compiler/front/typer-stuff/symbolmapstack/find-in-symbolmapstack.pkg
    package ip  =  inverse_path;		# inverse_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package mtt =  more_type_types;		# more_type_types		is from   src/lib/compiler/front/typer/types/more-type-types.pkg
    package pp  =  plain_prettyprint_buffer;	# plain_prettyprint_buffer	is from   src/lib/prettyprint/big/src/plain-prettyprint-buffer.pkg
    package ppt =  prettyprint_type;		# prettyprint_type		is from   src/lib/compiler/front/typer/print/prettyprint-type.pkg
    package syp =  symbol_path;			# symbol_path			is from   src/lib/compiler/front/typer-stuff/basics/symbol-path.pkg
    package syx =  symbolmapstack;		# symbolmapstack		is from   src/lib/compiler/front/typer-stuff/symbolmapstack/symbolmapstack.pkg
    package tc  =  typer_control;		# typer_control			is from   src/lib/compiler/front/typer/basics/typer-control.pkg
    package tdt =  type_declaration_types;	# type_declaration_types	is from   src/lib/compiler/front/typer-stuff/types/type-declaration-types.pkg
    package tys =  type_junk;			# type_junk			is from   src/lib/compiler/front/typer-stuff/types/type-junk.pkg
    package uj  =  unparse_junk;		# unparse_junk			is from   src/lib/compiler/front/typer/print/unparse-junk.pkg
    package ut  =  unparse_type;		# unparse_type			is from   src/lib/compiler/front/typer/print/unparse-type.pkg
    package vac =  variables_and_constructors;	# variables_and_constructors	is from   src/lib/compiler/front/typer-stuff/deep-syntax/variables-and-constructors.pkg
    package vh  =  varhome;			# varhome			is from   src/lib/compiler/front/typer-stuff/basics/varhome.pkg

#   package id  =  inlining_data;		# inlining_data			is from   src/lib/compiler/front/typer-stuff/basics/inlining-data.pkg

    prettyprint_typoid      =  ppt::prettyprint_typoid;
    prettyprint_type        =  ppt::prettyprint_type;
    prettyprint_typescheme  =  ppt::prettyprint_typescheme;
herein 

    package   prettyprint_value
    : (weak)  Prettyprint_Value
    {
#	internals =   tc::internals;
internals =   log::internals;

	fun by f x y
            =
            f y x;

	pps = pp::string;


	fun prettyprint_varhome stream a
            =
            pps stream (   " ["
                       +   (vh::print_varhome a)
                       +   "]"
                       );

	fun prettyprint_inlining_data inlining_info_to_string stream a
            =
            pps stream (" [" + (inlining_info_to_string a) + "]");

	fun prettyprint_constructor_representation stream representation
            =
            pp::string stream (vh::print_representation representation);

	fun prettyprint_csig stream csig
            =
            pp::string stream (vh::print_constructor_api csig);

	fun prettyprint_valcon stream
            =
            prettyprint_d
	    where
	        fun prettyprint_d ( tdt::VALCON { name, form => vh::EXCEPTION acc, ... } )
			=>
			{   uj::unparse_symbol  stream  name;
			    #
			    if *internals     prettyprint_varhome  stream  acc;     fi;
			};

		    prettyprint_d (tdt::VALCON { name, ... } )
			=>
			uj::unparse_symbol  stream  name;
                end;
	    end;

	fun prettyprint_debug_valcon stream symbolmapstack (tdt::VALCON { name, form, is_constant, typoid, signature, is_lazy } )
            =
	    {   (uj::en_pp  stream) ->   { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, break, ... };
		#
		unparse_symbol =  uj::unparse_symbol  stream;
	    
                begin_horizontal_else_vertical_box 3;
		pps "VALCON";
		break { spaces=>0, indent_on_wrap=>0 };
		pps "{ name = "; unparse_symbol name;					uj::unparse_comma_newline stream;
		pps "is_constant = "; pps (bool::to_string is_constant);		uj::unparse_comma_newline stream;
		pps "typoid = "; prettyprint_typoid  symbolmapstack  stream  typoid;	uj::unparse_comma_newline stream;
		pps "is_lazy = "; pps (bool::to_string is_lazy);			uj::unparse_comma_newline stream;

		pps "pick_valcon_form =";
		prettyprint_constructor_representation
		    stream
		    form;
                                                                                     uj::unparse_comma_newline stream;
		pps "signature = [";  prettyprint_csig stream signature;  pps "] }";
		end_box ();
	    };

	fun prettyprint_constructor stream symbolmapstack (tdt::VALCON { name, form, is_constant, typoid, signature, is_lazy } )
            =
	    {   (uj::en_pp stream) ->   { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, break, ... };
		#
		unparse_symbol =  uj::unparse_symbol  stream;
	    
                begin_horizontal_else_vertical_box 3;
		unparse_symbol name;
		pps " : ";
                prettyprint_typoid  symbolmapstack  stream  typoid;
		end_box ();
	    };

	fun prettyprint_sumtype
              (
		symbolmapstack: syx::Symbolmapstack,
		tdt::VALCON { name, typoid, ... }
              )
              stream
            =
	    {   (uj::en_pp  stream) ->   { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, ... };
		#	    
                begin_wrap_box 0;
		uj::unparse_symbol stream name;   pps " : ";
                prettyprint_typoid  symbolmapstack  stream  typoid;
		end_box ();
	    };

# Is this ever used?
	fun prettyprint_constructor_naming stream
            =
            prettyprint_constructor
            where

	        (uj::en_pp  stream) ->   { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, ... };

		fun prettyprint_constructor (tdt::VALCON { name, typoid, form=>vh::EXCEPTION _, ... }, symbolmapstack)
			=>
			{   begin_horizontal_else_vertical_box 0;
			    pps "exception ";
                            uj::unparse_symbol  stream  name; 

			    if (mtt::is_arrow_type  typoid)
				#
				pps " "; 
				prettyprint_typoid  symbolmapstack  stream  (mtt::domain  typoid);
			    fi;

			    pps ";"; 

			    end_box();
			};

		    prettyprint_constructor (con, symbolmapstack)
			=> 
			{   exception HIDDEN;
			    #
			    visible_valcon_type
				=
				{   type =   tys::sumtype_to_type   con;

				    (   type_junk::type_equality (
					    fis::find_type_via_symbol_path
					    (    symbolmapstack,
						 syp::SYMBOL_PATH
						 [ ip::last (type_junk::namepath_of_type type) ],
						 fn _ = raise exception HIDDEN
					    ),
					    type
					)
					except
                                            HIDDEN = FALSE
				    );
				};

			    if (*internals
                                or
				not visible_valcon_type
			    )
				begin_horizontal_else_vertical_box 0;
				pps "con ";
				prettyprint_sumtype (symbolmapstack, con) stream;
				pps ";";
				end_box ();
                            fi;
			};
                end;
	    end;

	fun prettyprint_var stream (vac::ORDINARY_VARIABLE { varhome, path, ... } )
		=>
		{   pps stream (syp::to_string path);
		    #
		    if *internals
			 prettyprint_varhome stream varhome;
		    fi;
		};

	    prettyprint_var stream (vac::OVERLOADED_IDENTIFIER { name, ... } )
		=>
		uj::unparse_symbol stream (name);

	    prettyprint_var stream (errorvar)
		=>
		pp::string stream "<errorvar>";
        end;

	fun prettyprint_debug_var inlining_info_to_string stream symbolmapstack
            = 
	    {   (uj::en_pp stream) ->   { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, ... };
		#
		prettyprint_varhome = prettyprint_varhome stream;
		prettyprint_inlining_data   = prettyprint_inlining_data inlining_info_to_string stream;

		fun prettyprintdebugvar (vac::ORDINARY_VARIABLE { varhome, path, var_type, inlining_data } )
			=> 
			{   begin_horizontal_else_vertical_box 0;
			    pps "vac::ORDINARY_VARIABLE";
			    begin_horizontal_else_vertical_box 3;
			    pps "( { varhome => ";   prettyprint_varhome varhome;                   uj::unparse_comma_newline stream;
			    pps "inlining_data => "; prettyprint_inlining_data inlining_data;        uj::unparse_comma_newline stream;
			    pps "path=";       pps (syp::to_string path);             uj::unparse_comma_newline stream;
			    pps "var_type => REF "; prettyprint_typoid  symbolmapstack  stream  *var_type; 
			    pps "} )";
			    end_box();
			    end_box();
			};

		    prettyprintdebugvar (vac::OVERLOADED_IDENTIFIER { name, alternatives, typescheme } )
			=> 
			{   begin_horizontal_else_vertical_box 0;
			    pps "vac::OVERLOADED_IDENTIFIER";
			    begin_horizontal_else_vertical_box 3;
			    pps "( { name="; uj::unparse_symbol stream (name);		uj::unparse_comma_newline stream;
			    pps "alternative=["; 
			    (uj::ppvseq stream 0 ", "
			      (fn stream = fn { indicator, variant }
                                  =
				  {   pps "{ indicator=";  prettyprint_typoid  symbolmapstack  stream   indicator; 
				      uj::unparse_comma_newline stream;
				      pps " variant =";
				      prettyprint_debug_var inlining_info_to_string stream symbolmapstack variant; pps "}";
				  }
                              )
			      *alternatives);
			    pps "]"; uj::unparse_comma_newline stream;
			    pps "typescheme=";   prettyprint_typescheme  symbolmapstack  stream  typescheme;   pps "} )";
			    end_box();
			    end_box();
			};

		    prettyprintdebugvar (errorvar) => pps "<ERRORvar>";
                end;
	    
                prettyprintdebugvar;
	    };

	fun prettyprint_variable stream
            =
            prettyprint_variable'
            where
	        (uj::en_pp stream) ->   { begin_horizontal_else_vertical_box, begin_wrap_box, end_box, pps, ... };
		#
		fun prettyprint_variable'
                        (
                          symbolmapstack: syx::Symbolmapstack,
			  vac::ORDINARY_VARIABLE { path, varhome, var_type, inlining_data }
			)
			=> 
			{   begin_horizontal_else_vertical_box 0;
			    pps "vac::ORDINARY_VARIABLE { path => ";
			    pps (syp::to_string path);

			    if *internals
			        pps ", varhome => ";
                                prettyprint_varhome stream  varhome;
			    fi;

			    pps ", inlining_data "; # prettyprint_inlining_data inlining_data;        uj::unparse_comma_newline stream;

			    pps ", var_type => ";
                            prettyprint_typoid  symbolmapstack  stream  *var_type;

			    pps "}";
			    end_box ();
			};

		    prettyprint_variable'
                        (
                          symbolmapstack,
                          vac::OVERLOADED_IDENTIFIER { name, alternatives=>REF alternatives, typescheme=>tdt::TYPESCHEME { body, ... } }
                        )
			=>
			{   begin_horizontal_else_vertical_box 0;

			    pps "vac::OVERLOADED_IDENTIFIER { name => ";
			    uj::unparse_symbol stream name;

                            pps ", typescheme => tdt::TYPESCHEME { body => ";
                            prettyprint_typoid  symbolmapstack  stream  body; 

			    pps ", ... }, alternatives => REF ";
			    uj::unparse_sequence
                                stream
                                  { separator  =>  by pp::break { spaces=>1, indent_on_wrap=>0 },
				    print_one  =>  (fn stream = fn { variant, ... } = prettyprint_variable' (symbolmapstack, variant)),
				    breakstyle =>  uj::HORIZONTAL_ELSE_VERTICAL
				  }
			        alternatives;
			    pps " }";
			    end_box();
		        };

		    prettyprint_variable' (_, errorvar)
                        =>
                        pps "<ERRORvar>;";
                end;
	    end;
    };								# package prettyprint_value 
end;								# stipulate











##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Copyright 1996 by AT&T Bell Laboratories 
## Copyright 2003 by The SML/NJ Fellowship 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
