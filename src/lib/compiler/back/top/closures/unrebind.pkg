## unrebind.pkg 

# Compiled by:
#     src/lib/compiler/core.sublib

# **************************************************************************
#                                                                          *
# "Alpha conversion": the closure converter introduces duplicate namings  *
# at function arguments (the free variables of known functions) and at     *
# SELECT's and OFFSET's from closures.  This function restores unique      *
# namings, and also eliminates OFFSET's of 0 (which are introduced as     *
# a side effect of trying to improve lazy display).  It assumes that a     *
# FIX has no free variables.                                               *
#                                                                          *
# **************************************************************************



###                         "Never try and teach a pig to sing:
###                          it's a waste of time,
###                          and it annoys the pig."
###
###                                      -- Robert A. Heinlein
###                                         Time Enough for Love



stipulate
    package fcf =  fatecode_form;		# fatecode_form		is from   src/lib/compiler/back/top/fatecode/fatecode-form.pkg
herein

    api Un_Rebind {
	#
	unrebind:  fcf::Function  ->  fcf::Function;
    };
end;



stipulate 
    package fcf =  fatecode_form;		# fatecode_form		is from   src/lib/compiler/back/top/fatecode/fatecode-form.pkg
    package hcv =  highcode_var;		# highcode_var		is from   src/lib/compiler/back/top/highcode/highcode-var.pkg
herein

    package   un_rebind
    : (weak)  Un_Rebind				# Un_Rebind		is from   src/lib/compiler/back/top/closures/unrebind.pkg
    {

	fun bug s
            =
            error_message::impossible ("UnRebind: " + s);


	fun unrebind (fk, v, args, cl, ce)
            =
	    {   fun rename rebind (fcf::VAR v)
			  =>
			  f rebind
                          where
			      fun f NIL
                                      =>
                                      fcf::VAR v;

				  f ((w: Int, v') ! t)
				      =>
				      v == w   ??   v'
				               ::   f t;
                              end;
			  end;

		    rename _ x
			=>
			x;
                end;


		fun f (kind, l, args, cl, b)
                    =
		    {   my (args', rebind')
                            = 
			    fold_right
                                (   fn (v, (args', rebind'))
                                       =
				       { v' = hcv::copy_highcode_variable v;
				       
                                           (   v' ! args',
                                               (v, fcf::VAR v') ! rebind'
                                           );
				       }
                                )

				(NIL, NIL)

                                args;
		    
                        (   kind,
                            l,
                            args',
                            cl,
                            g rebind' b
                        );
		    }

		also
		fun g (rebind: List( (fcf::Variable, fcf::Value) ) )
                    =
                    h
                    where
		        rename = rename rebind;

			recursive val h
                            =
			    fn fcf::DEFINE_RECORD { kind, fields, name, fate }
				   =>
				   {   name' = hcv::copy_highcode_variable  name;

				       fcf::DEFINE_RECORD
				         {	
					   kind,
					   fields =>  map (fn (v, p) = (rename v, p)) fields,
					   name   =>  name',
					   fate   =>  g  ((name, fcf::VAR name') ! rebind)   fate
				         };
				   };

			      fcf::GET_ADDRESS_OF_FIELD_I { i => 0, record, name, fate } =>   g ((name, rename record) ! rebind) fate;
			      fcf::GET_ADDRESS_OF_FIELD_I { i,      record, name, fate } =>   bug "unexpected non-zero FIELD_POINTER";
	     /*
				 let w' = hcv::copy_highcode_variable w
				  in fcf::GET_ADDRESS_OF_FIELD_I (i, rename v, w', g ((w, fcf::VAR w') ! rebind) e)
				 end
	     */
			      fcf::GET_FIELD_I { i, record, name, type, fate }
				  =>
				  {   name' =  hcv::copy_highcode_variable  name;

				      fcf::GET_FIELD_I {
					  i,
					  record =>  rename record,
					  name   =>  name',
					  type,
					  fate   =>  g ((name, fcf::VAR name') ! rebind) fate
				      };
				  };

			      fcf::TAIL_CALL   { func, args }    =>  fcf::TAIL_CALL {   func => rename func,   args => map rename args  };
			      fcf::DEFINE_FUNS { funs, fate }    =>  fcf::DEFINE_FUNS {  funs => map f funs,  fate => h fate  };

			      fcf::JUMPTABLE { i, xvar, fates }	 =>  fcf::JUMPTABLE { i => rename i, xvar, fates => map h fates };
			      fcf::IF_THEN_ELSE { op, args, xvar, thenfate, elsefate }     =>  fcf::IF_THEN_ELSE { op, args => map rename args, xvar, thenfate => h thenfate, elsefate => h elsefate };

			      fcf::STORE_TO_RAM   { op, args,             fate } =>  fcf::STORE_TO_RAM   { op, args => map rename args,             fate => h fate };
			      fcf::FETCH_FROM_RAM { op, args, name, type, fate } =>  fcf::FETCH_FROM_RAM { op, args => map rename args, name, type, fate => h fate };

			      fcf::MATH { op, args, name, type, fate }         =>  fcf::MATH { op,  args => map rename args,  name, type,  fate => h fate  };
			      fcf::PURE { op, args, name, type, fate }         =>  fcf::PURE { op,  args => map rename args,  name, type,  fate => h fate  };

			      fcf::RAW_C_CALL { kind, cfun_name, cfun_type, args, results, fate }
				  =>
				  fcf::RAW_C_CALL { kind, cfun_name, cfun_type,  args => map rename args,  results,  fate => h fate  };
			end;
		    
		    end;		# fun g

	    
                (fk, v, args, cl, g NIL ce);

	    };  # fun unrebind 
    };		# package un_rebind 
end;		# stipulate




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## Copyright 1996 by Bell Laboratories 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
