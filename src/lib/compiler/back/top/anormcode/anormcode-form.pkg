## anormcode-form.pkg 
#
# "A-Normal" intermediate code form.
# See extensive comments in src/lib/compiler/back/top/anormcode/anormcode-form.api

# Compiled by:
#     src/lib/compiler/core.sublib

###           "See, commas and semis and braces -- that's
###            not what a compiler *is*.
###            That's what a compiler *needs*.
###            What a compiler *is* -- is freedom!"
###
###                      -- Hacker Jack Sparrow.


stipulate
    package hbo =  highcode_baseops;				# highcode_baseops		is from   src/lib/compiler/back/top/highcode/highcode-baseops.pkg
    package hct =  highcode_type;				# highcode_type			is from   src/lib/compiler/back/top/highcode/highcode-type.pkg
    package tmp =  highcode_codetemp;				# highcode_codetemp		is from   src/lib/compiler/back/top/highcode/highcode-codetemp.pkg
    package hut =  highcode_uniq_types;				# highcode_uniq_types		is from   src/lib/compiler/back/top/highcode/highcode-uniq-types.pkg
    package sy  =  symbol;					# symbol			is from   src/lib/compiler/front/basics/map/symbol.pkg
    package vh  =  varhome;					# Should go away soon 
herein 

    package  anormcode_form
    : (weak) Anormcode_Form					# Anormcode_Form		is from   src/lib/compiler/back/top/anormcode/anormcode-form.api
    {



	# What kind of inlining behavior
        # is desired for the function?
	#
	Inlining_Hint
	  = INLINE_IF_SIZE_SAFE					# Only if trivially size-safe.
	  | INLINE_WHENEVER_POSSIBLE				# Inline whenever possible.
	  | INLINE_ONCE_WITHIN_ITSELF				# Only inline once within itself.
	  | INLINE_MAYBE  (Int, List(Int))			# Call-site dependent inlining:     #1 < sum (map2 (fn (a, w) => (known a) * w) (actuals, #2)
	  ;

	# What kind of recursive function
        # (aka loop) is this?
        #
	Loop_Kind
	  = OTHER_LOOP						#  something else 
	  | PREHEADER_WRAPPED_LOOP				#  loop wrapped in a preheader 
	  | TAIL_RECURSIVE_LOOP					#  properly tail-recursive 
	  ;

	Call_As
	  = CALL_AS_GENERIC_PACKAGE					# It is a generic package.
	  | CALL_AS_FUNCTION  hut::Calling_Convention				# It is a function.
	  ;

	Function_Notes
	  =
	  { inlining_hint:	Inlining_Hint,						# When should it be inlined?
	    private:	Bool,							# Are all the call sites known?
	    call_as:		Call_As,						# Calling convention: function vs generic package.
	    loop_info:		Null_Or( (List( hut::Uniqtype ), Loop_Kind))	# Is it recursive?
	  };

	Typefun_Notes
	  =
	  { inlining_hint:   Inlining_Hint
	  };

	# Classifying various kinds of records:
        #
	Record_Kind
	  = RK_VECTOR  hut::Uniqtyp				# All elements have same type. 
	  | RK_PACKAGE						# Package -- elements may be typeagnostic.
	  | RK_TUPLE  hut::Useless_Recordflag			# All elements are typelocked. 
	  ;


	# Dcon records the name of the constructor (for debugging),
	# the corresponding datatypeConstructorRepresentation,
	# and the highcode type hct::Uniqtype (which must be an arrow type). 
	#
	Dcon
	    =
            ( sy::Symbol,
              vh::Valcon_Form,
              hut::Uniqtype
            );



	# Case constants used to specify all possible switching statements.
	# Efficient switch generation can be applied to VAL_CASETAG and INT_CASETAG.
	# Otherwise, the switch is just a shorthand for a binary search tree.
	# Some of these instances  such as FLOAT64_CASETAG and VLEN_CASETAG will go away soon.	# XXX BUGGO FIXME
	#
	Casetag 										# Constant in a 'case' rule lefthandside.
	    = VAL_CASETAG   (Dcon, List( hut::Uniqtyp ), tmp::Codetemp)
	    | INT_CASETAG    Int								# Should use InfInf::int XXX BUGGO FIXME
	    | INT32_CASETAG  int32::Int 
	    | UNT_CASETAG    Unt 
	    | UNT32_CASETAG  unt32::Unt 
	    | FLOAT64_CASETAG   String 
	    | STRING_CASETAG String 
	    | VLEN_CASETAG   Int; 

	# Simple values, including
        # variables and static constants:
	#
	Value
	    = VAR     tmp::Codetemp
	    | INT     Int									# Should use integer::Int XXX BUGGO FIXME
	    | INT32   int32::Int
	    | UNT     Unt
	    | UNT32   unt32::Unt
	    | FLOAT64 String
	    | STRING  String;

	Expression
	    = RET  List( Value )
	    | LET  (List( tmp::Codetemp ), Expression, Expression)

	    | MUTUALLY_RECURSIVE_FNS (List( Function ), Expression) 
	    | APPLY  (Value, List( Value ))

	    | TYPEFUN   (Typefun, Expression)
	    | APPLY_TYPEFUN  (Value, List( hut::Uniqtyp ))

	    | SWITCH      (Value, vh::Valcon_Signature, List( (Casetag, Expression) ), Null_Or(Expression))
	    | CONSTRUCTOR (Dcon, List( hut::Uniqtyp ), Value, tmp::Codetemp, Expression)  

	    | RECORD    (Record_Kind, List(Value), tmp::Codetemp, Expression)
	    | GET_FIELD (Value, Int, tmp::Codetemp, Expression)

	    | RAISE   (Value, List( hut::Uniqtype ))
	    | EXCEPT  (Expression, Value)

	    | BRANCH  (Baseop, List( Value ), Expression, Expression)
	    | BASEOP  (Baseop, List( Value ), tmp::Codetemp , Expression)

	withtype
        Function
            =
            ( Function_Notes,
	      tmp::Codetemp,
	      List ( (tmp::Codetemp, hut::Uniqtype) ),					# Our args are values, so our parameters have types.
	      Expression
            )

	also
        Typefun
            =
            ( Typefun_Notes,
	      tmp::Codetemp,
	      List ( (tmp::Codetemp, hut::Uniqkind) ),					# Our args are types, so our parameters have kinds.
	      Expression
            )

	also
        Dictionary
            =
            { default: tmp::Codetemp,
	      table:  List( (List( hut::Uniqtyp ), tmp::Codetemp) )
	    }

	also
        Baseop
	    =
            ( Null_Or ( Dictionary ),
	      hbo::Baseop,
	      hut::Uniqtype,
	      List( hut::Uniqtyp )
            );
		 #  Invariant: baseop's hct::Uniqtype is always fully closed 


    };											# package anormcode
end;											# stipulate


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1997 YALE FLINT PROJECT 
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
