## nextcode-function-stack.api --- nextcode functions awaiting compilation.

# Compiled by:
#     src/lib/compiler/core.sublib

stipulate
    package fcf =  nextcode_form;							# nextcode_form		is from   src/lib/compiler/back/top/nextcode/nextcode-form.pkg
    package lbl =  codelabel;								# codelabel		is from   src/lib/compiler/back/low/code/codelabel.pkg
herein

    api Nextcode_Function_Stack {
	#
	package tcf:  Treecode_Form;							# Treecode_Form		is from   src/lib/compiler/back/low/treecode/treecode-form.api

	Function_Form
	  #
	  = NEXTCODE_FORM
	      ( fcf::Variable,								# fun_id
		List( fcf::Variable ),							# fun_parameters
		List( fcf::Type ),							# fun_parameter_types
		fcf::Instruction								# fun_body
	      )
	  #
	  | TREECODE_FORM  List( tcf::Expression )
          ;

	Callers_Info
	  #
	  = PRIVATE                            Ref( Function_Form )			# A fun is 'private' if all callers are known -- this allows the optimizer to customize the calling register protocol.
	  | PRIVATE_AND_NEEDS_HEAPLIMIT_CHECK  Ref( Function_Form )
	  #
	  | PUBLIC    { func:      Ref(  Null_Or(  fcf::Function ) ), 			# A fun is 'public' if it may have unknown callers -- if it is externally visible via the api or passed around as a value.
			fml_typs:  List( fcf::Type )
		      }
          ;


	push_nextcode_function
	    :
	    (fcf::Function, lbl::Codelabel)
	    ->
	    Callers_Info;

	pop_function:  Void ->  Null_Or( (lbl::Codelabel, Callers_Info) );

	push_function:  (lbl::Codelabel, Callers_Info) -> Void;
    };
end;





## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
