## fatecode-function-stack-g.pkg --- code and data fragments that need to be compiled.
#
# Decompose a compilation unit into clusters:
#
# This package is used (only) in:
#
#     src/lib/compiler/back/low/main/main/translate-fatecode-to-treecode-g.pkg

# Compiled by:
#     src/lib/compiler/core.sublib



###                 "I remember one occasion when I tried to add a little
###                  seasoning to a review, but I wasn't allowed to.
###                  The paper was by Dorothy Maharam, and it was a
###                  perfectly sound contribution to abstract measure theory.
###                  The domains of the underlying measures were not sets but
###                  elements of more general Boolean algebras, and their range
###                  consisted not of positive numbers but of certain abstract
###                  equivalence classes. My proposed first sentence was:
###
###                    "The author discusses valueless measures in pointless spaces."
###
###                     In: I want to be a Mathematician, Washington: MAA Spectrum, 1985, p. 120.




stipulate
    package fcf =  fatecode_form;						# fatecode_form			is from   src/lib/compiler/back/top/fatecode/fatecode-form.pkg
    package lbl =  codelabel;							# codelabel			is from   src/lib/compiler/back/low/code/codelabel.pkg
herein

    # This generic is invoked (only) from:
    #
    #     src/lib/compiler/back/low/main/main/translate-fatecode-to-treecode-g.pkg
    #
    generic package   fatecode_function_stack_g   (
	#             =========================
	#
	tcf:	Treecode_Form							# Treecode_Form			is from   src/lib/compiler/back/low/treecode/treecode-form.api
    )
    : (weak)  Fatecode_Function_Stack						# Fatecode_Function_Stack	is from   src/lib/compiler/back/low/main/fatecode/fatecode-function-stack.api
    {
	# Export to client packages:
	#
	package tcf =  tcf;							# "tcf" == "treecode_form".

	# This is state used in
	#
	#     src/lib/compiler/back/low/main/main/translate-fatecode-to-treecode-g.pkg
	#
	# to distinguish between functions still in fatecode form
	# and functions which have been converted to treecode form:
	#
	Function_Form
	  #
	  = TREECODE_FORM  List( tcf::Expression )
	  #
	  | FATECODE_FORM
	      ( fcf::Variable,							# Function id.
		List( fcf::Variable ),						# Function parameters.
		List( fcf::Type ),						# Types of function parameters.
		fcf::Expression							# Function body.
	      )
	  ;

	Function
	  #
	  = PRIVATE                            Ref( Function_Form )
	  | PRIVATE_AND_NEEDS_HEAPLIMIT_CHECK  Ref( Function_Form )
	  #
	  | PUBLIC   { func:       Ref(  Null_Or(  fcf::Function ) ), 
		       fml_typs:   List( fcf::Type )				# "fml_typs" may be cryptic for "formal types", maybe?
		     }
	  ;

	fun error msg
	    =
	    error_message::impossible ("Function." + msg);

	functions = REF ([]: List( (lbl::Codelabel, Function) ) );		# XXX BUGGO FIXME Icky thread-hostile mutable global variable.

	fun pop_function ()
	    = 
	    case *functions
		#
		function ! rest =>  THE function before (functions := rest);
		[]              =>  NULL;
	    esac;

	fun push_function  lf
	    =
	    functions :=  lf ! *functions;

	# Make compilation functions for this cluster.
	# Note the icky side-effects:
	#
	fun push_fatecode_function (arg as (callers_info, fun_id, fun_parameters, fun_parameter_types, fun_body), codelabel)
	    =
	    function
	    where
		function = case callers_info
			       #	
			      (fcf::PUBLIC | fcf::FATE) =>  PUBLIC { func=>REF (THE arg), fml_typs=>fun_parameter_types };
			       #	
			       fcf::PRIVATE                               =>  PRIVATE                           (REF (FATECODE_FORM (fun_id, fun_parameters, fun_parameter_types, fun_body)));
			       fcf::PRIVATE_NEEDS_HEAPLIMIT_CHECK     =>  PRIVATE_AND_NEEDS_HEAPLIMIT_CHECK (REF (FATECODE_FORM (fun_id, fun_parameters, fun_parameter_types, fun_body)));
			       #	
			       _                                          =>  error "make_fragments";
			   esac;

		functions :=  (codelabel, function) ! *functions;
	    end;

    };			# fatecode_function_stack_g 
end;


## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2011,
## released under Gnu Public Licence version 3.
