## rw-queue.pkg
#
# See comments in    src/lib/src/rw-queue.api

# Compiled by:
#     src/lib/std/standard.lib

stipulate

herein

    package   rw_queue	
    : (weak)  Rw_Queue											# Rw_Queue		is from   src/lib/src/rw-queue.api
    {
	Rw_Queue(X)											# Simple queue using the usual trick of adding to the input list,
	    =												# removing from the output list, and when the output list is empty,
	    RW_QUEUE											# reversing the input list and making it the output list.  This is a
	      {												# nice simple algorithm where both push and pull are O(1).
		output_list:  Ref( List(X) ),
		input_list:   Ref( List(X) )
	      };

	fun reverse (x,       [], rl) =>  (x, rl);
	    reverse (x, y ! rest, rl) =>  reverse (y, rest, x ! rl);
	end;

	fun reverse_and_prepend (      [], l) =>  l;
	    reverse_and_prepend (x ! rest, l) =>  reverse_and_prepend (rest, x ! l);
	end;

	fun make_rw_queue ()
	    =
	    RW_QUEUE
	      {
		output_list =>  REF [],
		input_list  =>  REF []
	      };

	fun same_queue ( RW_QUEUE { output_list=> refcell1, ... },
			 RW_QUEUE { output_list=> refcell2, ... }
		       )
	    =
	    refcell1 == refcell2;									# Taking advantage of the fact that refcells are equal only to themselves and thus uniquely identify a queue.

	fun queue_is_empty (RW_QUEUE { output_list => REF [],
                                               input_list => REF []
                                            }
                           )
				=>   TRUE;
	    queue_is_empty _	=>   FALSE;
	end;


	fun push_item_onto_back_of_queue (RW_QUEUE { input_list, ... }, item)
	    =
	    input_list :=  item  !  *input_list;


	fun unpull_item_onto_front_of_queue (RW_QUEUE { output_list, ... }, item)			# We occasionally use this when a thread needs to run immediately.
	    =
	    output_list :=  item  !  *output_list;


	fun pull_item_off_front_of_queue_or_raise_exception (RW_QUEUE { output_list, input_list } )
	    =
	    case *output_list
		#
		(x ! rest)
		    =>
		    {   output_list := rest;
			x;
		    };

		[] =>   case *input_list
			    #
			    (x ! rest)
				=>
				{   (reverse (x, rest, [])) ->   (y, rr);
				    #	
				    output_list := rr;
				    input_list  := [];
				    y;
				    #	
				};

			    [] =>  raise exception  FAIL "queue is empty";
			esac;

	    esac;


	fun pull_item_off_front_of_queue (RW_QUEUE { output_list, input_list } )			# Normal case.
	    =
	    case *output_list
		#
		(x ! rest) =>   {   output_list :=  rest;
				    #
				    THE x;
				};

		[]  =>  	case *input_list
				    #
				    (x ! rest) =>   {   (reverse (x, rest, []))
							    ->
							    (y, rr);

							output_list := rr;
							input_list  := [];
							# 
							THE y;
						    };

				    []         =>   NULL;
				esac;
	    esac;


	fun unpush_item_off_back_of_queue (RW_QUEUE { output_list, input_list } )				# Abnormal case included only for completeness -- currently unused.  -- 2012-03-28 CrT
	    =												# This is the exact reverse of the above fn.
	    case *input_list
		#
		(x ! rest) =>   {   input_list :=  rest;
				    #
				    THE x;
				};

		[]  =>		case *output_list
				    #
				    (x ! rest) =>   {   (reverse (x, rest, []))
							    ->
							    (y, rr);

							input_list  := rr;
							output_list := [];
							# 
							THE y;
						    };

				    []         =>   NULL;
				esac;
	    esac;


	fun clear_queue_to_empty (RW_QUEUE { output_list, input_list } )
	    =
	    {   output_list := [];
		input_list  := [];
	    };

	fun find_first_matching_item_and_remove_from_queue
	      (
		RW_QUEUE { output_list, input_list },
		predicate
	      )
	    =
	    remove_from_output_list (*output_list, [])
	    where
		fun remove_from_output_list ([], l)
			=>
			remove_from_input_list (*input_list, []);

		    remove_from_output_list (x ! rest, l)
			=>
			if (predicate x)	{   output_list := reverse_and_prepend (l, rest);
						    THE x;
						};	
			else			remove_from_output_list (rest, x ! l);
			fi;
		end 

		also
		fun remove_from_input_list ([], _) =>   NULL;
		    #
		    remove_from_input_list (x ! rest, l)
			=>
			if (predicate x)	{   input_list := reverse_and_prepend (l, rest);
						    THE x;
						};
			else			remove_from_input_list (rest, x ! l);
			fi;
		end;
	    end;

	# Some synonyms:
	#
	push = push_item_onto_back_of_queue;
	pull = pull_item_off_front_of_queue;
	#
	unpull = unpull_item_onto_front_of_queue;
	unpush = unpush_item_off_back_of_queue;
    };
end;


##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1989-1991 John H. Reppy
## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
