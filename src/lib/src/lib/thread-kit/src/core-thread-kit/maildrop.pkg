## maildrop.pkg
#
# Maildrops are essentially concurrency-safe replacements for REF cells.

# Compiled by:
#     src/lib/std/standard.lib






###          "We're fools whether we dance or not,
###           so we might as well dance."
###
###                   -- Japanese proverb



stipulate
    package fat =  fate;					# fate				is from   src/lib/std/src/nj/fate.pkg
    package itt =  internal_threadkit_types;			# internal_threadkit_types	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/internal-threadkit-types.pkg
    package rwq =  rw_queue;					# rw_queue			is from   src/lib/src/rw-queue.pkg
    package ts  =  thread_scheduler;				# thread_scheduler		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg
    #
    call_with_current_fate =  fat::call_with_current_fate;
    switch_to_fate         =  fat::switch_to_fate;
herein

    package   maildrop
    :         Maildrop						# Maildrop			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/maildrop.api
    {
	exception MAY_NOT_FILL_ALREADY_FULL_MAILDROP;
	#
	Fate(X) =   fat::Fate(X);
	#
	Maildrop(X) =   MAILDROP
			  {
			    priority:  Ref( Int ),
			    read_q:    rwq::Rw_Queue( (Ref( itt::Selectrun_Status ), Fate(X)) ),
			    value:     Ref(  Null_Or(X) )
			  };


	fun make_cell ()
	    =
	    MAILDROP  { priority =>  REF 0,
			value    =>  REF NULL,
			read_q   =>  rwq::make_rw_queue ()
		      };

	fun same_cell ( MAILDROP { value => v1, ... },
			MAILDROP { value => v2, ... }
		      )
	    =
	    v1 == v2;


	fun make_selectrun_status ()
	    =
	    REF (itt::SELECTRUN_IS_BLOCKED (ts::get_current_thread()));


	fun mark_selectrun_complete_and_return_thread  (selectrun_status  as  REF  (itt::SELECTRUN_IS_BLOCKED  thread_id))
		=>
		{   selectrun_status :=   itt::SELECTRUN_IS_COMPLETE;
		    #
		    thread_id;
		};

	    mark_selectrun_complete_and_return_thread  (REF (itt::SELECTRUN_IS_COMPLETE))
		=>
		raise exception FAIL "Compiler bug:  Attempt to cancel already-cancelled transaction-id";			# Never happens; here to suppress 'nonexhaustive match' compile warning.
	end;


	fun bump_priority (p as REF n)										# Bump a priority value by one, returning the old value.
	    =
	    {   p := n+1;
		n;
	    };


	Qy_Item(X)
	  #
	  = NO_ITEM
	  | ITEM  ((Ref itt::Selectrun_Status,  Fate(X)))
	  ;

	# Functions to clean channel input and output queues 
	#
	stipulate

	    fun clean ((REF itt::SELECTRUN_IS_COMPLETE, _) ! rest)				# Drop any cancelled transactions at start of list.
		    =>
		    clean rest;

		clean l  =>  l;
	    end;

	    fun clean_rev ([], result)								# Drop all cancelled transactions from list; result is in reverse order.
		    =>
		    result;

		clean_rev ((REF itt::SELECTRUN_IS_COMPLETE, _) ! rest,  result)
		    =>
		    clean_rev (rest, result);

		clean_rev (x ! rest,  result)
		    =>
		    clean_rev (rest, x ! result);
	    end;

	herein

	    fun clean_and_check (priority, rwq::RW_QUEUE { front, back } )
		=
		clean_front *front
		where
		    fun clean_front []
			    =>
			    clean_back *back;

			clean_front f
			    =>
			    case (clean f)
				#
				[] =>   clean_back  *back;

				f' =>   {   front :=  f';
					    #
					    bump_priority  priority;
				        };
			    esac;
		    end

		    also
		    fun clean_back []
			    =>
			    0;

			clean_back r
			    =>
			    {   back :=  [];
				#
				case (clean_rev (r, []))
				    #
				    [] =>   0;

				    rr =>   {   front :=  rr;
						#
						bump_priority  priority;
					    };
				esac;
			    };
		    end;
		end;


	    fun clean_and_remove (rwq::RW_QUEUE { front, back, ... } )
		=
		clean_front *front
		where
		    fun clean_front [] =>   clean_back  *back;
			#
			clean_front f
			    =>
			    case (clean f)
				#
				[]            =>    clean_back  *back;

				(item ! rest) =>    {   front :=  rest;
							#
							ITEM  item;
						    };
			    esac;
		    end

		    also
		    fun clean_back [] =>   NO_ITEM;
			#
			clean_back r
			    =>
			    {   back :=  [];
				#
				case (clean_rev (r, []))
				    #
				    [] => NO_ITEM;

				    item ! rest
					=>
					{   front :=  rest;
					    #
					    ITEM item;
					};
				esac;
			    };
		    end;
		end;

	    fun clean_and_enqueue (rwq::RW_QUEUE { front, back, ... }, item)
		=
		clean_front  *front
		where
		    fun clean_front [] =>   clean_back  *back;
			#
			clean_front f
			    =>
			    case (clean f)
				#
				[] =>   clean_back *back;

				f' =>   {   front := f';
					    #
					    back  := item ! *back;
				        };
			    esac;
		    end

		    also
		    fun clean_back []
			    =>
			    front := [item];

			clean_back r
			    =>
			    case (clean_rev (r, []))
				#
				[] => {  front := [item];  back  := []; };
				rr => {  back  := [item];  front := rr; };
			    esac;
		    end;
		end;
	end;					# stipulate


	# When a thread is resumed after being blocked
	# on a maildrop or oneshot_maildrop op there may
	# be other threads also blocked on the variable.
	#
	# This function is used to propagate the message
	# to all of the threads that are blocked on the
	# variable (or until one of them takes the value
	# in the mvar case).
	#
	# It must be called from an atomic region.
	# When the readQ is finally empty we leave
	# the atomic region.
	#
	# We must use "clean_and_remove" to get items
	# from the readQ in the unlikely event that
	# a single thread executes a choice of
	# multiple gets on the same variable.
	#
	fun relay_msg (read_q, msg)
	    =
	    case (clean_and_remove  read_q)
		#
		NO_ITEM =>   ts::reenable_thread_switching ();

		ITEM (selectrun_status, fate)
		    =>
		    call_with_current_fate
			(fn my_fate
			    =
			    {   ts::run_thread  (mark_selectrun_complete_and_return_thread  selectrun_status,   my_fate);
				#
				switch_to_fate  fate  msg;									# 
			    }
			);
	    esac;

	fun impossible ()
	    =
	    raise exception  FAIL "maildrop: impossible";


	# M-variables:
	#
	make_empty_maildrop
	    =
	    make_cell;


	fun make_full_maildrop  maildrop_value
	    =
	    MAILDROP { priority =>  REF 0,
		       read_q   =>  rwq::make_rw_queue (),
		       value    =>  REF (THE maildrop_value)
		     };

	same_maildrop =  same_cell;


	fun fill (MAILDROP { priority, read_q, value }, maildrop_value)
	    =
	    {   ts::disable_thread_switching ();
		#
		case *value
		    #
		    NULL =>
			{   value := THE maildrop_value;
			    #
			    case (clean_and_remove read_q)
				#
				NO_ITEM =>   ts::reenable_thread_switching ();

				ITEM (selectrun_status, fate)
				    =>
				    call_with_current_fate
					#
					(fn my_fate
					    =
					    {   ts::run_thread   (mark_selectrun_complete_and_return_thread  selectrun_status,   my_fate);
						#
						priority := 1;

						switch_to_fate  fate  maildrop_value;									# Resume empty' below.
					    }
					);
			    esac;
			};

		    THE _ =>
			{   ts::reenable_thread_switching ();
			    #
			    raise exception MAY_NOT_FILL_ALREADY_FULL_MAILDROP;
			};
		esac;
	    };


	fun empty' (MAILDROP { priority, read_q, value } )
	    =
	    itt::ISREADY_MAILOPS [is_mailop_ready_to_fire]
	    where
		fun start_watching_for_mailop_ready					# Reppy refers to 'start_watching_for_mailop_ready' as 'blockFn'.
		      {
			selectrun_status,						# 'select' is supposed to fire exactly one mailop: 'selectrun_status' is basically a mutex enforcing this.
			finish_selectrun,						# This typically does  selectrun_status := SELECTRUN_IS_COMPLETE;  and then sends nacks as appropriate.
			next
		      }
		    =
		    # This fn gets used in
		    #
		    #     src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
		    #
		    # when a
		    #
		    #     select [ ... ]
		    #
		    # call has no mailops ready to fire.  'select' must then block until
		    # at least one mailop is ready to fire.  It does this by calling the
		    #
		    #     start_watching_for_mailop_ready ()
		    #
		    # fn on each mailop in the list; each such call will typically
		    # make an entry in one or more queues of blocked threads.
		    #
		    # The first mailop to fire cancels the rest by setting
		    #
		    #     selectrun_status :=  SELECTRUN_IS_COMPLETE;
		    #
		    {
			(call_with_current_fate
			    (fn fate
				=
				{   rwq::push_item_into_back_of_queue (read_q, (selectrun_status, fate));
				    next ();
				    impossible ();								# next() should never return.
				}
			    )
			)
			    ->	maildrop_value;									# Execution will pick up here when fill() (above) eventually does:   switch_to_fate  fate  maildrop_value;

			finish_selectrun ();									# Do stuff like   selectrun_status := SELECTRUN_IS_COMPLETE;  and sending nacks as appropriate.
			value := NULL;										# Empty the maildrop.

			ts::reenable_thread_switching ();							# End uninterruptable scope.

			maildrop_value;										# Return value read from maildrop.
		    };

		fun is_mailop_ready_to_fire ()									# Reppy refers to 'is_mailop_ready_to_fire' as 'pollFn'
		    =
		    case *value
			#
			NULL  =>    itt::MAILOP_IS_NOT_READY_TO_FIRE  start_watching_for_mailop_ready;
			#
			THE v =>    itt::MAILOP_IS_READY_TO_FIRE
				      {
					priority    =>   bump_priority  priority,

					fire_mailop => .{   value := NULL;				# Reppy refers to 'fire_mailop' as 'doFn'.
							    ts::reenable_thread_switching ();
							    v;
							}
				      };
		    esac;
	    end;


	fun nonblocking_empty (MAILDROP { priority, read_q, value } )
	    =
	    {   ts::disable_thread_switching();
		#
		case *value
		    #
		    THE v =>    {   value := NULL;
				    ts::reenable_thread_switching ();
				    THE v;
				};

		    NULL =>     NULL;
		esac;
	    };


	fun empty (MAILDROP { priority, read_q, value } )
	    =
	    {   ts::disable_thread_switching();
		#
		case *value
		    #
		    NULL =>
			{   v = call_with_current_fate
				    (fn fate
					=
					{   rwq::push_item_into_back_of_queue (read_q, (make_selectrun_status(), fate));
					    #
					    ts::reenable_thread_switching_and_dispatch_next_thread ();
					}
				    );

			    value := NULL;
			    ts::reenable_thread_switching ();
			    v;
			};

		    THE v =>
			{   value := NULL;
			    ts::reenable_thread_switching ();
			    v;
			};
		esac;
	    };


	fun peek (MAILDROP { priority, read_q, value } )
	    =
	    {   ts::disable_thread_switching ();
		#
		case *value
		    #
		    NULL =>     {   v =  call_with_current_fate
					    (fn fate
						=
						{   rwq::push_item_into_back_of_queue (read_q, (make_selectrun_status(), fate));
						    #
						    ts::reenable_thread_switching_and_dispatch_next_thread ();
						}
					    );

				    relay_msg (read_q, v);

				    v;
				};

		    THE v =>    {   ts::reenable_thread_switching ();
				    v;
				};
		esac;
	    };


	fun peek' (MAILDROP { priority, read_q, value } )
	    =
	    {
		fun start_watching_for_mailop_ready					# Reppy refers to 'start_watching_for_mailop_ready' as 'blockFn'.
		      {
			selectrun_status,						# 'select' is supposed to fire exactly one mailop: 'selectrun_status' is basically a mutex enforcing this.
			finish_selectrun,						# Do any required end-of-selectrun work such as  selectrun_status := SELECTRUN_IS_COMPLETE;  and sending nacks as appropriate.
			next
		      }
		    =
		    # This fn gets used in
		    #
		    #     src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
		    #
		    # when a
		    #
		    #     select [ ... ]
		    #
		    # call has no mailops ready to fire.  'select' must then block until
		    # at least one mailop is ready to fire.  It does this by calling the
		    #
		    #     start_watching_for_mailop_ready ()
		    #
		    # fn on each mailop in the list; each such call will typically
		    # make an entry in one or more queues of blocked threads.
		    #
		    # The first mailop to fire cancels the rest by setting
		    #
		    #     selectrun_status :=  SELECTRUN_IS_COMPLETE;
		    #
		    {   v = call_with_current_fate
				(fn fate
				    =
				    {   rwq::push_item_into_back_of_queue (read_q, (selectrun_status, fate));
					next ();
					impossible ();
				    }
				);

			finish_selectrun();
			relay_msg (read_q, v);
			v;
		    };

		fun is_mailop_ready_to_fire ()									# Reppy refers to 'is_mailop_ready_to_fire' as 'pollFn'.
		    =
		    case *value
			#	        
			NULL  =>    itt::MAILOP_IS_NOT_READY_TO_FIRE  start_watching_for_mailop_ready;
			#
			THE v =>    itt::MAILOP_IS_READY_TO_FIRE
				      {
					priority    =>   bump_priority priority,
					#
					fire_mailop => .{   ts::reenable_thread_switching ();			# Reppy refers to 'fire_mailop' as 'doFn'.
							    v;
							}
				      };
		    esac;

		itt::ISREADY_MAILOPS [is_mailop_ready_to_fire];
	    };


	fun nonblocking_peek (MAILDROP { priority, read_q, value } )
	    =
	    {   ts::disable_thread_switching ();
		#
		case *value
		    #
		    THE v =>    {   ts::reenable_thread_switching ();
				    #
				    THE v;
				};

		    NULL =>     NULL;
		esac;
	    };


	# Swap the current contents of the cell with a new value.
	#
	# This function has the effect of an
	# get_mail followed by a put_mail,
	# except that it is guaranteed to be atomic.
	#
	# It is also somewhat more efficient.
	#
	fun swap (MAILDROP { priority, read_q, value }, new_v)
	    =
	    {   ts::disable_thread_switching ();

		case *value
		    #
		    NULL =>     {   v = call_with_current_fate
					    (fn fate
						=
						{   rwq::push_item_into_back_of_queue (read_q, (make_selectrun_status(), fate));
						    #
						    ts::reenable_thread_switching_and_dispatch_next_thread ();
						}
					    );

				    value :=  THE new_v;

				    relay_msg  (read_q,  new_v);					# Relay the new value to any other blocked threads.

				    v;
				};

		    THE v =>    {   value := THE new_v;
				    ts::reenable_thread_switching ();
				    v;
				};
		esac;
	    };


	fun swap' (MAILDROP { priority, read_q, value }, new_maildrop_value)
	    =
	    itt::ISREADY_MAILOPS [ is_mailop_ready_to_fire ]
	    where
		fun start_watching_for_mailop_ready							# Reppy refers to 'start_watching_for_mailop_ready' as 'blockFn'.
		      {
			selectrun_status,								# 'select' is supposed to fire exactly one mailop: 'selectrun_status' is basically a mutex enforcing this.
			finish_selectrun,								# Do any required end-of-selectrun work such as  selectrun_status := SELECTRUN_IS_COMPLETE;  and sending nacks as appropriate.
			next
		      }
		    =
		    # This fn gets used in
		    #
		    #     src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
		    #
		    # when a
		    #
		    #     select [ ... ]
		    #
		    # call has no mailops ready to fire.  'select' must then block until
		    # at least one mailop is ready to fire.  It does this by calling the
		    #
		    #     start_watching_for_mailop_ready ()
		    #
		    # fn on each mailop in the list; each such call will typically
		    # make an entry in one or more queues of blocked threads.
		    #
		    # The first mailop to fire cancels the rest by setting
		    #
		    #     selectrun_status :=  SELECTRUN_IS_COMPLETE;
		    #
		    {   (call_with_current_fate
			    (fn fate
				=
				{   rwq::push_item_into_back_of_queue (read_q, (selectrun_status, fate));
				    next ();
				    impossible();
				}
			    )
			)
			    -> old_maildrop_value;

			finish_selectrun ();
			value := THE new_maildrop_value;
			relay_msg (read_q, new_maildrop_value);
			old_maildrop_value;
		    };

		fun is_mailop_ready_to_fire ()
		    =
		    case *value
			#
			NULL  =>    itt::MAILOP_IS_NOT_READY_TO_FIRE  start_watching_for_mailop_ready;

			THE v =>    itt::MAILOP_IS_READY_TO_FIRE
				      {
					priority    =>   bump_priority priority,
					#
					fire_mailop => .{   value := THE new_maildrop_value;				# Reppy refers to 'fire_mailop' as 'doFn'.
							    ts::reenable_thread_switching ();
							    v;
							}
				      };
		    esac;
	    end;

    };						# package maildrop 
end;

## COPYRIGHT (c) 1989-1991 John H. Reppy
## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


