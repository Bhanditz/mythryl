## internal-threadkit-types.pkg
#
# These are the concrete representations of the various threadkit types.
# These types are abstract (or not even visible) outside this library.

# Compiled by:
#     src/lib/std/standard.lib


###	       "The programmer who is not in love with lisp by age twenty has no spirit of adventure.
###		The programmer who is still in love with lisp by age thirty has no common sense."
###
###							-- Walt Filmore


stipulate
    package fat =  fate;					# fate		is from   src/lib/std/src/nj/fate.pkg
herein

    package internal_threadkit_types {

	# Thread IDs --- see
	#
	#     src/lib/src/lib/thread-kit/src/core-thread-kit/thread.pkg
	#
	Thread_State
	    =
	    THREAD_STATE
	      { thread_id:		Int,				# A unique ID. 
		name:			String,				# Purely for display to humans.
		didmail:		Ref( Bool ),			# Set this whenever this thread does some concurrency operation; thread-scheduler favors mail-performing threads.
		#
		exception_handler:	Ref( Exception -> Void ),	# Root-level exception handler hook.
		properties:	        Ref(  List(  Exception ) ),	# Holds thread-local properties.

		dead:			Condition_Variable		# Set when the thread dies. 
	      }

	# Select_Run_Status refcells are used to track blocked
	# threads in the various waiting queues.
	#
	# They are set to SELECT_RUN_IS_COMPLETE when they (or
	# some other competing) mailop is selected
	# -- i.e., when they are no longer a candidate
	# for execution.
	#
	also
	Select_Run_Status
	  #
	  = SELECT_RUN_IS_COMPLETE
	  | SELECT_RUN_IS_BLOCKED  Thread_State

	# Condition variables --- see
	#     src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
	# These are essentially Void-valued oneshot_maildrop instances,
	# and are used for various internal synchronization
	# conditions, e.g., nack mail_ops, I/O synchronization,
	# and thread termination:
	#
	also
	Condition_Variable
	    =
	    CONDITION_VARIABLE  Ref( Condvar_State )

	also
	Condvar_State									# "condvar" == "condition variable".
	  #
	  = CONDVAR_UNSET   List  { mailop_done:	Ref( Select_Run_Status ),		# Unset condvar together with the list of threads waiting for it to be set.
				    clean_up:		Void -> Void,
				    fate:		fate::Fate( Void )
				  }
	  | CONDVAR_VALUE   Int								# Condvar which has been set.  The int value is a priority counting the number of times that
	  ;										# the condvar has been waited on without firint -- see wait_on_condvar__mailop()   
											# in   src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg

	# When a mailop is not ready to fire, we call a function of this type
	# to put it to sleep (i.e., enter it into appropriate thread wait queues):
	#
	Unready_Mailop(X) =       { mailop_done:	Ref( Select_Run_Status ),	# This refcell is set to SELECT_RUN_IS_COMPLETE when the op is no longer a candidate to fire. (Either completed or aborted.)
				    clean_up:		Void -> Void,
				    next:		Void -> Void
				  }
				  ->
				  X;



	#################################################################
	# Mailops -- see
	#     src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg


	Mailop_Readiness(X)
	  #
	  = MAILOP_IS_READY_TO_FIRE							# MAILOP_IS_READY_TO_FIRE means this select[...] rule is ready to fire. (select[...] can fire at most one rule per invocation.)
	      {
		priority:	Int,
		fire_mailop:	Void -> X						# We fire a mailop by calling fire_mailop().	Reppy refers to 'fire_mailop' as 'doFn'.
	      }

	  | MAILOP_IS_NOT_READY_TO_FIRE  Unready_Mailop(X)				# MAILOP_IS_NOT_READY_TO_FIRE means this select[...] mailop is not ready to fire.
	  ;

	Isready_Mailop(X)								# When 'select' needs to know if a given select rule is ready to fire, it calls a fn of this type.
	    =
	    Void -> Mailop_Readiness(X);

	Mailop(X)
	  = ISREADY_MAILOPS	List( Isready_Mailop(X) )				# This is the meat-and-potatoes usual case.
	  | CHOOSE		List(           Mailop(X) )
	  | DYNAMIC_MAILOP	Void         -> Mailop(X)
	  | WITH_NACK		Mailop(Void) -> Mailop(X)
	  ;

	# Useful when debugging threadkit internals:
	#
	fun thread_to_string (THREAD_STATE { thread_id, ... } )
	    =
	    cat [ "[",
		   number_string::pad_left '0' 6 (int::to_string thread_id),
		  "]"
		];

    };
end;

## COPYRIGHT (c) 1989-1991 John H. Reppy
## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
