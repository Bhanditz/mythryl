## mailop.pkg
#
# Implementation of mailop values and the mailop combinators.
#
# Some important requirements on the implementation
# of base mailop values:
#
#  1)  An  is_mailop_ready_to_fire(), fire_mailop()
#      or start_watching_for_mailop_ready() fn
#      is always called from inside an uninterruptible_scope.			# "uninterruptible_scope" == "critical section" == "atomic region" == ...
#
#  2)  An is_mailop_ready_to_fire() returns an integer priority.
#      This is   0 when not enabled,
#	        -1 for fixed priority and
#               >0 for dynamic priority.
#      The standard scheme is to associate a counter
#      with the underlying synchronization value and
#      to increase it by one for each synchronization attempt.
#
#  3)  A start_watching_for_mailop_ready fn is responsible
#      for ending the uninterruptible scope.
#
#      A fire_mailop should NOT end the uninterruptible scope.
#
#  4)  Each start_watching_for_mailop_ready fn is responsible for executing
#      the "finish_selectrun" action prior to ending the uninterruptible scope.

# Compiled by:
#     src/lib/std/standard.lib





stipulate
    package fat =  fate;							# fate				is from   src/lib/std/src/nj/fate.pkg
    package itt =  internal_threadkit_types;					# internal_threadkit_types	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/internal-threadkit-types.pkg
    package rwq =  rw_queue;							# rw_queue			is from   src/lib/src/rw-queue.pkg
    package ts  =  thread_scheduler;						# thread_scheduler		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg
herein

    package mailop: (weak)
			    api {
				include Mailop;					# Mailop			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.api
				#
				set_condvar_in_uninterruptible_scope:   itt::Condition_Variable -> Void;
				wait_on_condvar':			itt::Condition_Variable -> Mailop( Void );	# Exported for use in   src/lib/src/lib/thread-kit/src/core-thread-kit/thread.pkg
			    }
    {
	call_with_current_control_fate	=  fat::call_with_current_control_fate;
	switch_to_control_fate		=  fat::switch_to_control_fate;
	call_with_current_fate		=  fat::call_with_current_fate;
#	switch_to_fate			=  fat::switch_to_fate;

	# Some inline functions
	# to improve performance:
	#
	fun map f
	    =
	    map'
	    where
		fun map' [] => [];
		    map' [a] => [f a];
		    map' [a, b] => [f a, f b];
		    map' [a, b, c] => [f a, f b, f c];
		    map' (a ! b ! c ! d ! r) => (f a) ! (f b) ! (f c) ! (f d) ! (map' r);
		end;
	    end;
	#
	fun apply f
	    =
	    apply'
	    where
		fun apply' [] => ();
		    apply' (x ! r) => { f x; apply' r;};
		end;
	    end;
	#
	fun fold_left f init l
	    =
	    foldf (l, init)
	    where
		fun foldf ([], accum) => accum;
		    foldf (x ! r, accum) => foldf (r, f (x, accum));
		end;
	    end;
	#
	fun error msg
	    =
	    raise exception FAIL msg;

	Mailop_Readiness   ==  itt::Mailop_Readiness;
	Mailop             ==  itt::Mailop;
	Isready_Mailop(X) =  itt::Isready_Mailop(X);


	# Condition variables.
	#
	# Because these variables are set inside
	# atomic regions we have to use different
	# conventions for clean-up, etc.  Instead
	# of requiring the start_watching_for_mailop_ready fate
	# to call the finish_selectrun fn and to end
	# the uninterruptible scope, we call the finish_selectrun
	# function when setting the condition variable
	# (in set_condvar_in_uninterruptible_scope), and have the invariant
	# that the start_watching_for_mailop_ready fate is dispatched
	# outside the atomic region.


									# Nomenclature: What I'm calling "uninterruptible_scope" is usually called "critical section" or "atomic region"
									# in the literature.  I dislike "critical" because it is vague. ("critical" in what sense? Who knows?)
									# "atomic" is literally correct ("a-tomic" == "not cuttable" -- indivisible) but the modern reader is not
									# likely to take it in that sense at first blush.  And neither "section" nor "region" are as apropos as "scope".
	# Set a condition variable.
	# We assume that this function is always
	# executed in an uninterruptible scope.
	#
	fun set_condvar_in_uninterruptible_scope (itt::CONDITION_VARIABLE state)
	    =
	    case *state
		#
		itt::CONDVAR_IS_NOT_SET  waiting_threads									# waiting_threads is the list of threads sitting
		    =>														# blocked waiting for this condvar to be set.
		    {   ts::foreground_run_queue ->  rwq::RW_QUEUE { back, ... };
			#
			state :=    itt::CONDVAR_IS_SET  1;									# Set the condition variable.  NB: The '1' is a priority, not its (non-existent) value.
			#
			back :=     run  waiting_threads									# Add to foreground run queue all threads that were waiting for condvar to be set.
				    where
					fun run [] =>   *back;
					    #
					    run ( { selectrun_status=>REF itt::SELECTRUN_IS_COMPLETE, ... } ! rest)
						=>
						run rest;									# Drop completed selectrun.

					    run ( { selectrun_status as REF (itt::SELECTRUN_IS_BLOCKED thread), finish_selectrun, fate } ! rest)
						=>
						{   selectrun_status :=   itt::SELECTRUN_IS_COMPLETE;
						    #
						    finish_selectrun ();							# Do stuff like   selectrun_status := SELECTRUN_IS_COMPLETE;   and sending nacks.

						    (thread, fate)  !  (run rest);
						};
					end;
				    end;
		    };

		 _ => error "condvar already set";
	    esac;


	# The mailop constructor for
	# waiting on a condition variable:
	#
	fun wait_on_condvar' (itt::CONDITION_VARIABLE  condvar_state)								# This fn is used (only) below and in   src/lib/src/lib/thread-kit/src/core-thread-kit/thread.pkg
	    =
	    ISREADY_MAILOPS [is_mailop_ready_to_fire]
	    where 
		fun start_watching_for_mailop_ready										# Reppy refers to 'start_watching_for_mailop_ready' as 'blockFn'.
		      {
			selectrun_status,											# 'select' is supposed to fire exactly one mailop: 'selectrun_status' is basically a mutex enforcing this.
			finish_selectrun,											# Does stuff like  selectrun_status := SELECTRUN_IS_COMPLETE;  and sending nacks.
			return_to__start_mailop_watches__loop
		      }
		    =
		    call_with_current_fate
			#
			(fn fate
			    =
			    {   waiting_threads											# The list of threads waiting for the condvar to be set.
				    =
				    case *condvar_state
					#
					itt::CONDVAR_IS_NOT_SET waiting_threads =>   waiting_threads;
					itt::CONDVAR_IS_SET _			=>   raise exception FAIL "Bug in wait_on_condvar'";
				    esac;					#    Above exception should not happen:  is_mailop_ready_to_fire() only queues us up if *condvar_state is not CONDVAR_IS_SET.
				#
				waiting_thread =  { selectrun_status,  finish_selectrun,  fate };

				condvar_state :=  itt::CONDVAR_IS_NOT_SET (waiting_thread ! waiting_threads);			# Add ourself to list of threads waiting for condvar to be set.

				return_to__start_mailop_watches__loop ();							# Does not return.
			    }
			);
		#
		fun is_mailop_ready_to_fire ()											# Reppy refers to 'is_mailop_ready_to_fire' as 'pollFn'.
		    =
		    case *condvar_state
			#
			itt::CONDVAR_IS_SET priority
			    =>
			    {   condvar_state :=   itt::CONDVAR_IS_SET (priority+1);						# Increment the condition-variable priority.
				#
				MAILOP_IS_READY_TO_FIRE {  priority,  fire_mailop  }
				where
				    fun fire_mailop ()										# Reppy refers to fire_mailop as 'doFn'.
					=
					{   condvar_state :=  itt::CONDVAR_IS_SET  1;						# Set condvar priority back to minimum.
					    #
					    ts::reenable_thread_switching ();							# End uninterruptable scope.
					};
				end;
			    };

			itt::CONDVAR_IS_NOT_SET _
			    =>
			    MAILOP_IS_NOT_READY_TO_FIRE  start_watching_for_mailop_ready;
		    esac;
	    end;


	# A mailop which is always ready to fire
	# and produces given result:
	#
	fun always'  result													# This is used a lot in (for example)   src/lib/std/src/socket/threadkit-socket.pkg
	    =
	    ISREADY_MAILOPS
	      [
		fn () = itt::MAILOP_IS_READY_TO_FIRE
			  { priority	=> -1,
			    fire_mailop	=> fn () =  {   ts::reenable_thread_switching ();					# Reppy refers to fire_mailop as 'doFn'.
							result;
						    }
			  }
	      ];

	# A mailop which is never ready to fire:
	#
	never' = ISREADY_MAILOPS [];												# Used in:	src/lib/x-kit/widget/basic/topwindow.pkg
																#		src/lib/x-kit/xclient/pkg/window/widget-cable.pkg
																
	# These generate mailops on-the-fly while 'select' is running.
	# The second is given a mailop with which to detect client
	# abort of the generated mailop:
	#
	dynamic_mailop		 =  DYNAMIC_MAILOP;
	dynamic_mailop_with_nack =  DYNAMIC_MAILOP_WITH_NACK;									# This is mainly used in:   src/lib/std/src/io/threadkit-winix-text-file-for-os-g.pkg
																# Also used in:		    src/lib/std/src/io/threadkit-winix-mailslot-io-g.pkg
																#			    src/lib/std/src/posix/threadkit-winix-data-file-io-driver-for-posix.pkg
																#			    	

	# Combine a list of mailops into a single mailop:
	#
	fun cat_mailops (mailops:  List(  Mailop(X) ))										# This gets called in (for example):   src/lib/std/src/io/threadkit-winix-mailslot-io-g.pkg
	    =															# A frequent idiom is  do_mailop (cat_mailops mailops);
	    gather (reverse mailops, [])											#
	    where
		fun gather ([],                               results) =>  ISREADY_MAILOPS results;				# Done, return results.
		    #
		    gather (ISREADY_MAILOPS []       ! rest,  results) =>  gather (rest,           results);
		    gather (ISREADY_MAILOPS [mailop] ! rest,  results) =>  gather (rest, mailop  ! results);
		    gather (ISREADY_MAILOPS  mailops ! rest,  results) =>  gather (rest, mailops @ results);
		    #
		    gather (mailops, []) =>  gather' (mailops, []);
		    gather (mailops, l ) =>  gather' (mailops, [ISREADY_MAILOPS l]);
		end 

		also
		fun gather' ([], [mailop]) =>  mailop;
		    gather' ([], mailops)  =>  CHOOSE_MAILOP mailops;
		    #
		    gather'  (CHOOSE_MAILOP mailops ! rest,  mailops')
			=>
			gather' (rest, mailops @ mailops');

		    gather' (ISREADY_MAILOPS base_mailops ! rest, ISREADY_MAILOPS base_mailops' ! rest')
			=>
			gather' (rest, ISREADY_MAILOPS (base_mailops @ base_mailops') ! rest');

		    gather' (mailop ! rest, mailops')
			=>
			gather' (rest, mailop ! mailops');
		end;
	    end;
	#
	fun make_select_rule (mailop, wfn)				# This is the "==>" op used in select[...] rules.
	    =
	    wrap' mailop
	    where
		fun wrap_base_mailop  is_mailop_ready_to_fire  ()
		    =
		    case (is_mailop_ready_to_fire ())
			#
			MAILOP_IS_READY_TO_FIRE { priority, fire_mailop }		=>  MAILOP_IS_READY_TO_FIRE { priority, fire_mailop => wfn o fire_mailop };
			MAILOP_IS_NOT_READY_TO_FIRE start_watching_for_mailop_ready	=>  MAILOP_IS_NOT_READY_TO_FIRE (wfn o start_watching_for_mailop_ready);
		    esac;
		#
		fun wrap' (ISREADY_MAILOPS base_mailops)	=>  ISREADY_MAILOPS (map  wrap_base_mailop  base_mailops);		# Curried application yields thunklist that will do  case (is_mailop_ready_to_fire() )... esac
		    #															# when later invoked.
		    wrap' (CHOOSE_MAILOP mailops)		=>  CHOOSE_MAILOP	     (map wrap' mailops);
		    #															# when later invoked.
		    wrap' (DYNAMIC_MAILOP make_mailop)		=>  DYNAMIC_MAILOP           (fn ()     =  make_select_rule (make_mailop(), wfn));
		    wrap' (DYNAMIC_MAILOP_WITH_NACK f)		=>  DYNAMIC_MAILOP_WITH_NACK (fn mailop =  make_select_rule (f mailop,      wfn));
		end;
	    end;

	(==>) =  make_select_rule;			# Infix synonym for readability.
	#
	fun make_exception_handling_mailop (mailop, exception_handler_fn)
	    =
	    wrap' mailop
	    where
		fun wrap f x
		    =
		    f x
		    except
			exn = exception_handler_fn exn;
		#
		fun wrap_base_mailop  is_mailop_ready_to_fire ()
		    =
		    case (is_mailop_ready_to_fire ())
			#
			MAILOP_IS_READY_TO_FIRE { priority, fire_mailop }		=>  MAILOP_IS_READY_TO_FIRE { priority, fire_mailop => wrap fire_mailop };
			MAILOP_IS_NOT_READY_TO_FIRE start_watching_for_mailop_ready	=>  MAILOP_IS_NOT_READY_TO_FIRE (wrap start_watching_for_mailop_ready);
		    esac;
		#
		fun wrap' (ISREADY_MAILOPS  base_mailops) =>  ISREADY_MAILOPS  (map  wrap_base_mailop  base_mailops);
		    #
		    wrap' (CHOOSE_MAILOP mailops)	=>  CHOOSE_MAILOP (map  wrap'  mailops);
		    #
		    wrap' (DYNAMIC_MAILOP make_mailop)	=>  DYNAMIC_MAILOP	     (fn ()     =  make_exception_handling_mailop (make_mailop(), exception_handler_fn));
		    wrap' (DYNAMIC_MAILOP_WITH_NACK f)	=>  DYNAMIC_MAILOP_WITH_NACK (fn mailop =  make_exception_handling_mailop (f mailop,      exception_handler_fn));
		end;
	    end;

	Prepared_Mailops(X)
	  = NACKFREE_MAILOPS  List( Isready_Mailop(X) )
	  | NACKFULL_MAILOPS  List( Prepared_Mailops(X) )
	  | WITHNACK_MAILOP  (itt::Condition_Variable, Prepared_Mailops(X))
	  ;

    /* +DEBUG
    fun sayGroup (msg, eg) = let
	  fun f (NACKFREE_MAILOPS l, sl) = "NACKFREE_MAILOPS(" ! int::to_string (list::length l) ::()) ! sl
	    | f (NACKFULL_MAILOPS l, sl) = "NACKFULL_MAILOPS(" ! g (l, ")" ! sl)
	    | f (WITHNACK_MAILOP  l, sl) = "WITHNACK_MAILOP(" ! f(#2 l, ")" ! sl)
	  also g ([], sl) = sl
	    | g ([x], sl) = f (x, sl)
	    | g (x ! r, sl) = f (x, ", " ! g (r, sl))
	  in
	    Debug::sayDebugId (string::cat (msg ! ": " ! f (eg, ["\n"])))
	  end
    -DEBUG*/

	# Prepare mailop expression to run.
        # In particular, this evaluates all
        # dynamic rules to generate the actual
	# final rules to select between:
	#
	fun prepare (ISREADY_MAILOPS l)
		=>
		NACKFREE_MAILOPS l;

	    prepare mailop
		=>
		prepare' mailop
		where
		    fun prepare' (DYNAMIC_MAILOP  make_mailop)
			    =>
			    prepare' (make_mailop ());

			prepare' (DYNAMIC_MAILOP_WITH_NACK f)
			    =>
			    {   condvar = itt::CONDITION_VARIABLE (REF (itt::CONDVAR_IS_NOT_SET []));
				#
				WITHNACK_MAILOP  (condvar,  prepare' (f (wait_on_condvar'  condvar)));
			    };

			prepare' (ISREADY_MAILOPS  mailops)
			    =>
			    NACKFREE_MAILOPS  mailops;

			prepare' (CHOOSE_MAILOP  mailops)
			    =>
			    prepare_mailops (mailops, [])								# Optimistically assume nackfree; we'll fall back toprepare_nackfull_mailops() routine if we're wrong.
			    where
				fun prepare_mailops ([], mailops)
					=>
					NACKFREE_MAILOPS mailops;

				    prepare_mailops (mailop ! rest, mailops')
					=>
					case (prepare' mailop)
					    #
					    NACKFREE_MAILOPS mailops =>  prepare_mailops          (rest,  mailops @                   mailops' );
					    NACKFULL_MAILOPS mailops =>  prepare_nackfull_mailops (rest,  mailops @ [NACKFREE_MAILOPS mailops']);
					    /*        */     mailops =>  prepare_nackfull_mailops (rest, [mailops,   NACKFREE_MAILOPS mailops']);
					esac;
				end

				also
				fun prepare_nackfull_mailops ([], [group]) =>  group;						# The general case vs prepare_mailops above handling the nice simple (and common) case.
				    prepare_nackfull_mailops ([], l)       =>  NACKFULL_MAILOPS l;
				    #
				    prepare_nackfull_mailops (mailop ! rest, l)
					=>
					case (prepare' mailop, l)
					    #				      
					    (NACKFREE_MAILOPS mailops, NACKFREE_MAILOPS mailops' ! rest')
						=>
						prepare_nackfull_mailops (rest, NACKFREE_MAILOPS (mailops @ mailops') ! rest');

					    (NACKFULL_MAILOPS mailops, l) =>   prepare_nackfull_mailops (rest, mailops @ l);
					    (             mailops, l) =>   prepare_nackfull_mailops (rest, mailops ! l);		# Here 'mailops' can be NACKFREE_MAILOPS or WITHNACK_MAILOP.
				       esac;
	end;    end;  end;  end;end;


	stipulate
	    #
	    count =  REF 0;					# Runs circularly around range 0..999,999
	    #
	    fun pick_fairly i					# The point here is just to pick fairly among 'i' alternative ready mailops,
		=						# so that we don't consistently pass over any one ready mailop.
		{   j =  *count;
		    #
		    if (j == 1000000)   count := 0;
		    else                count := j+1;
		    fi;

		    int::rem (j, i);
		};
	herein
	    #
	    fun pick_highest_priority_mailop_breaking_ties_fairly ([(_, fire_mailop)], _)
		    =>
		    fire_mailop;													# Only one choice -- easy work!

		pick_highest_priority_mailop_breaking_ties_fairly (l, n)
		    =>
		    max (l, 0, 0, [])
		    where
			#
			fun priority -1 =>  n;
			    priority  p =>  p;
			end;
			#
			fun max ((p, fire_mailop) ! rest,  max_p,  k,  fire_mailop_fns)
				=>
				{   p =  priority p;
				    #
				    if    (p >  max_p)   max (rest,     p, 1,  [fire_mailop]                 );				# mailop is higher priority than any other yet seen -- discard other candidates.
				    elif  (p == max_p)   max (rest, max_p, k+1, fire_mailop ! fire_mailop_fns);				# mailop is equal-highest priority yet encountered -- add it to list of candidates.
				    else                 max (rest, max_p, k,                 fire_mailop_fns);				# mailop is low-priority, ignore it.
				    fi;
				};

			    max ([], _, k, [fire_mailop])
				=>
				fire_mailop;												# Scanned all mailops and we have a unique highest-priority mailop, so choose it.

			    max ([], _, k, fire_mailop_fns)
				=>
				list::nth (fire_mailop_fns, pick_fairly k);								# Scanned all mailops and we have several highest-priority mailops, so break tie fairly.
			end;
		    end;
	     end;
	end;
	#
	fun make__selectrun_status__and__finish_selectrun ()
	    =
	    {   selectrun_status =    REF (itt::SELECTRUN_IS_BLOCKED (ts::get_current_thread()));
		#
		finish_selectrun    =    .{   selectrun_status :=  itt::SELECTRUN_IS_COMPLETE;  };

		{ selectrun_status, finish_selectrun };
	    };


	stipulate
	    # When we have exactly one mailop in the select[...] we can use simple logic:
	    #
	    fun do_one_mailop  (is_mailop_ready_to_fire:  Isready_Mailop(X))
		=
		{   ts::disable_thread_switching ();
		    #
		    case (is_mailop_ready_to_fire ())
			#	      
			MAILOP_IS_READY_TO_FIRE { fire_mailop, ... }					# Reppy refers to 'fire_mailop' as 'doFn'.
			    =>
			    fire_mailop ();

			MAILOP_IS_NOT_READY_TO_FIRE  start_watching_for_mailop_ready
			    =>
			    {   (make__selectrun_status__and__finish_selectrun ())
				    ->
				    { selectrun_status, finish_selectrun };
				#
				start_watching_for_mailop_ready
				  { selectrun_status,
				    finish_selectrun,
				    return_to__start_mailop_watches__loop =>  ts::reenable_thread_switching_and_dispatch_next_thread	# Since we have only one mailop, we do not need to actually loop here.
				  };
			    };
		    esac;
		};

	    Test_Mailops_For_Readiness_To_Fire_Result(X)
		#
		= NO_READY_MAILOPS { start_mailop_watch__fns:	List (itt::Start_Watching_For_Mailop_Ready__Fn(X)) }
		|    READY_MAILOPS { fire_mailop_fns:		List ((Int, (Void -> X))),	n: Int }
		;


	herein

	    # This function handles the case of picking
	    # and firing one of a list of mailops thunks
	    # (w/o any negative acknowledgements).
	    #
	    # It also handles NEVER.
	    #
	    fun do_nackfree_mailops []       =>  ts::dispatch_next_thread ();							# 'select' with empty rule list -- nothing to do.
		do_nackfree_mailops [mailop] =>  do_one_mailop  mailop;								# This is the only call to   do_one_mailop().
		#
		do_nackfree_mailops  mailops
		    =>
		    {
		        ts::disable_thread_switching ();									# Start uninterruptible scope. (Aka "critical section", "atomic region" etc.)
			#
			case (test_mailops_for_readiness_to_fire (mailops, []))
			    #
			    READY_MAILOPS { fire_mailop_fns, n }
				=>
				{   fire_mailop =   pick_highest_priority_mailop_breaking_ties_fairly  (fire_mailop_fns, n);	# Pick a select[....] mailop to fire ...
				    #
				    fire_mailop ();										# ... and then fire it.
				};

			    NO_READY_MAILOPS { start_mailop_watch__fns }
				=>
				call_with_current_control_fate
				    #
				    (fn fate
					=
					{   start_mailop_watches__loop  start_mailop_watch__fns;
					    #	
					    error "[start_mailop_watches__loop]";						# Above should never return.
					}
					where
					    switch_to_control_fate =  switch_to_control_fate  fate;
					    #
					    (make__selectrun_status__and__finish_selectrun ())
						->
						{ selectrun_status, finish_selectrun };
					    #
					    fun start_mailop_watches__loop [] =>   ts::reenable_thread_switching_and_dispatch_next_thread ();
						#
						start_mailop_watches__loop (start_watching_for_mailop_ready ! rest)
						    =>
						    switch_to_control_fate
							#
							(start_watching_for_mailop_ready
							  { selectrun_status,
							    finish_selectrun,
							    return_to__start_mailop_watches__loop =>  fn () =  start_mailop_watches__loop rest		# maildrop.pkg, mailslot.pkg etc call this to return control to us.
							  }
							);
					    end;
					end
				    );
			    
			esac;
		    }
		    where
			#
			fun test_mailops_for_readiness_to_fire (is_mailop_ready_to_fire ! rest,  start_mailop_watch__fns)				# In this loop we have not yet found any mailops ready to fire.
				=>
				case (is_mailop_ready_to_fire ())
				    #
				    MAILOP_IS_NOT_READY_TO_FIRE start_watching_for_mailop_ready	=>  test_mailops_for_readiness_to_fire   (rest, start_watching_for_mailop_ready ! start_mailop_watch__fns);
				    MAILOP_IS_READY_TO_FIRE { priority, fire_mailop }		=>  test_mailops_for_readiness_to_fire'  (rest, [(priority, fire_mailop)], 1);
				esac;

			    test_mailops_for_readiness_to_fire ([], start_mailop_watch__fns)								# Done -- no ready-to-fire mailops found and no candidates left to check.
				=>
				NO_READY_MAILOPS { start_mailop_watch__fns };										# No mailops were ready to fire; return list of fns which
			end																# each start a watch on one mailop for readiness to fire.

			also
			fun test_mailops_for_readiness_to_fire' (is_mailop_ready_to_fire ! rest,  fire_mailop_fns,  n)					# In this loop we have found at least one mailop which is ready to fire.
				=>
				case (is_mailop_ready_to_fire ())
				    #
				    MAILOP_IS_READY_TO_FIRE { priority, fire_mailop } =>  test_mailops_for_readiness_to_fire' (rest, (priority, fire_mailop) ! fire_mailop_fns, n+1);
				    _                                                 =>  test_mailops_for_readiness_to_fire' (rest,                           fire_mailop_fns, n  );
				esac;

			    test_mailops_for_readiness_to_fire' ([],  fire_mailop_fns,  n)
				=>
			         READY_MAILOPS { fire_mailop_fns, n };											# At least one mailop was ready to fire; return the fns which
																			# will fire the ready-to-fire mailops.

			end;																# fun do_ready_mailops

																			# NOTE: Maybe we should just keep
																			#       track of the max priority above?
																			#       What about fairness to fixed
																			#       priority mailops (e::g., always, timeout?)

		    end;																# where
	    end;																	# fun do_nackfree_mailops
	end;																		# stipulate

	stipulate
	    # Walk the mailop group tree,
	    # collecting the base mailops
	    # (with associated ack flags),
	    # also a list of nackstates.
	    #
	    # A nackstate is a
	    #     (Condvar, List(Flag(Ack)))
	    # pair, where the flags are
	    # those associated with the
	    # mailops covered by the nack
	    # condvar.
	    #
	    fun gather_base_mailops_and_nackstates  mailops
		=
		case mailops
		    #
		    NACKFULL_MAILOPS _
			=>
			gather_mailops (mailops, [], [])
			where 
			    un_wrapped_flag =  REF FALSE;
			    #
			    fun reverse_and_prepend_mailops  (mailop ! rest,  results) =>  reverse_and_prepend_mailops  (rest,  (mailop, un_wrapped_flag) ! results);
				reverse_and_prepend_mailops  (           [],  results) =>  results;
			    end;
			    #
			    fun gather_mailops (NACKFREE_MAILOPS nackfree_mailops, bl, nackstates)
				    =>
				    (reverse_and_prepend_mailops (nackfree_mailops, bl), nackstates);

				gather_mailops (NACKFULL_MAILOPS nackfull_mailops, bl, nackstates)
				    =>
				    fold_left  f  (bl, nackstates)  nackfull_mailops
				    where
					fun f (group', (bl', nackstates'))
					    =
					    gather_mailops (group', bl', nackstates');
				    end;

				gather_mailops (withnack as WITHNACK_MAILOP _, bl, nackstates)
				    =>
				    gather_wrapped (withnack, bl, nackstates);
			    end;
			end;

		    group =>   gather_wrapped (group, [], []);
		esac
		where
		    fun gather_wrapped (group, bl, nackstates)
			    =
			    {    (gather (group, bl, [], nackstates))
				    ->
				    (bl, _, nackstates);

				(bl, nackstates);
			    }
			    where
				fun gather (NACKFREE_MAILOPS mailops, bl, all_flags, nackstates)
					=>
					{   (reverse_and_prepend_mailops (mailops, bl, all_flags))
						->
						(bl', all_flags');

					    (bl', all_flags', nackstates);
					}
					where
					    fun reverse_and_prepend_mailops ([],  bl,  all_flags)
						    =>
						    (bl, all_flags);

						reverse_and_prepend_mailops (mailop ! rest,  bl,  all_flags)
						    =>
						    {   flag = REF FALSE;
							#
							reverse_and_prepend_mailops  (rest,  (mailop, flag) ! bl,  flag ! all_flags);
						    };
					    end;
					end;


				    gather (NACKFULL_MAILOPS group, bl, all_flags, nackstates)
					=>
					fold_left  f  (bl, all_flags, nackstates)  group
					where
					    fun f (group', (bl', all_flags', nackstates'))
						=
						gather (group', bl', all_flags', nackstates');
					end;

				    gather (WITHNACK_MAILOP (condvar, group), bl, all_flags, nackstates)
					=>
					{   (gather (group, bl, [], nackstates))
						->
						(bl', all_flags', nackstates');

					    ( bl',
                                              all_flags' @ all_flags,
					      (condvar, all_flags')  !  nackstates'
					    );
					};
				end;									# fun gather
			    end;									# where
		end;											# where

	    Test_Mailops_For_Readiness_To_Fire_Result(X)
		#
		= NO_READY_MAILOPS { start_mailop_watch__fns:	List ((itt::Start_Watching_For_Mailop_Ready__Fn(X), Ref(Bool)) }
		|    READY_MAILOPS { fire_mailop_fns:		List ((Int, (Void -> X))),	n: Int }
		;

	herein
	    # This function handles the more
	    # complicated case of running a
	    # mailop expression where negative
	    # acknowledgements are involved.
	    #
	    fun do_nackfull_mailops  group
		=
		{   ts::disable_thread_switching ();							# Start uninterruptible scope (aka "critical section" aka "atomic region"...)
		    #
		    do_mailops (mailops, []);
		}
		where
		    (gather_base_mailops_and_nackstates  group)
			->
			(mailops, nackstates);
		    #
		    fun send_nacks_as_appropriate ()
			=
			apply  check_condvar  nackstates						# NB: We capture 'nackstates' here for later use in 'finish_selectrun_fn'.
			where
			    # check_condvar checks the flags of a nackstate.
			    # If they are all FALSE then the
			    # corresponding condvar is set to signal
			    # the negative ack.
			    #
			    fun check_condvar (condvar, flags)
				=
				check_flags flags
				where
				    fun check_flags ((REF TRUE) ! _) =>	 ();
					check_flags (_ ! rest)	     =>	 check_flags  rest;
					check_flags []		     =>	 set_condvar_in_uninterruptible_scope  condvar;
				    end;
				end;
			end;
		    #
		    fun do_mailops ((is_mailop_ready_to_fire, flag) ! rest,   start_mailop_watch__fns)						# In this loop we have not yet found a mailop ready to fire.
			    =>
			    case (is_mailop_ready_to_fire ())
				#
				MAILOP_IS_READY_TO_FIRE      { priority, fire_mailop }	     =>  do_ready_mailops (rest, [(priority, (fire_mailop, flag))], 1);
				MAILOP_IS_NOT_READY_TO_FIRE  start_watching_for_mailop_ready =>  do_mailops       (rest, (start_watching_for_mailop_ready, flag) ! start_mailop_watch__fns);
			    esac;

			do_mailops ([], start_mailop_watch__fns)										# End of the line -- no ready-to-fire mailops found and none left to check,
			    =>															# so we must block until some mailop becomes ready to fire.
			    call_with_current_control_fate
				(fn fate
				    =
				    {   start_mailop_watches__loop  start_mailop_watch__fns;							# Ask each mailop in the select[...] list to enqueue itself as appropriate. 
					#
					error "[start_mailop_watches__loop]";									# This cannot happen -- start_mailop_watches__loop() dispatches next thread when done.
				    }
				    where
					switch_to_control_fate =  switch_to_control_fate  fate;
					#
					selectrun_status =   REF (itt::SELECTRUN_IS_BLOCKED (ts::get_current_thread ()));			# 'selectrun_status' is basically a mutex ensuring exactly one mailop fires per selectrun.
					#
					fun finish_selectrun_fn  flag  ()									# This will be called by the first mailop to fire. 
					    =
					    {   selectrun_status := itt::SELECTRUN_IS_COMPLETE;
						flag := TRUE;
						send_nacks_as_appropriate ();
					    };
					#
					fun start_mailop_watches__loop [] =>   ts::reenable_thread_switching_and_dispatch_next_thread ();
					    #
					    start_mailop_watches__loop ((start_watching_for_mailop_ready, flag)  !  rest)
						=>
						switch_to_control_fate
						    (
							start_watching_for_mailop_ready
							  {
							    selectrun_status,
							    finish_selectrun =>  finish_selectrun_fn flag,
							    return_to__start_mailop_watches__loop =>  fn () =  start_mailop_watches__loop rest	# maildrop.pkg, mailslot.pkg etc call this to return control to us.
							  }
						    );
					end;													# fn start_mailop_watches__loop
				    end														# where
				);
		    end

		    also
		    fun do_ready_mailops ((is_mailop_ready_to_fire, flag) ! rest, fire_mailop_fns, n)			# In this loop we have found at least one select[...] mailop ready to fire.
			    =>
			    case (is_mailop_ready_to_fire ())
				#
				MAILOP_IS_READY_TO_FIRE { priority, fire_mailop }
				    =>
				    do_ready_mailops  (rest,  (priority,  (fire_mailop, flag)) ! fire_mailop_fns,  n+1);

				_   =>   do_ready_mailops  (rest,  fire_mailop_fns,  n);
			    esac;

			do_ready_mailops ([], fire_mailop_fns, n)							# End of line -- pick one select[...] mailop to fire and then fire it.
			    =>
			    {   (pick_highest_priority_mailop_breaking_ties_fairly (fire_mailop_fns, n))
				    ->
				    (fire_mailop, flag);

				flag := TRUE;

				send_nacks_as_appropriate ();

				fire_mailop();
			    };
		    end;
															# NOTE: Maybe above we should just
															# keep track of the max priority?
															# What about fairness to fixed
															# priority mailops (e::g., always, timeout?)
															#

		end;													# fun do_nackfull_mailops
	end;														# stipulate


	#
	fun do_mailop  mailop												# External entrypoint.
	    =
	    case (prepare mailop)
		#
		NACKFREE_MAILOPS mailops =>  do_nackfree_mailops  mailops;
		/*            */ mailops =>  do_nackfull_mailops  mailops;
	    esac;


															# 'select' is our core entrypoint, the 'select' used by clients
															# to do style handle-multiple-mail-sources thread I/O via
															# statements looking like
															#
															#     select [
															#	  foo' ==> .{ do_this (); },
															#	  bar' ==> .{ do_that (); },
															#         ...
															#     ];
															#
															# We have two main cases:
															#  1) If one or more mailops in the list is ready to fire, we pick one and fire it.
															#  2) If no mailop in the list is ready to fire, we must block until one becomes ready, then continue as in 1).
	fun select mailops
	    =
	    case (prepare_mailops (mailops, []))
		#
		NACKFREE_MAILOPS mailops  =>   do_nackfree_mailops  mailops;						# This is special-case handling for simple special case of no WITH_NACK mailops.
		/*            */ mailops  =>   do_nackfull_mailops  mailops;						# This is the general case.
	    esac
	    where
 		# Preparation.  During this phase we need to:
		#
		#  o Expand DYNAMIC_MAILOP										# These are essentially hacks to allow generating
		#    and    DYNAMIC_MAILOP_WITH_NACK clauses.								# mailops on the fly while 'select' is running.
		#
		#  o Figure out whether we have any WITH_NACK clauses,
		#    which complicate things.
		#
                #  In the common case of no WITH_NACK mailops
		#  we return NACKFREE_MAILOPS _, otherwise    
		#  we return NACKFULL_MAILOPS _ or WITHNACK_MAILOP _.
		# 
	        fun prepare_mailops  (mailop ! rest,  mailops')								# prepare_mailops          handles the nice simple nack-free case;
			=>												# prepare_nackfull_mailops handles the case where one or more WITH_NACK clauses are present.
			case (prepare_one_mailop  mailop)
			    #
			    /* */ NACKFREE_MAILOPS mailops  =>  prepare_mailops           (rest,  mailops @                   mailops' );
			    /* */ NACKFULL_MAILOPS mailops  =>  prepare_nackfull_mailops  (rest,  mailops @ [NACKFREE_MAILOPS mailops']);
			    wm as WITHNACK_MAILOP   _       =>  prepare_nackfull_mailops  (rest, [wm,        NACKFREE_MAILOPS mailops']);
			esac;

		    prepare_mailops ([], mailops)	    =>	NACKFREE_MAILOPS  mailops;				# Done!
		end

		also
		fun prepare_nackfull_mailops ([], [result]) =>  result;
		    prepare_nackfull_mailops ([], results)  =>  NACKFULL_MAILOPS results;
		    #
		    prepare_nackfull_mailops (mailop ! rest,  results)
			=>
			case (prepare_one_mailop  mailop,  results)
			    #
			    (NACKFREE_MAILOPS mailops,  NACKFREE_MAILOPS mailops' ! rest')
				=>
				prepare_nackfull_mailops (rest, NACKFREE_MAILOPS (mailops @ mailops') ! rest');

			    (NACKFULL_MAILOPS mailops, results) =>   prepare_nackfull_mailops  (rest,  mailops @ results);
			    (other,                    results) =>   prepare_nackfull_mailops  (rest,    other ! results);		# 'other' can be (NACKFREE_MAILOPS _) or (WITHNACK_MAILOP _).
			esac;
		end

		also
		fun prepare_one_mailop (ISREADY_MAILOPS  mailops)     =>   NACKFREE_MAILOPS mailops;
		    prepare_one_mailop (CHOOSE_MAILOP    mailops)     =>   prepare_mailops (mailops, []);
		    prepare_one_mailop (DYNAMIC_MAILOP   make_mailop) =>   prepare_one_mailop (make_mailop ());				# Generate a select[...] mailop on the fly, then recursively prepare it.
		    #
		    prepare_one_mailop (DYNAMIC_MAILOP_WITH_NACK make_mailop)								# Like as DYNAMIC_MAILOP but with a nack mailop to signal client-code abort.
			=>
			{   condvar =  itt::CONDITION_VARIABLE  (REF  (itt::CONDVAR_IS_NOT_SET []));
			    #
			    WITHNACK_MAILOP  (condvar,  prepare_one_mailop  (make_mailop  (wait_on_condvar'  condvar)));
			};

		end;
	    end;			# fun select
    };					# package mailop
end;

## COPYRIGHT (c) 1989-1991 John H. Reppy
## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.



##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


