## binarytree-ximp.pkg
#
# This file is intended purely for clone-and-mutate
# construction of new X imps ("ximps").
#
# For the big picture see the imp dataflow diagram in
#
#     src/lib/x-kit/xclient/src/window/xsession.pkg

# Compiled by:
#     src/lib/test/unit-tests.lib





stipulate
    include threadkit;													# threadkit				is from   src/lib/src/lib/thread-kit/src/core-thread-kit/threadkit.pkg
herein


    package   binarytree_ximp
    : (weak)  Binarytree_Ximp												# Binarytree_Ximp			is from   src/lib/x-kit/xclient/src/wire/binarytree-ximp.api
    {
	include binarytree_ximp_clientport;										# binarytree_ximp_clientport		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/binarytree-ximp-clientport.pkg

	Startup_Gun = Mailop(Void);											# Purely for readability.
	#
	Binarytree_Ximp_State = Ref( Int );										# Opaque wrapper for all nonephemeral mutable state maintained by ximp.

	Patronplea  =  STOP
		    ;
	Patronport = Mailslot( Patronplea );


	Clientplea  =  SET_STATE Int
		    |  PASS_SUBTREE_SUM Oneshot_Maildrop( Int )
		    ;
	Clientq     = Mailqueue( Clientplea );

	Outports = {  leftkid:  Null_Or(Clientport),
                  rightkid:  Null_Or(Clientport)
		};

	Configport = Mailslot ( { outports: 	    Outports,
				  me:		    Binarytree_Ximp_State,
				  startup_gun':	    Startup_Gun
				}
			      );

	Inports	= { configport:		Configport,
		    clientport:		Clientport,
		    patronport:		Patronport
		  };


	fun run {   me:			Binarytree_Ximp_State,								# These four values will be statically globally visible
		    patronport:		Patronport,									# throughout the code body for the imp.
		    clientq:		Clientq,
		    outports:		Outports,
		    to:			Replyqueue(Void)								# The name makes   foo::pass_something(imp) to .{ ... }   syntax read well.
		}
	    =
	    loop ()
	    where
		fun loop ()												# Outer loop for the imp.
		    =
		    {   do_one_mailop' to [
			    #
			    (take_from_mailslot'  patronport  ==>  do_patronplea),
			    (take_from_mailqueue' clientq     ==>  do_clientplea)
			];

			loop ();
		    }	
		    where
			fun shut_down_binarytree_imp' ()
			    =
			    {
				thread_exit { success => TRUE };							# Will not return.	
			    };

			fun do_patronplea  STOP
			    =
			    shut_down_binarytree_imp' ();								# Will not return.	

			fun do_clientplea  (SET_STATE i)
			        =>
				me := i;

			    do_clientplea  (PASS_SUBTREE_SUM reply_oneshot)
				=>
				case outports.leftkid
				    #
				    THE leftkid =>  leftkid.pass_subtree_sum to .{
							#
							sum =  *me  +  #subtree_sum;

							case outports.rightkid
							    #
							    THE rightkid => rightkid.pass_subtree_sum to .{
										#
										put_in_oneshot (reply_oneshot, sum + #subtree_sum);
									    };


							    NULL    =>  put_in_oneshot (reply_oneshot, sum);
							esac;
						    };

				    NULL    =>  case outports.rightkid
						    #
						    THE rightkid => rightkid.pass_subtree_sum to .{
									#
									put_in_oneshot (reply_oneshot, *me + #subtree_sum);
								    };

						    NULL    =>  put_in_oneshot (reply_oneshot, *me);
						esac;
				esac;
			end;
		    end;
	    end;	



	fun startup   (reply_oneshot:  Oneshot_Maildrop( Inports ))   ()						# Root fn of imp microthread.  Note currying -- second arg is provided by make_thread.
	    =
	    {   configport =  make_mailslot  ()					:  Configport;
		patronport =  make_mailslot  ()					:  Patronport;

		clientport  = { set_state, pass_subtree_sum, get_subtree_sum };

		to         =  make_replyqueue();

		put_in_oneshot (reply_oneshot, { configport, patronport, clientport });					# Return value from make_binarytree_ximp().

		(take_from_mailslot  configport)									# Input args from configure_binarytree_ximp().
		    ->
		    { me, outports, startup_gun' };

		block_until_mailop_fires  startup_gun';								# Wait for the starting gun.

		run { me, patronport, clientq, outports, to };								# Will not return.
	    }
	    where
		clientq =  make_mailqueue (get_current_microthread())	:  Clientq;

		fun set_state (i: Int)											# External entrypoint.
		    =	
		    put_in_mailqueue  (clientq, SET_STATE i);


		fun pass_subtree_sum  (replyqueue: Replyqueue(Void))  (reply_handler: Int -> Void)			# External entrypoint.
		    =
		    {
			reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (clientq, PASS_SUBTREE_SUM reply_oneshot);

			put_in_replyqueue (replyqueue, (get_from_oneshot' reply_oneshot) ==> reply_handler);
		    };

		fun get_subtree_sum  ()											# External entrypoint.
		    =
		    {   reply_oneshot =  make_oneshot_maildrop():  Oneshot_Maildrop( Int );
			#
			put_in_mailqueue  (clientq, PASS_SUBTREE_SUM reply_oneshot);

			get_from_oneshot reply_oneshot;
		    };
	    end;


        ##########################################################################################
	# External entrypoints
	#
	fun make_binarytree_ximp_state i										# External entrypoint.
	    =
	    REF i;

	fun make_binarytree_ximp  (name: String)									# External entrypoint.
	    =
	    {   reply_oneshot = make_oneshot_maildrop();
		#
		xlogger::make_thread  name  (startup  reply_oneshot);							# Note that startup() is curried -- second arg is provided by make_thread.

		get_from_oneshot  reply_oneshot;
	    };

	fun configure_binarytree_ximp											# External entrypoint.
	      (
		configport:	Configport,
		me:		Binarytree_Ximp_State,
		outports:	Outports,
		startup_gun':	Startup_Gun
	      )
	    =
	    put_in_mailslot  (configport, { me, outports, startup_gun' });


	fun shut_down_binarytree_ximp  (patronport: Patronport)								# External entrypoint.
	    =
	    put_in_mailslot  (patronport,  STOP);

	fun clientport_to_mailqueue x = x;										# For debugging

    };															# package binarytree_ximp
end;




##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1990, 1991 by John H. Reppy.  See SMLNJ-COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2013,
## released per terms of SMLNJ-COPYRIGHT.
