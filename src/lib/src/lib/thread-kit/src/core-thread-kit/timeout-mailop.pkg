## timeout-mailop.pkg
#
# Mailops that wait until a given time.

# Compiled by:
#     src/lib/std/standard.lib





stipulate
    package fat =  fate;						# fate				is from   src/lib/std/src/nj/fate.pkg
    package itt =  internal_threadkit_types;				# internal_threadkit_types	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/internal-threadkit-types.pkg
    package mop =  mailop;						# mailop			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
    package sch =  thread_scheduler;					# thread_scheduler		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg
    package tim =  time;						# time				is from   src/lib/std/time.pkg
    #
    Mailop(X) =  mop::Mailop(X);
herein

    package timeout_mailop: (weak)  api {
	#
	include Timeout_Mailop;						# Timeout_Mailop		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/timeout-mailop.api

	reset_sleep_queue_to_empty
	    :
	    Void -> Void;

	wake_sleeping_threads_whose_time_has_come
	    :
	    Void -> Void;

	time_until_next_sleeping_thread_wakes
	    :
	    Void -> Null_Or( tim::Time );
    }

    {


	# The list of threads waiting for timeouts.
	# It is sorted in increasing order
	# of time value.
	#
	# NOTE: We may want to use some sort of
	# balanced search package in the future.		XXX BUGGO FIXME
	#
	Item = ( tim::Time,
		 Void -> Void,
		 Ref (itt::Select_Run_Status),
		 fat::Fate( Void )
	       );
	#
	sleep_queue
	    =
	    REF ([]: List( Item ));


	fun time_wait (t, f, id, k)
	    =
	    sleep_queue := insert *sleep_queue
	    where
		fun insert []
			=>
			[ (t, f, id, k) ];

		    insert ((_, _, REF itt::SELECT_RUN_IS_COMPLETE, _) ! rest)
			=>
			# Drop completed transaction in passing:
			#
			insert rest;

		    insert (list as ((item as (t', _, _, _)) ! rest))
		       =>
		       tim::(<) (t', t)  ??  item ! insert rest
					  ::  (t, f, id, k) ! list;
		end;
	    end;


	# Drop all completed transactions from itemlist.
	# Return cleaned list:
	#
	fun drop_cancelled_queue_entries  items
	    =
	    drop_them  items
	    where
		fun drop_them ((_, _, REF itt::SELECT_RUN_IS_COMPLETE, _) ! rest)
			=>
			drop_them  rest;

		    drop_them (item ! rest)
			=>
			item  !  drop_them  rest;

		    drop_them [] => [];
		end;
	    end;


	# Find all sleeping threads whose
	# time has come and move them to
	# run queue.
	#
	# Return list of still-sleeping threads.
	#
	fun wake_and_remove_sleeping_threads_whose_time_has_come  q
	    =
	    wake_them q
	    where
		now =  sch::get_approximate_time ();

		fun wake_them ((_, _, REF itt::SELECT_RUN_IS_COMPLETE, _) ! rest)
			=>
			wake_them rest;

		    wake_them (list as ((item as (t', f, mailop_done as REF (itt::SELECT_RUN_IS_BLOCKED thread), fate)) ! rest))
			=>
			if (tim::(<=) (t', now))
			    #
			   sch::push_into_run_queue (thread, fate);
			   f ();							# Cleanup function. 
			   wake_them  rest;
		       else
			   drop_cancelled_queue_entries  list;
		       fi;

		   wake_them [] => [];
	       end;
	    end;


	fun time_until_next_sleeping_thread_wakes ()
	    =
	    case (drop_cancelled_queue_entries *sleep_queue)
		#
		[]  =>  NULL;

		(q as ((t, _, _, _) ! _))
		    =>
		    {   now =  sch::get_approximate_time ();
			#
			tim::(<=) (t, now)
			    ##
			    ??   THE (tim::zero_time)
			    ::   THE (tim::(-) (t, now));
		    };
	    esac;


	fun wake_sleeping_threads_whose_time_has_come ()
	    =
	    case *sleep_queue
		#
		[]     =>   ();
		#
		queue  =>   sleep_queue
				:=
				wake_and_remove_sleeping_threads_whose_time_has_come
				    queue;
	    esac;


	fun reset_sleep_queue_to_empty ()
	    =
	    sleep_queue := [];


	# NOTE: Unlike other base mail_ops, the
	#           set_up__mailop_ready_to_fire__watch()
	#       fns of time-out mail_ops do not have to exit the
	#       uninterruptable scope or execute the clean-up
	#       operation -- this is done when they are removed
	#       from the wait queue.
	#
	fun timeout_in'  sleep_duration
	    =
	    itt::ISREADY_MAILOPS [ is_mailop_ready_to_fire ]
	    where
		fun set_up__mailop_ready_to_fire__watch { mailop_done, clean_up, next }		# Reppy refers to 'set_up__mailop_ready_to_fire__watch' as 'blockFn'.
		    =
		    # This fn gets used in
		    #
		    #     src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
		    #
		    # when a
		    #
		    #     select [ ... ]
		    #
		    # call has no mailops ready to fire.  'select' must then block until
		    # at least one mailop is ready to fire.  It does this by calling the
		    #
		    #     set_up__mailop_ready_to_fire__watch ()
		    #
		    # fn on each mailop in the list; each such call will typically
		    # make an entry in one or more queues of blocked threads.  The
		    # first mailop to fire cancels the rest by setting  mailop_done.
		    #
		    { # now =    sch::get_approximate_time ();					# Replaced by below 2012-02-01 CrT because 100ms wait was coming back after 99ms, triggering 'make check' alarm.
			now =    tim::get_current_time_utc ();
			#
			fate::call_with_current_fate
			    (fn fate
				=
				{   time_wait
					( tim::(+) (sleep_duration, now),
					  clean_up,
					  mailop_done,
					  fate
					);

				    next ();
				}
			    );

			sch::reenable_thread_switching ();
		    };

		fun is_mailop_ready_to_fire ()							# Reppy refers to 'is_mailop_ready_to_fire' as 'pollFn'.
		    =
		    if (sleep_duration == tim::zero_time)
			#
			itt::MAILOP_IS_READY_TO_FIRE
			  { priority	=>  -1,
			    fire_mailop	=>  sch::reenable_thread_switching			# Reppy refers to 'fire_mailop' as 'doFn'.
			  };
		    else
			itt::MAILOP_IS_NOT_READY_TO_FIRE set_up__mailop_ready_to_fire__watch;
		    fi;

	    end;


	fun sleep_for  sleep_duration
	    =
	    mop::do_mailop  (timeout_in'  sleep_duration);


	fun timeout_at'  wakeup_time
	    =
	    itt::ISREADY_MAILOPS [ is_mailop_ready_to_fire ]
	    where
		fun set_up__mailop_ready_to_fire__watch { mailop_done, clean_up, next }		# Reppy refers to 'set_up__mailop_ready_to_fire__watch' as 'blockFn'.
		    =
		    # This fn gets used in
		    #
		    #     src/lib/src/lib/thread-kit/src/core-thread-kit/mailop.pkg
		    #
		    # when a
		    #
		    #     select [ ... ]
		    #
		    # call has no mailops ready to fire.  'select' must then block until
		    # at least one mailop is ready to fire.  It does this by calling the
		    #
		    #     set_up__mailop_ready_to_fire__watch ()
		    #
		    # fn on each mailop in the list; each such call will typically
		    # make an entry in one or more queues of blocked threads.  The
		    # first mailop to fire cancels the rest by setting  mailop_done.
		    #
		    {   fate::call_with_current_fate
			    (
			     fn fate
				=
				{   time_wait (wakeup_time, clean_up, mailop_done, fate);
				    #
				    next ();
				}
			    );

			sch::reenable_thread_switching ();
		    };

		fun is_mailop_ready_to_fire ()							# Reppy refers to 'is_mailop_ready_to_fire' as 'pollFn'.
		    =
		    if (tim::(<=) (wakeup_time, sch::get_approximate_time ()))
			#
			itt::MAILOP_IS_READY_TO_FIRE
			  { priority	=>  -1,
			    fire_mailop	=>  sch::reenable_thread_switching			# Reppy refers to 'fire_mailop' as 'doFn'.
			  };
		    else
			itt::MAILOP_IS_NOT_READY_TO_FIRE  set_up__mailop_ready_to_fire__watch;
		    fi;
	    end;


	fun sleep_until  wakeup_time
	    =
	    mop::do_mailop  (timeout_at'  wakeup_time);


    };
end;



##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################


## COPYRIGHT (c) 1989-1991 John H. Reppy
## COPYRIGHT (c) 1995 AT&T Bell Laboratories.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.
