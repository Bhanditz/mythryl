## mailqueue.pkg
#
# Unbounded queues of thread-to-thread mail messages.

# Compiled by:
#     src/lib/std/standard.lib




stipulate
    package fat =  fate;				# fate				is from   src/lib/std/src/nj/fate.pkg
    package itt =  internal_threadkit_types;		# internal_threadkit_types	is from   src/lib/src/lib/thread-kit/src/core-thread-kit/internal-threadkit-types.pkg
    package ts  =  thread_scheduler;			# thread_scheduler		is from   src/lib/src/lib/thread-kit/src/core-thread-kit/thread-scheduler.pkg
    #
    Fate(X) =   fat::Fate(X);
    #
    call_with_current_fate =  fat::call_with_current_fate;
    switch_to_fate         =  fat::switch_to_fate;
herein

    package mailqueue: (weak)
    api {

	include Mailqueue;				# Mailqueue			is from   src/lib/src/lib/thread-kit/src/core-thread-kit/mailqueue.api

	reset_mailqueue:  Mailqueue(X) -> Void;

    }
    {
	Queue(X)
	    =
	    { front:  List(X),
	      rear:   List(X)
	    };

	fun enqueue ( { front, rear }, x)
	    =
	    { front,
	      rear => x ! rear
	    };

	fun dequeue ( { front => x ! rest, rear } ) =>  ( { front=>rest, rear }, x);
	    dequeue ( { front => [],       rear } ) =>  dequeue { front=>list::reverse rear, rear=> [] };
	end;
	    # Remove item from queue and return it plus new queue.
	    # This will raise an exception if queue is empty, but
	    # caller guarantees that queue is nonempty.

	Mailqueue_State(X)
	  = EMPTY      Queue( (Ref( itt::Mailop_Done ), Fate(X)))
	  | NONEMPTY  (Int, Queue(X))					# NB: The queue of the NONEMPTY constructor should never be empty -- use EMPTY instead.
	  ;

	Mailqueue(X) = MAILQUEUE  Ref( Mailqueue_State(X) );

	empty_queue =  EMPTY  { front => [],
				rear  => []
			      };

	fun reset_mailqueue (MAILQUEUE state)
	    =
	    state :=  empty_queue;

	fun make_mailqueue ()
	    =
	    MAILQUEUE (REF empty_queue);

	fun same_mailqueue ( MAILQUEUE s1,
			     MAILQUEUE s2
	                   )
	    =
	    s1 == s2;							# Compare the refcells -- each refcell is equal only to itself, and different mailqueues will always have different refcells.


	fun make__mailop_done__refcell ()
	    =
	    REF (itt::MAILOP_PENDING_FOR (ts::get_current_thread()));


	fun end_transaction_and_return_thread_state (mailop_done as REF (itt::MAILOP_PENDING_FOR thread_state))
		=>
		{   mailop_done :=   itt::MAILOP_DONE;
		    #
		    thread_state;
		};

	    end_transaction_and_return_thread_state  (REF (itt::MAILOP_DONE))
		=>
		raise exception FAIL "Compiler bug:  Attempt to cancel already-cancelled transaction";			# Never happens; here to suppress 'nonexhaustive match' compile warning.
	end;
	    

	Mailqueue_Item(X)
	  #
	  = NO_ITEM
	  #
	  |    ITEM   ( Ref(itt::Mailop_Done),
			Fate(X),
			Mailqueue_State(X)
		      )
	  ;

	stipulate

	    fun clean ((REF itt::MAILOP_DONE, _) ! rest) =>   clean rest;
		clean l => l;
	    end;

	    fun clean_rev ([], l)
		    =>
		    l;

		clean_rev ((REF itt::MAILOP_DONE, _) ! rest,  l)
		    =>
		    clean_rev (rest,  l);

		clean_rev (x ! rest,  l)
		    =>
		    clean_rev (rest,  x ! l);
	    end;

	herein

	    fun clean_and_remove (q as { front, rear } )
		=
		clean_front front
		where

		    fun clean_front []
			    =>
			    clean_rear rear;

			clean_front f
			    =>
			    case (clean f)
				#
				[] => clean_rear rear;

				((id, k) ! rest)
				    =>
				    ITEM (id, k, EMPTY { front=>rest, rear } );
			    esac;
		    end

		    also
		    fun clean_rear []
			    =>
			    NO_ITEM;

			clean_rear r
			    =>
			    case (clean_rev (r, []))
				#
				[]             =>  NO_ITEM;
				(id, k) ! rest =>  ITEM (id, k, EMPTY { front=>rest, rear => [] } );
			    esac;
		    end;
		end;
	end;

	fun push (MAILQUEUE qstate, x)
	    =
	    {   ts::disable_thread_switching ();
		#
		case *qstate
		    #
		    EMPTY q
			=>
			case (clean_and_remove q)
			    #
			    NO_ITEM =>
				{
				    qstate :=  NONEMPTY (1, { front => [x], rear => [] } );
				    #
				    ts::reenable_thread_switching();
				};

			    ITEM (mailop_done, get_fate, qstate')
				=>
				call_with_current_fate
				    (fn fate
					=
					{   qstate :=  qstate';
					    #
					    ts::run_thread   (end_transaction_and_return_thread_state  mailop_done,   fate);

					    switch_to_fate  get_fate  x;								# 
					}
				    );
			esac;

		    NONEMPTY (p, q)
			=> 
			call_with_current_fate								# We force a context switch here to prevent a producer from outrunning a consumer.
			    #
			    (fn fate
				=
				{   qstate :=  NONEMPTY (p, enqueue (q, x));
				    #
				    ts::reenable_thread_switching_and_yield_to_next_thread  fate;
				}
			    );
		 esac;
	    };

	fun get_msg (qstate, q)
	    =
	    {   (dequeue q) ->   (q', msg);
		#
		case q'
		    #
		    { front => [],
		      rear  => []
		    }
			=>   qstate :=  empty_queue;
		    _   =>   qstate :=  NONEMPTY (1, q');

		esac;

		ts::reenable_thread_switching ();

		msg;
	    };

	fun pull (MAILQUEUE qstate)
	    =
	    {   ts::disable_thread_switching ();
		#
		case *qstate
		    #
		    EMPTY q
			=>
			{   msg =   call_with_current_fate
					(
					 fn get_fate
					    =
					    {   qstate :=  EMPTY (enqueue (q, (make__mailop_done__refcell(), get_fate)));

						ts::reenable_thread_switching_and_dispatch_next_thread ();
					    }
					);

			    ts::reenable_thread_switching ();

			    msg;
			 };

		  NONEMPTY (priority, q)
		      =>
		      get_msg (qstate, q);

		esac;
	    };

	fun pull' (MAILQUEUE qstate)
	    =
	    {
		fun wait_for { mailop_done, clean_up, next }
		    =
		    {   q = case *qstate    EMPTY    q =>  q;
			    /* */	    NONEMPTY _ =>  raise exception FAIL "Compiler bug: Unsupported NONEMPTY case in poll'/wait_for";	# Should be impossible, since is_ready() (below) only queues us up if *qstate is EMPTY.
			    esac;

			msg = call_with_current_fate
				  (fn get_fate
				      =
				      {   qstate :=  EMPTY (enqueue (q, (mailop_done, get_fate)));
					  #
					  next ();

					  raise exception FAIL "Mailqueue: impossible";			# next() should never return.
				      }
				  );

			clean_up ();

			ts::reenable_thread_switching ();

			msg;
		    };

		fun is_ready ()
		    =
		    case *qstate
			#
			EMPTY _ =>   itt::MAILOP_UNREADY  wait_for;
			#
			NONEMPTY (priority, q)
			    =>
			    {   qstate :=  NONEMPTY (priority+1, q);
				#
				itt::MAILOP_READY
				  {
				    priority,
				    do_it =>   .{  get_msg (qstate, q);  }
				  };
			    };
		    esac;


		itt::BASE_MAILOPS  [ is_ready ];
	    };

	fun nonblocking_pull (MAILQUEUE qstate)
	    =
	    {   ts::disable_thread_switching();

		case *qstate
		    #
		    EMPTY q
			=>
			{   ts::reenable_thread_switching ();
			    NULL;
			};

		    NONEMPTY (priority, q)
			=>
			THE (get_msg (qstate, q));
		esac;
	    };
    };				# package mailqueue
end;



## COPYRIGHT (c) 1989-1991 John H. Reppy
## COPYRIGHT (c) 1995 AT&T Bell Laboratories
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2012,
## released under Gnu Public Licence version 3.



##########################################################################
#   The following is support for outline-minor-mode in emacs.		 #
#  ^C @ ^T hides all Text. (Leaves all headings.)			 #
#  ^C @ ^A shows All of file.						 #
#  ^C @ ^Q Quickfolds entire file. (Leaves only top-level headings.)	 #
#  ^C @ ^I shows Immediate children of node.				 #
#  ^C @ ^S Shows all of a node.						 #
#  ^C @ ^D hiDes all of a node.						 #
#  ^HFoutline-mode gives more details.					 #
#  (Or do ^HI and read emacs:outline mode.)				 #
#									 #
# Local variables:							 #
# mode: outline-minor							 #
# outline-regexp: "[{ \t]*\\(fun \\)"			 		 #
# End:									 #
##########################################################################
